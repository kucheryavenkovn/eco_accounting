#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Исключаем из проверки реквизиты, заполнение которых стало необязательным:
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаАренды Тогда
		Если НачалоПериода > ОкончаниеПериода Тогда
			ТекстСообщения = НСтр("ru = 'Начало периода превышает окончание периода'");
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(, "Корректность",
					НСтр("ru = 'Начало периода'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,
				"НачалоПериода", "Объект", Отказ);
		КонецЕсли;
		
		Если Не ПостояннаяЧастьОплаты И Не ПеременнаяЧастьОплаты Тогда
			ТекстСообщения = НСтр("ru = 'Необходимо указать по крайней мере один способ заполнения счета (по постоянной или по переменной части оплаты)'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,
				"ПостояннаяЧастьОплаты", "Объект", Отказ);
		КонецЕсли;
		
		Если ПостояннаяЧастьОплаты И ВариантУчетаНДС > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Для формирования документов по постоянной части значение варианта учета НДС должно быть ""В соответствии с договором""'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,
				"ВариантУчетаНДС", "Объект", Отказ);
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаПени Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НачалоПериода");	
		МассивНепроверяемыхРеквизитов.Добавить("ОкончаниеПериода");	
	КонецЕсли;
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры


#КонецЕсли


