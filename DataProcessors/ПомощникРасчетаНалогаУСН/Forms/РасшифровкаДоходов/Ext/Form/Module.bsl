#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Организация, НачалоПериода, КонецПериода");
	
	НастроитьДинамическийСписок();
	
	Элементы.СоздатьДокумент.Доступность = ПравоДоступа("Изменение", Метаданные.Документы.ЗаписьКУДиР);
	
	Заголовок = ТекстЗаголовка();
	
	ОбновитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" И Источник <> ЭтотОбъект
		 И Не ЗначениеЗаполнено(Параметр) Тогда
		
		Элементы.Список.Обновить();
		ОбновитьИтоги();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ОбновитьИтоги();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокумент(Команда)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация", Организация);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ЗаписьКУДиР.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИтоги()
	
	ИтогиДинамическогоСписка = ОбщегоНазначенияБП.ИтогиДинамическогоСписка(Список, "Сумма");
	ИтогоСумма = ИтогиДинамическогоСписка.Сумма;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДинамическийСписок()
	
	НастроитьЗапросСпискаДляРасшифровкиДоходовНаТорговомСборе();
	
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
	Список.Параметры.УстановитьЗначениеПараметра("НачалоПериода", НачалоДня(НачалоПериода));
	Список.Параметры.УстановитьЗначениеПараметра("КонецПериода", КонецДня(КонецПериода));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЗапросСпискаДляРасшифровкиДоходовНаТорговомСборе()
	
	Если Не ЭтоРасшифровкаДоходовНаТорговомСборе(КлючНазначенияИспользования) Тогда
		// Используется стандартный запрос.
		Возврат;
	КонецЕсли;
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(Список.ТекстЗапроса);
	
	ЗапросСхемы = СхемаЗапроса.ПакетЗапросов[0];
	Оператор = ЗапросСхемы.Операторы[0];
	
	// Подменим источник поля Сумма на доходы по деятельности на торговом сборе.
	
	ВыражениеСумма = ОбщегоНазначенияБП.ВыражениеПоляЗапроса(ЗапросСхемы, Оператор, "Сумма");
	ИндексПоляСумма = Оператор.ВыбираемыеПоля.Индекс(ВыражениеСумма);
	
	Оператор.ВыбираемыеПоля.Установить(ИндексПоляСумма,
		Новый ВыражениеСхемыЗапроса("РегистрНакопленияКнигаУчетаДоходовИРасходов.ДоходТорговыйСбор"));
	
	// Подменим отбор ненулевых доходов на отбор доходов по деятельности на торговом сборе
	
	ВыражениеОтборСумма = Оператор.Отбор.Найти("РегистрНакопленияКнигаУчетаДоходовИРасходов.Графа5 <> 0");
	ИндексОтбораСумма = Оператор.Отбор.Индекс(ВыражениеОтборСумма);
	
	Оператор.Отбор.Установить(ИндексОтбораСумма,
		Новый ВыражениеСхемыЗапроса("РегистрНакопленияКнигаУчетаДоходовИРасходов.ДоходТорговыйСбор <> 0"));
	
	Список.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

&НаСервере
Функция ТекстЗаголовка()
	
	Если ЭтоРасшифровкаДоходовНаТорговомСборе(КлючНазначенияИспользования) Тогда
		НазваниеФормы = НСтр("ru = 'Доходы от деятельности на торговом сборе'");
	Иначе
		НазваниеФормы = НСтр("ru = 'Доходы'");
	КонецЕсли;
	
	ПредставлениеПериода
		= БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина);
	
	Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 за %2 %3'"),
			НазваниеФормы,
			ПредставлениеПериода,
			БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Организация));
	Иначе
		Возврат СтрШаблон(НСтр("ru = '%1 за %2'"),
			НазваниеФормы,
			ПредставлениеПериода);
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоРасшифровкаДоходовНаТорговомСборе(КлючНазначенияИспользованияФормы)
	
	Возврат КлючНазначенияИспользованияФормы = "ДоходыТорговыйСбор";
	
КонецФункции

#КонецОбласти