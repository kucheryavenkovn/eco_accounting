#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПланСчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ИмяФормы = "ПланСчетов.Хозрасчетный.ФормаСписка";

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КорреспонденцииСчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ИмяФормы = "РегистрСведений.КорреспонденцииСчетов.ФормаСписка";

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура СправкиРасчетыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыВыполненияКоманды = Новый Структура("ПутьКПодсистеме", "Отчеты.СправкиРасчеты");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения("ОбработчикОткрытияФормы",
		ЭтотОбъект, ПараметрыВыполненияКоманды);

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрыУчетаМалыхПредприятийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыполненияКоманды = Новый Структура("ПутьКПодсистеме",
		"Отчеты.РегистрыБУСубъектовМалогоПредпринимательства");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения("ОбработчикОткрытияФормы",
		ЭтотОбъект, ПараметрыВыполненияКоманды);

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);

КонецПроцедуры

&НаКлиенте
Процедура РегистрыНалоговогоУчетаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыполненияКоманды = Новый Структура("ПутьКПодсистеме",
		"Отчеты.НалогНаПрибыль");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения("ОбработчикОткрытияФормы",
		ЭтотОбъект, ПараметрыВыполненияКоманды);

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ФункциональныеОпции = ФункциональныеОпцииФормы();
	
	Элементы.Письма.Видимость = ФункциональныеОпции.ПоказыватьЭлементы1СОтчетности;
	Элементы.Сверки.Видимость = ФункциональныеОпции.ПоказыватьЭлементы1СОтчетности;
	Элементы.ЕГРЮЛ.Видимость  = ФункциональныеОпции.ПоказыватьЭлементы1СОтчетности;
	
	Элементы.ЗаписиКнигиДоходовИРасходовУСН.Видимость = ФункциональныеОпции.ИспользуетсяУСН;
	Элементы.РегистрацияСуммыУбыткаУСН.Видимость      = ФункциональныеОпции.ИспользуетсяУСН;
	Элементы.АнализУчетаПоУСН.Видимость               = ФункциональныеОпции.ИспользуетсяУСН;
	
	Элементы.РегистрыНалоговогоУчета.Видимость        = ФункциональныеОпции.ИспользуетсяОСНО;
	Элементы.АнализУчетаПоНалогуНаПрибыль.Видимость   = ФункциональныеОпции.ИспользуетсяОСНО;
	
	Элементы.ГруппаНДС.Видимость                      = ФункциональныеОпции.ИспользуетсяНДС;
	Элементы.РегламентныеОперацииНДС.Видимость        = ФункциональныеОпции.ИспользуетсяНДС;
	Элементы.АнализУчетаПоНДС.Видимость               = ФункциональныеОпции.ИспользуетсяНДС;
	Элементы.НаличиеСчетовФактур.Видимость            = ФункциональныеОпции.ИспользуетсяНДС;
	
	Элементы.ГруппаНПД.Видимость                      = ФункциональныеОпции.ИспользуетсяНПД;
	
	Элементы.ГруппаИП.Видимость                       = ФункциональныеОпции.ИспользуетсяНДФЛИП;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФункциональныеОпцииФормы()
	
	ФункциональныеОпции = Новый Структура;
	ФункциональныеОпции.Вставить("ПоказыватьЭлементы1СОтчетности",
		ПолучитьФункциональнуюОпцию("ПоказыватьЭлементы1СОтчетности"));
	ФункциональныеОпции.Вставить("ИспользуетсяУСН",
		ПолучитьФункциональнуюОпцию("ИспользуетсяУСН"));
	ФункциональныеОпции.Вставить("ИспользуетсяНПД",
		ПолучитьФункциональнуюОпцию("ДоступнаИнтеграцияСПлатформойСамозанятые"));
	ФункциональныеОпции.Вставить("ИспользуетсяНДС",
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО") ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП"));
	ФункциональныеОпции.Вставить("ИспользуетсяОСНО",
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО"));
	ФункциональныеОпции.Вставить("ИспользуетсяНДФЛИП",
		ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП"));
		
	
	Возврат ФункциональныеОпции;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикОткрытияФормы(ПараметрыФормы, ПараметрыВыполненияКоманды) Экспорт

	ВариантыОтчетовКлиент.ПоказатьПанельОтчетов(ПараметрыВыполненияКоманды.ПутьКПодсистеме, Неопределено);
	
КонецПроцедуры

#КонецОбласти