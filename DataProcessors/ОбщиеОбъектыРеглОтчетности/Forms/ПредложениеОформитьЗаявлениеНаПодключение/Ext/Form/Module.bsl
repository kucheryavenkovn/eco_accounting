#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоРежимОграниченнойФункциональности = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЭтоРежимОграниченнойФункциональности();
	
	ВариантСКнопками = Ложь;
		
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда 
		Организация = Параметры.Организация;
	КонецЕсли;

	Если Параметры.Свойство("ПоказыватьКнопки") И Параметры.ПоказыватьКнопки Тогда
		
		Если ВариантСКнопками Тогда 
			Элементы.ФлагБольшеНеПоказывать.Видимость = Ложь;
		Иначе
			Элементы.КнопкаБольшеНеПоказывать.Видимость = Ложь;
			Элементы.КнопкаПоказатьПозже.Видимость = Ложь;
			Элементы.ФлагБольшеНеПоказывать.Видимость = Истина;
		КонецЕсли;
		
		Элементы.ГруппаКнопок.Видимость = Истина;
		
		БольшеНеПоказывать 	= ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение");
		
		Если БольшеНеПоказывать = Истина Тогда
			Отказ = Истина;
		КонецЕсли;
		
		ВремяПоследнегоНапоминания 	= ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "ВремяПоследнегоНапоминания");
		Если ВремяПоследнегоНапоминания <> Неопределено 
			И ВремяПоследнегоНапоминания + 4 * 60 * 60 > ТекущаяДатаСеанса() Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоРежимОграниченнойФункциональности Тогда
		
		ИмяКонстанты = "ОписаниеТарифовURL";
		
		Если Метаданные.Константы.Найти(ИмяКонстанты) <> Неопределено Тогда
			СсылкаПерейдитеНаПлатныйТариф = Константы[ИмяКонстанты].Получить();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СистемнаяИнформацияКомпьютера = Новый СистемнаяИнформация();
	Если СистемнаяИнформацияКомпьютера.ТипПлатформы = ТипПлатформы.Windows_x86
		И (НЕ ЗначениеЗаполнено(СистемнаяИнформацияКомпьютера.ОперативнаяПамять)
		ИЛИ СистемнаяИнформацияКомпьютера.ОперативнаяПамять < 4608) Тогда
		
		ИнформацияПрограммыПросмотра = ВРег(СистемнаяИнформацияКомпьютера.ИнформацияПрограммыПросмотра);
		ЭтоМалопроизводительныйКомпьютер = НЕ ЗначениеЗаполнено(ИнформацияПрограммыПросмотра)
			ИЛИ СтрНайти(ИнформацияПрограммыПросмотра, "WIN64") = 0 И СтрНайти(ИнформацияПрограммыПросмотра, "X64") = 0
			И СтрНайти(ИнформацияПрограммыПросмотра, "WOW64") = 0;
		
	Иначе
		ЭтоМалопроизводительныйКомпьютер = Ложь;
	КонецЕсли;
	
	ЕстьПоддержкаВидео = Ложь;
	
	МакетВидеоСКотом(, Истина, ЭтоМалопроизводительныйКомпьютер); // 80х24
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.ФлагБольшеНеПоказывать.Видимость И ФлагБольшеНеПоказывать Тогда 
		СохранитьЗначениеБольшеНеПоказыватьНаСервере(Истина);		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	Попытка
		Ссылка = ДанныеСобытия.href;
	Исключение
		Возврат;
	КонецПопытки;
	
	Если ДанныеСобытия.Href = Неопределено Тогда 
		
		Возврат;
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "&connect") > 0 Тогда 
		
		Закрыть(Истина);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "&call") > 0 Тогда 
		
		ЗаказатьОбратныйЗвонок();
		
	ИначеЕсли СтрНайти(Ссылка, "&ОформлениеЗаявления") <> 0 Тогда		
		
		Закрыть(Истина);
		
	ИначеЕсли ЭтоРежимОграниченнойФункциональности И СтрНайти(Ссылка, "&Тарифы") <> 0 Тогда
		
		Если ЗначениеЗаполнено(СсылкаПерейдитеНаПлатныйТариф) Тогда
			ПоказатьВебСтраницу(СсылкаПерейдитеНаПлатныйТариф);
		Иначе
			ПоказатьВебСтраницу("http://v8.1c.ru/edi/edi_app/1c-otchetnost/rates/index.htm");
		КонецЕсли;
		
	ИначеЕсли Ссылка <> Неопределено Тогда
		
		ПоказатьВебСтраницу(Ссылка);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НажатиеБольшеНеПоказывать(Команда)
		
	СохранитьЗначениеБольшеНеПоказыватьНаСервере(Истина);		
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НажатиеПоказатьПозже(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаказатьОбратныйЗвонокПослеЗакрытияФормыТелефона(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЭтоМалопроизводительныйКомпьютер И ЕстьПоддержкаВидео Тогда
		ОбъектHTML = ЭтотОбъект.Элементы.ПолеHTMLДокумента.Документ;
		Если ТипЗнч(ОбъектHTML) = Тип("COMОбъект") Тогда 
			Если ОбъектHTML.all.videoWindow.currentTime > 0.2 Тогда // 0.2 минимальное значение незапущенного видео в IE для Win10
				ОбъектHTML.all.videoWindow.play();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказатьОбратныйЗвонок() Экспорт
	
	Реквизиты = РеквизитыДляОбратногоЗвонка();
		
	// Обратный звонок
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("НомерТелефона", Реквизиты.Телефон);
	ПараметрыОткрытия.Вставить("Фио", Реквизиты.Имя);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаказатьОбратныйЗвонокПослеЗакрытияФормыТелефона", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ЗаказОбратногоЗвонка", ПараметрыОткрытия, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаСервере
Функция МакетВидеоСКотом(
		Знач ДополнительныйТекстHTML = "",
		Знач ЗадатьHTMLДокумент = Ложь,
		Знач ЭтоМалопроизводительныйКомпьютер = Ложь)
	
	Если ЭтоМалопроизводительныйКомпьютер Тогда
		ЕстьПоддержкаВидео = Ложь;
		
	ИначеЕсли ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ЕстьПоддержкаВидео = Истина;
		
	Иначе
		СистемнаяИнформация = Новый СистемнаяИнформация();
		ЕстьПоддержкаВидео = ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения,
				"8.3.14.0") < 0
			ИЛИ ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.15.1917") > 0
				И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.16.0") < 0
			ИЛИ ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.16.1298") > 0
				И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.17.0") < 0
			ИЛИ ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.17.1286") > 0
				И ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.18.0") < 0
			ИЛИ ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.18.535") >= 0;
	КонецЕсли;
	
	ШаблонHTML = "<!DOCTYPE HTML PUBLIC ""-//W3C//DTD HTML 4.01//EN"">
	|<HTML>
	|	<HEAD>
	|		<META http-equiv=""x-ua-compatible"" content=""IE=9"" >
	|		<META content='text/html; charset=utf-8' http-equiv=Content-Type>
	|
	|
	|<!--[if IE 7]>
	|<style>
	|
	|	html{ overflow: hidden; }
	|
	|</style>
	|<![endif]-->
	|
	|<script type='text/javascript'>
	|
	|	" + ?(ЭтоМалопроизводительныйКомпьютер ИЛИ НЕ ЕстьПоддержкаВидео, "", "
	|	function getPlayer()
	|	{
	|		var player = document.getElementById('videoWindow');
	|		if (typeof player.play === 'undefined') {
	|			player.pause = function() {};
	|			player.play = function() {};
	|			player.paused = true;
	|			player.old = true;
	|			player.currentTime = 0;
	|		} else {
	|			player.old = false;
	|		}
	|			
	|		return player;
	|	}
	|
	|	function playPause()
	|	{
	|		var player = getPlayer();
	|		if (player.paused){
	|			player.play();
	|		} else {
	|			player.pause();
	|		}
	|	}
	|	
	|	function videoCanBePlayed()
	|	{
	|		var player = getPlayer()
	|		return !player.old;
	|	}") + "
	|	
	|	function pause()
	|	{" + ?(ЭтоМалопроизводительныйКомпьютер ИЛИ НЕ ЕстьПоддержкаВидео, "", "
	|		getPlayer().pause();") + "
	|	}
	|	
	|	function playAd()
	|	{
	|		var elem = document.getElementById('playLayer');
	|		elem.style.visibility = 'hidden';
	|		elem.style.display = 'none';
	|		" + ?(ЕстьПоддержкаВидео, "
	|		if (videoCanBePlayed()) {
	|			var elem = document.getElementById('videoWindow');
	|			elem.parentNode.style.visibility = 'visible';
	|			elem.play();
	|		} else {", "") + "
	|			var elem = document.getElementById('videoStub');
	|			elem.parentNode.style.visibility = 'visible';
	|		" + ?(ЕстьПоддержкаВидео, "
	|		}", "") + "
	|	}
	|
	|</script>
	|
	|<style>
	|
	|	" + ?(ЭтоМалопроизводительныйКомпьютер ИЛИ НЕ ЕстьПоддержкаВидео, "", "
	|   #videoWindow {
	|		width: 640px;
	|   }") + "
	|
	|   .centered {
	|   	text-align: center;
	|		position: relative; 
	|   }
	|
	|   .limit_width {
	|   	max-width: 600px;
	|   }
	|
	|	h2 {
    |		font-family: Helvetica, Tahoma, Verdana, Arial;
	|		font-weight: normal;
   	|	} 
	|
	|	A:link { 
	|   	text-decoration: none;
	|   	color: blue;
	|   } 
	|   A:visited { 
	|   	text-decoration: none;
	|   	color: blue;
	|   } 
	|   A:active { 
	|   	text-decoration: none;
	|   	color: blue;
	|   } 
	|   A:hover {
	|   	text-decoration: none;
	|   	color: black;
	|   } 
	|
	|	.header_text { 
	|		font-family: Helvetica, Tahoma, Verdana, Arial;
	|   } 
	|
	|	.button { 
	|		border: 1px solid #bfbfbf;
	|		padding: 10px 10px 7px 5px;
	|		border-radius: 5px; 
	|		font-family: Helvetica, Tahoma, Verdana, Arial;
	|		font-weight: normal;
	|		font-size: 16px;
	|		transition-duration: 0.3s;
	|		display: inline-block;	
	|		outline: medium none;
	|		box-shadow: 0 1px 2px 0 rgba(0,0,0,0.2);
	|	}
	|
	|	.button_w1 { 
	|		background-color: #ffffff;
	|		background-image: linear-gradient(to bottom, rgba(255, 255, 255, 0), rgba(32, 32, 32, 0.05));
	|		background-image: -ms-linear-gradient(bottom, rgba(255, 255, 255, 0) 0%, rgba(32, 32, 32, 0.05) 100%);
	|		filter:  progid:DXImageTransform.Microsoft.gradient(startColorStr='#00ffffff', EndColorStr='#10202020'); /* IE6,IE7 */
	|		-ms-filter: ""progid:DXImageTransform.Microsoft.gradient(startColorStr='#00ffffff', EndColorStr='#10202020')""; /* IE8 */
	|		width: 200px;
	|	}
	|
	|	.button_w2 { 
	|		background-color: #ffdc00;
	|		background-image: linear-gradient(to bottom, rgba(255, 255, 255, 0), rgba(32, 32, 32, 0.05));
	|		background-image: -ms-linear-gradient(bottom, rgba(255, 255, 255, 0) 0%, rgba(32, 32, 32, 0.05) 100%);
	|		filter:  progid:DXImageTransform.Microsoft.gradient(startColorStr='#00ffffff', EndColorStr='#10202020'); /* IE6,IE7 */
	|		-ms-filter: ""progid:DXImageTransform.Microsoft.gradient(startColorStr='#00ffffff', EndColorStr='#10202020')""; /* IE8 */
	|		width: 180px;
	|	}
	|
	|	.button:link { 
	|   	text-decoration: none;
	|   	color: black;
	|   } 
	|   .button:visited { 
	|   	text-decoration: none;
	|   	color: black;
	|   } 
	|   .button:active { 
	|   	text-decoration: none;
	|   	color: black;
	|   } 
	|   .button:hover {
	|   	text-decoration: none;
	|   	color: black;
	|		border: 1px solid #777777;
	|   } 
 	|
	|	.clear_all {
	|		margin: 0;
	|		padding: 0;
	|		border: none;
	|	}	
	|
	|	.spacer {
	|		margin-right: 20px;
	|	}
	|
	|	.spacer_top {
	|		margin-top: 26px;
	|	}
	|
	|	.std_spacer {
	|		margin: 26px 0px 26px 0px;
	|		padding: 0;
	|	}
	|	
	|	.layer {
	|		position: absolute; 
	|	}
	|	
	|	.buttons {
	|		position: absolute; 
	|		margin-top: 395px;
	|		left: 50%;
	|		margin-left: -220px;
	|		min-width: 440px;
	|		z-index: 9; 
	|	}
	|
   	|	.fg { 
	|		width: 640px;
	|		height: 362px;
	|		left: 50%;
	|		margin-left: -320px;
	|		z-index: 2; 
	|
	|		background-image: url('data:image/png;base64,%BASE64COVER%');
	|		background-repeat: no-repeat;
	|		background-size: 640px 362px;
	|
	|	}
	|
	|	.bg { 
	|		z-index: 1; 
	|		left: 50%;
	|		margin-left: -320px;
	|   	visibility: hidden;
	|	}
	|
	|	.stub { 
	|		width: 640px;
	|	}
	|
	|	.img_stub { 
	|		width: 600px;
	|		height: 360px;
	|	}
	|
	|	.bg_stub { 
	|		z-index: 1; 
	|		left: 50%;
	|		margin-left: -320px;
	|	}
	|
	|	.ext_link {
	|		display: inline-block;
	|	}
	|
	|	.ext_link:link {
	|		text-decoration: underline;
	|	}
	|	.ext_link:visited {
	|		text-decoration: underline;
	|	}
	|	.ext_link:active {
	|		text-decoration: underline;
	|	}
	|
	|	.button_play { 
	|		margin-top: 120px;
	|	    opacity: 0.8;
	|	    transition: .3s ease;
	|	}
	|
	|	.button_play:hover { 
	|	    opacity: 1;
	|	}
	|
	|	.button_ico { 
	|	    width: 24px;
	|	    height: 24px;
	|		border: none;
	|		margin: -5px 10px 0 0;	
	|		vertical-align: middle;
	|	}
	|	
	|	.scaled { 
	|	    height: 50px
	|	}
	|
	|   .bordered {
	|		border: 1px solid red;
	|   } 
	|
	|   .bordered2 {
	|		border: 1px solid blue;
	|   } 
	|
	|   .bordered3 {
	|		border: 1px solid green;
	|   } 
	|
	|
	|</style>
	|</HEAD>
	|<BODY class='clear_all'>
	|	<div class='clear_all centered'>
	|		<img class='clear_all scaled spacer_top' src='data:image/png;base64,%BASE64LOGO%'/>
	|		<p id='textAdd' class='clear_all spacer_top header_text'>%ДополнительныйТекст%</p>
	|		<div class='clear_all spacer_top'>
	|			<div id='playLayer' class='layer fg'>
	|				<img id='playButton' class='button_play' onclick='playAd()' src='data:image/png;base64,%BASE64PLAYBTN%'/>
	|			</div>
	|			" + ?(ЭтоМалопроизводительныйКомпьютер ИЛИ НЕ ЕстьПоддержкаВидео, "", "<div class='layer bg'>
	|				<video id='videoWindow' preload onclick='playPause()'> 
	|					<source id='adSource0' type='video/mp4' src='data:video/mp4;base64,%BASE64VIDEO%'>
	|				</video>
	|			</div>
	|			") + "<div class='layer bg stub'>
	|				<img class='clear_all img_stub' id='videoStub' src='data:image/png;base64,%BASE64ANIMATORPIC%'/>
	|				" + ?(ЭтоМалопроизводительныйКомпьютер ИЛИ ЕстьПоддержкаВидео, "<div class='layer bg_stub'>
	|					&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"
						+ "&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"
						+ "&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"
						+ "&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;"
						+ "<a class='ext_link' href='https://www.youtube.com/watch?v=32_Q-z1BAas'>Посмотреть видео</a>
	|				</div>
	|			", "") + "</div>
	|		</div>
	|		<div class='buttons spacer_bottom'>
	|			<a class='button button_w2 spacer' href='&connect' onclick='pause()'>
	|				<img class='button_ico' src='data:image/gif;base64,%BASE64PLUG%'>ПОДКЛЮЧИТЬСЯ</a>	
	|			<a class='button button_w1' href='&call' onclick='pause()'>
	|				<img class='button_ico' src='data:image/gif;base64,%BASE64CALL%'>ОБРАТНЫЙ ЗВОНОК</a>
	|		</div>
	|	</div>
	|</BODY>
	|<script type='text/javascript'>
	|
	|	var elem = document.getElementById('textAdd');
	|	if (elem.innerHTML=='') {
	|		elem.style.visibility = 'hidden';
	|		elem.style.display = 'none';
	|	}
	|	
	|	" + ?(ЕстьПоддержкаВидео, "
	|	if (!videoCanBePlayed()) {", "") + "
	|	" + ?(ЭтоМалопроизводительныйКомпьютер ИЛИ ЕстьПоддержкаВидео, "
	|		var elem = document.getElementById('playLayer');
	|		elem.style.visibility = 'hidden';
	|		elem.style.display = 'none';
	|		var elem = document.getElementById('videoStub');
	|		elem.parentNode.style.visibility = 'visible';", "") + "
	|	" + ?(ЕстьПоддержкаВидео, "
	|	}", "") + "
	|
	|</script>
	|</HTML>";
	
	Если НЕ ЭтоМалопроизводительныйКомпьютер Тогда
		// Видео mp42 (mp42/isom), AVC+AAC, 640 x 352 в последнем варианте
		Макет = ПолучитьОбщийМакет("РекламныйЭлементВидео");
		ДанныеВидеоBase64 = Макет.ПолучитьТекст();
	КонецЕсли;
	
	// Картинка с котом, под кнопкой play
	Макет = ПолучитьОбщийМакет("РекламныйЭлементОбложка");
	ДанныеКартинкиСКотомBase64 = Макет.ПолучитьТекст();
	Если СтрЧислоСтрок(ДанныеКартинкиСКотомBase64) > 1 Тогда
		ДанныеКартинкиСКотомBase64 = СтрПолучитьСтроку(ДанныеКартинкиСКотомBase64, 1);
	КонецЕсли;
	
	// Картинка с котом и сноской для ссылки
	Если ЭтоМалопроизводительныйКомпьютер ИЛИ ЕстьПоддержкаВидео Тогда
		ДанныеКартинкиСКотом2Base64 = ДанныеКартинкиСКотомBase64;
	Иначе
		Макет = ПолучитьОбщийМакет("РекламныйЭлементСтарыйБраузер");
		ДанныеКартинкиСКотом2Base64 = Макет.ПолучитьТекст();
	КонецЕсли;
	
	// Картинка верхнего логотипа
	Макет = ПолучитьОбщийМакет("РекламныйЭлементЛого");
	ДанныеЛогоBase64 = Макет.ПолучитьТекст();
	
	// Картинка кнопки play 180x127
	Макет = ПолучитьОбщийМакет("РекламныйЭлементКнопкаВоспроизведение");
	ДанныеКнопкиPlayBase64 = Макет.ПолучитьТекст();
	
	// Картинка иконки "звонок"
	Макет = ПолучитьОбщийМакет("РекламныйЭлементИконкаCall");
	ИконкаCallBase64 = Макет.ПолучитьТекст();
	
	// Картинка иконки "подключиться"
	Макет = ПолучитьОбщийМакет("РекламныйЭлементИконкаPlug");
	ИконкаPlugBase64 = Макет.ПолучитьТекст();
	
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%ДополнительныйТекст%", ДополнительныйТекстHTML);
	Если НЕ ЭтоМалопроизводительныйКомпьютер Тогда
		ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64VIDEO%", ДанныеВидеоBase64);
	КонецЕсли;
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64COVER%", ДанныеКартинкиСКотомBase64);
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64ANIMATORPIC%", ДанныеКартинкиСКотом2Base64);
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64LOGO%", ДанныеЛогоBase64);
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64PLAYBTN%", ДанныеКнопкиPlayBase64);
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64CALL%", ИконкаCallBase64);
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "%BASE64PLUG%", ИконкаPlugBase64);
	
	Если ЗадатьHTMLДокумент Тогда
		HTMLДокумент = ШаблонHTML;
	КонецЕсли;
	
	Возврат ШаблонHTML;
	
КонецФункции

// Приводит к формату 89012345678 или возвращает пустую строку
&НаСервере
Функция НормализованныйНомерТелефона(Знач Телефон) 
	
	Телефон = СокрЛП(Телефон);
	Телефон = СтрЗаменить(Телефон, " ", "");
	Телефон = СтрЗаменить(Телефон, "(", "");
	Телефон = СтрЗаменить(Телефон, ")", "");
	
	Если СтрНачинаетсяС(Телефон, "7") Тогда 
		Телефон = "8" + Сред(Телефон, 2);
	ИначеЕсли СтрНачинаетсяС(Телефон, "+7") Тогда 
		Телефон = "8" + Сред(Телефон, 3);
	КонецЕсли;
	Если СтрДлина(Телефон) < 11 Тогда
		Телефон = "";
	КонецЕсли;
	
	Возврат Телефон;
	
КонецФункции

&НаСервере
Функция ТелефонИзКонтактнойИнформацииПоВиду(ЗаписиКонтактнойИнформации, ВидКонтактнойИнформации)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Вид", ВидКонтактнойИнформации); 
	
	СтрокиСТелефоном = ЗаписиКонтактнойИнформации.НайтиСтроки(ПараметрыОтбора);	
	Если СтрокиСТелефоном.Количество() > 0 Тогда 
		НомерТелефона = НормализованныйНомерТелефона(СтрокиСТелефоном[0].Представление);
		Возврат НомерТелефона;
	КонецЕсли;
	
	Возврат "";
				
КонецФункции

&НаСервере
Функция РеквизитыДляОбратногоЗвонка()
	
	Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	Телефон = "";
	
	Результат = Новый Структура("Имя, Телефон", Пользователь, "");	
	
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		
		НаборРеквизитов = Новый Массив;
		НаборРеквизитов.Добавить("ФизическоеЛицо");
		НаборРеквизитов.Добавить("Наименование");
			
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Пользователь, НаборРеквизитов);		
		Физлицо = Реквизиты.ФизическоеЛицо;
				
		Если Реквизиты.Наименование = Пользователи.ПолноеИмяНеуказанногоПользователя() Тогда 
			
			Возврат Результат;
			
		ИначеЕсли ЗначениеЗаполнено(Физлицо) Тогда 
			
			Данные = ФизическиеЛицаЗарплатаКадры.ДанныеФизическогоЛицаДляРегламентированнойОтчетности(Физлицо);			
			Пользователь = СтрШаблон("%1 %2 %3", СокрЛП(Данные.Фамилия), СокрЛП(Данные.Имя), СокрЛП(Данные.Отчество));
			
			Если ЗначениеЗаполнено(Пользователь) Тогда 
				Результат.Вставить("Имя", Пользователь);
			КонецЕсли;
						
			ОтборФизлиц = Новый Массив;
			ОтборФизлиц.Добавить(Физлицо);
			
			ТребуемыеВиды = Новый Массив;
			ТребуемыеВиды.Добавить(ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица"));
			ТребуемыеВиды.Добавить(ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица"));
			ЗаписиКонтактнойИнформации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(ОтборФизлиц,, ТребуемыеВиды);
						
			Если ЗаписиКонтактнойИнформации.Количество() > 0 Тогда
				
				НомерТелефона = ТелефонИзКонтактнойИнформацииПоВиду(ЗаписиКонтактнойИнформации, ТребуемыеВиды[0]);
				Если ЗначениеЗаполнено(НомерТелефона) Тогда 
					Результат.Вставить("Телефон", НомерТелефона);
					Возврат Результат;
				КонецЕсли;
				
				НомерТелефона = ТелефонИзКонтактнойИнформацииПоВиду(ЗаписиКонтактнойИнформации, ТребуемыеВиды[1]);
				Если ЗначениеЗаполнено(НомерТелефона) Тогда 
					Результат.Вставить("Телефон", НомерТелефона);
					Возврат Результат;
				КонецЕсли;	
								
			КонецЕсли;
			
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьВебСтраницу(АдресСтраницы) 
	
	Попытка
	
		ПерейтиПоНавигационнойСсылке(АдресСтраницы);

	Исключение
		
		ПоказатьПредупреждение(, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьЗначениеБольшеНеПоказыватьНаСервере(БольшеНеПоказывать)
	
	ХранилищеОбщихНастроек.Сохранить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение", БольшеНеПоказывать);
	
КонецПроцедуры

#КонецОбласти




