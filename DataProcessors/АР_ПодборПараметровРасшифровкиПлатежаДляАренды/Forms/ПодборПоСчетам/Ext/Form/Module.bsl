
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ЗаполнитьДанныеПоСчетам(АдресВременногоХранилища, НазначениеПлатежа)
			
	РасшифровкаПлатежа = Объект.РасшифровкаПлатежа.Выгрузить();
	РасшифровкаПлатежа.Очистить();
	
	НазначениеПлатежа = "Оплата по счетам";
	Для Каждого СтрокаТЧ Из Объект.СчетаНаАренду Цикл
		
		ПараметрыНачисленияПени = АР_ПроцедурыНачисления.ПолучитьПараметрыНачисленияПени(СтрокаТЧ.ДоговорКонтрагента, СтрокаТЧ.СчетНаАренду.Дата);
		ОбособленныйУчетПени = ?(ПараметрыНачисленияПени = Неопределено, Ложь, ПараметрыНачисленияПени.СпособПогашенияПени = ПредопределенноеЗначение("Перечисление.АР_СпособыПогашенияПени.ВестиОбособленныйУчетПени"));
		
		ОстатокСуммыСписания = СтрокаТЧ.Сумма;
		Для Каждого СтрокаСчета Из СтрокаТЧ.СчетНаАренду.Состав  Цикл
			
			СуммаВзаиморасчетов = СтрокаСчета.Сумма + ?(СтрокаТЧ.СчетНаАренду.СуммаВключаетНДС, 0, СтрокаСчета.СуммаНДС);
			СуммаСписания = Мин(ОстатокСуммыСписания, СуммаВзаиморасчетов);
			СтрокаПлатеж = РасшифровкаПлатежа.Добавить();
			СтрокаПлатеж.ДоговорКонтрагента = СтрокаТЧ.ДоговорКонтрагента;
			СтрокаПлатеж.СуммаПлатежа = СуммаСписания;
			
			СтрокаПлатеж.АР_ОбъектАренды = СтрокаСчета.ОбъектАренды;
			СтрокаПлатеж.АР_УслугаАренды = СтрокаСчета.Номенклатура;
			СтрокаПлатеж.АР_ПериодАренды = СтрокаСчета.ПериодАренды;
			СтрокаПлатеж.АР_СчетНаАренду = СтрокаТЧ.СчетНаАренду;
			СтрокаПлатеж.АР_Депозит = ЗначениеЗаполнено(СтрокаТЧ.СчетНаАренду) И СтрокаТЧ.СчетНаАренду.ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаДепозита;
			СтрокаПлатеж.АР_Пени = ЗначениеЗаполнено(СтрокаТЧ.СчетНаАренду) И СтрокаТЧ.СчетНаАренду.ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаПени И ОбособленныйУчетПени;
			СтрокаПлатеж.СтавкаНДС = СтрокаСчета.СтавкаНДС;
			
			Если СтрокаТЧ.ДоговорКонтрагента.АР_ВестиВзаиморасчетыПоПериодам И Не ЗначениеЗаполнено(СтрокаПлатеж.АР_ПериодАренды) Тогда
				СтрокаПлатеж.АР_ПериодАренды = АР_ОбщиеПроцедуры.ОпределитьНачалоПериодаОплатыПоДоговору(СтрокаТЧ.СчетНаАренду.НачалоПериода, СтрокаТЧ.ДоговорКонтрагента);
			КонецЕсли;
			
			ОстатокСуммыСписания = ОстатокСуммыСписания - СуммаСписания;
			Если ОстатокСуммыСписания <= 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		Если ОстатокСуммыСписания > 0 Тогда
			
			СтрокаПлатеж = РасшифровкаПлатежа.Добавить();
			СтрокаПлатеж.ДоговорКонтрагента = СтрокаТЧ.ДоговорКонтрагента;
			СтрокаПлатеж.АР_СчетНаАренду = СтрокаТЧ.СчетНаАренду;
			СтрокаПлатеж.СуммаПлатежа = ОстатокСуммыСписания;
			СтрокаПлатеж.АР_Депозит = ЗначениеЗаполнено(СтрокаТЧ.СчетНаАренду) И СтрокаТЧ.СчетНаАренду.ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаДепозита;
			СтрокаПлатеж.АР_Пени = ЗначениеЗаполнено(СтрокаТЧ.СчетНаАренду) И СтрокаТЧ.СчетНаАренду.ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаПени И ОбособленныйУчетПени;
			
		КонецЕсли;
		НазначениеПлатежа = НазначениеПлатежа + " " + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаТЧ.СчетНаАренду.Номер, Истина, Истина) + ",";
	КонецЦикла;	
	Если Прав(НазначениеПлатежа, 1) = "," Тогда
		НазначениеПлатежа = Лев(НазначениеПлатежа, СтрДлина(НазначениеПлатежа) - 1);
	КонецЕсли;
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(РасшифровкаПлатежа, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоОтборуНаСервере()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АР_СчетНаАренду.Ссылка КАК СчетНаАренду,
	|	АР_СчетНаАренду.СуммаДокумента КАК Сумма,
	|	АР_СчетНаАренду.ДоговорКонтрагента
	|ИЗ
	|	Документ.АР_СчетНаАренду КАК АР_СчетНаАренду
	|ГДЕ
	|	АР_СчетНаАренду.Контрагент = &Контрагент
	|	И ВЫБОР
	|			КОГДА &ДатаКон = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА АР_СчетНаАренду.ОкончаниеПериода >= &ДатаНач
	|			ИНАЧЕ АР_СчетНаАренду.НачалоПериода <= &ДатаКон
	|					И АР_СчетНаАренду.ОкончаниеПериода >= &ДатаНач
	|		КОНЕЦ
	|	И НЕ АР_СчетНаАренду.ПометкаУдаления
	|	И (АР_СчетНаАренду.ДоговорКонтрагента = &ДоговорКонтрагента
	|			ИЛИ &ДоговорКонтрагента = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|	И АР_СчетНаАренду.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ДатаКон", ?(ЗначениеЗаполнено(ОкончаниеПериода), КонецДня(ОкончаниеПериода), ОкончаниеПериода));
	Запрос.УстановитьПараметр("ДатаНач", НачалоПериода);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СчетаНаАренду.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;   
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		НачалоПериода = Результат.ДатаНачала;
		ОкончаниеПериода = Результат.ДатаОкончания;	
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = "Подбор по контрагенту " + Контрагент;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Не ЭтаФорма.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	АдресВременногоХранилища = "";
	НазначениеПлатежа = "";
	ЗаполнитьДанныеПоСчетам(АдресВременногоХранилища, НазначениеПлатежа);
	ЭтаФорма.Закрыть(Новый Структура("АдресВременногоХранилища, НазначениеПлатежа", АдресВременногоХранилища, НазначениеПлатежа));	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтотОбъект);
	АР_ОбщиеПроцедурыКлиент.ВыборПериодаПоДоговору(НачалоПериода, ОкончаниеПериода, Неопределено, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоОтбору(Команда)
	
	ДобавитьПоОтборуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	Объект.СчетаНаАренду.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Контрагент") Тогда
		ЭтаФорма.Контрагент = Параметры.Контрагент;	
	КонецЕсли;
	Если Параметры.Свойство("Организация") Тогда
		ЭтаФорма.Организация = Параметры.Организация;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаНаАрендуСчетНаАрендуПриИзменении(Элемент)
	
	ТекДанные = Элементы.СчетаНаАренду.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекДанные.СчетНаАренду) Тогда
		РеквизитыСчета = АР_ОбщиеПроцедуры.ЗначенияРеквизитовОбъекта(ТекДанные.СчетНаАренду,"ДоговорКонтрагента, СуммаДокумента");
		ТекДанные.ДоговорКонтрагента = РеквизитыСчета.ДоговорКонтрагента; 	
		ТекДанные.Сумма = РеквизитыСчета.СуммаДокумента; 	
	Иначе
		Объект.СчетаНаАренду.Удалить(Объект.СчетаНаАренду.Индекс(ТекДанные));
	КонецЕсли;
	
КонецПроцедуры
