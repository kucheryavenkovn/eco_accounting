#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПроверитьЗаписиВФоне(Параметры, АдресРезультата) Экспорт

	Результат = ПроверитьЗаписи(Параметры.Исправлять, Ложь);
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);

КонецПроцедуры

Процедура ИсправитьОтложенно(Параметры) Экспорт
	
	Результат = ПроверитьЗаписи(Истина, Истина);
	Параметры.ОбработкаЗавершена = Результат.ДокументыСОшибками.Количество() = 0;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверитьЗаписи(Исправлять, Отложенно)
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ДокументыСОшибками", Новый Массив);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СчетаБУ.Ссылка КАК Счет
		|ПОМЕСТИТЬ СчетаБезПодразделений
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК СчетаБУ
		|ГДЕ
		|	НЕ СчетаБУ.УчетПоПодразделениям
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетаБУ.Ссылка КАК Счет
		|ПОМЕСТИТЬ СчетаБезВалют
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК СчетаБУ
		|ГДЕ
		|	НЕ СчетаБУ.Валютный
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Регистраторы.Ссылка КАК Ссылка,
		|	МАКСИМУМ(Регистраторы.Дата) КАК Дата
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|		Проводки.Период КАК Дата,
		|		Проводки.Регистратор КАК Ссылка
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный КАК Проводки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаБезПодразделений КАК СчетаБезПодразделений
		|			ПО Проводки.СчетДт = СчетаБезПодразделений.Счет
		|				И (Проводки.ПодразделениеДт ЕСТЬ НЕ NULL )
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|		Проводки.Период,
		|		Проводки.Регистратор
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный КАК Проводки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаБезПодразделений КАК СчетаБезПодразделений
		|			ПО Проводки.СчетКт = СчетаБезПодразделений.Счет
		|				И (Проводки.ПодразделениеКт ЕСТЬ НЕ NULL )
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|		Проводки.Период,
		|		Проводки.Регистратор
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный КАК Проводки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаБезВалют КАК СчетаБезВалют
		|			ПО Проводки.СчетДт = СчетаБезВалют.Счет
		|				И (Проводки.ВалютаДт ЕСТЬ НЕ NULL )
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|		Проводки.Период,
		|		Проводки.Регистратор
		|	ИЗ
		|		РегистрБухгалтерии.Хозрасчетный КАК Проводки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаБезВалют КАК СчетаБезВалют
		|			ПО Проводки.СчетКт = СчетаБезВалют.Счет
		|				И (Проводки.ВалютаКт ЕСТЬ НЕ NULL )) КАК Регистраторы
		|
		|СГРУППИРОВАТЬ ПО
		|	Регистраторы.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата УБЫВ,
		|	Ссылка УБЫВ
		|;
		|ВЫБРАТЬ
		|СчетаБезПодразделений.Счет КАК Счет
		|ИЗ
		|СчетаБезПодразделений КАК СчетаБезПодразделений
		|;
		|ВЫБРАТЬ
		|СчетаБезВалют.Счет КАК Счет
		|ИЗ
		|СчетаБезВалют КАК СчетаБезВалют");
	Если Не Отложенно Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000", "ВЫБРАТЬ РАЗЛИЧНЫЕ");
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	ОсновнойРезультат = Результат[2];
	Если ОсновнойРезультат.Пустой() Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	СчетаБезПодразделений = Результат[3].Выгрузить().ВыгрузитьКолонку("Счет");
	СчетаБезВалют = Результат[4].Выгрузить().ВыгрузитьКолонку("Счет");
	
	Выборка = ОсновнойРезультат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		РезультатПроверки.ДокументыСОшибками.Добавить(Выборка.Ссылка);
		Если Не Исправлять Тогда
			Продолжить;
		КонецЕсли;
		
		Набор = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(Выборка.Ссылка);
		Набор.Прочитать();
		
		Для каждого Проводка Из Набор Цикл
			Если СчетаБезПодразделений.Найти(Проводка.СчетДт) <> Неопределено Тогда
				Проводка.ПодразделениеДт = Null;
			КонецЕсли;
			Если СчетаБезПодразделений.Найти(Проводка.СчетКт) <> Неопределено Тогда
				Проводка.ПодразделениеКт = Null;
			КонецЕсли;
			Если СчетаБезВалют.Найти(Проводка.СчетДт) <> Неопределено Тогда
				Проводка.ВалютаДт = Null;
			КонецЕсли;
			Если СчетаБезВалют.Найти(Проводка.СчетКт) <> Неопределено Тогда
				Проводка.ВалютаКт = Null;
			КонецЕсли;
		КонецЦикла;
		
		Набор.ОбменДанными.Загрузка = Истина; 
		Набор.Записать();
	
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#КонецЕсли