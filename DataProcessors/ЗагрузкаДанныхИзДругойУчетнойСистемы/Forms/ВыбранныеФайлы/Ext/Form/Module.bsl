#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.ВыбранныеФайлы) = Тип("Массив") Тогда
		Сч = 1;
		Для Каждого ДанныеФайла Из Параметры.ВыбранныеФайлы Цикл
			Строка = Файлы.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, ДанныеФайла);
			Строка.Номер = Сч;
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлы

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр = ФильтрДляВыбораФайлов();
	ДиалогВыбораФайла.Заголовок = 
		НСтр("ru = 'Выберите файлы'");
	
	ПараметрыЗагрузки.Диалог = ДиалогВыбораФайла;
	
	ОповещениеЗавершенияВыбораФайла = Новый ОписаниеОповещения("ОбработкаВыбораФайла", ЭтотОбъект);
	
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОповещениеЗавершенияВыбораФайла, ПараметрыЗагрузки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодтвердитьВыбор(Команда)
	
	ЗакрыватьПриВыборе = Истина;
	ВыбранныеФайлы = Новый Массив;
	Для Каждого Строка Из Файлы Цикл
		СвойстваФайла = ЗагрузкаДанныхИзДругихУчетныхСистемКлиентСервер.НовыеСвойстваФайла();
		ЗаполнитьЗначенияСвойств(СвойстваФайла, Строка);
		ВыбранныеФайлы.Добавить(СвойстваФайла);
	КонецЦикла;
	ОповеститьОВыборе(ВыбранныеФайлы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаВыбораФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Файл Из Результат Цикл
		ОписаниеФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(Файл.Имя);
		Поиск = Новый Структура("Имя", ОписаниеФайла.Имя);
		СуществующиеСтроки = Файлы.НайтиСтроки(Поиск);
		Если ЗначениеЗаполнено(СуществующиеСтроки) Тогда
			Продолжить;
		КонецЕсли;
		Строка = Файлы.Добавить();
		Строка.Имя = ОписаниеФайла.Имя;
		Строка.Хранение = Файл.Хранение;
		Строка.Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ОписаниеФайла.Расширение);
		Строка.Номер = Файлы.Количество();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ФильтрДляВыбораФайлов()
	
	ПоддерживаемыеРасширения = ЗагрузкаДанныхИзДругихУчетныхСистемКлиентСервер.ПоддерживаемыеРасширения();
	СтрокиФильтра = Новый Массив;
	ШаблонФильтра = "*.%1";
	Для Каждого Расширение Из ПоддерживаемыеРасширения Цикл
		СтрокиФильтра.Добавить(СтрШаблон(ШаблонФильтра, Расширение));
	КонецЦикла;
	ШаблонТекстаФильтра = НСтр("ru = 'Загружаемые данные|%1'");
	Возврат СтрШаблон(ШаблонТекстаФильтра, СтрСоединить(СтрокиФильтра, ";"));
	
КонецФункции

#КонецОбласти













