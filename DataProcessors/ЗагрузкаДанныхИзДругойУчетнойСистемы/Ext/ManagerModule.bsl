#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПрочитатьДанныеДляЗагрузки(Параметры, АдресРезультата) Экспорт
	
	ДанныеЗагрузки = ПрочитатьВыбранныеФайлы(Параметры);
	
	ПоместитьВоВременноеХранилище(ДанныеЗагрузки, АдресРезультата);
	
КонецПроцедуры

Процедура ЗагрузитьДанные(ДанныеЗагрузки, АдресРезультата) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибки", Неопределено);
	Результат.Вставить("Формат", "");
	
	Если ДанныеЗагрузки.ФорматЗагружаемыхДанных = ИмяФорматаТекстовогоФайла() Тогда
		Результат.Формат = ДанныеЗагрузки.ФорматЗагружаемыхДанных;
		ЗагрузитьДанныеИзТекстовогоФайла(ДанныеЗагрузки, Результат, АдресРезультата);
	ИначеЕсли ДанныеЗагрузки.ФорматЗагружаемыхДанных = ИмяФорматаЭлектронныхТаблиц() Тогда
		Результат.Формат = ДанныеЗагрузки.ФорматЗагружаемыхДанных;
		ЗагрузитьДанныеИзЭлектронныхТаблиц(ДанныеЗагрузки, Результат, АдресРезультата);
	Иначе
		Ошибки = Неопределено;
		ТекстОшибки = НСтр("ru = 'Не определен формат загружаемых файлов'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.Ошибки, "", ТекстОшибки, "");
		ЗаписьЖурналаРегистрации(
			ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

#Область ЧтениеДанных

Функция ПрочитатьВыбранныеФайлы(Свойства)
	
	РезультатВыполнения = НовыйРезультатЧтенияФайлов();
	
	Если ТипЗнч(Свойства.ДанныеФайлов) <> Тип("Массив") Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
			РезультатВыполнения.Ошибки, , НСтр("ru = 'Не удалось прочитать файлы'"), "");
		Возврат РезультатВыполнения;
	КонецЕсли;
	
	ПоддерживаемыеРасширения = ЗагрузкаДанныхИзДругихУчетныхСистемКлиентСервер.ПоддерживаемыеРасширения();
	ЧитаемыеФайлы = НовоеОписаниеФайловКЧтению();
	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог();
	Для Каждого СвойстваФайла Из Свойства.ДанныеФайлов Цикл
		Если ПоддерживаемыеРасширения.Найти(СвойстваФайла.Расширение) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(СвойстваФайла.Расширение);
		СвойстваФайла.ДвоичныеДанные.Записать(ИмяВременногоФайла);
		Если ЗагрузкаДанныхИзДругихУчетныхСистемКлиентСервер.ЭтоZipАрхив(СвойстваФайла.Расширение) Тогда
			ЧтениеАрхива = Новый ЧтениеZipФайла(ИмяВременногоФайла);
			ЧтениеАрхива.ИзвлечьВсе(ВременныйКаталог);
			ЧтениеАрхива.Закрыть();
			НайденныеФайлы = НайтиФайлы(ВременныйКаталог, ПолучитьМаскуВсеФайлы());
			Для Каждого Файл Из НайденныеФайлы Цикл
				Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(Файл.Расширение);
				Если ПоддерживаемыеРасширения.Найти(Расширение) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ДобавитьФайлКПрочтению(Файл.ПолноеИмя, Расширение, ЧитаемыеФайлы);
			КонецЦикла;
		Иначе
			ДобавитьФайлКПрочтению(ИмяВременногоФайла, СвойстваФайла.Расширение, ЧитаемыеФайлы);
		КонецЕсли;
	КонецЦикла;
	
	ОтборТекстовыхФайлов = Новый Структура(
		"Расширение", ЗагрузкаДанныхИзДругихУчетныхСистемКлиентСервер.РасширениеТекстовогоФайла());
	ТекстовыеФайлы = ЧитаемыеФайлы.НайтиСтроки(ОтборТекстовыхФайлов);
	Если ЗначениеЗаполнено(ТекстовыеФайлы) Тогда
		РезультатВыполнения.ФорматЗагружаемыхДанных = ИмяФорматаТекстовогоФайла();
		ЗагружаемыеДанные = ТекстовыеФайлы;
	Иначе
		РезультатВыполнения.ФорматЗагружаемыхДанных = ИмяФорматаЭлектронныхТаблиц();
		ЗагружаемыеДанные = ЧитаемыеФайлы;
	КонецЕсли;
	
	ПрочитатьЗагружаемыеДанные(ЗагружаемыеДанные, РезультатВыполнения, Свойства.Организация);
	
	Для Каждого СвойстваФайла Из ЧитаемыеФайлы Цикл
		ФайловаяСистема.УдалитьВременныйФайл(СвойстваФайла.Путь);
	КонецЦикла;
	ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция НовыйРезультатЧтенияФайлов()
	
	Результат = Новый Структура;
	// Свойство "Ошибки" заполняется при добавлении новых ошибок,
	// см. ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю,
	// если ошибок нет, то должно иметь значение Неопределено
	Результат.Вставить("Ошибки", Неопределено);
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить(ИмяКонтрагенты(), НовыеРеквизитыКонтрагента());
	Результат.Вставить(ИмяНоменклатура(), НоваяТаблицаНоменклатуры());
	Результат.Вставить("ФорматЗагружаемыхДанных", "");
	Возврат Результат;
	
КонецФункции

Функция НовоеОписаниеФайловКЧтению()
	
	Описание = Новый ТаблицаЗначений;
	Описание.Колонки.Добавить("Путь");
	Описание.Колонки.Добавить("Расширение");
	Возврат Описание;
	
КонецФункции

Процедура ДобавитьФайлКПрочтению(Путь, Расширение, ФайлыКЧтению)
	
	Строка = ФайлыКЧтению.Добавить();
	Строка.Путь = Путь;
	Строка.Расширение = Расширение;
	
КонецПроцедуры

Процедура ПрочитатьЗагружаемыеДанные(Данные, РезультатВыполнения, Организация)
	
	ОписаниеФайлов = НовоеОписаниеЗагружаемыхФайлов();
	ЗагружаемыеДанные = НоваяТаблицаЗагружаемыхДанных();
	ЭтоТекстовыеФайлы = РезультатВыполнения.ФорматЗагружаемыхДанных = ИмяФорматаТекстовогоФайла();
	Если ЭтоТекстовыеФайлы Тогда
		ЗаполнитьОписаниеИменамиФайловДляЗагрузкиИзТекстовыхФайлов(ОписаниеФайлов);
		ДобавитьВОписаниеТекстовыеФайлы(Данные, ОписаниеФайлов);
	Иначе
		ЗаполнитьОписаниеИменамиФайловДляЗагрузкиИзЭлектронныхТаблиц(ОписаниеФайлов);
		ДобавитьВОписаниеФайлыЭлектронныхТаблиц(Данные, ОписаниеФайлов);
	КонецЕсли;
	
	Для Каждого СтрокаОписания Из ОписаниеФайлов Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаОписания.ПутьКФайлу) Тогда
			Продолжить;
		КонецЕсли;
		
		ПрочитанныеДанные = ?(ЭтоТекстовыеФайлы,
			ПрочитатьДанныеИзТекстовогоФайла(СтрокаОписания, РезультатВыполнения.Ошибки, ЗагружаемыеДанные, Организация),
			ПрочитатьДанныеИзЭлектроннойТаблицы(СтрокаОписания, РезультатВыполнения.Ошибки, ЗагружаемыеДанные));
		
		Если ПрочитанныеДанные = Неопределено Тогда
			Возврат;
		ИначеЕсли ПрочитанныеДанные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если РезультатВыполнения.Свойство(СтрокаОписания.ИмяФайла) Тогда
			Для Каждого ПрочитаннаяСтрока Из ПрочитанныеДанные Цикл
				НовыеДанные = РезультатВыполнения[СтрокаОписания.ИмяФайла].Добавить();
				ЗаполнитьЗначенияСвойств(НовыеДанные, ПрочитаннаяСтрока);
			КонецЦикла;
		Иначе
			РезультатВыполнения.Вставить(СтрокаОписания.ИмяФайла, ПрочитанныеДанные);
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатВыполнения.Вставить("ЗагружаемыеДанные", ЗагружаемыеДанные);
	
КонецПроцедуры

Функция НовоеОписаниеЗагружаемыхФайлов()
	
	ОписаниеЗагружаемыхФайлов = Новый ТаблицаЗначений;
	ОписаниеЗагружаемыхФайлов.Колонки.Добавить("ИмяФайла");
	ОписаниеЗагружаемыхФайлов.Колонки.Добавить("Заголовок"); // Заголовок находится непосредственно в тексте файла
	ОписаниеЗагружаемыхФайлов.Колонки.Добавить("ПутьКФайлу");
	ОписаниеЗагружаемыхФайлов.Колонки.Добавить("Кодировка");
	Возврат ОписаниеЗагружаемыхФайлов;
	
КонецФункции

Функция НоваяТаблицаЗагружаемыхДанных()

	ЗагружаемыеДанные = Новый ТаблицаЗначений;
	
	Обработка = Метаданные.Обработки.ЗагрузкаДанныхИзДругойУчетнойСистемы;
	КолонкиТаблицы = Обработка.ТабличныеЧасти.ЗагружаемыеДанные.Реквизиты;
	
	Для Каждого Колонка Из КолонкиТаблицы Цикл
	
		ЗагружаемыеДанные.Колонки.Добавить(Колонка.Имя, Колонка.Тип);
	
	КонецЦикла;
	
	ЗагружаемыеДанные.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)));
	
	Возврат ЗагружаемыеДанные;
	
КонецФункции

Процедура ЗаполнитьОписаниеИменамиФайловДляЗагрузкиИзТекстовыхФайлов(Описание)
	
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяРеквизитыОрганизации(), "реквизиты организации");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяРеквизитыОрганизации(), "реквизиты ип");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяКонтрагенты(), "контрагенты");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяИсходящиеДокументы(), "исходящие документы");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяДокументыДвиженияДенег(), "деньги");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяВходящиеДокументы(), "входящие документы");
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеИменамиФайловДляЗагрузкиИзЭлектронныхТаблиц(Описание)
	
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяКонтрагенты(), "Название контрагента");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяНоменклатура(), "Наименование товара");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяКассоваяКнига(), "КАССОВАЯ   КНИГА");
	ДобавитьСтрокуВОписаниеФайлов(Описание, ИмяДокументы(), "Тип документа");
	
КонецПроцедуры

Процедура ДобавитьВОписаниеТекстовыеФайлы(ТекстовыеФайлы, ОписаниеФайлов, Путь = Неопределено, Кодировка = Неопределено, Уровень = 0)
	
	Для Каждого СвойстваФайла Из ТекстовыеФайлы Цикл
		Если ЗначениеЗаполнено(Путь) И СвойстваФайла.Путь <> Путь Тогда
			Продолжить;
		КонецЕсли;
		ЧтениеТекста = Новый ЧтениеТекста(СвойстваФайла.Путь, Кодировка);
		ПерваяСтрока = ЧтениеТекста.ПрочитатьСтроку();
		Подстроки = СтрРазделить(ПерваяСтрока, ";", Истина);
		Заголовок = Подстроки[0];
		НайденнаяСтрокаВОписании = ОписаниеФайлов.Найти(НРег(Заголовок), "Заголовок");
		Если НайденнаяСтрокаВОписании <> Неопределено Тогда
			НайденнаяСтрокаВОписании.ПутьКФайлу = СвойстваФайла.Путь;
			НайденнаяСтрокаВОписании.Кодировка = Кодировка;
		ИначеЕсли Уровень = 0 Тогда
			Для Каждого Кодировка Из КодировкаТекста Цикл
				ДобавитьВОписаниеТекстовыеФайлы(ТекстовыеФайлы, ОписаниеФайлов, СвойстваФайла.Путь, Кодировка, Уровень + 1);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВОписаниеФайлыЭлектронныхТаблиц(Файлы, ОписаниеФайлов)
	
	// Заголовок файла обычно находится где-то вначале, нет смысла искать заголовок по всему документу
	МаксимальныйНомерСтроки = МаксимальныйНомерСтрокиДляПоискаЗаголовка();
	МаксимальныйНомерКолонки = МаксимальныйНомерКолонкиДляПоискаЗаголовка();
	
	Для Каждого СтрокаФайла Из Файлы Цикл
		Если Не ЭтоПоддерживаемаяЭлектроннаяТаблица(СтрокаФайла.Расширение) Тогда
			Продолжить;
		КонецЕсли;
		Таблица = ПрочитатьЭлектроннуюТаблицуВТабличныйДокумент(СтрокаФайла.Путь);
		ГраницаСтрока = Мин(Таблица.ВысотаТаблицы, МаксимальныйНомерСтроки);
		ГраницаКолонка = Мин(Таблица.ШиринаСтраницы, МаксимальныйНомерКолонки);
		
		ФайлДобавлен = Ложь;
		Для НомерСтроки = 1 По ГраницаСтрока Цикл
			Для НомерКолонки = 1 По ГраницаКолонка Цикл
				ТекстЯчейки = Таблица.Область(НомерСтроки, НомерКолонки).Текст;
				Если Не ЗначениеЗаполнено(ТекстЯчейки) Тогда
					Продолжить;
				КонецЕсли;
				СтрокаОписания = ОписаниеФайлов.Найти(ТекстЯчейки, "Заголовок");
				Если СтрокаОписания = Неопределено Тогда
					Продолжить;
				ИначеЕсли ЗначениеЗаполнено(СтрокаОписания.ПутьКФайлу) Тогда
					// Могут попасться несколько файлов одного и того же типа, например, кассовая книга за 2000 год,
					// потом за 2001 год и так далее
					НоваяСтрокаОписания = ОписаниеФайлов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаОписания, СтрокаОписания);
					СтрокаОписания = НоваяСтрокаОписания;
				КонецЕсли;
				СтрокаОписания.ПутьКФайлу = СтрокаФайла.Путь;
				ФайлДобавлен = Истина;
				Прервать;
			КонецЦикла;
			Если ФайлДобавлен Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ПрочитатьДанныеИзТекстовогоФайла(СтрокаОписания, Ошибки, ЗагружаемыеДанные, Организация)
	
	Если СтрокаОписания.ИмяФайла = ИмяРеквизитыОрганизации() Тогда
		
		ПрочитанныеДанные = ПрочитатьРеквизитыОрганизации(СтрокаОписания, Ошибки);
		
		Если Не ИННОрганизацииСоответствуетЗагружаемымДанным(ПрочитанныеДанные, Организация) Тогда
			
			ТекстОшибки = НСтр("ru = 'ИНН организации в базе и файле различается.
				|Проверьте реквизиты организации или выберите другой файл'");
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки, "");
			Возврат Неопределено;
			
		КонецЕсли;
	Иначе
		ПрочитанныеДанные = ПрочитатьТаблицуДанныхИзТекстовогоФайла(
			СтрокаОписания, Ошибки);
		ЗаполнитьКоличествоОбъектов(СтрокаОписания.ИмяФайла, ПрочитанныеДанные, ЗагружаемыеДанные);
	КонецЕсли;
	
	Возврат ПрочитанныеДанные;
	
КонецФункции

Функция ПрочитатьДанныеИзЭлектроннойТаблицы(СтрокаОписания, Ошибки, ЗагружаемыеДанные)
	
	ПрочитанныеДанные = ПрочитатьЭлектроннуюТаблицу(СтрокаОписания, Ошибки);
	Если СтрокаОписания.ИмяФайла = ИмяДокументы() Тогда
		Если ПрочитанныеДанные.Колонки.Найти(ИмяКолонкиИдентифицирующейРазделПокупки()) = Неопределено Тогда
			СтрокаОписания.ИмяФайла = ИмяРазделаПродажи();
		Иначе
			СтрокаОписания.ИмяФайла = ИмяРазделаПокупки();
		КонецЕсли;
		Для Каждого Строка Из ПрочитанныеДанные Цикл
			Если (Не ЗначениеЗаполнено(Строка.Количество) Или Строка.Количество = "-")
				И ЗначениеЗаполнено(Строка.ФактическоеКоличество) Тогда
				Строка.Количество = Строка.ФактическоеКоличество;
				Строка.СуммаБезНДС = Строка.ФактическоеКоличество * Строка.Цена;
				Строка.СуммаНДС = 0;
			КонецЕсли;
			Если Строка.ТипДокумента = "УПД" Тогда
				Строка.ТипДокумента = ?(Строка.ВидНоменклатуры = "Услуга", "Акт", "Накладная");
			КонецЕсли;
		КонецЦикла;
		ДанныеПоКоличествуДокументов = ПрочитанныеДанные.Скопировать( , "Дата, Номер, ТипДокумента, КоличествоОбъектов");
		ДанныеПоКоличествуДокументов.Свернуть("Дата, Номер, ТипДокумента, КоличествоОбъектов");
		ЗаполнитьКоличествоОбъектов(СтрокаОписания.ИмяФайла, ДанныеПоКоличествуДокументов, ЗагружаемыеДанные);
	ИначеЕсли СтрокаОписания.ИмяФайла = ИмяНоменклатура() Тогда
		ДанныеПоКоличествуНоменклатуры = ПрочитанныеДанные.Скопировать( ,
			ИмяКолонкиИдентифицирующейНоменклатуру() + ", КоличествоОбъектов");
		ДанныеПоКоличествуНоменклатуры.Свернуть(ИмяКолонкиИдентифицирующейНоменклатуру() + ", КоличествоОбъектов");
		ЗаполнитьКоличествоОбъектов(СтрокаОписания.ИмяФайла, ДанныеПоКоличествуНоменклатуры, ЗагружаемыеДанные);
	Иначе
		ЗаполнитьКоличествоОбъектов(СтрокаОписания.ИмяФайла, ПрочитанныеДанные, ЗагружаемыеДанные);
	КонецЕсли;
	
	Возврат ПрочитанныеДанные;
	
КонецФункции

Функция ПрочитатьТаблицуДанныхИзТекстовогоФайла(СтрокаОписанияФайла, Ошибки)
	
	ЧтениеТекста = Новый ЧтениеТекста(СтрокаОписанияФайла.ПутьКФайлу, СтрокаОписанияФайла.Кодировка);
	СодержимоеФайла = ЧтениеТекста.Прочитать();
	СоставДанных = НоваяТаблицаРеквизитов();
	ИмяФайла = СтрокаОписанияФайла.ИмяФайла;
	ЗаполнитьСоответствияРеквизитовТекстовыхФайлов(ИмяФайла, СоставДанных);
	Если ИмяФайла = ИмяКонтрагенты() Тогда
		СтрокаШапкаТаблицы = ОпределитьНомераКолонокДанныхКонтрагентов(СоставДанных, СодержимоеФайла);
	Иначе
		СтрокаШапкаТаблицы = ОпределитьНомераКолонокДокументов(СоставДанных, СодержимоеФайла);
	КонецЕсли;
	// ТаблицаДанных формируется обязательно после процедур "ОпределитьНомераКолонок...", т.к. в ходе этих процедур
	// предусмотрено изменение свойств коллекции СоставДанных
	ТаблицаДанных = НоваяТаблицаДанных(СоставДанных);
	Если СтрокаШапкаТаблицы = 0 Тогда
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Неверный формат файла %1'", СтрокаОписанияФайла.ИмяФайла));
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИмяФайла", ТекстОшибки, "");
	Иначе
		ЗаполнитьТаблицуДанныхПоТекстовомуФайлу(
			ТаблицаДанных, СоставДанных, СодержимоеФайла, СтрокаШапкаТаблицы, ИмяФайла);
	КонецЕсли;
	Возврат ТаблицаДанных;
	
КонецФункции

Процедура ЗаполнитьСоответствияРеквизитовТекстовыхФайлов(ИмяФайла, ТаблицаРеквизитов)
	
	Если ИмяФайла = ИмяКонтрагенты() Тогда
		ЗаполнитьСоответствияРеквизитовКонтрагентовДляЗагрузкиИзТекстовогоФайла(ТаблицаРеквизитов);
	ИначеЕсли ИмяФайла = ИмяВходящиеДокументы() Или ИмяФайла = ИмяИсходящиеДокументы() Тогда
		ЗаполнитьСоответствияРеквизитовВходящихИсходящихДокументов(ТаблицаРеквизитов);
	ИначеЕсли ИмяФайла = ИмяДокументыДвиженияДенег() Тогда
		ЗаполнитьСоответствияРеквизитовДенежныхДокументов(ТаблицаРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКоличествоОбъектов(ИмяТаблицы, ТаблицаДанных, ЗагружаемыеДанные)
	
	ТаблицаТиповДокументов = ТаблицаСопоставления(ИмяТипыДокументов());
	
	Если ТаблицаДанных.Колонки.Найти("ТипДокумента") = Неопределено Тогда
		
		НайденнаяСтрока = ЗагружаемыеДанные.Найти(ИмяТаблицы, "ИмяОбъекта");
		Если НайденнаяСтрока = Неопределено Тогда
			НовСтрока = ЗагружаемыеДанные.Добавить();
			НовСтрока.ИмяОбъекта           = ИмяТаблицы;
			НовСтрока.ПредставлениеОбъекта = ИмяТаблицы;
			НовСтрока.КоличествоОбъектов   = ТаблицаДанных.Количество();
			НовСтрока.ИмяРеквизитаФормы    = ИмяТаблицы;
			НовСтрока.Приоритет            = 1;
		Иначе
			НайденнаяСтрока.КоличествоОбъектов = НайденнаяСтрока.КоличествоОбъектов + ТаблицаДанных.Количество();
		КонецЕсли;
		
	Иначе
		
		ТаблицаДокументов = ТаблицаДанных.Скопировать( , "ТипДокумента, КоличествоОбъектов");
		ТаблицаДокументов.ЗаполнитьЗначения(1, "КоличествоОбъектов");
		ТаблицаДокументов.Свернуть("ТипДокумента", "КоличествоОбъектов");
		
		Для Каждого СтрокаДанных Из ТаблицаДокументов Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаДанных.ТипДокумента) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураПоиска = Новый Структура("РазделДанных, ТипДокумента", ИмяТаблицы, СтрокаДанных.ТипДокумента);
			НайденныеСтроки = ТаблицаТиповДокументов.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				НайденнаяСтрока = ЗагружаемыеДанные.Найти(НайденныеСтроки[0].ПредставлениеОбъекта, "ПредставлениеОбъекта");
				Если НайденнаяСтрока = Неопределено Тогда
					НовСтрока = ЗагружаемыеДанные.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрока, НайденныеСтроки[0]);
					НовСтрока.КоличествоОбъектов = СтрокаДанных.КоличествоОбъектов;
				Иначе
					НайденнаяСтрока.КоличествоОбъектов = НайденнаяСтрока.КоличествоОбъектов + СтрокаДанных.КоличествоОбъектов;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗагружаемыеДанные.Сортировать("Приоритет");
	
КонецПроцедуры

Функция ПрочитатьЭлектроннуюТаблицу(СтрокаОписанияФайла, Ошибки)
	
	Таблица = ПрочитатьЭлектроннуюТаблицуВТабличныйДокумент(СтрокаОписанияФайла.ПутьКФайлу);
	СоставДанных = НоваяТаблицаРеквизитов();
	ИмяФайла = СтрокаОписанияФайла.ИмяФайла;
	ЗаполнитьСоответствияРеквизитовЭлектронныхТаблиц(ИмяФайла, СоставДанных);
	ЭтоДокументы = ИмяФайла = ИмяДокументы();
	Если ИмяФайла = ИмяКассоваяКнига() Тогда
		Возврат ПрочитатьКассовуюКнигуИзЭлектроннойТаблицы(СоставДанных, Таблица);
	КонецЕсли;
	ИмяОбласти = ?(ИмяФайла = ИмяНоменклатура(), ИмяОбластиНоменклатура(), Неопределено);
	СтрокаШапкаТаблицы = ОпределитьНомераКолонокВЭлектроннойТаблице(СоставДанных, Таблица, ИмяОбласти);
	Если ЭтоДокументы Тогда
		УдалитьОтсутствующиеПроверяемыеКолонкиВСоставеДанных(СоставДанных);
	КонецЕсли;
	ТаблицаДанных = НоваяТаблицаДанных(СоставДанных);
	Если ЭтоДокументы И ТаблицаДанных.Колонки.Найти(ИмяКолонкиИдентифицирующейНоменклатуру()) = Неопределено Тогда
		Возврат ТаблицаДанных;
	КонецЕсли;
	Если СтрокаШапкаТаблицы = 0 Тогда
		ТекстОшибки = НСтр(СтрШаблон("ru = 'Неверный формат файла %1'", СтрокаОписанияФайла.ИмяФайла));
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , ТекстОшибки, "");
	Иначе
		ОбязательныеРеквизиты = РеквизитыОбязательныеКЗаполнению(ИмяФайла);
		ПрекращатьЧтениеВСлучаеПустойСтроки = ЭтоДокументы;
		ЗаполнитьТаблицуДанных(
			ТаблицаДанных, СоставДанных, Таблица, СтрокаШапкаТаблицы, ИмяОбласти, 
			ОбязательныеРеквизиты, ПрекращатьЧтениеВСлучаеПустойСтроки);
		Если ЭтоДокументы Тогда
			ОбработатьТаблицуДанныхДляДокументовПокупкиПродажи(ТаблицаДанных);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция НоваяТаблицаРеквизитов()
	
	ТаблицаРеквизитов = Новый ТаблицаЗначений;
	ТаблицаРеквизитов.Колонки.Добавить(ИмяРеквизита1С(),
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1024)));
	ТаблицаРеквизитов.Колонки.Добавить(ИмяРеквизитаВФайле(), 
		Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(1024)));
	ТаблицаРеквизитов.Колонки.Добавить("Значение");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Процедура ЗаполнитьТаблицуДанныхПоТекстовомуФайлу(ТаблицаДанных, СоставДанных, Текст, СтрокаШапкиТаблицы, ИмяФайла)
	
	Начало = СтрокаШапкиТаблицы + 1;
	КоличествоСтрок = СтрЧислоСтрок(Текст);
	Если ИмяФайла = ИмяКонтрагенты() Тогда
		ОбязательныеРеквизиты = РеквизитыКонтрагентовОбязательныеКЗаполнению();
	Иначе
		ОбязательныеРеквизиты = Новый Массив;
	КонецЕсли;
	СтрокиКУдалению = Новый Массив;
	ДанныеОбъекта = Неопределено;
	// Прочитанный файл может содержать строки с переносом, например, когда у документа есть многострочный комментарий
	// нужно учесть подобные ситуации. Признаком их возникновения является наличие в строке кавычки " и отсутствие
	// закрывающей кавычки
	СоставнаяСтрока = Новый Массив;
	Для Сч = Начало По КоличествоСтрок Цикл
		СтрокаСформирована = Ложь;
		КавычкиОткрыты = Ложь;
		Пока Не СтрокаСформирована Цикл
			Строка = СтрПолучитьСтроку(Текст, Сч);
			СоставнаяСтрока.Добавить(Строка);
			КоличествоКавычекВСтроке = СтрЧислоВхождений(Строка, """");
			Если КоличествоКавычекВСтроке % 2 <> 0 Тогда
				// Нечетное количество кавычек сигнализирует о том что обнаружен перенос строки, либо завершение переноса
				Если Не КавычкиОткрыты Тогда
					// Обнаружили перенос строки, следовательно объект еще не прочитан полностью,
					// в связи с этим необходимо прочитать строки дальше, пока не будет найдена закрывающая кавычка
					Сч = Сч + 1;
					КавычкиОткрыты = Истина;
				Иначе
					// Обнаружили закрывающую кавычку, читаем завершающую строку и устанавливаем признак, оповещающий о том,
					// что объект полностью прочитан
					СтрокаСформирована = Истина;
				КонецЕсли;
			Иначе
				Если КавычкиОткрыты Тогда
					// Находимся внутри строки с переносом и перенос еще не завершен
					// читаем эту часть строки и переходим к следующей
					Сч = Сч + 1;
				Иначе
					// Признаков переноса не обнаружили
					СтрокаСформирована = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Строка = СтрСоединить(СоставнаяСтрока, Символы.ПС);
		СоставнаяСтрока.Очистить();
		Если Не ЗначениеЗаполнено(Строка) Тогда
			Продолжить;
		КонецЕсли;
		// В реквизитах с типом "Строка" могут содержаться символы разделителя, которые по факту разделителями не являются
		// например в комментарии может содержаться символ ";"
		// Необходимо разделить строку на подстроки с учетом этой ситуации
		Подстроки = РазбитьНаПодстрокиБезУчетаРазделителейВнутриКавычек(Строка, Неопределено);
		ДобавленНовыйОбъект = Ложь;
		Для Каждого ОписаниеРеквизита Из СоставДанных Цикл
			ИмяРеквизита = ОписаниеРеквизита[ИмяРеквизита1С()];
			Если ТипЗнч(ОписаниеРеквизита.Значение) = Тип("Число") Тогда
				НайденноеЗначение = Подстроки[ОписаниеРеквизита.Значение];
				Если ЗначениеЗаполнено(НайденноеЗначение) Тогда
					Если Не ДобавленНовыйОбъект Тогда
						ДобавленНовыйОбъект = Истина;
						ДанныеОбъекта = ТаблицаДанных.Добавить();
					КонецЕсли;
					ДанныеОбъекта[ИмяРеквизита] = НайденноеЗначение;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ОписаниеРеквизита.Значение) = Тип("ТаблицаЗначений") Тогда
				Если ДанныеОбъекта = Неопределено Или (Не ДобавленНовыйОбъект
					И Не ТипЗнч(ДанныеОбъекта[ИмяРеквизита]) = Тип("ТаблицаЗначений")) Тогда
					// Возникла ошибочная ситуация - читается строка с вложенными реквизитами объекта, но нет
					// данных об их владельце
					Продолжить;
				ИначеЕсли ДобавленНовыйОбъект Тогда
					// Читаем данные нового объекта, для вложенных реквизитов нужно добавить табличную часть
					ДанныеОбъекта[ИмяРеквизита] = НоваяТаблицаДанных(ОписаниеРеквизита.Значение);
				КонецЕсли;
				СтрокаВложенногоРеквизита = Неопределено;
				Для Каждого ОписаниеВложенногоРеквизита Из ОписаниеРеквизита.Значение Цикл
					Если ТипЗнч(ОписаниеВложенногоРеквизита.Значение) = Тип("Число")
						И ОписаниеВложенногоРеквизита.Значение <= Подстроки.ВГраница() Тогда
						ПрочитанноеЗначение = Подстроки[ОписаниеВложенногоРеквизита.Значение];
					Иначе
						Продолжить;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ПрочитанноеЗначение) Тогда
						Продолжить;
					КонецЕсли;
					Если СтрокаВложенногоРеквизита = Неопределено Тогда
						СтрокаВложенногоРеквизита = ДанныеОбъекта[ИмяРеквизита].Добавить();
					КонецЕсли;
					СтрокаВложенногоРеквизита[ОписаниеВложенногоРеквизита[ИмяРеквизита1С()]] = ПрочитанноеЗначение;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
		Если ДанныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Реквизит Из ОбязательныеРеквизиты Цикл
			Если ТаблицаДанных.Колонки.Найти(Реквизит) <> Неопределено
				И Не ЗначениеЗаполнено(ДанныеОбъекта[Реквизит]) Тогда
				СтрокиКУдалению.Добавить(ДанныеОбъекта);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокиКУдалению) Тогда
		Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
			ТаблицаДанных.Удалить(УдаляемаяСтрока);
		КонецЦикла;
	КонецЕсли;
	
	Если ИмяФайла = ИмяДокументыДвиженияДенег() Тогда
		ОбработатьДанныеДокументовДвиженияДенег(ТаблицаДанных);
	КонецЕсли;
	
КонецПроцедуры

Функция ПрочитатьЭлектроннуюТаблицуВТабличныйДокумент(Путь)
	
	Таблица = Новый ТабличныйДокумент;
	Таблица.Прочитать(Путь);
	// Разгруппируем все объединенные ячейки. Работа с такими ячейками не поддерживается
	Таблица.Область().Разъединить();
	Возврат Таблица;
	
КонецФункции

Процедура ЗаполнитьСоответствияРеквизитовЭлектронныхТаблиц(ИмяФайла, СоставДанных)
	
	Если ИмяФайла = ИмяНоменклатура() Тогда
		ЗаполнитьСоответствияРеквизитовНоменклатуры(СоставДанных);
	ИначеЕсли ИмяФайла = ИмяКонтрагенты() Тогда
		ЗаполнитьСоответствияРеквизитовКонтрагентовДляЗагрузкиИзЭлектроннойТаблицы(СоставДанных);
	ИначеЕсли ИмяФайла = ИмяКассоваяКнига() Тогда
		ЗаполнитьСоответствияРеквизитовКассовойКниги(СоставДанных);
	ИначеЕсли ИмяФайла = ИмяДокументы() Тогда
		ЗаполнитьСоответствияРеквизитовДокументовПокупкиПродажи(СоставДанных);
	КонецЕсли;
	
КонецПроцедуры

Функция ОпределитьНомераКолонокВЭлектроннойТаблице(
	СоставДанных, Таблица, ИмяОбласти, ПерваяСтрока = 0, ПерваяКолонка = 1, ПоследняяКолонка = 0)
	
	СтрокаШапкаТаблицы = 0;
	Если ЗначениеЗаполнено(ИмяОбласти) Тогда
		Область = Таблица.Области.Найти(ИмяОбласти);
		Если Область = Неопределено Тогда
			Возврат СтрокаШапкаТаблицы;
		КонецЕсли;
		Начало = ?(ЗначениеЗаполнено(ПерваяСтрока), ПерваяСтрока, Область.Верх);
		Окончание = Область.Низ;
	Иначе
		Начало = 1;
		Окончание = Таблица.ВысотаТаблицы;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПоследняяКолонка) Тогда
		ПоследняяКолонка = Таблица.ШиринаСтраницы;
	КонецЕсли;
	
	Для НомерСтроки = Начало По Окончание Цикл
		Если СтрокаШапкаТаблицы <> 0 Тогда
			Прервать;
		КонецЕсли;
		Для НомерКолонки = ПерваяКолонка По ПоследняяКолонка Цикл
			ТекстЯчейки = Таблица.Область(НомерСтроки, НомерКолонки).Текст;
			Если Не ЗначениеЗаполнено(ТекстЯчейки) Тогда
				Продолжить;
			КонецЕсли;
			ТекстЯчейки = СтрЗаменить(ТекстЯчейки, Символы.ПС, " ");
			НайденныйРеквизит = СоставДанных.Найти(НРег(СокрЛП(ТекстЯчейки)), ИмяРеквизитаВФайле());
			Если НайденныйРеквизит = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НайденныйРеквизит.Значение = НомерКолонки;
			СтрокаШапкаТаблицы = НомерСтроки;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СтрокаШапкаТаблицы;
	
КонецФункции

Процедура ЗаполнитьТаблицуДанных(
	ПрочитанныеДанные, СоставДанных, Таблица, СтрокаШапкаТаблицы, ИмяОбласти, ОбязательныеРеквизиты,
	ПрекращатьЧтениеВСлучаеПустойСтроки, ЗначенияДляЗаполнения = Неопределено)
	
	Если ЗначениеЗаполнено(ИмяОбласти) Тогда
		Область = Таблица.Области.Найти(ИмяОбласти);
		Окончание = Область.Низ;
	Иначе
		Окончание = Таблица.ВысотаТаблицы;
	КонецЕсли;
	
	Начало = СтрокаШапкаТаблицы + 1;
	СтрокиКУдалению = Новый Массив;
	
	Для НомерСтроки = Начало По Окончание Цикл
		ДанныеОбъекта = Неопределено;
		Для каждого ОписаниеРеквизита Из СоставДанных Цикл
			Если ОписаниеРеквизита.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ТекстЯчейки = Таблица.Область(НомерСтроки, ОписаниеРеквизита.Значение).Текст;
			Если Не ЗначениеЗаполнено(ТекстЯчейки) Тогда
				Продолжить;
			КонецЕсли;
			Если ДанныеОбъекта = Неопределено Тогда
				ДанныеОбъекта = ПрочитанныеДанные.Добавить();
			КонецЕсли;
			ДанныеОбъекта[ОписаниеРеквизита[ИмяРеквизита1С()]] = ТекстЯчейки;
		КонецЦикла;
		Если ДанныеОбъекта = Неопределено Тогда
			Если ПрекращатьЧтениеВСлучаеПустойСтроки Тогда
				Прервать;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если ОбязательныеРеквизиты = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		УдалитьСтроку = Ложь;
		Для Каждого ИмяРеквизита Из ОбязательныеРеквизиты Цикл
			Если Не ЗначениеЗаполнено(ДанныеОбъекта[ИмяРеквизита]) Тогда
				УдалитьСтроку = Истина;
			Иначе
				УдалитьСтроку = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если УдалитьСтроку Тогда
			СтрокиКУдалению.Добавить(ДанныеОбъекта);
		КонецЕсли;
		Если ЗначенияДляЗаполнения <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЗначенияДляЗаполнения);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		ПрочитанныеДанные.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Функция ШапкаОбластиКассовойКниги()
	
	Возврат "Касса за";
	
КонецФункции

Функция ЧисловыеРеквизитыКассовойКниги()
	
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("СуммаПоступления");
	ИменаРеквизитов.Добавить("СуммаСписания");
	
	Возврат ИменаРеквизитов;
	
КонецФункции

Функция ИменаПроверяемыхКолонокДляДокументовПокупкиПродажи()
	
	ИменаКолонок = Новый Массив;
	ИменаКолонок.Добавить(ИмяКолонкиИдентифицирующейРазделПокупки());
	ИменаКолонок.Добавить(ИмяКолонкиИдентифицирующейНоменклатуру());
	ИменаКолонок.Добавить("Контрагент");
	Возврат ИменаКолонок;
	
КонецФункции

Функция РеквизитыКассовойКнигиОбязательныеКЗаполнению()
	
	ОбязательныеРеквизиты = Новый Массив;
	ОбязательныеРеквизиты.Добавить("Контрагент");
	ОбязательныеРеквизиты.Добавить("Номер");
	ОбязательныеРеквизиты.Добавить("СчетУчета");
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область ЗагрузкаДанных

Процедура ЗагрузитьДанныеИзТекстовогоФайла(ДанныеЗагрузки, Результат, АдресРезультата)
	
	ДанныеЗагрузки.Вставить("Номенклатура", НоваяТаблицаНоменклатуры());
	ДанныеЗагрузки.Вставить("ДокументыКПроведению", НовыеДокументыКПроведению());
	
	ДанныеОрганизации = Неопределено;
	Организация = Неопределено;
	Если ДанныеЗагрузки.Свойство(ИмяРеквизитыОрганизации(), ДанныеОрганизации) Тогда
		Организация = ЗаписатьРеквизитыОрганизации(
			ДанныеОрганизации, Результат, ДанныеЗагрузки.Организация);
	Иначе
		Организация = ДанныеЗагрузки.Организация;
	КонецЕсли;
	
	Если Организация = Неопределено Тогда
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
		Возврат;
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяКонтрагенты()) Тогда
		НайтиСоздатьКонтрагентов(ДанныеЗагрузки.Контрагенты, Результат.Ошибки);
		ДлительныеОперации.СообщитьПрогресс( , ИмяКонтрагенты());
	КонецЕсли;
	
	ТаблицаТиповДокументов = ТаблицаСопоставления(ИмяТипыДокументов());
	
	Если ДанныеЗагрузки.Свойство(ИмяДокументыДвиженияДенег()) Тогда
		ЗагрузитьДокументыИзТекстовыхФайлов(ИмяДокументыДвиженияДенег(), ДанныеЗагрузки, ТаблицаТиповДокументов, Результат.Ошибки);
		ТекстПрогресса = ЗагруженныеОбъектыРаздела(ТаблицаТиповДокументов, ИмяДокументыДвиженияДенег());
		ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяВходящиеДокументы()) Тогда
		ЗагрузитьДокументыИзТекстовыхФайлов(ИмяВходящиеДокументы(), ДанныеЗагрузки, ТаблицаТиповДокументов, Результат.Ошибки);
		ТекстПрогресса = ЗагруженныеОбъектыРаздела(ТаблицаТиповДокументов, ИмяВходящиеДокументы());
		ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяИсходящиеДокументы()) Тогда
		ЗагрузитьДокументыИзТекстовыхФайлов(ИмяИсходящиеДокументы(), ДанныеЗагрузки, ТаблицаТиповДокументов, Результат.Ошибки);
		ТекстПрогресса = ЗагруженныеОбъектыРаздела(ТаблицаТиповДокументов, ИмяИсходящиеДокументы());
		ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗагрузки.ДокументыКПроведению) Тогда
		ПровестиДокументы(ДанныеЗагрузки.ДокументыКПроведению);
		ДлительныеОперации.СообщитьПрогресс( , ИмяОперацииПроведенияДокументов());
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

Процедура ЗагрузитьДанныеИзЭлектронныхТаблиц(ДанныеЗагрузки, Результат, АдресРезультата)
	
	ДанныеЗагрузки.Вставить("ДокументыКПроведению", НовыеДокументыКПроведению());
	ТаблицаТиповДокументов = ТаблицаСопоставления(ИмяТипыДокументов());
	
	Если ДанныеЗагрузки.Свойство(ИмяНоменклатура()) Тогда
		ЗагрузитьНоменклатуру(ИмяНоменклатура(), ДанныеЗагрузки, Результат.Ошибки);
		ДлительныеОперации.СообщитьПрогресс( , ИмяНоменклатура());
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяКонтрагенты()) Тогда
		НайтиСоздатьКонтрагентов(ДанныеЗагрузки[ИмяКонтрагенты()], Результат.Ошибки);
		ДлительныеОперации.СообщитьПрогресс( , ИмяКонтрагенты());
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяКассоваяКнига()) Тогда
		ЗагрузитьКассовуюКнигуИзЭлектронныхТаблиц(ДанныеЗагрузки, Результат.Ошибки);
		ТекстПрогресса = ЗагруженныеОбъектыРаздела(ТаблицаТиповДокументов, ИмяКассоваяКнига());
		ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяРазделаПокупки()) Тогда
		ЗагрузитьДокументыПокупкиПродажи(ИмяРазделаПокупки(), ДанныеЗагрузки, Результат.Ошибки, ТаблицаТиповДокументов);
		ТекстПрогресса = ЗагруженныеОбъектыРаздела(ТаблицаТиповДокументов, ИмяРазделаПокупки());
		ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	КонецЕсли;
	
	Если ДанныеЗагрузки.Свойство(ИмяРазделаПродажи()) Тогда
		ЗагрузитьДокументыПокупкиПродажи(ИмяРазделаПродажи(), ДанныеЗагрузки, Результат.Ошибки, ТаблицаТиповДокументов);
		ТекстПрогресса = ЗагруженныеОбъектыРаздела(ТаблицаТиповДокументов, ИмяРазделаПродажи());
		ДлительныеОперации.СообщитьПрогресс( , ТекстПрогресса);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗагрузки.ДокументыКПроведению) Тогда
		ПровестиДокументы(ДанныеЗагрузки.ДокументыКПроведению);
		ДлительныеОперации.СообщитьПрогресс( , ИмяОперацииПроведенияДокументов());
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область Организация

Функция ЗаписатьРеквизитыОрганизации(ПрочитанныеДанные, Результат, Организация)
	
	РеквизитыОрганизации = Новый Структура;
	Для Каждого Строка Из ПрочитанныеДанные Цикл
		РеквизитыОрганизации.Вставить(Строка[ИмяРеквизита1С()], Строка.Значение);
	КонецЦикла;
	РеквизитыОрганизации.ИмяИП = РеквизитыОрганизации.Имя;
	РеквизитыОрганизации.ФамилияИП = РеквизитыОрганизации.Фамилия;
	РеквизитыОрганизации.ОтчествоИП = РеквизитыОрганизации.Отчество;
	
	РеквизитыОрганизации.НаименованиеПолное = УдалитьКавычки(РеквизитыОрганизации.НаименованиеПолное);
	РеквизитыОрганизации.НаименованиеСокращенное = УдалитьКавычки(РеквизитыОрганизации.НаименованиеСокращенное);
	
	ИсходныеСообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
	ЮрЛицо = ИдентификационныеНомераНалогоплательщиков.ЭтоИННЮридическогоЛица(РеквизитыОрганизации.ИНН);
	НачатьТранзакцию();
	Попытка
		ОрганизацияОбъект = Организация.ПолучитьОбъект();
		Если ЮрЛицо Тогда
			РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		Иначе
			РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		КонецЕсли;
		ОрганизацияОбъект.Заполнить(РеквизитыОрганизации);
		ЗаполнитьЗначенияСвойств(ОрганизацияОбъект, РеквизитыОрганизации,
			"ИНН, КПП, КодОКВЭД, ОГРН, ИмяИП, ФамилияИП, ОтчествоИП, НаименованиеПолное,
			|НаименованиеСокращенное, ЮридическоеФизическоеЛицо");
		Если ЗначениеЗаполнено(РеквизитыОрганизации.ОГРНИП) Тогда
			ОрганизацияОбъект.ОГРН = РеквизитыОрганизации.ОГРНИП;
		КонецЕсли;
		Если Не ЮрЛицо Тогда
			ОрганизацияОбъект.НаименованиеПолное =
				ОрганизацииФормыКлиентСервер.ПолноеНаименованиеИндивидуальногоПредпринимателя(
				РеквизитыОрганизации.Фамилия, РеквизитыОрганизации.Имя, РеквизитыОрганизации.Отчество);
			ОрганизацияОбъект.Наименование =
				ОрганизацииФормыКлиентСервер.НаименованиеИндивидуальногоПредпринимателя(
				РеквизитыОрганизации.Фамилия, РеквизитыОрганизации.Имя, РеквизитыОрганизации.Отчество);
			ОрганизацияОбъект.НаименованиеСокращенное =
				ОрганизацииФормыКлиентСервер.СокращенноеНаименованиеИндивидуальногоПредпринимателя(
				РеквизитыОрганизации.Фамилия, РеквизитыОрганизации.Имя, РеквизитыОрганизации.Отчество);
		Иначе
			ОрганизацияОбъект.Наименование = РеквизитыОрганизации.НаименованиеСокращенное;
		КонецЕсли;
		
		ЗаписатьКонтактныеДанныеОрганизации(ОрганизацияОбъект, РеквизитыОрганизации);
		
		// ОКВЭД
		Если ЗначениеЗаполнено(ОрганизацияОбъект.КодОКВЭД) Тогда
			Классификатор = ОбщегоНазначенияБПВызовСервера.ПолучитьКлассификатор("ОКВЭД");
			ОрганизацияОбъект.НаименованиеОКВЭД = Классификатор.Получить(ОрганизацияОбъект.КодОКВЭД);
		КонецЕсли;
		
		// Регистрация в налоговом органе
		Если Не ЗначениеЗаполнено(ОрганизацияОбъект.РегистрацияВНалоговомОргане) Тогда
			СоздатьРегистрациюВНалоговомОргане(ОрганизацияОбъект, РеквизитыОрганизации, ЮрЛицо);
		КонецЕсли;
		
		// ПФР
		Если Не ЗначениеЗаполнено(ОрганизацияОбъект.КодОрганаПФР) Тогда
			
			РеквизитыРегистрацииПФР = Новый Структура;
			Для Каждого СтрокаПФР Из РеквизитыОрганизации.РегистрацияВПФР Цикл
				РеквизитыРегистрацииПФР.Вставить(СтрокаПФР[ИмяРеквизита1С()], СтрокаПФР.Значение);
			КонецЦикла;
			ЗаполнитьЗначенияСвойств(ОрганизацияОбъект, РеквизитыРегистрацииПФР);
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИсторияРегистрацийВОрганеПФР");
			ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписейИсторияПФР = РегистрыСведений.ИсторияРегистрацийВОрганеПФР.СоздатьНаборЗаписей();
			НаборЗаписейИсторияПФР.Отбор.Организация.Установить(Организация);
			НаборЗаписейИсторияПФР.Прочитать();
			ЗаписьИсторияПФР = НаборЗаписейИсторияПФР.Добавить();
			ЗаписьИсторияПФР.Организация = Организация;
			ЗаписьИсторияПФР.РегистрационныйНомерПФР = РеквизитыРегистрацииПФР.РегистрационныйНомерПФР;
			ЗаписьИсторияПФР.Период = РегистрыСведений.ИсторияРегистрацийВОрганеПФР.ДатаОтсчетаПериодическихСведений();
			НаборЗаписейИсторияПФР.Записать();
			
		КонецЕсли;
		
		// ФСС
		Если Не ЗначениеЗаполнено(ОрганизацияОбъект.КодПодчиненностиФСС) Тогда
			РеквизитыРегистрацииФСС = Новый Структура;
			Для Каждого СтрокаФСС Из РеквизитыОрганизации.РегистрацияВФССОрганизации Цикл
				РеквизитыРегистрацииФСС.Вставить(СтрокаФСС[ИмяРеквизита1С()], СтрокаФСС.Значение);
			КонецЦикла;
			ЗаполнитьЗначенияСвойств(ОрганизацияОбъект, РеквизитыРегистрацииФСС);
		КонецЕсли;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтветственныеЛицаОрганизаций");
		ЭлементБлокировки.УстановитьЗначение("СтруктурнаяЕдиница", Организация);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		// Руководитель
		НаборЗаписей = РегистрыСведений.ОтветственныеЛицаОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Организация);
		НаборЗаписей.Отбор.ОтветственноеЛицо.Установить(Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		НаборЗаписей.Прочитать();
		Если Не ЗначениеЗаполнено(НаборЗаписей) Тогда
			РеквизитыРуководителя = Новый Структура;
			Для Каждого СтрокаРуководитель Из РеквизитыОрганизации.РуководительОрганизации Цикл
				РеквизитыРуководителя.Вставить(СтрокаРуководитель[ИмяРеквизита1С()], СтрокаРуководитель.Значение);
			КонецЦикла;
			Запись = НаборЗаписей.Добавить();
			Запись.СтруктурнаяЕдиница = Организация;
			Запись.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.Руководитель;
			Запись.Период = Дата("19800101");
			Запись.ФизическоеЛицо = ОтветственныеЛицаБП.ПолучитьСоздатьФизЛицо(РеквизитыРуководителя);
			Запись.Должность = ОтветственныеЛицаБП.ПолучитьСоздатьДолжность(РеквизитыРуководителя.Должность);
			НаборЗаписей.Записать();
		КонецЕсли;
	
		// Бухгалтер
		НаборЗаписей = РегистрыСведений.ОтветственныеЛицаОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(Организация);
		НаборЗаписей.Отбор.ОтветственноеЛицо.Установить(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
		НаборЗаписей.Прочитать();
		Если Не ЗначениеЗаполнено(НаборЗаписей) Тогда
			РеквизитыБухгалтера = Новый Структура;
			Для Каждого СтрокаБухгалтер Из РеквизитыОрганизации.Бухгалтер Цикл
				РеквизитыБухгалтера.Вставить(СтрокаБухгалтер[ИмяРеквизита1С()], СтрокаБухгалтер.Значение);
			КонецЦикла;
			Запись = НаборЗаписей.Добавить();
			Запись.СтруктурнаяЕдиница = Организация;
			Запись.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер;
			Запись.Период = Дата("19800101");
			Запись.ФизическоеЛицо = ОтветственныеЛицаБП.ПолучитьСоздатьФизЛицо(РеквизитыБухгалтера);
			Запись.Должность = ОтветственныеЛицаБП.ПолучитьСоздатьДолжность(НСтр("ru = 'Главный бухгалтер'",
				ОбщегоНазначения.КодОсновногоЯзыка()));
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
		ОрганизацияОбъект.Записать();
	Исключение
		МассивСообщенийОбОшибках = Новый Массив;
		МассивСообщенийОбОшибках.Добавить(НСтр("ru = 'Не удалось загрузить организацию'"));
		СообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
		Для Каждого Сообщение Из СообщенияПользователю Цикл
			МассивСообщенийОбОшибках.Добавить(Сообщение.Текст);
		КонецЦикла;
		ТекстОшибки = СтрСоединить(МассивСообщенийОбОшибках, Символы.ПС);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.Ошибки, "", ТекстОшибки, "");
		ЗаписьЖурналаРегистрации(
			ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.Организации, , ОписаниеОшибки());
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецПопытки;
	
	Для Каждого Сообщение Из ИсходныеСообщенияПользователю Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
	РеквизитыБанковскихСчетов = Новый Массив;
	Для Каждого Реквизит Из РеквизитыОрганизации Цикл
		Если СтрНайти(Реквизит.Ключ, "РасчетныйСчет") <> 0 Тогда
			РеквизитыБанковскихСчетов.Добавить(Реквизит.Значение);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(РеквизитыБанковскихСчетов) Тогда
		ОсновнойБанковскийСчет = СоздатьБанковскиеСчета(Организация, РеквизитыБанковскихСчетов);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОсновнойБанковскийСчет) Тогда
		ОрганизацияОбъект = Организация.ПолучитьОбъект();
		ОрганизацияОбъект.ОсновнойБанковскийСчет = ОсновнойБанковскийСчет;
		ОрганизацияОбъект.Записать();
	КонецЕсли;
	
	ПроверитьИзменитьСистемуНалогообложения(Организация);
	
	Возврат Организация;
	
КонецФункции

Функция ПрочитатьРеквизитыОрганизации(СтрокаОписанияФайла, Ошибки)
	
	ЧтениеТекста = Новый ЧтениеТекста(СтрокаОписанияФайла.ПутьКФайлу, СтрокаОписанияФайла.Кодировка);
	СодержимоеФайла = ЧтениеТекста.Прочитать();
	ПрочитанныеРеквизиты = СформироватьТаблицуРеквизитовОрганизации(СодержимоеФайла);
	
	Если Не ЗначениеЗаполнено(ПрочитанныеРеквизиты) Тогда
		ТекстСообщения = СтрШаблон(НСтр(
			"ru = 'Неверный формат файла %1'"), СтрокаОписанияФайла.ПутьКФайлу);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИмяФайла", ТекстСообщения, "");
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПрочитанныеРеквизиты;
	
КонецФункции

Функция ИННОрганизацииСоответствуетЗагружаемымДанным(ПрочитанныеДанные, Организация)
	
	Если ПрочитанныеДанные = Неопределено Или Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтрокаИНН = ПрочитанныеДанные.Найти("ИНН", ИмяРеквизита1С());
	
	Если СтрокаИНН <> Неопределено Тогда
		
		ИННВыбраннойОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Организация, "ИНН");
			
		Если ЗначениеЗаполнено(ИННВыбраннойОрганизации)
			И ИННВыбраннойОрганизации <> СтрокаИНН.Значение Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаписатьКонтактныеДанныеОрганизации(Организация, ДанныеОрганизации)
	
	Если ДанныеОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		ПрочитанныйАдрес = ДанныеОрганизации.ЮридическийАдресОрганизации;
	Иначе
		ПрочитанныйАдрес = ДанныеОрганизации.АдресФизЛица;
	КонецЕсли;
	
	ДанныеАдреса = Новый Структура;
	Для Каждого Элемент Из ПрочитанныйАдрес Цикл
		ДанныеАдреса.Вставить(Элемент[ИмяРеквизита1С()], Элемент.Значение);
	КонецЦикла;
	
	// Приведем структуру к стандартному виду, для корректной обработки адреса,
	// см. РаботаСАдресами СтруктураАдресаВСтруктуруJSON
	ОбработатьАдрес(ДанныеАдреса);
	
	ПредставлениеАдреса = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ДанныеАдреса, "");
	
	УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(Организация, ПредставлениеАдреса,
		Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	
	Если ЗначениеЗаполнено(ДанныеОрганизации.ПочтовыйАдрес) Тогда
		ПочтовыйАдрес = ДанныеОрганизации.ПочтовыйАдрес;
	Иначе
		ПочтовыйАдрес = ПредставлениеАдреса;
	КонецЕсли;
		
	УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(Организация, ПочтовыйАдрес,
		Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
		
	УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(Организация, ПочтовыйАдрес,
		Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации);
		
	Если ЗначениеЗаполнено(ДанныеОрганизации.ТелефонОрганизации) Тогда
	
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(Организация, ДанныеОрганизации.ТелефонОрганизации,
			Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьРегистрациюВНалоговомОргане(ОрганизацияОбъект, РеквизитыОрганизации, ЮрЛицо)
	
	ПрочитанныеДанныеРегистрации = РеквизитыОрганизации.РегистрацияВНалоговомОргане;
	РеквизитыРегистрацииИФНС = Новый Структура;
	Для Каждого СтрокаИФНС Из ПрочитанныеДанныеРегистрации Цикл
		РеквизитыРегистрацииИФНС.Вставить(СтрокаИФНС[ИмяРеквизита1С()], СтрокаИФНС.Значение);
	КонецЦикла;
	
	РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.СоздатьЭлемент();
	РегистрацияВНалоговомОргане.Владелец = ОрганизацияОбъект.Ссылка;
	
	РеквизитыНалоговогоОргана = ДанныеГосударственныхОрганов.РеквизитыНалоговогоОрганаПоКоду(
		РеквизитыРегистрацииИФНС.Код);
		
	Если Не ЗначениеЗаполнено(РеквизитыНалоговогоОргана.ОписаниеОшибки) Тогда
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыНалоговогоОргана);
		ПлатежныеРеквизитыФНСПредставление =
			ДанныеГосударственныхОрганов.ПредставлениеПлатежныхРеквизитовГосударственногоОргана(РеквизитыНалоговогоОргана);
		ЗаполнитьЗначенияСвойств(РегистрацияВНалоговомОргане, РеквизитыНалоговогоОргана);
		РегистрацияВНалоговомОргане.НаименованиеИФНС = РеквизитыНалоговогоОргана.ПолноеНаименование;
		КодРегионаРегистрации = ДанныеГосударственныхОрганов.КодРегионаПоКодуНалоговогоОргана(
			РегистрацияВНалоговомОргане.Код);
		РегистрацияВНалоговомОргане.КодРегиона = КодРегионаРегистрации;
	Иначе
		ЗаполнитьЗначенияСвойств(РегистрацияВНалоговомОргане, РеквизитыРегистрацииИФНС);
		РегистрацияВНалоговомОргане.НаименованиеИФНС = РеквизитыРегистрацииИФНС.Наименование;
	КонецЕсли;
	
	Если ЮрЛицо Тогда
		РегистрацияВНалоговомОргане.КПП = РеквизитыОрганизации.КПП;
	КонецЕсли;
	
	РегистрацияВНалоговомОргане.Записать();
	
	Если ЗначениеЗаполнено(РеквизитыНалоговогоОргана.Адрес) И ЗначениеЗаполнено(РеквизитыНалоговогоОргана.Ссылка) Тогда
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
		РеквизитыНалоговогоОргана.Ссылка,
		РеквизитыНалоговогоОргана.Адрес,
		Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
		
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
		РеквизитыНалоговогоОргана.Ссылка,
		РеквизитыНалоговогоОргана.Адрес,
		Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента);
		
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
		РеквизитыНалоговогоОргана.Ссылка,
		РеквизитыНалоговогоОргана.Адрес,
		Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	КонецЕсли;
	
	ОрганизацияОбъект.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане.Ссылка;
	ОрганизацияОбъект.НаименованиеНалоговогоОргана = РегистрацияВНалоговомОргане.НаименованиеИФНС;
	ОрганизацияОбъект.КодНалоговогоОргана = РегистрацияВНалоговомОргане.Код;
	
	// История регистраций в налоговом органе
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИсторияРегистрацийВНалоговомОргане");
	ЭлементБлокировки.УстановитьЗначение("СтруктурнаяЕдиница", ОрганизацияОбъект.Ссылка);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	НаборЗаписей = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.СтруктурнаяЕдиница.Установить(ОрганизацияОбъект.Ссылка);
	НаборЗаписей.Прочитать();
	СтрокаНабораЗаписей = НаборЗаписей.Добавить();
	СтрокаНабораЗаписей.Период = РегистрыСведений.ИсторияРегистрацийВНалоговомОргане.ДатаОтсчетаПериодическихСведений();
	СтрокаНабораЗаписей.СтруктурнаяЕдиница = ОрганизацияОбъект.Ссылка;
	СтрокаНабораЗаписей.РегистрацияВНалоговомОргане = РегистрацияВНалоговомОргане.Ссылка;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ПроверитьИзменитьСистемуНалогообложения(Организация, СуммаРасходУСН = 0, СуммаПатент = 0)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиСистемыНалогообложенияСрезПоследних.Период КАК Период,
		|	НастройкиСистемыНалогообложенияСрезПоследних.Организация КАК Организация,
		|	НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНПатент КАК ПрименяетсяУСНПатент,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСН КАК ПрименяетсяУСН
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(, Организация = &Организация) КАК НастройкиСистемыНалогообложенияСрезПоследних";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		МенеджерЗаписи = РегистрыСведений.НастройкиСистемыНалогообложения.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = Выборка.Организация;
		МенеджерЗаписи.Период = Выборка.Период;
		МенеджерЗаписи.Прочитать();
		Модифицированность = Ложь;
		Если Выборка.СистемаНалогообложения <> Перечисления.СистемыНалогообложения.Упрощенная Тогда
			МенеджерЗаписи.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;
			МенеджерЗаписи.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы;
			Модифицированность = Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(СуммаРасходУСН) И Не МенеджерЗаписи.ПрименяетсяУСНДоходыМинусРасходы Тогда
			МенеджерЗаписи.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы;
			Модифицированность = Истина;
		КонецЕсли;
		Если ЗначениеЗаполнено(СуммаПатент) И Не МенеджерЗаписи.ПрименяетсяУСНПатент Тогда
			МенеджерЗаписи.ПрименяетсяУСНПатент = Истина;
			Модифицированность = Истина;
		КонецЕсли;
		Если Модифицированность Тогда
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьТаблицуРеквизитовОрганизации(Текст)
	
	ТаблицаРеквизитовОрганизации = НоваяТаблицаРеквизитов();
	ЗаполнитьСоответствияРеквизитовОрганизации(ТаблицаРеквизитовОрганизации);
	КоличествоСтрок = СтрЧислоСтрок(Текст);
	Для Сч = 1 По КоличествоСтрок Цикл
		// Файл состоит из трех колонок, в первой колонке хранится имя реквизита, если реквизит содержит
		// вложенные реквизиты, то их имена хранятся во второй колонке и заполняться начинают в той же строке,
		// в которой хранится имя основного реквизита. Третья колонка содержит значение реквизита
		Строка = ПрочитатьСтрокуРеквизитовОрганизации(Текст, Сч);
		Если ЗначениеЗаполнено(Строка.ИмяОсновногоРеквизита) Тогда
			Реквизит = ТаблицаРеквизитовОрганизации.Найти(Строка.ИмяОсновногоРеквизита, ИмяРеквизитаВФайле());
			Если Реквизит = Неопределено Тогда
				Продолжить;
			// В текстовом блоке с информацией о расчетных счетах вложенные реквизиты повторяются, т.к. счетов
			// может быть несколько, поэтому загрузку счетов отрабатываем по отдельному алгоритму
			ИначеЕсли Реквизит[ИмяРеквизита1С()] = ИмяРеквизитаДляРасчетныхСчетов() Тогда
				// Данный реквизит используется в служебных целях,
				// для отслеживания начала события загрузки расчетных счетов, поэтому сразу удаляется из таблицы реквизитов
				ТаблицаРеквизитовОрганизации.Удалить(Реквизит);
				Сч = ПрочитатьРасчетныеСчетаОрганизации(Сч, Текст, ТаблицаРеквизитовОрганизации);
				Продолжить;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ИмяВложенногоРеквизита) Тогда
				// Строка содержит значение основного реквизита
				Реквизит.Значение = Строка.Значение;
			Иначе
				// С этой строки начинается чтение вложенных реквизитов
				Если ТипЗнч(Реквизит.Значение) <> Тип("ТаблицаЗначений") Тогда
					Продолжить;
				КонецЕсли;
				ЗаполнитьЗначениеВНайденнойСтроке(
					Реквизит.Значение, Строка.ИмяВложенногоРеквизита, ИмяРеквизитаВФайле(), Строка.Значение);
			КонецЕсли;
		ИначеЕсли Реквизит = Неопределено Тогда
			Продолжить;
		ИначеЕсли ЗначениеЗаполнено(Строка.ИмяВложенногоРеквизита) Тогда
			// Продолжается чтение вложенных реквизитов
			Если ТипЗнч(Реквизит.Значение) <> Тип("ТаблицаЗначений") Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначениеВНайденнойСтроке(
				Реквизит.Значение, Строка.ИмяВложенногоРеквизита, ИмяРеквизитаВФайле(), Строка.Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат ТаблицаРеквизитовОрганизации;
	
КонецФункции

Процедура ОбработатьАдрес(ДанныеАдреса)
	
	Помещения = ТипыПомещений();
	ЗаполнитьЗначенияСвойств(Помещения, ДанныеАдреса);
	ДанныеАдреса.Вставить("Помещения", Новый Массив);
	Для каждого Элемент Из Помещения Цикл
		ДанныеАдреса.Удалить(Элемент.Ключ);
		Если ЗначениеЗаполнено(Элемент.Значение) Тогда
			Помещение = Новый Структура;
			Помещение.Вставить("Тип", Элемент.Ключ);
			Помещение.Вставить("Номер", Элемент.Значение);
			ДанныеАдреса.Помещения.Добавить(Помещение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствияРеквизитовОрганизации(ТаблицаРеквизитов)
	
	ДобавитьСоответствие(ТаблицаРеквизитов, "НаименованиеПолное", "полное название организации");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НаименованиеСокращенное", "краткое название организации");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ИНН", "инн");
	ДобавитьСоответствие(ТаблицаРеквизитов, "КПП", "кпп");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ОГРН", "огрн");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ОГРНИП", "огрнип");
	ДобавитьСоответствие(ТаблицаРеквизитов, "КодОКВЭД", "основной оквэд");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Имя", "имя");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ИмяИП");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Фамилия", "фамилия");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ФамилияИП");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Отчество", "отчество");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ОтчествоИП");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТелефонОрганизации", "телефон");
	// Служебный реквизит, необходимый для процедуры Справочники.Организации.СоздатьОрганизацию
	ДобавитьСоответствие(ТаблицаРеквизитов, "ОписаниеОшибки");
	// Данное свойство не читается из файла, а заполняется в процессе создания нового объекта
	ДобавитьСоответствие(ТаблицаРеквизитов, "ЮридическоеФизическоеЛицо");
	СтрокаАдресОрганизации = ДобавитьСоответствие(ТаблицаРеквизитов, "ЮридическийАдресОрганизации", "юридический адрес");
	СтрокаАдресОрганизации.Значение = СоответствияВложенныхРеквизитовАдреса();
	СтрокаАдресИП = ДобавитьСоответствие(ТаблицаРеквизитов, "АдресФизЛица", "адрес прописки");
	СтрокаАдресИП.Значение = СоответствияВложенныхРеквизитовАдреса();
	ДобавитьСоответствие(ТаблицаРеквизитов, "ПочтовыйАдрес", "адрес для документов");
	СтрокаРуководитель = ДобавитьСоответствие(ТаблицаРеквизитов, "РуководительОрганизации", "руководитель");
	СтрокаРуководитель.Значение = СоответствияВложенныхРеквизитовОтветственногоЛица();
	СтрокаБухгалтер = ДобавитьСоответствие(ТаблицаРеквизитов, "Бухгалтер", "бухгалтер");
	СтрокаБухгалтер.Значение = СоответствияВложенныхРеквизитовОтветственногоЛица();
	СтрокаПФР = ДобавитьСоответствие(ТаблицаРеквизитов, "РегистрацияВПФР", "реквизиты в пфр");
	СтрокаПФР.Значение = СоответствияВложенныхРеквизитовПФР();
	СтрокаФСС = ДобавитьСоответствие(ТаблицаРеквизитов, "РегистрацияВФССОрганизации", "реквизиты в фсс");
	СтрокаФСС.Значение = СоответствияВложенныхРеквизитовФСС();
	СтрокаИФНС = ДобавитьСоответствие(ТаблицаРеквизитов, "РегистрацияВНалоговомОргане", "ифнс по месту регистрации");
	СтрокаИФНС.Значение = СоответствияВложенныхРеквизитовИФНС();
	ДобавитьСоответствие(
		ТаблицаРеквизитов, ИмяРеквизитаДляРасчетныхСчетов(), ОбозначениеРеквизитаДляРасчетныхСчетовВФайле());
	
КонецПроцедуры

Функция ПрочитатьСтрокуРеквизитовОрганизации(Текст, НомерСтроки)
	
	Строка = СтрПолучитьСтроку(Текст, НомерСтроки);
	Подстроки = СтрРазделить(Строка, ";", Истина);
	ДанныеСтроки = Новый Структура;
	ДанныеСтроки.Вставить("ИмяОсновногоРеквизита", НРег(СокрЛП(Подстроки[0])));
	ДанныеСтроки.Вставить("ИмяВложенногоРеквизита", НРег(СокрЛП(Подстроки[1])));
	ДанныеСтроки.Вставить("Значение", СокрЛП(Подстроки[2]));
	Возврат ДанныеСтроки;
	
КонецФункции

Функция ТипыПомещений()
	
	ТипыПомещений = Новый Структура;
	ТипыПомещений.Вставить("Квартира");
	ТипыПомещений.Вставить("Помещение");
	ТипыПомещений.Вставить("Офис");
	Возврат ТипыПомещений;
	
КонецФункции

Функция СоответствияВложенныхРеквизитовАдреса()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, "Регион", "регион");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НаселенныйПункт", "населенный пункт");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Улица", "улица");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Дом", "дом");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Корпус", "корпус");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Квартира", "квартира");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Помещение", "помещение");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Офис", "офис");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Индекс", "индекс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ОКТМО", "октмо");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция СоответствияВложенныхРеквизитовОтветственногоЛица()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, "Должность", "должность");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Фамилия", "фамилия");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Имя", "имя");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Отчество", "отчество");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция СоответствияВложенныхРеквизитовПФР()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, "РегистрационныйНомерПФР", "рег. номер работодателя в пфр");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НомерСоглашения", "номер соглашения с пфр");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ДатаСоглашения", "дата соглашения");
	ДобавитьСоответствие(ТаблицаРеквизитов, "КодОрганаПФР", "код пфр");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НаименованиеТерриториальногоОрганаПФР", "наименование пфр");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ИПРегистрационныйНомерПФР", "регномер ип");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СНИЛС", "снилс");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция СоответствияВложенныхРеквизитовФСС()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, "РегистрационныйНомерФСС", "рег. номер в фсс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "КодПодчиненностиФСС", "код фсс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Ставка", "ставка на травм");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция СоответствияВложенныхРеквизитовИФНС()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, "Код", "код");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Наименование", "наименование");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Процедура ПроверитьУстановитьЗаписьУчетнойПолитики(Организация, ДатаУчетнойПолитики)
	
	Если ТипЗнч(Организация) <> Тип("СправочникСсылка.Организации") Или ТипЗнч(ДатаУчетнойПолитики) <> Тип("Дата") Тогда
		Возврат;
	КонецЕсли;
	
	Если УчетнаяПолитика.Существует(Организация, ДатаУчетнойПолитики) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУчета.СкопироватьПараметрыУчетнойПолитикиНаНовуюДату(
		"УчетнаяПолитика",Организация, ТекущаяДатаСеанса(), ДатаУчетнойПолитики);
	
	НастройкиУчета.СкопироватьПараметрыУчетнойПолитикиНаНовуюДату(
		"НастройкиСистемыНалогообложения",Организация, ТекущаяДатаСеанса(), ДатаУчетнойПолитики);
	
КонецПроцедуры

Функция ОсновноеПодразделениеОрганизации(Организация)
	
	Подразделение = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
	Если ЗначениеЗаполнено(Подразделение) И Не БухгалтерскийУчетПереопределяемый.ПодразделениеПринадлежитОрганизации(
		Подразделение, Организация) Тогда
		Подразделение = БухгалтерскийУчетПереопределяемый.ПустоеПодразделение();
	КонецЕсли;
	Возврат Подразделение;
	
КонецФункции

#КонецОбласти

#Область БанковскиеСчета

Функция СоздатьБанковскиеСчета(Владелец, МассивСчетов)
	
	ОсновнойБанковскийСчет = Неопределено;
	
	Для Каждого ПараметрыСчета Из МассивСчетов Цикл
		
		Если Не ЗначениеЗаполнено(ПараметрыСчета) Тогда
			Продолжить;
		КонецЕсли;
		
		// Реквизиты счета могут располагаться в таблице либо одной строкой, и в этом случае каждый параметр выделен
		// в отдельную колону, либо каждый параметр размещается в отдельной строке таблицы
		Если ПараметрыСчета.Количество() = 1 Тогда
			РеквизитыСчета = ПрочитатьРеквизитыРасчетногоСчетаИзТаблицы(ПараметрыСчета[0]);
		Иначе
			РеквизитыСчета = СформироватьРеквизитыСчета(ПараметрыСчета);
		КонецЕсли;
		ОбработатьРеквизитыСчета(РеквизитыСчета);
		РеквизитыСчета.Владелец = Владелец;
		
		СозданныйСчет = Справочники.БанковскиеСчета.СоздатьЭлемент();
		СозданныйСчет.Заполнить(РеквизитыСчета);
		ЗаполнитьЗначенияСвойств(СозданныйСчет, РеквизитыСчета);
		СозданныйСчет.Записать();
		
		Если ОсновнойБанковскийСчет = Неопределено Тогда
			ОсновнойБанковскийСчет = СозданныйСчет.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОсновнойБанковскийСчет;
	
КонецФункции

Функция ПрочитатьРасчетныеСчетаОрганизации(НомерНачальнойСтроки, Текст, ТаблицаРеквизитов)
	
	КоличествоСтрок = СтрЧислоСтрок(Текст);
	ПорядковыйНомерСчета = 1;
	Для НомерСтроки = НомерНачальнойСтроки По КоличествоСтрок Цикл
		Строка = ПрочитатьСтрокуРеквизитовОрганизации(Текст, НомерСтроки);
		Если ЗначениеЗаполнено(Строка.ИмяОсновногоРеквизита)
			И Строка.ИмяОсновногоРеквизита <> ОбозначениеРеквизитаДляРасчетныхСчетовВФайле() Тогда
			// Закончился текстовый блок с информацией о расчетных счетах, далее чтение файла продолжится
			// по основному алгоритму
			Возврат НомерСтроки - 1;
		КонецЕсли;
		
		Если Строка.ИмяВложенногоРеквизита = РазделительРасчетныхСчетов() Тогда
			// С этой строки начинается информация о новом счете
			Реквизит = ТаблицаРеквизитов.Добавить();
			Реквизит[ИмяРеквизита1С()] = СтрШаблон("%1%2", ИмяРеквизитаДляРасчетныхСчетов(), Формат(ПорядковыйНомерСчета, "ЧГ="));
			Реквизит.Значение = СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов();
			ЗаполнитьЗначениеВНайденнойСтроке(
				Реквизит.Значение, Строка.ИмяВложенногоРеквизита, ИмяРеквизитаВФайле(), Строка.Значение);
			ПорядковыйНомерСчета = ПорядковыйНомерСчета + 1;
		Иначе
			Если ТипЗнч(Реквизит.Значение) = Тип("ТаблицаЗначений") Тогда
				ЗаполнитьЗначениеВНайденнойСтроке(
					Реквизит.Значение, Строка.ИмяВложенногоРеквизита, ИмяРеквизитаВФайле(), Строка.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НомерСтроки;
	
КонецФункции

Функция ПрочитатьРеквизитыРасчетногоСчетаИзТаблицы(ПараметрСчета)
	
	РеквизитыСчета = НовыеРеквизитыБанковскогоСчета();
	ПрочитанныеДанные = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ПараметрСчета);
	ЗаполнитьЗначенияСвойств(РеквизитыСчета, ПрочитанныеДанные);
	
	Возврат РеквизитыСчета;
	
КонецФункции

Функция СформироватьРеквизитыСчета(ПараметрыСчета)
	
	РеквизитыСчета = НовыеРеквизитыБанковскогоСчета();
	Для Каждого Строка Из ПараметрыСчета Цикл
		Если РеквизитыСчета.Свойство(Строка[ИмяРеквизита1С()])
			И ЗначениеЗаполнено(Строка.Значение) Тогда
			РеквизитыСчета[Строка[ИмяРеквизита1С()]] = Строка.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РеквизитыСчета;
	
КонецФункции

Процедура ОбработатьРеквизитыСчета(РеквизитыСчета)
	
	РеквизитыСчета.НомерСчета = СтрЗаменить(РеквизитыСчета.НомерСчета, " ", "");
	РеквизитыСчета.НомерСчета = СтрЗаменить(РеквизитыСчета.НомерСчета, Символы.НПП, "");
	РеквизитыСчета.КоррСчетБанка = СтрЗаменить(РеквизитыСчета.КоррСчетБанка, " ", "");
	РеквизитыСчета.КоррСчетБанка = СтрЗаменить(РеквизитыСчета.КоррСчетБанка, Символы.НПП, "");
	РеквизитыСчета.БИК = БанковскиеПравила.ИсправитьБИК(РеквизитыСчета.БИК);
	РеквизитыСчета.НаименованиеБанка = УдалитьКавычки(РеквизитыСчета.НаименованиеБанка);
	
	// Для российского счета можно определить валюту по номеру счета.
	Если БанковскиеПравила.СтрокаСоответствуетФорматуБанковскогоСчета(РеквизитыСчета.НомерСчета)
		И Не БанковскиеПравила.ЭтоРублевыйСчет(РеквизитыСчета.НомерСчета) Тогда
		КодВалюты = БанковскиеПравила.КодВалютыБанковскогоСчета(РеквизитыСчета.НомерСчета);
		РеквизитыСчета.ВалютаДенежныхСредств = БанковскиеСчетаВызовСервера.ПолучитьВалютуПоКоду(КодВалюты);
		РеквизитыСчета.Валютный = Истина;
	Иначе
		РеквизитыСчета.ВалютаДенежныхСредств
			= ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		РеквизитыСчета.Валютный = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаНомераРасчетногоСчета(), "р/с");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаНаименованияБанка(),     "наименование банка");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаБИКБанка(),              "бик банка");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаКоррСчетБанка(),         "корсчет банка");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаКоррСчетБанка(),         "корр. счет банка");
	
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция НовыеРеквизитыБанковскогоСчета()
	
	РеквизитыБанковскогоСчета = Новый Структура;
	// возможные типы: СправочникСсылка.Организации или СправочникСсылка.Контрагенты
	РеквизитыБанковскогоСчета.Вставить("Владелец", Справочники.Организации.ПустаяСсылка()); 
	РеквизитыБанковскогоСчета.Вставить("НомерСчета", "");
	РеквизитыБанковскогоСчета.Вставить("НаименованиеБанка", "");
	РеквизитыБанковскогоСчета.Вставить("БИК", "");
	РеквизитыБанковскогоСчета.Вставить("КоррСчетБанка", "");
	РеквизитыБанковскогоСчета.Вставить("ВалютаДенежныхСредств", Справочники.Валюты.ПустаяСсылка());
	РеквизитыБанковскогоСчета.Вставить("Валютный", Ложь);
	
	Возврат РеквизитыБанковскогоСчета;
	
КонецФункции

Функция СформироватьДанныеРасчетныхСчетовИзСтроки(ПрочитанныеДанные)
	
	ДанныеРасчетныхСчетов = СтрРазделить(ПрочитанныеДанные, Символы.ПС, Ложь);
	Результат = Новый Массив;
	Для каждого ДанныеРасчетногоСчета Из ДанныеРасчетныхСчетов Цикл
		ПараметрыСчета = СоответствияРеквизитовРасчетныхСчетовДляЭлектронныхТаблиц();
		Для Каждого СтрокаПараметра Из ПараметрыСчета Цикл
			СтрокаПараметра.Значение = СтрНайти(ДанныеРасчетногоСчета, СтрокаПараметра[ИмяРеквизитаВФайле()]);
		КонецЦикла;
		ПараметрыСчета.Сортировать("Значение");
		МаксимальныйИндекс = ПараметрыСчета.Количество() - 1;
		Для Индекс = 0 По МаксимальныйИндекс Цикл
			ТекСтрока = ПараметрыСчета[Индекс];
			Если ТекСтрока.Значение = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если Индекс = МаксимальныйИндекс Тогда
				ТекСтрока.Значение = СокрЛП(Сред(
					ДанныеРасчетногоСчета, ТекСтрока.Значение + СтрДлина(ТекСтрока[ИмяРеквизитаВФайле()])));
			Иначе
				СледующаяСтрока = ПараметрыСчета[Индекс + 1];
				НачальнаяПозиция = ТекСтрока.Значение + СтрДлина(ТекСтрока[ИмяРеквизитаВФайле()]);
				ДлинаЗначения = СледующаяСтрока.Значение - НачальнаяПозиция;
				ТекСтрока.Значение = СокрЛП(Сред(ДанныеРасчетногоСчета, НачальнаяПозиция, ДлинаЗначения));
				Если Прав(ТекСтрока.Значение, 1) = "," Тогда
					ТекСтрока.Значение = Лев(ТекСтрока.Значение, СтрДлина(ТекСтрока.Значение) - 1);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Результат.Добавить(ПараметрыСчета);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СоответствияРеквизитовРасчетныхСчетовДляЭлектронныхТаблиц()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаНомераРасчетногоСчета(), "Р/с:");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаНаименованияБанка(),     "Банк:");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаБИКБанка(),              "БИК:");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяРеквизитаКоррСчетБанка(),         "К/с:");
	
	Возврат ТаблицаРеквизитов;
	
КонецФункции

#КонецОбласти

#Область Контрагенты

Процедура НайтиСоздатьКонтрагентов(ДанныеКонтрагентов, Ошибки)
	
	ТекстОшибки = НСтр("ru = 'Не удалось записать контрагента %1 по причине:
		|%2'");
	
	ТаблицаПоиска = Новый  ТаблицаЗначений;
	ТаблицаПоиска.Колонки.Добавить("ПорядковыйНомер", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ТаблицаПоиска.Колонки.Добавить("ИНН", ОбщегоНазначения.ОписаниеТипаСтрока(12));
	ТаблицаПоиска.Колонки.Добавить("КПП", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	ТаблицаПоиска.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПоиска.Колонки.Добавить("КраткоеНаименованиеКонтрагента", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаПоиска.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Сч = 1;
	Для Каждого ПрочитанныеДанные Из ДанныеКонтрагентов Цикл
		ПрочитанныеДанные.КраткоеНаименованиеКонтрагента = Справочники.Контрагенты.ПолучитьКраткоеНаименованиеКонтрагента(
			ПрочитанныеДанные.Наименование);
		
		СтрокаПоиска = ТаблицаПоиска.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПоиска, ПрочитанныеДанные);
		СтрокаПоиска.ПорядковыйНомер = Сч;
		Сч = Сч + 1;
	КонецЦикла;
	Если ЗначениеЗаполнено(ТаблицаПоиска) Тогда
		НайтиКонтрагентов(ТаблицаПоиска);
	КонецЕсли;
	
	ИменаКолонокРасчетныхСчетов = Новый Массив;
	Если ДанныеКонтрагентов.Колонки.Найти(ИмяРеквизитаДляРасчетныхСчетов()) <> Неопределено Тогда
		ДанныеСчетаОднимРеквизитом = Истина;
	Иначе
		ДанныеСчетаОднимРеквизитом = Ложь;
		Для Каждого Колонка Из ДанныеКонтрагентов.Колонки Цикл
			Если СтрНайти(Колонка.Имя, ИмяРеквизитаДляРасчетныхСчетов()) Тогда
				ИменаКолонокРасчетныхСчетов.Добавить(Колонка.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ПрочитанныеДанные Из ДанныеКонтрагентов Цикл
		
		Если ЗначениеЗаполнено(ПрочитанныеДанные.ИНН) Тогда
			ПараметрыПоиска = Новый Структура("ИНН", ПрочитанныеДанные.ИНН);
			НайденныеСтроки = ТаблицаПоиска.НайтиСтроки(ПараметрыПоиска);
			Если ЗначениеЗаполнено(ПрочитанныеДанные.КПП) Тогда
				КПП = ПрочитанныеДанные.КПП;
			Иначе
				КПП = "";
			КонецЕсли;
			Для Каждого Строка Из НайденныеСтроки Цикл
				Если Строка.КПП = КПП Тогда
					ПрочитанныеДанные.Ссылка = Строка.Ссылка;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ПараметрыПоиска = Новый Структура("Наименование", ПрочитанныеДанные.Наименование);
			НайденныеСтроки = ТаблицаПоиска.НайтиСтроки(ПараметрыПоиска);
			Для Каждого Строка Из НайденныеСтроки Цикл
				Если Не ЗначениеЗаполнено(Строка.ИНН) Тогда
					ПрочитанныеДанные.Ссылка = Строка.Ссылка;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПрочитанныеДанные.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеЗаполнения = ДанныеЗаполненияКонтрагента(ПрочитанныеДанные);
		
		ДанныеРасчетныхСчетов = Новый Массив;
		Если ДанныеСчетаОднимРеквизитом Тогда
			Если ЗначениеЗаполнено(ПрочитанныеДанные[ИмяРеквизитаДляРасчетныхСчетов()]) Тогда
				ДанныеРасчетныхСчетов = СформироватьДанныеРасчетныхСчетовИзСтроки(
					ПрочитанныеДанные[ИмяРеквизитаДляРасчетныхСчетов()]);
			КонецЕсли;
		Иначе
			Для Каждого ИмяКолонки Из ИменаКолонокРасчетныхСчетов Цикл
				ДанныеРасчетныхСчетов.Добавить(ПрочитанныеДанные[ИмяКолонки]);
			КонецЦикла;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			НовыйКонтрагент = Справочники.Контрагенты.СоздатьКонтрагента(ДанныеЗаполнения);
			
			ПрочитанныеДанные.Ссылка = НовыйКонтрагент;
			
			ОсновнойБанковскийСчет = СоздатьБанковскиеСчета(НовыйКонтрагент, ДанныеРасчетныхСчетов);
			
			Если ЗначениеЗаполнено(ОсновнойБанковскийСчет) Тогда
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник.Контрагенты");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", НовыйКонтрагент);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				КонтрагентОбъект = НовыйКонтрагент.ПолучитьОбъект();
				КонтрагентОбъект.ДополнительныеСвойства.Очистить();
				КонтрагентОбъект.ОсновнойБанковскийСчет = ОсновнойБанковскийСчет;
				
				КонтрагентОбъект.Записать();
			КонецЕсли;
			
			ЗаполнитьАдресКонтрагента(НовыйКонтрагент, ПрочитанныеДанные);
			
			Если ЗначениеЗаполнено(ПрочитанныеДанные.Телефон) Тогда
				УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, ПрочитанныеДанные.Телефон,
					ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПрочитанныеДанные.EMail) Тогда
				УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, ПрочитанныеДанные.EMail,
					ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EmailКонтрагенты"));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПрочитанныеДанные.Сайт) Тогда
				УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, ПрочитанныеДанные.Сайт,
					ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ДругаяИнформацияКонтрагенты"));
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			ТекстСообщения = СтрШаблон(
				ТекстОшибки, ДанныеЗаполнения.Наименование,
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, "", ТекстСообщения, "");
			
			ТекстСообщения = СтрШаблон(
				ТекстОшибки, ДанныеЗаполнения.Наименование,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ЗаписьЖурналаРегистрации(
				ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Справочники.Контрагенты, , ТекстСообщения);
				
			ОтменитьТранзакцию();
				
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьАдресКонтрагента(НовыйКонтрагент, ПрочитанныеДанные)
	
	Если ЗначениеЗаполнено(ПрочитанныеДанные.Адрес) Тогда
		ЮрАдрес = ПрочитанныеДанные.Адрес;
		ФактАдрес = ПрочитанныеДанные.Адрес;
	Иначе
		ЮрАдрес = ?(ЗначениеЗаполнено(ПрочитанныеДанные.ЮридическийАдрес),
			ПрочитанныеДанные.ЮридическийАдрес, ПрочитанныеДанные.ФактическийАдрес);
		ФактАдрес = ?(ЗначениеЗаполнено(ПрочитанныеДанные.ФактическийАдрес),
			ПрочитанныеДанные.ФактическийАдрес, ПрочитанныеДанные.ЮридическийАдрес);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЮрАдрес) Тогда
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, ЮрАдрес,
			ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента"));
	КонецЕсли;

	Если ЗначениеЗаполнено(ФактАдрес) Тогда
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, ФактАдрес,
			ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента"));
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(НовыйКонтрагент, ФактАдрес,
			ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента"));
	КонецЕсли;
	
КонецПроцедуры

Функция ОпределитьНомераКолонокДанныхКонтрагентов(СоставДанных, Текст)
	
	КоличествоСтрок = СтрЧислоСтрок(Текст);
	КоличествоРасчетныхСчетов = 1;
	СтрокаШапкаТаблицы = 0;
	Для Сч = 1 По КоличествоСтрок Цикл
		Если СтрокаШапкаТаблицы <> 0 Тогда
			Прервать;
		КонецЕсли;
		Строка = СтрПолучитьСтроку(Текст, Сч);
		Подстроки = СтрРазделить(Строка, ";", Истина);
		Для Колонка = 0 По Подстроки.ВГраница() Цикл
			ПрочитанноеЗначение = НРег(СокрЛП(Подстроки[Колонка]));
			НайденныйРеквизит = СоставДанных.Найти(ПрочитанноеЗначение, ИмяРеквизитаВФайле());
			Если НайденныйРеквизит = Неопределено Или Не ЗначениеЗаполнено(ПрочитанноеЗначение) Тогда
				Если Колонка = 0 Тогда
					// В строке не содержится описания реквизитов
					Прервать;
				Иначе
					Продолжить;
				КонецЕсли;
			Иначе
				СтрокаШапкаТаблицы = Сч;
			КонецЕсли;
			
			Если ТипЗнч(НайденныйРеквизит.Значение) <> Тип("ТаблицаЗначений") Тогда
				// Не содержит вложенных реквизитов
				НайденныйРеквизит.Значение = Колонка;
			Иначе
				// Необходимо прочитать вложенные реквизиты
				ВложеннаяСтрока = СтрПолучитьСтроку(Текст, Сч + 1);
				ВложенныеПодстроки = СтрРазделить(ВложеннаяСтрока, ";", Истина);
				ВложеннаяКолонка = Колонка;
				Если НайденныйРеквизит[ИмяРеквизита1С()] = ИмяРеквизитаДляРасчетныхСчетов() Тогда
					// Расчетные счета записываются в файле подряд, их может быть несколько на одного контрагента
					// поэтому в структуре данных заведем отдельные реквизиты на каждый расчетный счет
					НайденныйРеквизит = ДобавитьСоответствие(
						СоставДанных, СтрШаблон("%1%2", ИмяРеквизитаДляРасчетныхСчетов(), Формат(КоличествоРасчетныхСчетов, "ЧГ=")));
					НайденныйРеквизит.Значение = СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов();
					КоличествоРасчетныхСчетов = КоличествоРасчетныхСчетов + 1;
				КонецЕсли;
				НачалосьЗаполнениеРеквизитов = Ложь;
				РазделительРасчетныхСчетов = РазделительРасчетныхСчетов();
				Пока ВложеннаяКолонка < ВложенныеПодстроки.ВГраница() Цикл
					ПрочитанноеЗначение = НРег(СокрЛП(ВложенныеПодстроки[ВложеннаяКолонка]));
					Если Не ЗначениеЗаполнено(ПрочитанноеЗначение)
						// В случае с расчетными счетами нужно контролировать,
						// что не началось чтение реквизитов следующего счета
						Или (ПрочитанноеЗначение = РазделительРасчетныхСчетов И НачалосьЗаполнениеРеквизитов) Тогда
						Прервать;
					Иначе
						НачалосьЗаполнениеРеквизитов = Истина;
						ВложенныйРеквизит = НайденныйРеквизит.Значение.Найти(ПрочитанноеЗначение, ИмяРеквизитаВФайле());
						Если ВложенныйРеквизит <> Неопределено Тогда
							ВложенныйРеквизит.Значение = ВложеннаяКолонка;
						КонецЕсли;
					КонецЕсли;
					ВложеннаяКолонка = ВложеннаяКолонка + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Расчетные счета выгружаются с порядковым номером ("РасчетныйСчет1" и т.д.), запись с именем "РасчетныйСчет"
	// без порядкового номера удаляем, т.к. она содержит исключительно служебную информацию
	// и далее использоваться не будет
	СтрокаРасчетногоСчета = СоставДанных.Найти(ИмяРеквизитаДляРасчетныхСчетов(), ИмяРеквизита1С());
	Если СтрокаРасчетногоСчета <> Неопределено Тогда
		СоставДанных.Удалить(СтрокаРасчетногоСчета);
	КонецЕсли;
	
	Возврат СтрокаШапкаТаблицы + 1;
	
КонецФункции

Функция РеквизитыКонтрагентовОбязательныеКЗаполнению()
	
	ОбязательныеРеквизиты = Новый Массив;
	ОбязательныеРеквизиты.Добавить("Наименование");
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

Процедура НайтиКонтрагентов(ТаблицаПоиска)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаПоиска.ИНН КАК ИНН,
	|	ТаблицаПоиска.КПП КАК КПП,
	|	ТаблицаПоиска.КраткоеНаименованиеКонтрагента КАК Наименование,
	|	ТаблицаПоиска.ПорядковыйНомер КАК ПорядковыйНомер
	|ПОМЕСТИТЬ ВТ_ДанныеИзФайла
	|ИЗ
	|	&ТаблицаПоиска КАК ТаблицаПоиска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеИзФайла.ИНН КАК ИНН,
	|	ВТ_ДанныеИзФайла.КПП КАК КПП,
	|	ВТ_ДанныеИзФайла.ПорядковыйНомер КАК ПорядковыйНомер,
	|	Контрагенты.Ссылка КАК Ссылка,
	|	ВТ_ДанныеИзФайла.Наименование КАК Наименование,
	|	1 КАК Порядок
	|ИЗ
	|	ВТ_ДанныеИзФайла КАК ВТ_ДанныеИзФайла
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ВТ_ДанныеИзФайла.ИНН = Контрагенты.ИНН
	|			И ВТ_ДанныеИзФайла.КПП = Контрагенты.КПП
	|ГДЕ
	|	ВТ_ДанныеИзФайла.ИНН <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ДанныеИзФайла.ИНН,
	|	ВТ_ДанныеИзФайла.КПП,
	|	ВТ_ДанныеИзФайла.ПорядковыйНомер,
	|	Контрагенты.Ссылка,
	|	ВТ_ДанныеИзФайла.Наименование,
	|	2
	|ИЗ
	|	ВТ_ДанныеИзФайла КАК ВТ_ДанныеИзФайла
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ВТ_ДанныеИзФайла.Наименование = Контрагенты.Наименование
	|ГДЕ
	|	ВТ_ДанныеИзФайла.ИНН = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядковыйНомер,
	|	Порядок";
	
	Запрос.УстановитьПараметр("ТаблицаПоиска", ТаблицаПоиска);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого СтрокаПоиска Из ТаблицаПоиска Цикл
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(СтрокаПоиска.ПорядковыйНомер, "ПорядковыйНомер") Тогда
			СтрокаПоиска.Ссылка = Выборка.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеЗаполненияКонтрагента(СтрокаДанных)
	
	ДанныеЗаполнения = Новый Структура;
	
	// Основная информация о контрагенте
	ДанныеЗаполнения.Вставить("Наименование", СтрокаДанных.КраткоеНаименованиеКонтрагента);
	ДанныеЗаполнения.Вставить("НаименованиеПолное", УдалитьКавычки(СтрокаДанных.Наименование));
	ДанныеЗаполнения.Вставить("ИНН", СтрокаДанных.ИНН);
	
	Если ИдентификационныеНомераНалогоплательщиков.ЭтоИННЮридическогоЛица(ДанныеЗаполнения.ИНН) Тогда
		ДанныеЗаполнения.Вставить("КПП", СтрокаДанных.КПП);
		КодПричиныПостановкиНаУчет = ИдентификационныеНомераНалогоплательщиков.КодПричиныПостановкиНаУчет(
			ДанныеЗаполнения.КПП);
		Если КодПричиныПостановкиНаУчет <> Неопределено Тогда
			ДанныеЗаполнения.Вставить("ОбособленноеПодразделение",
				Не ИдентификационныеНомераНалогоплательщиков.ЭтоПричинаПостановкиНаУчетОрганизацииВЦелом(
				КодПричиныПостановкиНаУчет));
		КонецЕсли;
	КонецЕсли;
	
	Если ИдентификационныеНомераНалогоплательщиков.ЭтоИННФизическогоЛица(ДанныеЗаполнения.ИНН) Тогда
		ДанныеЗаполнения.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	Иначе
		ДанныеЗаполнения.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("РегистрационныйНомер", СтрокаДанных.ОГРН);
	ДанныеЗаполнения.Вставить("КодПоОКПО", СтрокаДанных.КодПоОКПО);
	ДанныеЗаполнения.Вставить("Комментарий", СтрокаДанных.Комментарий);
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция СоставРеквизитовКонтрагентов()
	
	Реквизиты = НовыеУниверсальныеРеквизитыКонтрагентов();
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(
		Реквизиты, "Наименование", "название контрагента", "название контрагента");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "ИНН", "инн", "инн");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "КПП", "кпп", "кпп");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "ОГРН", "огрн", "огрн");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "КодПоОКПО", "окпо", "окпо");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "Адрес", "адрес контрагента");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "ЮридическийАдрес", , "юридический адрес");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "ФактическийАдрес", , "фактический адрес");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "Комментарий", "комментарий");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "Телефон", "телефон", "телефон");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "EMail", "электронная почта", "email");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "Сайт", , "сайт организации");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "КонтактноеЛицо", "контактное лицо");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, ИмяРеквизитаДляРасчетныхСчетов(), "расчетный счет");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "Ссылка");
	ДобавитьСтрокуРеквизитаДляНесколькихФорматов(Реквизиты, "КраткоеНаименованиеКонтрагента");
	Возврат Реквизиты;
	
КонецФункции

Процедура ЗаполнитьСоответствияРеквизитовКонтрагентовДляЗагрузкиИзТекстовогоФайла(ТаблицаРеквизитов)
	
	УниверсальныйСоставРеквизитов = СоставРеквизитовКонтрагентов();
	Для Каждого СтрокаРеквизит Из УниверсальныйСоставРеквизитов Цикл
		НовыйРеквизит = ТаблицаРеквизитов.Добавить();
		НовыйРеквизит[ИмяРеквизита1С()] = СтрокаРеквизит[ИмяРеквизита1С()];
		НовыйРеквизит[ИмяРеквизитаВФайле()] = СтрокаРеквизит[ИмяРеквизитаВФайле() + ИмяФорматаТекстовогоФайла()];
		Если НовыйРеквизит[ИмяРеквизита1С()] = "КонтактноеЛицо" Тогда
			НовыйРеквизит.Значение = СоответствияВложенныхРеквизитовКонтактногоЛица();
		ИначеЕсли НовыйРеквизит[ИмяРеквизита1С()] = ИмяРеквизитаДляРасчетныхСчетов() Тогда
			НовыйРеквизит.Значение = СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСоответствияРеквизитовКонтрагентовДляЗагрузкиИзЭлектроннойТаблицы(ТаблицаРеквизитов)
	
	УниверсальныйСоставРеквизитов = СоставРеквизитовКонтрагентов();
	Для Каждого СтрокаРеквизит Из УниверсальныйСоставРеквизитов Цикл
		НовыйРеквизит = ТаблицаРеквизитов.Добавить();
		НовыйРеквизит[ИмяРеквизита1С()] = СтрокаРеквизит[ИмяРеквизита1С()];
		НовыйРеквизит[ИмяРеквизитаВФайле()] = СтрокаРеквизит[ИмяРеквизитаВФайле() + ИмяФорматаЭлектронныхТаблиц()];
	КонецЦикла;
	
КонецПроцедуры

Функция СоответствияВложенныхРеквизитовКонтактногоЛица()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, "Должность", "должность");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Наименование", "фио");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция КонтрагентПоНаименованию(ЗагруженныеКонтрагенты, Наименование, Ошибки)
	
	НайденныйКонтрагент = ЗагруженныеКонтрагенты.Найти(Наименование, "Наименование");
	Если НайденныйКонтрагент = Неопределено Тогда
		РеквизитыКонтрагента = НовыеРеквизитыКонтрагента();
		НовыйКонтрагент = РеквизитыКонтрагента.Добавить();
		НовыйКонтрагент.Наименование = Наименование;
		НайтиСоздатьКонтрагентов(РеквизитыКонтрагента, Ошибки);
		Строка = ЗагруженныеКонтрагенты.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, НовыйКонтрагент);
		Возврат НовыйКонтрагент.Ссылка;
	Иначе
		Возврат НайденныйКонтрагент.Ссылка;
	КонецЕсли;
	
КонецФункции

Функция НовыеРеквизитыКонтрагента()
	
	Возврат НоваяТаблицаДанных(СоставРеквизитовКонтрагентов());
	
КонецФункции

Функция НовыеУниверсальныеРеквизитыКонтрагентов()
	
	Реквизиты = Новый ТаблицаЗначений;
	Реквизиты.Колонки.Добавить(ИмяРеквизита1С(), ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	Реквизиты.Колонки.Добавить(
		ИмяРеквизитаВФайле() + ИмяФорматаТекстовогоФайла(), ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	Реквизиты.Колонки.Добавить(
		ИмяРеквизитаВФайле() + ИмяФорматаЭлектронныхТаблиц(), ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#Область Документы

Функция НовыеДокументыКПроведению()
	
	ДокументыКПроведению = Новый ТаблицаЗначений;
	ДокументыКПроведению.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ДокументыКПроведению.Колонки.Добавить("Ссылка");
	Возврат ДокументыКПроведению;
	
КонецФункции

Процедура ЗагрузитьДокументыИзТекстовыхФайлов(РазделДанных, ЗагружаемыеДанные, ТаблицаТиповДокументов, Ошибки)
	
	ТаблицаДанных = ЗагружаемыеДанные[РазделДанных];
	
	ТаблицаДанных.Сортировать("ТипДокумента, Дата, Номер");
	
	Если РазделДанных = ИмяДокументыДвиженияДенег() Тогда
		СуммаРасходУСН = ТаблицаДанных.Итог("РасходыУСН");
		СуммаПатент = ТаблицаДанных.Итог("СуммаПатент");
		Если ЗначениеЗаполнено(СуммаПатент) Или ЗначениеЗаполнено(СуммаРасходУСН) Тогда
			ПроверитьИзменитьСистемуНалогообложения(ЗагружаемыеДанные.Организация, СуммаРасходУСН, СуммаПатент);
		КонецЕсли;
		ЗагрузитьДокументыДвиженияДенег(ТаблицаДанных, ЗагружаемыеДанные, Ошибки);
	Иначе
		ЗагрузитьНоменклатуру(РазделДанных, ЗагружаемыеДанные, Ошибки);
		СтрокиТиповДокументов = ТаблицаТиповДокументов.НайтиСтроки(Новый Структура("РазделДанных", РазделДанных));
		ПараметрыДокумента = НовыйПараметрыДокумента();
		Для Каждого СтрокаТипаДокумента Из СтрокиТиповДокументов Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыДокумента, СтрокаТипаДокумента);
			ЗагрузитьВходящиеИсходящиеДокументы(ТаблицаДанных, ЗагружаемыеДанные, Ошибки, ПараметрыДокумента);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПровестиДокументы(ТаблицаДокументов)
	
	ТаблицаДокументов.Сортировать("Дата");
	Для каждого Строка Из ТаблицаДокументов Цикл
		Попытка
			ДокОбъект = Строка.Ссылка.ПолучитьОбъект();
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ЗаписьЖурналаРегистрации(
				ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, ДокОбъект.Метаданные(), ДокОбъект.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьКассовуюКнигуИзЭлектронныхТаблиц(ЗагружаемыеДанные, Ошибки)
	
	ПрочитанныеДанные = ЗагружаемыеДанные[ИмяКассоваяКнига()];
	ВидыОперацийКассовыхДокументов = ТаблицаСопоставления(ИмяМакетаВидыОперацийКассовыхДокументов());
	Организация = ЗагружаемыеДанные.Организация;
	Подразделение = ОсновноеПодразделениеОрганизации(Организация);
	Для Каждого Строка Из ПрочитанныеДанные Цикл
		ПроверитьУстановитьЗаписьУчетнойПолитики(Организация, Строка.Дата);
		РеквизитыДокумента = НовыеРеквизитыДокумента();
		РеквизитыДокумента.Организация = Организация;
		РеквизитыДокумента.СуммаДокумента =
			?(ЗначениеЗаполнено(Строка.СуммаПоступления), Строка.СуммаПоступления, Строка.СуммаСписания);
		РеквизитыДокумента.Дата = Строка.Дата;
		РеквизитыДокумента.Номер = СформироватьНомерДокумента(
			Организация, Подразделение, Строка.ТипДокумента, Строка.Номер);
		Если ДокументНайденВБазе(РеквизитыДокумента, Строка.ТипДокумента) Тогда
			Продолжить;
		КонецЕсли;
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ТипДокумента", Строка.ТипДокумента);
		СтруктураПоиска.Вставить("СчетУчета", Строка.СчетУчета);
		СтрокиВидовОпераций = ВидыОперацийКассовыхДокументов.НайтиСтроки(СтруктураПоиска);
		Если ЗначениеЗаполнено(СтрокиВидовОпераций) Тогда
			ВидОперации = СтрокиВидовОпераций[0].ВидОперации;
		Иначе
			ВидОперации = "";
		КонецЕсли;
		Если Строка.ТипДокумента = ИмяДокументаПКО() Тогда
			РеквизитыДокумента.ВидОперации = ?(ЗначениеЗаполнено(ВидОперации), Перечисления.ВидыОперацийПКО[ВидОперации],
				Перечисления.ВидыОперацийПКО.ПрочийПриход);
		Иначе
			РеквизитыДокумента.ВидОперации = ?(ЗначениеЗаполнено(ВидОперации), Перечисления.ВидыОперацийРКО[ВидОперации],
				Перечисления.ВидыОперацийРКО.ПрочийРасход);
		КонецЕсли;
		НовыйДокумент = Документы[Строка.ТипДокумента].СоздатьДокумент();
		НовыйДокумент.Дата = РеквизитыДокумента.Дата;
		Если ЗначениеЗаполнено(СтрокиВидовОпераций) И ЗначениеЗаполнено(СтрокиВидовОпераций[0].СчетУчетаБП) Тогда
			СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду(СтрокиВидовОпераций[0].СчетУчетаБП);
		ИначеЕсли ЗначениеЗаполнено(Строка.СчетУчета) Тогда
			СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду(Строка.СчетУчета);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчета) Тогда
			НовыйДокумент.СчетУчетаРасчетовСКонтрагентом = СчетУчета;
		КонецЕсли;
		НовыйДокумент.Заполнить(РеквизитыДокумента);
		Если ЗначениеЗаполнено(Строка.Контрагент) Тогда
			Если ТипЗнч(НовыйДокумент.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
				НовыйДокумент.Контрагент = КонтрагентПоНаименованию(
					ЗагружаемыеДанные[ИмяКонтрагенты()], Строка.Контрагент, Ошибки);
			ИначеЕсли ТипЗнч(НовыйДокумент.Контрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				НовыйДокумент.Контрагент = НайтиСоздатьФизЛицо(Строка.Контрагент);
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(НовыйДокумент.Контрагент)
			И ТипЗнч(НовыйДокумент.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			НовыйДокумент.ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.ДоговорКонтрагентаИзОбъекта(НовыйДокумент);
		КонецЕсли;
		Если ЗначениеЗаполнено(НовыйДокумент.РасшифровкаПлатежа) Тогда
			СтрокаРасшифровкаПлатежа = НовыйДокумент.РасшифровкаПлатежа[0];
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровкаПлатежа, НовыйДокумент);
			СтрокаРасшифровкаПлатежа.СуммаПлатежа                 = РеквизитыДокумента.СуммаДокумента;
			СтрокаРасшифровкаПлатежа.СуммаВзаиморасчетов          = РеквизитыДокумента.СуммаДокумента;
			СтрокаРасшифровкаПлатежа.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.Автоматически;
			СтрокаРасшифровкаПлатежа.КурсВзаиморасчетов           = 1;
			СтрокаРасшифровкаПлатежа.КратностьВзаиморасчетов      = 1;
			СтрокаРасшифровкаПлатежа.СтавкаНДС                    = Перечисления.СтавкиНДС.БезНДС;
		КонецЕсли;
		// В некоторых видах документов сумма очищается в обработке заполнения
		НовыйДокумент.СуммаДокумента = РеквизитыДокумента.СуммаДокумента;
		НовыйДокумент.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
		СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(НовыйДокумент, РежимЗаписиДокумента.Запись);
		НовыйДокумент.Номер = РеквизитыДокумента.Номер;
		Если НовыйДокумент.ПроверитьЗаполнение() Тогда
			СтрокаДокументКПроведению = ЗагружаемыеДанные.ДокументыКПроведению.Добавить();
			СтрокаДокументКПроведению.Дата = НовыйДокумент.Дата;
		Иначе
			СтрокаДокументКПроведению = Неопределено;
		КонецЕсли;
		НовыйДокумент.ОбменДанными.Загрузка = Истина;
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		Если СтрокаДокументКПроведению <> Неопределено Тогда
			СтрокаДокументКПроведению.Ссылка = НовыйДокумент.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьДокументыПокупкиПродажи(ИмяРаздела, ЗагружаемыеДанные, Ошибки, ТаблицаТиповДокументов)
	
	СтрокиТиповДокументов = ТаблицаТиповДокументов.НайтиСтроки(Новый Структура("РазделДанных", ИмяРаздела));
	Для Каждого СтрокаТипа Из СтрокиТиповДокументов Цикл
		ПараметрыДокумента = НовыйПараметрыДокумента();
		ЗаполнитьЗначенияСвойств(ПараметрыДокумента, СтрокаТипа);
		ЗагрузитьДокументыПокупкиПродажиИзЭлектроннойТаблицы(ЗагружаемыеДанные, ИмяРаздела, Ошибки, ПараметрыДокумента);
	КонецЦикла;
	
КонецПроцедуры

Функция ОпределитьНомераКолонокДокументов(СоставДанных, Текст)
	
	// Структура файла предполагает размещение вложенных реквизитов в одной строке с основными реквизитами,
	// владельцы вложенных реквизитов определяются в строке выше и будут прочитаны в первую очередь
	КоличествоСтрок = СтрЧислоСтрок(Текст);
	СтрокаШапкаТаблицы = 0;
	// Имена основных и вложенных реквизитов могут совпадать, поэтому необходимо запомнить найденные вложенные реквизиты
	// чтобы сразу пропускать их при обходе основных
	КолонкиВложенныхРеквизитов = Новый Массив;
	Для Сч = 1 По КоличествоСтрок Цикл
		Если СтрокаШапкаТаблицы <> 0 Тогда
			Прервать;
		КонецЕсли;
		Строка = СтрПолучитьСтроку(Текст, Сч);
		Подстроки = СтрРазделить(Строка, ";", Истина);
		Для Колонка = 0 По Подстроки.ВГраница() Цикл
			Если КолонкиВложенныхРеквизитов.Найти(Колонка) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПрочитанноеЗначение = НРег(СокрЛП(Подстроки[Колонка]));
			НайденныйРеквизит = СоставДанных.Найти(ПрочитанноеЗначение, ИмяРеквизитаВФайле());
			Если НайденныйРеквизит = Неопределено Или Не ЗначениеЗаполнено(ПрочитанноеЗначение) Тогда
				Продолжить;
			ИначеЕсли ТипЗнч(НайденныйРеквизит.Значение) = Тип("ТаблицаЗначений") Тогда
				СтрокаВложенныхРеквизитов = СтрПолучитьСтроку(Текст, Сч + 1);
				ПодстрокиВложенныхРеквизитов = СтрРазделить(СтрокаВложенныхРеквизитов, ";", Истина);
				Для НомерВложенногоРеквизита = Колонка По Подстроки.ВГраница() Цикл
					ИмяРеквизита = НРег(СокрЛП(ПодстрокиВложенныхРеквизитов[НомерВложенногоРеквизита]));
					ВложенныйРеквизит = НайденныйРеквизит.Значение.Найти(ИмяРеквизита, ИмяРеквизитаВФайле());
					Если ВложенныйРеквизит <> Неопределено Тогда
						ВложенныйРеквизит.Значение = НомерВложенногоРеквизита;
						КолонкиВложенныхРеквизитов.Добавить(НомерВложенногоРеквизита);
					Иначе
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				НайденныйРеквизит.Значение = Колонка;
				СтрокаШапкаТаблицы = Сч;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Возврат СтрокаШапкаТаблицы;
	
КонецФункции

Функция ПрочитатьКассовуюКнигуИзЭлектроннойТаблицы(СоставДанных, Таблица)
	
	ТаблицаДанных = НоваяТаблицаДанных(СоставДанных);
	ИменаНечитаемыхОбластей = НечитаемыеОбластиКассовойКниги();
	ОбязательныеРеквизиты = РеквизитыОбязательныеКЗаполнению(ИмяКассоваяКнига());
	Для Каждого Область Из Таблица.Области Цикл
		Если ИменаНечитаемыхОбластей.Найти(Область.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ФорматКассовойКниги = ОпределитьФорматЛистаКассовойКниги(Таблица, Область);
		
		Если ФорматКассовойКниги = ИмяОдностраничногоФорматаКассовойКниги() Тогда
			ПрочитатьЛистКассовойКнигиВОдностраничномФормате(
				Таблица, Область, СоставДанных, ТаблицаДанных, ОбязательныеРеквизиты);
		Иначе
			ПрочитатьЛистКассовойКнигиВДвухстраничномФормате(
				Таблица, Область, СоставДанных, ТаблицаДанных, ОбязательныеРеквизиты);
		КонецЕсли;
	КонецЦикла;
	
	ОбработатьДанныеКассовойКниги(ТаблицаДанных);
	
	Возврат ТаблицаДанных;
	
КонецФункции

Процедура ПрочитатьЛистКассовойКнигиВОдностраничномФормате(
	Таблица, Область, СоставДанных, ТаблицаДанных, ОбязательныеРеквизиты)
	
	// Читаем дату - за какой день сформирован лист кассовой книги
	РезультатПоискаДаты = ДатаЛистаКассовойКнигиВОдностраничномФормате(Таблица, Область);
	
	Если Не ЗначениеЗаполнено(РезультатПоискаДаты.ДатаЛиста) Тогда
		Возврат;
	КонецЕсли;
	Попытка
		ДатаДокумента = ДатаИзСтроки(РезультатПоискаДаты.ДатаЛиста);
	Исключение
		Возврат;
	КонецПопытки;
	
	// Для каждого листа номера колонок в шапке определяем заново
	СоставДанных.ЗаполнитьЗначения(Неопределено, "Значение");
	СтрокаШапкиТаблицы =
		ОпределитьНомераКолонокВЭлектроннойТаблице(
		СоставДанных, Таблица, Область.Имя, РезультатПоискаДаты.НомерСтроки + 1);
	
	Если Не ЗначениеЗаполнено(СтрокаШапкиТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	// Определим где началась таблица с данными
	Начало = СтрокаШапкиТаблицы + 1;
	СтрокаНачалаТаблицы = 0;
	Для НомерСтроки = Начало По Область.Низ Цикл
		Если СтрокаНачалаТаблицы <> 0 Тогда
			Прервать;
		КонецЕсли;
		Для НомерКолонки = 1 По Таблица.ШиринаСтраницы Цикл
			ТекстЯчейки = Таблица.Область(НомерСтроки, НомерКолонки).Текст;
			Если СокрЛП(ТекстЯчейки) = НачалоТаблицыКассовойКниги() Тогда
				СтрокаНачалаТаблицы = НомерСтроки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если СтрокаНачалаТаблицы = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Читаем саму таблицу
	ДанныеДляЗаполнения = Новый Структура("Дата", ДатаДокумента);
	ЗаполнитьТаблицуДанных(
		ТаблицаДанных, СоставДанных, Таблица, СтрокаНачалаТаблицы,
		Область.Имя, ОбязательныеРеквизиты, Истина, ДанныеДляЗаполнения);
	
КонецПроцедуры

Процедура ПрочитатьЛистКассовойКнигиВДвухстраничномФормате(
	Таблица, Область, СоставДанных, ТаблицаДанных, ОбязательныеРеквизиты)
	
	// Читаем дату - за какой день сформирован лист кассовой книги
	РезультатПоискаДаты = ДатаЛистаКассовойКнигиВДвухстраничномФормате(Таблица, Область);
	
	Если Не ЗначениеЗаполнено(РезультатПоискаДаты.ДатаЛиста) Тогда
		Возврат;
	КонецЕсли;
	
	ШиринаЛиста = ШиринаЛистаКассовойКнигиПриДвухстраничномФормате(Таблица, Область);
	
	// Для каждого листа номера колонок в шапке определяем заново
	СоставДанных.ЗаполнитьЗначения(Неопределено, "Значение");
	СтрокаШапкиТаблицы =
		ОпределитьНомераКолонокВЭлектроннойТаблице(
		СоставДанных, Таблица, Область.Имя, РезультатПоискаДаты.НомерСтроки + 1, , ШиринаЛиста);
	
	Если Не ЗначениеЗаполнено(СтрокаШапкиТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	// Определим где началась таблица с данными
	Начало = СтрокаШапкиТаблицы + 1;
	СтрокаНачалаТаблицы = 0;
	Для НомерСтроки = Начало По Область.Низ Цикл
		Если СтрокаНачалаТаблицы <> 0 Тогда
			Прервать;
		КонецЕсли;
		Для НомерКолонки = 1 По Таблица.ШиринаСтраницы Цикл
			ТекстЯчейки = Таблица.Область(НомерСтроки, НомерКолонки).Текст;
			Если СокрЛП(ТекстЯчейки) = НачалоТаблицыКассовойКниги() Тогда
				СтрокаНачалаТаблицы = НомерСтроки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если СтрокаНачалаТаблицы = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Читаем саму таблицу
	ДанныеДляЗаполнения = Новый Структура("Дата", РезультатПоискаДаты.ДатаЛиста);
	ЗаполнитьТаблицуДанных(
		ТаблицаДанных, СоставДанных, Таблица, СтрокаНачалаТаблицы,
		Область.Имя, ОбязательныеРеквизиты, Истина, ДанныеДляЗаполнения);
	
	// Чтение второй страницы листа кассовой книги
	СоставДанных.ЗаполнитьЗначения(Неопределено, "Значение");
	СтрокаШапкиТаблицы = ОпределитьНомераКолонокВЭлектроннойТаблице(
		СоставДанных, Таблица, Область.Имя, РезультатПоискаДаты.НомерСтроки + 1, ШиринаЛиста + 1);
	Если ЗначениеЗаполнено(СтрокаШапкиТаблицы) Тогда
		ЗаполнитьТаблицуДанных(ТаблицаДанных, СоставДанных, Таблица, СтрокаШапкиТаблицы + 1,
			Область.Имя, ОбязательныеРеквизиты, Истина, ДанныеДляЗаполнения);
	КонецЕсли;
	
	// Обрабатываем случаи переноса наименования контрагента в разные строки
	ПредыдущаяСтрока = Неопределено;
	СтрокиКУдалению = Новый Массив;
	Для Каждого ПрочитаннаяСтрока Из ТаблицаДанных Цикл
		Если ЗначениеЗаполнено(ПрочитаннаяСтрока.Номер) Тогда
			ПредыдущаяСтрока = ПрочитаннаяСтрока;
		ИначеЕсли ЗначениеЗаполнено(ПрочитаннаяСтрока.Контрагент) И ПредыдущаяСтрока <> Неопределено Тогда
			ПредыдущаяСтрока.Контрагент = ПредыдущаяСтрока.Контрагент + " " + ПрочитаннаяСтрока.Контрагент;
			СтрокиКУдалению.Добавить(ПрочитаннаяСтрока);
		КонецЕсли;
	КонецЦикла;
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		ТаблицаДанных.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Функция ОпределитьФорматЛистаКассовойКниги(Таблица, Область)
	
	Для НомерСтроки = Область.Верх По Область.Низ Цикл
		Если Таблица.Область(НомерСтроки, 1).Текст = ПризнакДвухстраничногоФорматаКассовойКниги() Тогда
			Возврат ИмяДвухстраничногоФорматаКассовойКниги();
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИмяОдностраничногоФорматаКассовойКниги();
	
КонецФункции

Процедура ОбработатьДанныеДокументовДвиженияДенег(Данные)
	
	ЧисловыеРеквизиты = ЧисловыеРеквизитыДокументовДвиженияДенег();
	Для Каждого Строка Из Данные Цикл
		
		Для Каждого ИмяРеквизита Из ЧисловыеРеквизиты Цикл
			Строка[ИмяРеквизита] = ОбработатьЧисловойРеквизит(Строка[ИмяРеквизита]);
		КонецЦикла;
		
		Если НРег(Строка.ВидДенежныхСредств) = ТипДенегБанк() Тогда
			Строка.ТипДокумента = ?(ЗначениеЗаполнено(Строка.СуммаПоступления), "п.п. вх.", "п.п. исх.");
		ИначеЕсли НРег(Строка.ВидДенежныхСредств) = ТипДенегКасса() Тогда
			Строка.ТипДокумента = ?(ЗначениеЗаполнено(Строка.СуммаПоступления), "пко", "рко");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.СуммаПоступления) Тогда
			Строка.СуммаДокумента = Строка.СуммаПоступления;
		Иначе
			Строка.СуммаДокумента = Строка.СуммаСписания;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьТаблицуДанныхДляДокументовПокупкиПродажи(ТаблицаДанных)
	
	ЧисловыеРеквизиты = ЧисловыеРеквизитыДокументовПокупкиПродажи();
	Для Каждого Строка Из ТаблицаДанных Цикл
		Для Каждого ИмяРеквизита Из ЧисловыеРеквизиты Цикл
			Строка[ИмяРеквизита] = ОбработатьЧисловойРеквизит(Строка[ИмяРеквизита]);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьДокументыДвиженияДенег(ТаблицаДанных, ЗагружаемыеДанные, Ошибки)
	
	ВидыОперацийДвиженияДенег = ТаблицаСопоставления(ИмяМакетаВидыОперацийДвиженияДенег());
	ОпределитьСчетаУчетаДвиженияДенег(ВидыОперацийДвиженияДенег);
	
	ВалютаРегл = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Организация = ЗагружаемыеДанные.Организация;
	
	Для Каждого ПрочитанныеДанные Из ТаблицаДанных Цикл
		
		Если ЗначениеЗаполнено(ПрочитанныеДанные.СуммаПоступления) Тогда
			ПрочитанныеДанные.СуммаДокумента = ПрочитанныеДанные.СуммаПоступления;
		ИначеЕсли ЗначениеЗаполнено(ПрочитанныеДанные.СуммаСписания) Тогда
			ПрочитанныеДанные.СуммаДокумента = ПрочитанныеДанные.СуммаСписания;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПрочитанныеДанные.СуммаДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяДокумента = ОпределитьИмяДокументаДвиженияДенег(ПрочитанныеДанные);
		Если Не ЗначениеЗаполнено(ИмяДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыОперации = ВидОперацииДвиженияДенег(ПрочитанныеДанные, ИмяДокумента, ВидыОперацийДвиженияДенег);
		
		Если Не ЗначениеЗаполнено(ПараметрыОперации.ВидОперации) Тогда
			Продолжить;
		КонецЕсли;
		
		ПрочитанныеДанные.Дата = ДатаИзСтроки(ПрочитанныеДанные.Дата);
		
		ПроверитьУстановитьЗаписьУчетнойПолитики(Организация, ПрочитанныеДанные.Дата);
		
		НовыйДокумент = Документы[ИмяДокумента].СоздатьДокумент();
		РеквизитыДокумента = НовыеРеквизитыДокумента();
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ПрочитанныеДанные);
		РеквизитыДокумента.Графа5_УСН = ПрочитанныеДанные.ДоходыУСН;
		РеквизитыДокумента.Графа7_УСН = ПрочитанныеДанные.РасходыУСН;
		
		Если ЗначениеЗаполнено(ПрочитанныеДанные.НомерРасчетногоСчета) Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	БанковскиеСчета.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.БанковскиеСчета КАК БанковскиеСчета
			|ГДЕ
			|	БанковскиеСчета.НомерСчета = &НомерСчета";
			ПрочитанныеДанные.НомерРасчетногоСчета = СтрЗаменить(ПрочитанныеДанные.НомерРасчетногоСчета, " ", "");
			ПрочитанныеДанные.НомерРасчетногоСчета = СтрЗаменить(ПрочитанныеДанные.НомерРасчетногоСчета, Символы.НПП, "");
			Запрос.УстановитьПараметр("НомерСчета", ПрочитанныеДанные.НомерРасчетногоСчета);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				РеквизитыДокумента.СчетОрганизации = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
		НайденнаяСтрокаКонтрагента = ЗагружаемыеДанные.Контрагенты.Найти(
			РеквизитыДокумента.Контрагент, "Наименование");
		
		Если НайденнаяСтрокаКонтрагента <> Неопределено Тогда
			РеквизитыДокумента.Контрагент = НайденнаяСтрокаКонтрагента.Ссылка;
		КонецЕсли;
		
		РеквизитыДокумента.Организация = Организация;
		РеквизитыДокумента.ВидОперации = Перечисления[ПараметрыОперации.ИмяПеречисления][ПараметрыОперации.ВидОперации];
		
		НовыйДокумент.Дата = РеквизитыДокумента.Дата;
		
		НовыйДокумент.Заполнить(РеквизитыДокумента);
		
		// Принудительно заполняем реквизиты, которые не заполняются в обработке заполнения
		ЗаполнитьЗначенияСвойств(НовыйДокумент, РеквизитыДокумента, "Графа5_УСН, Графа7_УСН");
		
		СтруктураКурсаВзаиморасчетов =
			РаботаСКурсамиВалют.ПолучитьКурсВалюты(НовыйДокумент.ВалютаДокумента, ПрочитанныеДанные.Дата);
		
		// Для поступления на расчетный счет не заполняются в обработке заполнения
		ЭтоДокументПоступленияНаРС = Ложь;
		Если ТипЗнч(НовыйДокумент) = Тип("ДокументОбъект.ПоступлениеНаРасчетныйСчет") Тогда
			НовыйДокумент.НазначениеПлатежа = РеквизитыДокумента.НазначениеПлатежа;
			НовыйДокумент.НомерВходящегоДокумента = РеквизитыДокумента.НомерВходящегоДокумента;
			НовыйДокумент.КурсНаДатуПриобретенияРеализацииВалюты = СтруктураКурсаВзаиморасчетов.Курс;
			НовыйДокумент.СуммаУслуг = РеквизитыДокумента.СуммаУслуг;
			
			ЭтоДокументПоступленияНаРС = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОперации.Счет) Тогда
			НовыйДокумент.СчетУчетаРасчетовСКонтрагентом = ПараметрыОперации.Счет;
		КонецЕсли;
		
		ДополнитьРеквизитыДокументаДвиженияДенег(НовыйДокумент, ПрочитанныеДанные);
		
		Если ЗначениеЗаполнено(НовыйДокумент.Контрагент) Тогда
			НовыйДокумент.ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.ДоговорКонтрагентаИзОбъекта(НовыйДокумент);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НовыйДокумент.РасшифровкаПлатежа) Тогда
			
			СтрокаРасшифровкаПлатежа = НовыйДокумент.РасшифровкаПлатежа[0];
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровкаПлатежа, НовыйДокумент);
			СтрокаРасшифровкаПлатежа.СуммаПлатежа                   = ПрочитанныеДанные.СуммаДокумента;
			СтрокаРасшифровкаПлатежа.СуммаВзаиморасчетов            = ПрочитанныеДанные.СуммаДокумента;
			СтрокаРасшифровкаПлатежа.СпособПогашенияЗадолженности   = Перечисления.СпособыПогашенияЗадолженности.Автоматически;
			СтрокаРасшифровкаПлатежа.КурсВзаиморасчетов             = СтруктураКурсаВзаиморасчетов.Курс;
			СтрокаРасшифровкаПлатежа.КратностьВзаиморасчетов        = СтруктураКурсаВзаиморасчетов.Кратность;
			Если ЭтоДокументПоступленияНаРС Тогда
				СтрокаРасшифровкаПлатежа.КурсНаДатуПриобретенияРеализацииВалюты = СтруктураКурсаВзаиморасчетов.Курс;
				Если НовыйДокумент.ВидОперации = 
					Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажПоПлатежнымКартамИБанковскимКредитам Тогда
					// При данном виде операции счета учета расчетов с контрагентами различаются в табличной части и шапке документа,
					// счет в табличной части необходимо перезаполнить
					СтрокаРасшифровкаПлатежа.СчетУчетаРасчетовСКонтрагентом = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
				КонецЕсли;
			КонецЕсли;
			Если НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма Тогда
				Если ПрочитанныеДанные.ВидОперации = "Выплата процентов по кредиту" Тогда
					СтрокаРасшифровкаПлатежа.ВидПлатежаПоКредитамЗаймам = Перечисления.ВидыПлатежейПоКредитамЗаймам.УплатаПроцентов;
					СтрокаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств =
						УчетДенежныхСредствВызовСервера.СтатьяДДСПоУмолчанию("УплатаПроцентов");
				Иначе
					СтрокаРасшифровкаПлатежа.ВидПлатежаПоКредитамЗаймам = Перечисления.ВидыПлатежейПоКредитамЗаймам.ПогашениеДолга;
					СтрокаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств =
						УчетДенежныхСредствВызовСервера.СтатьяДДСПоУмолчанию("ПогашениеДолга");
				КонецЕсли;
			ИначеЕсли НовыйДокумент.ВидОперации
				= Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты Тогда
				
				СтрокаРасшифровкаПлатежа.КурсНаДатуПриобретенияРеализацииВалюты = СтрокаРасшифровкаПлатежа.КурсВзаиморасчетов;
				
			КонецЕсли;
		КонецЕсли;
		
		Если НовыйДокумент.ВидОперации =
			Перечисления.ВидыОперацийСписаниеДенежныхСредств.ЛичныеСредстваПредпринимателя Тогда
			
			Предприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ЗагружаемыеДанные.Организация, "ИндивидуальныйПредприниматель");
			НовыйДокумент.Контрагент = Предприниматель;
			
		КонецЕсли;
		
		// В некоторых видах документов сумма очищается в обработке заполнения
		НовыйДокумент.СуммаДокумента = ПрочитанныеДанные.СуммаДокумента;
		НовыйДокумент.Комментарий = ПрочитанныеДанные.ВидОперации;
		
		НовыйДокумент.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
		СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(НовыйДокумент, РежимЗаписиДокумента.Запись);
		
		Если НовыйДокумент.ПроверитьЗаполнение() Тогда
			СтрокаДокументКПроведению = ЗагружаемыеДанные.ДокументыКПроведению.Добавить();
			СтрокаДокументКПроведению.Дата = НовыйДокумент.Дата;
		Иначе
			СтрокаДокументКПроведению = Неопределено;
		КонецЕсли;
		
		НовыйДокумент.ОбменДанными.Загрузка = Истина;
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		Если СтрокаДокументКПроведению <> Неопределено Тогда
			СтрокаДокументКПроведению.Ссылка = НовыйДокумент.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйПараметрыДокумента()
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("РазделДанных");
	ПараметрыДокумента.Вставить("ТипДокумента");
	ПараметрыДокумента.Вставить("ИмяОбъекта");
	ПараметрыДокумента.Вставить("ИмяТабЧасти");
	ПараметрыДокумента.Вставить("ВидДоговора");
	ПараметрыДокумента.Вставить("ЕстьСтавкаНДС");
	ПараметрыДокумента.Вставить("ЕстьВидОперации");
	ПараметрыДокумента.Вставить("ЕстьДанныеВходящегоДокумента");
	ПараметрыДокумента.Вставить("ЕстьКонтрагент");
	ПараметрыДокумента.Вставить("ЕстьДоговорКонтрагента");
	ПараметрыДокумента.Вставить("ЕстьФизЛицо");
	ПараметрыДокумента.Вставить("ЕстьСклад");
	ПараметрыДокумента.Вставить("ЕстьПодразделение");
	ПараметрыДокумента.Вставить("ЭтоСчетФактура");
	ПараметрыДокумента.Вставить("ЭтоОтчетОРозничныхПродажах");
	ПараметрыДокумента.Вставить("ЗагружатьНомерДокумента");
	ПараметрыДокумента.Вставить("ЗаполнитьСчетаУчета");
	ПараметрыДокумента.Вставить("ВедетсяУчетВУСН");
	ПараметрыДокумента.Вставить("ДокументБезНДС");
	
	Возврат ПараметрыДокумента;
	
КонецФункции

Процедура ЗагрузитьВходящиеИсходящиеДокументы(ТаблицаДанных, ЗагружаемыеДанные, Ошибки, ПараметрыДокумента)
	
	НайденныеСтроки = ТаблицаДанных.НайтиСтроки(Новый Структура("ТипДокумента", ПараметрыДокумента.ТипДокумента));
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВидыОперацийДокументов          = ВидыОперацийДокументов();
	СтавкиНДС                       = СтавкиНДС();
	
	ДополнитьПараметрыВходящихИсходящихДокументов(ПараметрыДокумента);
	
	Организация = ЗагружаемыеДанные.Организация;
	Подразделение = ОсновноеПодразделениеОрганизации(Организация);
	ИмяДокумента = ПараметрыДокумента.ИмяОбъекта;
	
	Для Каждого Строка Из НайденныеСтроки Цикл
		
		Строка.Дата = ДатаИзСтроки(Строка.Дата);
		Строка.Номер = СформироватьНомерДокумента(Организация, Подразделение, ИмяДокумента, Строка.Номер);
		
		ПроверитьУстановитьЗаписьУчетнойПолитики(Организация, Строка.Дата);
		
		Если ПараметрыДокумента.ЕстьКонтрагент И ЗначениеЗаполнено(Строка.Контрагент) Тогда
			НайденнаяСтрокаКонтрагента = ЗагружаемыеДанные.Контрагенты.Найти(Строка.Контрагент, "Наименование");
			Если НайденнаяСтрокаКонтрагента <> Неопределено Тогда
				Строка.Контрагент = НайденнаяСтрокаКонтрагента.Ссылка;
			КонецЕсли;
		КонецЕсли;
			
		РеквизитыДокумента = НовыеРеквизитыДокумента();
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, Строка);
		РеквизитыДокумента.Организация = Организация;
		РеквизитыДокумента.СуммаДокумента = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Строка.Сумма);
		Если ДокументНайденВБазе(РеквизитыДокумента, ИмяДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйДокумент = Документы[ИмяДокумента].СоздатьДокумент();
		
		Если ПараметрыДокумента.ЭтоОтчетОРозничныхПродажах Тогда
			РеквизитыДокумента.Склад = НайтиСоздатьСклад(НаименованиеСкладаНТТ(),
				Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);
		ИначеЕсли ПараметрыДокумента.ЕстьСклад Тогда
			РеквизитыДокумента.Склад = НайтиСоздатьОсновнойСклад();
		КонецЕсли;
		
		НовыйДокумент.Заполнить(РеквизитыДокумента);
		ЗаполнитьЗначенияСвойств(НовыйДокумент, Строка);
		НовыйДокумент.СуммаДокумента = РеквизитыДокумента.СуммаДокумента;
		Если ПараметрыДокумента.ЕстьСклад И Не ЗначениеЗаполнено(НовыйДокумент.Склад) Тогда
			НовыйДокумент.Склад = РеквизитыДокумента.Склад;
		КонецЕсли;
		
		ВидОперации = СтрШаблон("%1_%2", ПараметрыДокумента.РазделДанных, ПараметрыДокумента.ТипДокумента);
		Если ПараметрыДокумента.ЕстьВидОперации Тогда
			НовыйДокумент.ВидОперации = ВидыОперацийДокументов.Получить(НРег(ВидОперации));
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьДанныеВходящегоДокумента Тогда
			НовыйДокумент.НомерВходящегоДокумента = РеквизитыДокумента.Номер;
			НовыйДокумент.ДатаВходящегоДокумента  = РеквизитыДокумента.Дата;
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьДоговорКонтрагента Тогда
			РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
				НовыйДокумент.ДоговорКонтрагента, НовыйДокумент.Контрагент, НовыйДокумент.Организация,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыДокумента.ВидДоговора));
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьФизЛицо Тогда
			РеквизитыДокумента.ФизЛицо = НайтиСоздатьФизЛицо(НаименованиеСлужебногоФизЛица());
		КонецЕсли;
		
		Если ПараметрыДокумента.ЭтоСчетФактура Тогда
			ЗаполнитьРеквизитыСФ(НовыйДокумент, СтавкиНДС, Строка);
		КонецЕсли;
		
		Если Не ПустаяСтрока(ПараметрыДокумента.ИмяТабЧасти) Тогда
			ЗаполнитьТабличнуюЧасть(
				НовыйДокумент, ПараметрыДокумента, СтавкиНДС, Строка.ТабличнаяЧасть, ЗагружаемыеДанные.Номенклатура);
		КонецЕсли;
		
		Если ПараметрыДокумента.ЗаполнитьСчетаУчета Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("ТабличнаяЧасть", ПараметрыДокумента.ИмяТабЧасти);
			СчетаУчетаВДокументах.ЗаполнитьТаблицу(
				Документы[ИмяДокумента], РеквизитыДокумента, НовыйДокумент[ПараметрыДокумента.ИмяТабЧасти], Отбор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НовыйДокумент.Контрагент) Тогда
			РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(НовыйДокумент);
			Если Не ЗначениеЗаполнено(НовыйДокумент.ДоговорКонтрагента) Тогда
				НовыйДокумент.ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.ДоговорКонтрагентаИзОбъекта(НовыйДокумент);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыДокумента.ДокументБезНДС Тогда
			НовыйДокумент.НДСНеВыделять = Истина;
		КонецЕсли;
		
		Если НовыйДокумент.ПроверитьЗаполнение() Тогда
			СтрокаДокументКПроведению = ЗагружаемыеДанные.ДокументыКПроведению.Добавить();
			СтрокаДокументКПроведению.Дата = НовыйДокумент.Дата;
		Иначе
			СтрокаДокументКПроведению = Неопределено;
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьПодразделение Тогда
			Склад = ?(ПараметрыДокумента.ЕстьСклад, НовыйДокумент.Склад, Справочники.Склады.ПустаяСсылка());
			ПодразделениеПоУмолчанию = ОбщегоНазначенияБП.ПолучитьПодразделение(Организация, Склад);
			Если НЕ ЗначениеЗаполнено(ПодразделениеПоУмолчанию) Тогда
				ПодразделениеПоУмолчанию = Подразделение;
			КонецЕсли;
			НовыйДокумент.ПодразделениеОрганизации = ПодразделениеПоУмолчанию;
		КонецЕсли;
		
		НовыйДокумент.ОбменДанными.Загрузка = Истина;
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		Если СтрокаДокументКПроведению <> Неопределено Тогда
			СтрокаДокументКПроведению.Ссылка = НовыйДокумент.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НовыеРеквизитыДокумента()
	
	РеквизитыДокумента = Новый Структура;
	
	РеквизитыДокумента.Вставить("Контрагент");
	РеквизитыДокумента.Вставить("ДоговорКонтрагента");
	РеквизитыДокумента.Вставить("Комментарий");
	РеквизитыДокумента.Вставить("Дата");
	РеквизитыДокумента.Вставить("СтруктурнаяЕдиница");
	РеквизитыДокумента.Вставить("ФизЛицо");
	РеквизитыДокумента.Вставить("Номер");
	РеквизитыДокумента.Вставить("Организация");
	РеквизитыДокумента.Вставить("НазначениеПлатежа");
	РеквизитыДокумента.Вставить("НомерВходящегоДокумента");
	РеквизитыДокумента.Вставить("Графа5_УСН");
	РеквизитыДокумента.Вставить("Графа7_УСН");
	РеквизитыДокумента.Вставить("СуммаДокумента");
	РеквизитыДокумента.Вставить("СуммаУслуг");
	РеквизитыДокумента.Вставить("ВидОперации");
	РеквизитыДокумента.Вставить("СчетОрганизации");
	РеквизитыДокумента.Вставить("Склад");
	
	Возврат РеквизитыДокумента;
	
КонецФункции

Процедура ЗагрузитьДокументыПокупкиПродажиИзЭлектроннойТаблицы(
	ЗагружаемыеДанные, ИмяРаздела, Ошибки, ПараметрыДокумента)
	
	ТаблицаДанных = ЗагружаемыеДанные[ИмяРаздела];
	НайденныеСтрокиПоТипуДокумента =
		ТаблицаДанных.НайтиСтроки(Новый Структура("ТипДокумента", ПараметрыДокумента.ТипДокумента));
	Если НайденныеСтрокиПоТипуДокумента.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовим таблицу номенклатуры
	ЗагрузитьНоменклатуру(ИмяРаздела, ЗагружаемыеДанные, Ошибки);
	
	// ТаблицаДанных содержит строки табличной части, необходимо отдельно выделить информацию,
	// к какому документу какая строка относится
	ШапкаДокумента = ТаблицаДанных.Скопировать(НайденныеСтрокиПоТипуДокумента,
		"Дата, ДатаПрошлогоПериода, Номер, НомерПрошлогоПериода, ТипДокумента");
	ШапкаДокумента.Свернуть("Дата, ДатаПрошлогоПериода, Номер, НомерПрошлогоПериода, ТипДокумента");
	
	ВидыОперацийДокументов = ВидыОперацийДокументов();
	СтавкиНДС = СтавкиНДС();
	
	ДополнитьПараметрыВходящихИсходящихДокументов(ПараметрыДокумента);
	
	Организация = ЗагружаемыеДанные.Организация;
	Подразделение = ОсновноеПодразделениеОрганизации(Организация);
	ИмяДокумента = ПараметрыДокумента.ИмяОбъекта;
	
	Для Каждого СтрокаШапки Из ШапкаДокумента Цикл
		
		ПоискСтрокДокумента = Новый Структура("Дата, Номер, ТипДокумента");
		ЗаполнитьЗначенияСвойств(ПоискСтрокДокумента, СтрокаШапки);
		СтрокиДокумента = ТаблицаДанных.НайтиСтроки(ПоискСтрокДокумента);
		ТабличнаяЧастьДокумента = ТаблицаДанных.Скопировать(СтрокиДокумента);
		Строка = СтрокиДокумента[0];
		
		Если ЗначениеЗаполнено(СтрокаШапки.ДатаПрошлогоПериода) И СтрокаШапки.ДатаПрошлогоПериода <> "-" Тогда
			СтрокаШапки.Дата = СтрокаШапки.ДатаПрошлогоПериода;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаШапки.НомерПрошлогоПериода) И СтрокаШапки.НомерПрошлогоПериода <> "-" Тогда
			СтрокаШапки.Номер = СтрокаШапки.НомерПрошлогоПериода;
		КонецЕсли;
		
		Строка.Дата = ДатаИзСтроки(СтрокаШапки.Дата);
		Если Не ЗначениеЗаполнено(Строка.Дата) Тогда
			Строка.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		Строка.Номер = СформироватьНомерДокумента(
			Организация, Подразделение, ИмяДокумента, СтрокаШапки.Номер);
		
		ПроверитьУстановитьЗаписьУчетнойПолитики(Организация, Строка.Дата);
		
		РеквизитыДокумента = НовыеРеквизитыДокумента();
		
		РеквизитыДокумента.СуммаДокумента = ТабличнаяЧастьДокумента.Итог("СуммаСНДС");
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, Строка);
		РеквизитыДокумента.Организация = Организация;
		
		Если ДокументНайденВБазе(РеквизитыДокумента, ИмяДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.Контрагент) И ПараметрыДокумента.ЕстьКонтрагент Тогда
			РеквизитыДокумента.Контрагент = КонтрагентПоНаименованию(
				ЗагружаемыеДанные[ИмяКонтрагенты()], Строка.Контрагент, Ошибки);
		ИначеЕсли ЗначениеЗаполнено(Строка.Контрагент) И ПараметрыДокумента.ЕстьФизЛицо Тогда
			РеквизитыДокумента.ФизЛицо = НайтиСоздатьФизЛицо(Строка.Контрагент);
		КонецЕсли;
		
		НовыйДокумент = Документы[ИмяДокумента].СоздатьДокумент();
		
		Если ПараметрыДокумента.ЭтоОтчетОРозничныхПродажах Тогда
			РеквизитыДокумента.Склад = НайтиСоздатьСклад(НаименованиеСкладаНТТ(),
				Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);
		ИначеЕсли ПараметрыДокумента.ЕстьСклад И ЗначениеЗаполнено(Строка.Склад) И Не Строка.Склад = "-" Тогда
			РеквизитыДокумента.Склад = НайтиСоздатьСклад(Строка.Склад);
		ИначеЕсли ПараметрыДокумента.ЕстьСклад Тогда
			РеквизитыДокумента.Склад = НайтиСоздатьОсновнойСклад();
		КонецЕсли;
		
		НовыйДокумент.Дата = РеквизитыДокумента.Дата;
		НовыйДокумент.Заполнить(РеквизитыДокумента);
		Если Не ЗначениеЗаполнено(НовыйДокумент.Номер) Тогда
			НовыйДокумент.Номер = РеквизитыДокумента.Номер;
		КонецЕсли;
		Если ПараметрыДокумента.ЕстьСклад И Не ЗначениеЗаполнено(НовыйДокумент.Склад) Тогда
			НовыйДокумент.Склад = РеквизитыДокумента.Склад;
		КонецЕсли;
		
		ВидОперации = СтрШаблон("%1_%2", ПараметрыДокумента.РазделДанных, ПараметрыДокумента.ТипДокумента);
		Если ПараметрыДокумента.ЕстьВидОперации Тогда
			НовыйДокумент.ВидОперации = ВидыОперацийДокументов.Получить(НРег(ВидОперации));
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьДанныеВходящегоДокумента Тогда
			НовыйДокумент.НомерВходящегоДокумента = СтрокаШапки.Номер;
			НовыйДокумент.ДатаВходящегоДокумента  = РеквизитыДокумента.Дата;
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьДоговорКонтрагента Тогда
			РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
				НовыйДокумент.ДоговорКонтрагента, НовыйДокумент.Контрагент, НовыйДокумент.Организация,
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыДокумента.ВидДоговора));
		КонецЕсли;
		
		Если ПараметрыДокумента.ЭтоСчетФактура Тогда
			ЗаполнитьРеквизитыСФ(НовыйДокумент, СтавкиНДС, Строка);
		КонецЕсли;
		
		Если Не ПустаяСтрока(ПараметрыДокумента.ИмяТабЧасти) Тогда
			ЗаполнитьТабличнуюЧасть(
				НовыйДокумент, ПараметрыДокумента, СтавкиНДС, ТабличнаяЧастьДокумента, ЗагружаемыеДанные[ИмяНоменклатура()]);
		КонецЕсли;
		
		Если ПараметрыДокумента.ЗаполнитьСчетаУчета Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("ТабличнаяЧасть", ПараметрыДокумента.ИмяТабЧасти);
			СчетаУчетаВДокументах.ЗаполнитьТаблицу(
				Документы[ИмяДокумента], РеквизитыДокумента, НовыйДокумент[ПараметрыДокумента.ИмяТабЧасти], Отбор);
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьКонтрагент И ЗначениеЗаполнено(НовыйДокумент.Контрагент) Тогда
			РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(НовыйДокумент);
			Если Не ЗначениеЗаполнено(НовыйДокумент.ДоговорКонтрагента) Тогда
				НовыйДокумент.ДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.ДоговорКонтрагентаИзОбъекта(НовыйДокумент);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыДокумента.ДокументБезНДС Тогда
			НовыйДокумент.НДСНеВыделять = Истина;
		КонецЕсли;
		
		Если НовыйДокумент.ПроверитьЗаполнение() Тогда
			СтрокаДокументКПроведению = ЗагружаемыеДанные.ДокументыКПроведению.Добавить();
			СтрокаДокументКПроведению.Дата = НовыйДокумент.Дата;
		Иначе
			СтрокаДокументКПроведению = Неопределено;
		КонецЕсли;
		
		Если ПараметрыДокумента.ЕстьПодразделение Тогда
			Склад = ?(ПараметрыДокумента.ЕстьСклад, НовыйДокумент.Склад, Справочники.Склады.ПустаяСсылка());
			ПодразделениеПоУмолчанию = ОбщегоНазначенияБП.ПолучитьПодразделение(Организация, Склад);
			Если НЕ ЗначениеЗаполнено(ПодразделениеПоУмолчанию) Тогда
				ПодразделениеПоУмолчанию = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
					"ОсновноеПодразделениеОрганизации");
			КонецЕсли;
			Если БухгалтерскийУчетПереопределяемый.ПодразделениеПринадлежитОрганизации(
				ПодразделениеПоУмолчанию, Организация) Тогда
				НовыйДокумент.ПодразделениеОрганизации = ПодразделениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
		
		НовыйДокумент.ОбменДанными.Загрузка = Истина;
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		Если СтрокаДокументКПроведению <> Неопределено Тогда
			СтрокаДокументКПроведению.Ссылка = НовыйДокумент.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НечитаемыеОбластиКассовойКниги()
	
	Области = Новый Массив;
	Области.Добавить("Титульный_лист");
	Области.Добавить("Подписной_лист");
	Возврат Области;
	
КонецФункции

Функция ДатаЛистаКассовойКнигиВОдностраничномФормате(Таблица, Область)
	
	Результат = НовыйРезультатПоискаДатыЛистаКассовойКниги();
	
	Для НомерСтроки = Область.Верх По Область.Низ Цикл
		Для НомерКолонки = 1 По Таблица.ШиринаСтраницы Цикл
			ТекстЯчейки = Таблица.Область(НомерСтроки, НомерКолонки).Текст;
			Если СокрЛП(ТекстЯчейки) = ШапкаОбластиКассовойКниги() Тогда
				Для Сч = НомерКолонки + 1 По Таблица.ШиринаСтраницы Цикл
					ДатаЛиста = Таблица.Область(НомерСтроки, Сч).Текст;
					Если ЗначениеЗаполнено(ДатаЛиста) Тогда
						Результат.ДатаЛиста = ДатаЛиста;
						Результат.НомерСтроки = НомерСтроки;
						Возврат Результат;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДатаЛистаКассовойКнигиВДвухстраничномФормате(Таблица, Область)
	
	Результат = НовыйРезультатПоискаДатыЛистаКассовойКниги();
	СоставнаяДата = Новый Массив;
	НумерацияМесяцев = СоответствиеМесяцев();
	
	Для НомерСтроки = Область.Верх По Область.Низ Цикл
		Для НомерКолонки = 1 По Таблица.ШиринаСтраницы Цикл
			ТекстЯчейки = Таблица.Область(НомерСтроки, НомерКолонки).Текст;
			Если СокрЛП(ТекстЯчейки) = ШапкаОбластиКассовойКниги() Тогда
				Для Сч = НомерКолонки + 1 По Таблица.ШиринаСтраницы Цикл
					ТекстЯчейки = Таблица.Область(НомерСтроки, Сч).Текст;
					Если Не ЗначениеЗаполнено(ТекстЯчейки) Тогда
						Продолжить;
					КонецЕсли;
					Если (Не ЗначениеЗаполнено(СоставнаяДата) Или СоставнаяДата.Количество() = 2)
						И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекстЯчейки) Тогда
						СоставнаяДата.Добавить(ТекстЯчейки);
					ИначеЕсли СоставнаяДата.Количество() = 1 Тогда
						НомерМесяца = НумерацияМесяцев.Получить(ТекстЯчейки);
						Если НомерМесяца <> Неопределено Тогда
							СоставнаяДата.Добавить(НомерМесяца);
						КонецЕсли;
					КонецЕсли;
					Если СоставнаяДата.Количество() = 3 Тогда
						Результат.НомерСтроки = НомерСтроки;
						Результат.ДатаЛиста = Дата(СоставнаяДата[2], СоставнаяДата[1], СоставнаяДата[0]);
						Возврат Результат;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ШиринаЛистаКассовойКнигиПриДвухстраничномФормате(Таблица, Область)
	
	Для НомерСтроки = Область.Верх По Область.Низ Цикл
		Для НомерКолонки = 1 По Таблица.ШиринаСтраницы Цикл
			Если Таблица.Область(НомерСтроки, НомерКолонки).Текст
				= ЛинияРазделаЛистаКассовойКнигиВДвухстраничномФормате() Тогда
				Возврат НомерКолонки - 1;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

Функция НачалоТаблицыКассовойКниги()
	
	Возврат "Остаток на начало дня";
	
КонецФункции

Функция ПризнакДвухстраничногоФорматаКассовойКниги()
	
	Возврат "    П   О   Л   Е         Д   Л   Я         П   О   Д   Ш   И   В   К   И";
	
КонецФункции

Функция ЛинияРазделаЛистаКассовойКнигиВДвухстраничномФормате()
	
	Возврат "Л       и         н        и        я                   о        т        р        е        з       а";
	
КонецФункции

Процедура ОбработатьДанныеКассовойКниги(ПрочитанныеДанные)
	
	ЧисловыеРеквизиты = ЧисловыеРеквизитыКассовойКниги();
	Для Каждого Строка Из ПрочитанныеДанные Цикл
		Для Каждого Реквизит Из ЧисловыеРеквизиты Цикл
			Строка[Реквизит] = ОбработатьЧисловойРеквизит(Строка[Реквизит]);
		КонецЦикла;
		Если ЗначениеЗаполнено(Строка.СуммаПоступления) Тогда
			Строка.ТипДокумента = ИмяДокументаПКО();
		Иначе
			Строка.ТипДокумента = ИмяДокументаРКО();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЧисловыеРеквизитыДокументовДвиженияДенег()
	
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("СуммаПоступления");
	ИменаРеквизитов.Добавить("СуммаСписания");
	ИменаРеквизитов.Добавить("СуммаУслуг");
	ИменаРеквизитов.Добавить("СуммаПатент");
	ИменаРеквизитов.Добавить("ДоходыУСН");
	ИменаРеквизитов.Добавить("РасходыУСН");
	
	Возврат ИменаРеквизитов;
	
КонецФункции

Функция ЧисловыеРеквизитыДокументовПокупкиПродажи()
	
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("Цена");
	ИменаРеквизитов.Добавить("СуммаБезНДС");
	ИменаРеквизитов.Добавить("СуммаНДС");
	ИменаРеквизитов.Добавить("СуммаСНДС");
	
	Возврат ИменаРеквизитов;
	
КонецФункции

Процедура ОпределитьСчетаУчетаДвиженияДенег(ТаблицаВидовОпераций)
	
	ТаблицаВидовОпераций.Колонки.Добавить("Счет");
	Для Каждого Строка Из ТаблицаВидовОпераций Цикл
		Если Не ЗначениеЗаполнено(Строка.СчетРасчетов) Тогда
			Продолжить;
		КонецЕсли;
		
		Строка.Счет = ПланыСчетов.Хозрасчетный.НайтиПоКоду(Строка.СчетРасчетов);
	КонецЦикла;
	
КонецПроцедуры

Функция ОпределитьИмяДокументаДвиженияДенег(ПрочитанныеДанные)
	
	Если НРег(ПрочитанныеДанные.ВидДенежныхСредств) = ТипДенегБанк() Тогда
		Если ЗначениеЗаполнено(ПрочитанныеДанные.СуммаПоступления) Тогда
			Возврат "ПоступлениеНаРасчетныйСчет";
		Иначе
			Возврат "СписаниеСРасчетногоСчета";
		КонецЕсли;
	ИначеЕсли НРег(ПрочитанныеДанные.ВидДенежныхСредств) = ТипДенегКасса() Тогда
		Если ЗначениеЗаполнено(ПрочитанныеДанные.СуммаПоступления) Тогда
			Возврат "ПриходныйКассовыйОрдер";
		Иначе
			Возврат "РасходныйКассовыйОрдер";
		КонецЕсли;
	КонецЕсли;
	Возврат "";
	
КонецФункции

Функция ВидОперацииДвиженияДенег(ДанныеДокумента, ИмяДокумента, ТаблицаВидовОпераций)
	
	ПараметрыОперации = НовыйПараметрыОперацииДвиженияДенег();
	
	СтруктураПоиска = Новый Структура("ИмяДокумента, ИсходныйВидОперации", ИмяДокумента, ДанныеДокумента.ВидОперации);
	СтрокиВидОперации = ТаблицаВидовОпераций.НайтиСтроки(СтруктураПоиска);
	
	Если СтрокиВидОперации.Количество() = 0 Тогда
		СтруктураПоиска.ИсходныйВидОперации = "Не указан";
		СтрокиВидОперации = ТаблицаВидовОпераций.НайтиСтроки(СтруктураПоиска);
	КонецЕсли;
	
	Если СтрокиВидОперации.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОперации, СтрокиВидОперации[0]);
	КонецЕсли;
	
	Возврат ПараметрыОперации;
	
КонецФункции

Процедура ДополнитьРеквизитыДокументаДвиженияДенег(ДокументОбъект, ДанныеФайла)
	
	ВидыОперацийПрочийДоход  = МассивВидыОперацийПрочийДоход();
	ВидыОперацийПрочийРасход = МассивВидыОперацийПрочийРасход();
	ВидыОперацийНалог = ВидыОперацийУплатаНалога();
	
	Если ВидыОперацийПрочийДоход.Найти(ДокументОбъект.ВидОперации) <> Неопределено Тогда
	
		ДокументОбъект.СубконтоКт1 = ОбщегоНазначения.ПредопределенныйЭлемент(
			"Справочник.ПрочиеДоходыИРасходы.ПрочиеВнереализационныеДоходыРасходы");
	
	ИначеЕсли ВидыОперацийПрочийРасход.Найти(ДокументОбъект.ВидОперации) <> Неопределено Тогда
		
		ДокументОбъект.СубконтоДт1 = ОбщегоНазначения.ПредопределенныйЭлемент(
			"Справочник.ПрочиеДоходыИРасходы.ПрочиеВнереализационныеДоходыРасходы");
	
	ИначеЕсли ВидыОперацийНалог.Найти(ДокументОбъект.ВидОперации) <> Неопределено Тогда
		
		ДокументОбъект.Налог = ОбщегоНазначения.ПредопределенныйЭлемент(
			"Справочник.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы");
		Если ДанныеФайла.ВидОперации = ИмяОперацииУплатыПенейШтрафовПоНалогам() Тогда
			ДокументОбъект.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Штраф;
		Иначе
			ДокументОбъект.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВидыОперацийДокументов()
	
	ВидыОпераций = Новый Соответствие;
	ВидыОпераций.Вставить("входящие_документы_акт", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги);
	ВидыОпераций.Вставить("входящие_документы_накладная", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары);
	ВидыОпераций.Вставить(
		"входящие_документы_авансовый отчет", Перечисления.ВидыОперацийАвансовыйОтчет.ПокупкаОплатаПрочее);
	ВидыОпераций.Вставить("исходящие_документы_акт", Перечисления.ВидыОперацийРеализацияТоваров.Услуги);
	ВидыОпераций.Вставить("исходящие_документы_накладная", Перечисления.ВидыОперацийРеализацияТоваров.Товары);
	ВидыОпераций.Вставить("исходящие_документы_счёт-фактура", Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
	ВидыОпераций.Вставить(
		"исходящие_документы_отчет о рознице", Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах);
	ВидыОпераций.Вставить("документы_покупка_акт", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги);
	ВидыОпераций.Вставить("документы_покупка_накладная", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары);
	ВидыОпераций.Вставить("документы_покупка_упд", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары);
	ВидыОпераций.Вставить(
		"документы_покупка_товары и материалы", Перечисления.ВидыОперацийАвансовыйОтчет.ПокупкаОплатаПрочее);
	ВидыОпераций.Вставить(
		"документы_покупка_авансовый отчёт: товары, материалы, услуги",
		Перечисления.ВидыОперацийАвансовыйОтчет.ПокупкаОплатаПрочее);
	ВидыОпераций.Вставить("документы_продажа_накладная", Перечисления.ВидыОперацийРеализацияТоваров.Товары);
	ВидыОпераций.Вставить("документы_продажа_упд", Перечисления.ВидыОперацийРеализацияТоваров.Товары);
	ВидыОпераций.Вставить("документы_продажа_накладная на возврат",
		Перечисления.ВидыОперацийВозвратТоваровПоставщику.ПокупкаКомиссия);
	ВидыОпераций.Вставить("документы_продажа_акт", Перечисления.ВидыОперацийРеализацияТоваров.Услуги);
	ВидыОпераций.Вставить("документы_продажа_cчет-фактура", Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию);
	ВидыОпераций.Вставить(
		"документы_продажа_отчет о розничной продаже", Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах);
	
	Возврат ВидыОпераций;
	
КонецФункции

Функция СтавкиНДС()
	
	Ставки = Новый Соответствие;
	Ставки.Вставить("Без НДС", Перечисления.СтавкиНДС.БезНДС);
	Ставки.Вставить("НДС 0%", Перечисления.СтавкиНДС.НДС0);
	Ставки.Вставить("НДС 10%", Перечисления.СтавкиНДС.НДС10);
	Ставки.Вставить("НДС 18%", Перечисления.СтавкиНДС.НДС18);
	Ставки.Вставить("НДС 20%", Перечисления.СтавкиНДС.НДС20);
	Ставки.Вставить("0%", Перечисления.СтавкиНДС.НДС0);
	Ставки.Вставить("10%", Перечисления.СтавкиНДС.НДС10);
	Ставки.Вставить("18%", Перечисления.СтавкиНДС.НДС18);
	Ставки.Вставить("20%", Перечисления.СтавкиНДС.НДС20);
	Ставки.Вставить("", Перечисления.СтавкиНДС.БезНДС);
	
	Возврат Ставки;
	
КонецФункции

Процедура ДополнитьПараметрыВходящихИсходящихДокументов(ПараметрыДокумента)
	
	ДокументыАвтонумерации = МассивДокументовАвтонумерации();
	
	ИмяДокумента = ПараметрыДокумента.ИмяОбъекта;
	МетаданныеДокумента = Метаданные.Документы[ИмяДокумента];
	
	ПараметрыДокумента.ЕстьВидОперации = ОбщегоНазначения.ЕстьРеквизитОбъекта("ВидОперации", МетаданныеДокумента);
	ПараметрыДокумента.ЕстьДанныеВходящегоДокумента = ОбщегоНазначения.ЕстьРеквизитОбъекта(
		"НомерВходящегоДокумента", МетаданныеДокумента);
	ПараметрыДокумента.ЕстьКонтрагент = ОбщегоНазначения.ЕстьРеквизитОбъекта("Контрагент", МетаданныеДокумента);
	ПараметрыДокумента.ЕстьДоговорКонтрагента = ОбщегоНазначения.ЕстьРеквизитОбъекта(
		"ДоговорКонтрагента", МетаданныеДокумента);
	ПараметрыДокумента.ЕстьФизЛицо = ОбщегоНазначения.ЕстьРеквизитОбъекта("ФизЛицо", МетаданныеДокумента);
	ПараметрыДокумента.ЭтоСчетФактура = ИмяДокумента = ИмяДокументаСчетФактура();
	ПараметрыДокумента.ЭтоОтчетОРозничныхПродажах = ИмяДокумента = ИмяДокументаОтчетОРозничныхПродажах();
	ПараметрыДокумента.ИмяТабЧасти = ИмяТабличнойЧасти(ПараметрыДокумента.ТипДокумента);
	ПараметрыДокумента.ЕстьСтавкаНДС = ОбщегоНазначенияБП.ЕстьРеквизитТабЧастиДокумента(
		"СтавкаНДС", МетаданныеДокумента, ПараметрыДокумента.ИмяТабЧасти);
	ПараметрыДокумента.ВидДоговора = ВидДоговораСКонтрагентом(ИмяДокумента);
	ПараметрыДокумента.ЗагружатьНомерДокумента = (ДокументыАвтонумерации.Найти(ИмяДокумента) = Неопределено);
	ПараметрыДокумента.ЕстьСклад = ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", МетаданныеДокумента);
	ПараметрыДокумента.ЕстьПодразделение = ОбщегоНазначения.ЕстьРеквизитОбъекта(
		"ПодразделениеОрганизации", МетаданныеДокумента);
	ПараметрыДокумента.ЗаполнитьСчетаУчета = Булево(ПараметрыДокумента.ЗаполнитьСчетаУчета);
	ПараметрыДокумента.ВедетсяУчетВУСН = ИмяДокумента = "ПоступлениеТоваровУслуг";
	ПараметрыДокумента.ДокументБезНДС = ИмяДокумента = "ПоступлениеТоваровУслуг";
	
КонецПроцедуры

Функция НайтиСоздатьСклад(НаименованиеСклада, ТипСклада = Неопределено)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Склады.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Наименование = &Наименование");
	Если ЗначениеЗаполнено(ТипСклада) Тогда
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить("Склады.ТипСклада = &ТипСклада");
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Запрос.УстановитьПараметр("Наименование", НаименованиеСклада);
	Запрос.УстановитьПараметр("ТипСклада", ТипСклада);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		НовыйСклад = Справочники.Склады.СоздатьЭлемент();
		НовыйСклад.Наименование = НаименованиеСклада;
		НовыйСклад.ТипСклада = ?(ЗначениеЗаполнено(ТипСклада), ТипСклада, Перечисления.ТипыСкладов.ОптовыйСклад);
		НовыйСклад.Записать();
		Возврат НовыйСклад.Ссылка;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

Функция НайтиСоздатьОсновнойСклад()
	
	Кол_ВоСкладов = Справочники.Склады.КоличествоСкладов();
	Если Кол_ВоСкладов = 0 Тогда
		ОсновнойСклад = Неопределено;
	Иначе
		ОсновнойСклад = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнойСклад");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОсновнойСклад) Тогда
		Возврат ОсновнойСклад;
	Иначе
		НаименованиеОсновногоСклада = НСтр("ru = 'Основной склад'", ОбщегоНазначения.КодОсновногоЯзыка());
		Возврат НайтиСоздатьСклад(НаименованиеОсновногоСклада);
	КонецЕсли;
	
КонецФункции

Функция НайтиСоздатьФизЛицо(Наименование)
	
	ФизЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.Наименование = &Наименование";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ФизЛицо = Выборка.Ссылка;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизЛицо) Тогда
		
		ФизЛицоОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
		ФизЛицоОбъект.Заполнить(Неопределено);
		
		ФизЛицоОбъект.Наименование = Наименование;
		ФизЛицоОбъект.Записать();
		
		ФизЛицо = ФизЛицоОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат ФизЛицо;
	
КонецФункции

Процедура ЗаполнитьРеквизитыСФ(СчетФактура, СтавкиНДС, ДанныеСФ)
	
	ДанныеСФ.Сумма = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ДанныеСФ.Сумма);
	СуммаНДС = 0;
	Для Каждого Строка Из ДанныеСФ.ТабличнаяЧасть Цикл
		СуммаНДС = СуммаНДС + ОбработатьЧисловойРеквизит(Строка.СуммаНДС);
	КонецЦикла;
	СчетФактура.ВидСчетаФактуры = Перечисления.ВидСчетаФактурыВыставленного.НаРеализацию;
	СчетФактура.ДокументОснование = НайтиДокументРеализации(ДанныеСФ);
	СчетФактура.Сумма = ДанныеСФ.Сумма;
	СчетФактура.СуммаНДСДокумента = СуммаНДС;
	
	НовСтрока = СчетФактура.ДокументыОснования.Добавить();
	НовСтрока.ДокументОснование = СчетФактура.ДокументОснование;
	
	Если Не ПолучитьФункциональнуюОпцию("УплачиватьНДСспецРежимы") Тогда
		Константы.УплачиватьНДСспецРежимы.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧасть(
	НовыйДокумент, ПараметрыДокумента, СтавкиНДС, ДанныеТабличнойЧасти, ТаблицаНоменклатуры)
	
	ВключенУчетНДС = Ложь;
	КолонкаНоменклатура = ИмяКолонкиИдентифицирующейНоменклатуру();
	ЗаполнятьПоСуммеБезНДС = ДанныеТабличнойЧасти.Колонки.Найти("СуммаБезНДС") <> Неопределено;
	Для Каждого Строка Из ДанныеТабличнойЧасти Цикл
		НоваяСтрока = НовыйДокумент[ПараметрыДокумента.ИмяТабЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Если ЗаполнятьПоСуммеБезНДС И ЗначениеЗаполнено(Строка.СуммаБезНДС) Тогда
			НоваяСтрока.Сумма = ?(ЗначениеЗаполнено(Строка.СуммаБезНДС), Строка.СуммаБезНДС, Строка.Сумма);
		ИначеЕсли Не ЗаполнятьПоСуммеБезНДС Тогда
			НоваяСтрока.Сумма = ?(ЗначениеЗаполнено(Строка.СуммаСНДС), Строка.СуммаСНДС, Строка.Сумма);
		КонецЕсли;
		Если ДанныеТабличнойЧасти.Колонки.Найти(КолонкаНоменклатура) <> Неопределено Тогда
			НайденнаяНоменклатура = ТаблицаНоменклатуры.Найти(Строка[КолонкаНоменклатура], КолонкаНоменклатура);
			Если НайденнаяНоменклатура <> Неопределено Тогда
				НоваяСтрока.Номенклатура = НайденнаяНоменклатура.Ссылка;
				СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаУчетаНоменклатуры(
					НовыйДокумент.Организация, НоваяСтрока.Номенклатура);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СчетаУчета);
			КонецЕсли;
		КонецЕсли;
		Если ПараметрыДокумента.ЕстьСтавкаНДС Тогда
			НоваяСтрока.СтавкаНДС = СтавкиНДС.Получить(Строка.СтавкаНДС);
			Если Не ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) Тогда
				НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			КонецЕсли;
			Если ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) И Не НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
				Если Не ВключенУчетНДС Тогда
					МетаданныеДокумента = НовыйДокумент.Метаданные();
					Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ДокументБезНДС", МетаданныеДокумента) Тогда
						НовыйДокумент.ДокументБезНДС = Ложь;
					ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("НДСНеВыделять", МетаданныеДокумента) Тогда
						НовыйДокумент.НДСНеВыделять = Ложь;
					КонецЕсли;
					НовыйДокумент.СуммаВключаетНДС = Не ЗаполнятьПоСуммеБезНДС;
					Если ОбщегоНазначения.ЕстьРеквизитОбъекта("НДСВключенВСтоимость", МетаданныеДокумента) Тогда
						НовыйДокумент.НДСВключенВСтоимость = Не ЗаполнятьПоСуммеБезНДС;
					КонецЕсли;
				КонецЕсли;
				Если Не ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) Тогда
					ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(
						НоваяСтрока, НовыйДокумент, ПараметрыДокумента.ИмяТабЧасти, НовыйДокумент.Метаданные());
				КонецЕсли;
				Если Не ЗначениеЗаполнено(НоваяСтрока.СуммаНДС) Тогда
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(
						НоваяСтрока, НовыйДокумент.СуммаВключаетНДС);
				КонецЕсли;
				ПараметрыДокумента.ДокументБезНДС = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если ПараметрыДокумента.ВедетсяУчетВУСН Тогда
			НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыДокумента.ЭтоОтчетОРозничныхПродажах Тогда
		ДанныеДокумента = НовыеДанныеОбъекта();
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, НовыйДокумент);
		ДанныеДокумента.СпособЗаполненияЦены = Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам;
		Для Каждого Строка Из НовыйДокумент[ПараметрыДокумента.ИмяТабЧасти] Цикл
			ДанныеДокумента.СтавкаНДС = Строка.СтавкаНДС;
			СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
				Строка.Номенклатура, ДанныеДокумента, Ложь);
			Если СведенияОНоменклатуре = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Строка.Цена = СведенияОНоменклатуре.Цена;
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(Строка, 1);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(Строка, ДанныеДокумента.СуммаВключаетНДС);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция НовыеДанныеОбъекта()
	
	Данные = Новый Структура;
	Данные.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Данные.Вставить("Дата", Дата(1, 1, 1));
	Данные.Вставить("СуммаВключаетНДС", Ложь);
	Данные.Вставить("Склад", Справочники.Склады.ПустаяСсылка());
	Данные.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.ПустаяСсылка());
	Данные.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПустаяСсылка());
	
	Возврат Данные;
	
КонецФункции

Функция НовыйРезультатПоискаДатыЛистаКассовойКниги()
	
	Результат = Новый Структура;
	// На каждом листе кассовой книги отдельно читается дата за которую сформирован лист
	Результат.Вставить("ДатаЛиста", Дата(1, 1, 1));
	// Номер строки на которой найдена дата листа, дальнейшее чтение листа продолжается со следующей строки
	Результат.Вставить("НомерСтроки", 0);
	Возврат Результат;
	
КонецФункции

Функция НовыйПараметрыОперацииДвиженияДенег()
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ВидОперации");
	ПараметрыОперации.Вставить("ИмяПеречисления");
	ПараметрыОперации.Вставить("Счет");
	
	Возврат ПараметрыОперации;
	
КонецФункции

Функция МассивВидыОперацийПрочийДоход()
	
	МассивОпераций = Новый Массив;
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПрочееПоступление);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийПКО.ПрочийПриход);
	
	Возврат МассивОпераций;
	
КонецФункции

Функция МассивВидыОперацийПрочийРасход()
	
	МассивОпераций = Новый Массив;
	МассивОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание);
	МассивОпераций.Добавить(Перечисления.ВидыОперацийРКО.ПрочийРасход);
	
	Возврат МассивОпераций;
	
КонецФункции

Функция ВидыОперацийУплатаНалога()
	
	ВидыОпераций = Новый Массив;
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.УплатаНалога);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
	ВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц);
	Возврат ВидыОпераций;
	
КонецФункции

Функция МассивДокументовАвтонумерации()
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить("ПоступлениеТоваровУслуг");
	МассивДокументов.Добавить("РеализацияТоваровУслуг");
	МассивДокументов.Добавить("СчетНаОплатуПокупателю");
	МассивДокументов.Добавить("ПоступлениеНаРасчетныйСчет");
	МассивДокументов.Добавить("СписаниеСРасчетногоСчета");
	МассивДокументов.Добавить("ПриходныйКассовыйОрдер");
	МассивДокументов.Добавить("РасходныйКассовыйОрдер");
	
	Возврат МассивДокументов;
	
КонецФункции

Функция ИмяТабличнойЧасти(ТипДокумента)
	
	ИмяТабЧасти = "";
	Если ТипДокумента <> "Счёт-фактура" Тогда
		ИмяТабЧасти = ?(НРег(ТипДокумента) = "акт", "Услуги", "Товары");
	КонецЕсли;
	
	Возврат ИмяТабЧасти;
	
КонецФункции

Функция ВидДоговораСКонтрагентом(ИмяДокумента)
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("СчетНаОплатуПоставщика", Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	Соответствие.Вставить("ПоступлениеТоваровУслуг", Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	Соответствие.Вставить("СчетНаОплатуПокупателю", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	Соответствие.Вставить("РеализацияТоваровУслуг", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	
	Возврат Соответствие.Получить(ИмяДокумента);
	
КонецФункции

Функция НайтиДокументРеализации(ПараметрыСчетаФактуры)
	
	ДокументРеализации = Документы.РеализацияТоваровУслуг.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", ПараметрыСчетаФактуры.Контрагент);
	Запрос.УстановитьПараметр("СуммаДокумента", ПараметрыСчетаФактуры.Сумма);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ПараметрыСчетаФактуры.Дата));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
	|	И РеализацияТоваровУслуг.СуммаДокумента = &СуммаДокумента
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, ДЕНЬ) = &Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДокументРеализации = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ДокументРеализации;
	
КонецФункции

Процедура ЗаполнитьСоответствияРеквизитовВходящихИсходящихДокументов(ТаблицаРеквизитов)
	
	ДобавитьСоответствие(ТаблицаРеквизитов, "Дата", "дата документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипДокумента", "тип документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Номер", "номер документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Контрагент", "наименование контрагента");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Сумма", "сумма документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Комментарий", "комментарий");
	СтрокаТабличнаяЧасть = ДобавитьСоответствие(ТаблицаРеквизитов, "ТабличнаяЧасть", "фактурная часть");
	СтрокаТабличнаяЧасть.Значение = СоответствияТабличнойЧастиДокументов();
	СтрокаРасчетныйСчетОрганизации = ДобавитьСоответствие(ТаблицаРеквизитов, "РасчетныйСчет", "расчетный счет");
	СтрокаРасчетныйСчетОрганизации.Значение = СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов();
	СтрокаРасчетныйСчетОрганизации = ДобавитьСоответствие(
		ТаблицаРеквизитов, "РасчетныйСчетКонтрагента", "расчетный счет контрагента");
	СтрокаРасчетныйСчетОрганизации.Значение = СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов();
	
КонецПроцедуры

Процедура ЗаполнитьСоответствияРеквизитовКассовойКниги(ТаблицаРеквизитов)
	
	ДобавитьСоответствие(ТаблицаРеквизитов, "Номер", "номер доку- мента");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Номер", "номер докумен- та");
	// Значение даты хранится в шапке каждого листа кассовой книги, заполняется отдельно
	ДобавитьСоответствие(ТаблицаРеквизитов, "Дата");
	// Тип документа определяется в момент чтения данных из файла
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипДокумента");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Контрагент",
		"от кого получено или кому выдано");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СчетУчета", "номер кор- респонди- рующего счета, субсчета");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаПоступления",
		"приход, руб. коп.");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаСписания",
		"расход, руб. коп.");
	
КонецПроцедуры

Процедура ЗаполнитьСоответствияРеквизитовДокументовПокупкиПродажи(ТаблицаРеквизитов)
	
	ДобавитьСоответствие(ТаблицаРеквизитов, "Дата", "дата документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Номер", "номер документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ДатаПрошлогоПериода", "дата забытого документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НомерПрошлогоПериода", "номер забытого документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипДокумента", "тип документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипНакладной", "тип накладной");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Контрагент", "контрагент / сотрудник");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Контрагент", "контрагент");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Склад", "склад");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Склад", "со склада");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ВидНоменклатуры", "тип");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяКолонкиИдентифицирующейНоменклатуру(), "наименование");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ЕдиницаИзмерения", "ед.изм.");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Количество", "кол-во");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ФактическоеКоличество", "фактическое кол-во");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Цена", "цена без ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаБезНДС", "сумма без ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СтавкаНДС", "ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаНДС", "сумма ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаСНДС", "сумма с ндс");
	ДобавитьСоответствие(
		ТаблицаРеквизитов, ИмяКолонкиИдентифицирующейРазделПокупки(), "наличие несоответствия по количеству / качеству");
	
КонецПроцедуры

Процедура ЗаполнитьСоответствияРеквизитовДенежныхДокументов(ТаблицаРеквизитов)
	
	ДобавитьСоответствие(ТаблицаРеквизитов, "Дата", "дата");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипДокумента", "тип документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НомерВходящегоДокумента", "№ документа");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Номер");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Контрагент", "контрагент");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ВидОперации", "тип операции");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ВидДенежныхСредств", "тип денег (банк/касса/прочее)");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НомерРасчетногоСчета", "р/с");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НаименованиеБанка", "наименование банка");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НазначениеПлатежа", "описание");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаПоступления", "поступило");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаСписания", "списано");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаУслуг", "комиссия");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ДоходыУСН", "учитывается в налоге усн (доход)");
	ДобавитьСоответствие(ТаблицаРеквизитов, "РасходыУСН", "учитывается в налоге усн (расход)");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаПатент", "учитывается в патенте");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаДокумента"); // заполняется при загрузке документа
	СтрокаРасчетныйСчетОрганизации = ДобавитьСоответствие(
		ТаблицаРеквизитов, "РасчетныйСчетКонтрагента", "расчетный счет контрагента");
	СтрокаРасчетныйСчетОрганизации.Значение = СоответствияРеквизитовРасчетныхСчетовДляТекстовыхФайлов();
	
КонецПроцедуры

Функция СоответствияТабличнойЧастиДокументов()
	
	ТаблицаРеквизитов = НоваяТаблицаРеквизитов();
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяКолонкиИдентифицирующейНоменклатуру(), "наименование");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Количество",               "количество");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ЕдиницаИзмерения",         "ед. измерения");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Цена",                     "цена за ед. товара");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаСНДС",                "сумма с ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СуммаНДС",                 "ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "СтавкаНДС",                "ставка ндс");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ПроцентСкидки",            "процент скидки");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Сумма",                    "сумма");
	// Свойство заполняется в момент загрузки документа
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипДокумента");
	Возврат ТаблицаРеквизитов;
	
КонецФункции

Функция ТипыДокументовСВидомУслуги()
	
	ИменаТипов = Новый Массив;
	ИменаТипов.Добавить("акт");
	Возврат ИменаТипов;
	
КонецФункции

Функция СформироватьНомерДокумента(Организация, Подразделение, ТипДокумента, Номер)
	
	ПрефиксНомера = ПрефиксацияОбъектовБПСобытия.ПрефиксИнформационнойБазыОрганизацииПодразделения(
		Организация, Подразделение, "");
	ДлинаНомера = Метаданные.Документы[ТипДокумента].ДлинаНомера;
	Возврат ПрефиксНомера + СтроковыеФункцииКлиентСервер.ДополнитьСтроку(
		Номер, ДлинаНомера - СтрДлина(ПрефиксНомера), "0", "Слева");
	
КонецФункции

Функция ДокументНайденВБазе(РеквизитыДокумента, ТипДокумента)
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	&ТаблицаДокумента КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Дата = &Дата
		|	И ТаблицаДокумента.Номер = &Номер
		|	И ТаблицаДокумента.СуммаДокумента = &СуммаДокумента
		|	И ТаблицаДокумента.Организация = &Организация";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаДокумента", "Документ." + ТипДокумента);
	Запрос.УстановитьПараметр("Дата", РеквизитыДокумента.Дата);
	Запрос.УстановитьПараметр("Номер", РеквизитыДокумента.Номер);
	Запрос.УстановитьПараметр(
		"СуммаДокумента", ?(ЗначениеЗаполнено(РеквизитыДокумента.СуммаДокумента), РеквизитыДокумента.СуммаДокумента, 0));
	Запрос.УстановитьПараметр("Организация", РеквизитыДокумента.Организация);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#Область Номенклатура

Функция НоваяТаблицаНоменклатуры()
	
	ОписаниеРеквизитов = НоваяТаблицаРеквизитов();
	ЗаполнитьСоответствияРеквизитовНоменклатуры(ОписаниеРеквизитов);
	Номенклатура = Новый ТаблицаЗначений;
	Для Каждого Строка Из ОписаниеРеквизитов Цикл
		Номенклатура.Колонки.Добавить(Строка[ИмяРеквизита1С()]);
	КонецЦикла;
	
	Возврат Номенклатура;
	
КонецФункции

Процедура ЗаполнитьСоответствияРеквизитовНоменклатуры(ТаблицаРеквизитов)
	
	ДобавитьСоответствие(ТаблицаРеквизитов, "Родитель",                 "группа");
	ДобавитьСоответствие(ТаблицаРеквизитов, ИмяКолонкиИдентифицирующейНоменклатуру(), "наименование товара");
	ДобавитьСоответствие(ТаблицаРеквизитов, "Артикул",                  "артикул");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ЕдиницаИзмерения",         "единица измерения");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ЦенаПродажи",              "цена продажи");
	ДобавитьСоответствие(ТаблицаРеквизитов, "НДС",                      "ндс");
	// Свойства, заполняемые в момент загрузки номенклатуры
	ДобавитьСоответствие(ТаблицаРеквизитов, "Ссылка");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ТипДокументаВладельца");
	ДобавитьСоответствие(ТаблицаРеквизитов, "ВидНоменклатуры");
	
КонецПроцедуры

Функция НайтиСоздатьЕдиницуИзмерения(ЕдиницаИзмерения)
	
	// Поскольку в загружаемом файле единица измерения может содержать абсолютно любое значение
	// используем конструкцию Попытка-Исключения, для обхода некорректных значений,
	// заведомо не являющихся кодом единицы измерения по классификатору
	Попытка
		НайденнаяЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ЕдиницаИзмеренияПоКоду(
			ЕдиницаИзмерения, ЕдиницаИзмерения);
	Исключение
		НайденнаяЕдиницаИзмерения = Неопределено;
		ЗаписьЖурналаРегистрации(
			ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, Метаданные.Справочники.КлассификаторЕдиницИзмерения, ,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат НайденнаяЕдиницаИзмерения;
	
КонецФункции

Функция РеквизитыНоменклатурыОбязательныеКЗаполнению()
	
	ОбязательныеРеквизиты = Новый Массив;
	ОбязательныеРеквизиты.Добавить(ИмяКолонкиИдентифицирующейНоменклатуру());
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

Процедура ЗагрузитьНоменклатуру(ИмяРаздела, ПрочитанныеДанные, Ошибки)
	
	ТекстОшибки = НСтр("ru = 'Не удалось записать номенклатуру %1 по причине:
		|%2'");
	
	СуществующаяНоменклатура = ПрочитанныеДанные[ИмяНоменклатура()];
	
	Если ИмяРаздела = ИмяНоменклатура() Тогда
		// Номенклатура прочитана отдельным файлом, который загружается перед документами
		НоваяНоменклатура = СуществующаяНоменклатура;
	Иначе
		// Информация о номенклатуре прочитана в составе табличной части документов, в первую очередь необходимо
		// проверить, не была ли номенклатура загружена ранее
		НоваяНоменклатура = НоваяТаблицаНоменклатуры();
		ДанныеРаздела = ПрочитанныеДанные[ИмяРаздела];
		Для Каждого СтрокаДокумента Из ДанныеРаздела Цикл
			Если ДанныеРаздела.Колонки.Найти("ТабличнаяЧасть") <> Неопределено Тогда
				Для Каждого СтрокаТЧ Из СтрокаДокумента.ТабличнаяЧасть Цикл
					ДополнитьТаблицуНовойНоменклатуры(СтрокаТЧ, СуществующаяНоменклатура, НоваяНоменклатура,
						СтрокаДокумента.ТипДокумента);
				КонецЦикла;
			Иначе
				ДополнитьТаблицуНовойНоменклатуры(СтрокаДокумента, СуществующаяНоменклатура, НоваяНоменклатура,
					СтрокаДокумента.ТипДокумента);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоваяНоменклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.Наименование КАК Наименование,
		|	Номенклатура.Артикул КАК Артикул,
		|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Наименование В(&НаименованияЗагружаемойНоменклатуры)
		|	И НЕ Номенклатура.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.ЭтоГруппа
		|	И Номенклатура.Наименование В (&НаименованияГрупп)";
		
	Запрос.УстановитьПараметр(
		"НаименованияЗагружаемойНоменклатуры",
		НоваяНоменклатура.ВыгрузитьКолонку(ИмяКолонкиИдентифицирующейНоменклатуру()));
	Запрос.УстановитьПараметр(
		"НаименованияГрупп", НоваяНоменклатура.ВыгрузитьКолонку("Родитель"));
		
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	ГруппыНоменклатуры = РезультатЗапроса[1].Выгрузить();
	
	ПараметрыСозданияНоменклатуры = НовыеПараметрыСозданияНоменклатуры();
	ЗаполнитьПараметрыСозданияНоменклатуры(ПараметрыСозданияНоменклатуры, ПрочитанныеДанные.Организация);
	
	ОбработаннаяНоменклатура = Новый Массив;
	Для Каждого СтрокаНоменклатура Из НоваяНоменклатура Цикл
		Наименование = СтрокаНоменклатура[ИмяКолонкиИдентифицирующейНоменклатуру()];
		Если ОбработаннаяНоменклатура.Найти(Наименование) = Неопределено Тогда
			ОбработаннаяНоменклатура.Добавить(Наименование);
		Иначе
			Продолжить;
		КонецЕсли;
		Если Выборка.НайтиСледующий(Наименование, "Наименование") Тогда
			СтрокаНоменклатура.Ссылка = Выборка.Ссылка;
			ТребуетсяЗаполнитьАртикул = ЗначениеЗаполнено(СтрокаНоменклатура.Артикул)
				И Не ЗначениеЗаполнено(Выборка.Артикул);
			ТребуетсяЗаполнитьЕдиницуИзмерения = ЗначениеЗаполнено(СтрокаНоменклатура.ЕдиницаИзмерения)
				И Не ЗначениеЗаполнено(Выборка.ЕдиницаИзмерения);
			ИзменитьОбъект = ТребуетсяЗаполнитьАртикул Или ТребуетсяЗаполнитьЕдиницуИзмерения;
			Если ИзменитьОбъект Тогда
				НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Если ТребуетсяЗаполнитьАртикул Тогда
					НоменклатураОбъект.Артикул = СтрокаНоменклатура.Артикул;
				КонецЕсли;
				Если ТребуетсяЗаполнитьЕдиницуИзмерения Тогда
					ЕдиницаИзмерения = НайтиСоздатьЕдиницуИзмерения(СтрокаНоменклатура.ЕдиницаИзмерения);
					НоменклатураОбъект.ЕдиницаИзмерения =
						?(ЗначениеЗаполнено(ЕдиницаИзмерения), ЕдиницаИзмерения,
						ПараметрыСозданияНоменклатуры.ЕдиницаИзмеренияПоУмолчанию);
				КонецЕсли;
				Попытка
					НоменклатураОбъект.Записать();
				Исключение
					ШаблонОшибки = НСтр("ru = 'Не удалось изменить номенклатуру %1 по причине:
						|%2'");
					ТекстОшибки = СтрШаблон(
						ШаблонОшибки, Наименование, ПодробноеПредставлениеОшибки(ОписаниеОшибки()));
					ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки, "");
					ЗаписьЖурналаРегистрации(
						ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
						Метаданные.Справочники.Номенклатура, , ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
		Иначе
			СоздатьНоменклатуру(СтрокаНоменклатура, ГруппыНоменклатуры, ПараметрыСозданияНоменклатуры, Ошибки);
		КонецЕсли;
		Если ПараметрыСозданияНоменклатуры.ДоступнаУстановкаЦен И ЗначениеЗаполнено(СтрокаНоменклатура.ЦенаПродажи)
			И ЗначениеЗаполнено(СтрокаНоменклатура.Ссылка) Тогда
			УстановитьЦенуНоменклатуры(СтрокаНоменклатура, ПараметрыСозданияНоменклатуры);
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	// Если номенклатура загружается отдельным файлом, то она загружается в первую очередь и вся автоматически
	// попадает в таблицу с загруженной номенклатурой, если номенклатура загружается в составе файла
	// с данными документов, то в таблицу с загруженной номенклатурой добавляются только те позиции, которые
	// не были загружены ранее
	Если ИмяРаздела = ИмяНоменклатура() Тогда
		Возврат;
	КонецЕсли;
	Для Каждого СтрокаНовойНоменклатуры Из НоваяНоменклатура Цикл
		СтрокаСуществующейНоменклатуры = СуществующаяНоменклатура.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСуществующейНоменклатуры, СтрокаНовойНоменклатуры);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьТаблицуНовойНоменклатуры(Строка, СуществующаяНоменклатура, НоваяНоменклатура, ТипДокумента)
	
	КолонкаНаименование = ИмяКолонкиИдентифицирующейНоменклатуру();
	Если ЗначениеЗаполнено(Строка[КолонкаНаименование]) Тогда
		Если СуществующаяНоменклатура.Найти(Строка[КолонкаНаименование]) = Неопределено
			И НоваяНоменклатура.Найти(Строка[КолонкаНаименование], КолонкаНаименование) = Неопределено Тогда
			СтрокаНовойНоменклатуры = НоваяНоменклатура.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНовойНоменклатуры, Строка);
			СтрокаНовойНоменклатуры.ТипДокументаВладельца = ТипДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция НовыеПараметрыСозданияНоменклатуры()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДоступнаУстановкаЦен", Истина);
	Параметры.Вставить("ВалютаРеглУчета", Справочники.Валюты.ПустаяСсылка());
	Параметры.Вставить("ЕдиницаИзмеренияПоУмолчанию", Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка());
	Параметры.Вставить("ВидТовар", Справочники.ВидыНоменклатуры.ПустаяСсылка());
	Параметры.Вставить("ВидУслуга", Справочники.ВидыНоменклатуры.ПустаяСсылка());
	Параметры.Вставить("ВидСтавкиНДСПоУмолчанию", Перечисления.ВидыСтавокНДС.ПустаяСсылка());
	Параметры.Вставить("СтавкаНДСПоУмолчанию", Перечисления.СтавкиНДС.ПустаяСсылка());
	Параметры.Вставить("ТипыДокументовДляУслуг");
	Параметры.Вставить("ПлательщикНДС");
	Возврат Параметры;
	
КонецФункции

Процедура ЗаполнитьПараметрыСозданияНоменклатуры(ПараметрыСозданияНоменклатуры, Организация)
	
	ПараметрыСозданияНоменклатуры.ДоступнаУстановкаЦен = 
		ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ЦеныНоменклатурыДокументов);
	ПараметрыСозданияНоменклатуры.ВалютаРеглУчета =
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ПараметрыСозданияНоменклатуры.ЕдиницаИзмеренияПоУмолчанию =
		Справочники.КлассификаторЕдиницИзмерения.ПолучитьЕдиницуИзмеренияПоУмолчанию();
	ПараметрыСозданияНоменклатуры.ВидТовар = Справочники.ВидыНоменклатуры.НайтиСоздатьЭлементыТовар();
	ПараметрыСозданияНоменклатуры.ВидУслуга = Справочники.ВидыНоменклатуры.НайтиСоздатьЭлементыУслуга();
	ПараметрыСозданияНоменклатуры.ТипыДокументовДляУслуг = ТипыДокументовСВидомУслуги();
	ПараметрыСозданияНоменклатуры.ПлательщикНДС =
		УчетнаяПолитика.ПлательщикНДС(Организация, ТекущаяДатаСеанса());
	ПараметрыСозданияНоменклатуры.СтавкаНДСПоУмолчанию =
		УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(ТекущаяДатаСеанса(), ПараметрыСозданияНоменклатуры.ПлательщикНДС);
	ПараметрыСозданияНоменклатуры.ВидСтавкиНДСПоУмолчанию =
		Перечисления.ВидыСтавокНДС.ВидСтавки(ПараметрыСозданияНоменклатуры.СтавкаНДСПоУмолчанию);
	
КонецПроцедуры

Процедура УстановитьЦенуНоменклатуры(СтрокаНоменклатура, ПараметрыНоменклатуры)
	
	МенеджерЗаписи = РегистрыСведений.ЦеныНоменклатурыДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = СтрокаНоменклатура.Ссылка;
	МенеджерЗаписи.СпособЗаполненияЦены = Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам;
	МенеджерЗаписи.Цена = СтрокаНоменклатура.ЦенаПродажи;
	МенеджерЗаписи.ЦенаВключаетНДС = ПараметрыНоменклатуры.ПлательщикНДС;
	МенеджерЗаписи.Валюта = ПараметрыНоменклатуры.ВалютаРеглУчета;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура СоздатьНоменклатуру(СтрокаНоменклатура, ГруппыНоменклатуры, ПараметрыСоздания, Ошибки)
	
	СтавкиНДС = СтавкиНДС();
	РеквизитНаименование = ИмяКолонкиИдентифицирующейНоменклатуру();
	НоменклатураОбъект = Справочники.Номенклатура.СоздатьЭлемент();
	Если ЗначениеЗаполнено(СтрокаНоменклатура.Родитель) Тогда
		СтрокаГруппы = ГруппыНоменклатуры.Найти(СтрокаНоменклатура.Родитель, "Наименование");
		Если СтрокаГруппы <> Неопределено Тогда
			НоменклатураОбъект.Родитель = СтрокаГруппы.Ссылка;
		Иначе
			НоваяГруппа = Справочники.Номенклатура.СоздатьГруппу();
			НоваяГруппа.Наименование = СтрокаНоменклатура.Родитель;
			НоваяГруппа.Записать();
			НоменклатураОбъект.Родитель = НоваяГруппа.Ссылка;
			СтрокаГруппы = ГруппыНоменклатуры.Добавить();
			СтрокаГруппы.Ссылка = НоваяГруппа.Ссылка;
			СтрокаГруппы.Наименование = НоваяГруппа.Наименование;
		КонецЕсли;
	КонецЕсли;
	НоменклатураОбъект.Наименование = СтрокаНоменклатура[РеквизитНаименование];
	НоменклатураОбъект.НаименованиеПолное = СтрокаНоменклатура[РеквизитНаименование];
	Если ЗначениеЗаполнено(СтрокаНоменклатура.ВидНоменклатуры) Тогда
		Если НРег(СтрокаНоменклатура.ВидНоменклатуры) = ИмяВидаНоменклатурыТовар() Тогда
			НоменклатураОбъект.ВидНоменклатуры = ПараметрыСоздания.ВидТовар;
		ИначеЕсли НРег(СтрокаНоменклатура.ВидНоменклатуры) = ИмяВидаНоменклатурыУслуга() Тогда
			НоменклатураОбъект.ВидНоменклатуры = ПараметрыСоздания.ВидУслуга;
			НоменклатураОбъект.Услуга = Истина;
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(СтрокаНоменклатура.ТипДокументаВладельца) И
		ПараметрыСоздания.ТипыДокументовДляУслуг.Найти(НРег(СтрокаНоменклатура.ТипДокументаВладельца)) <> Неопределено Тогда
		НоменклатураОбъект.ВидНоменклатуры = ПараметрыСоздания.ВидУслуга;
		НоменклатураОбъект.Услуга = Истина;
	Иначе
		НоменклатураОбъект.ВидНоменклатуры = ПараметрыСоздания.ВидТовар;
	КонецЕсли;
	
	ПрочитаннаяСтавкаНДС = ОбработатьСтроковоеЗначениеСтавкиНДС(СтрокаНоменклатура.НДС);
	СтавкаНДС = СтавкиНДС.Получить(ПрочитаннаяСтавкаНДС);
	ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.ВидСтавки(СтавкаНДС);
	НоменклатураОбъект.ВидСтавкиНДС =
		?(ЗначениеЗаполнено(ВидСтавкиНДС), ВидСтавкиНДС, ПараметрыСоздания.ВидСтавкиНДСПоУмолчанию);
	
	НоменклатураОбъект.Артикул = СтрокаНоменклатура.Артикул;
	НоменклатураОбъект.ЕдиницаИзмерения = НайтиСоздатьЕдиницуИзмерения(СтрокаНоменклатура.ЕдиницаИзмерения);
	Если Не ЗначениеЗаполнено(НоменклатураОбъект.ЕдиницаИзмерения) Тогда
		НоменклатураОбъект.ЕдиницаИзмерения = ПараметрыСоздания.ЕдиницаИзмеренияПоУмолчанию;
	КонецЕсли;
	Попытка
		НоменклатураОбъект.Записать();
		СтрокаНоменклатура.Ссылка = НоменклатураОбъект.Ссылка;
	Исключение
		ШаблонОшибки = НСтр("ru = 'Не удалось записать номенклатуру %1 по причине:
			|%2'");
		ТекстОшибки = СтрШаблон(
			ШаблонОшибки, СтрокаНоменклатура[РеквизитНаименование], ПодробноеПредставлениеОшибки(ОписаниеОшибки()));
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстОшибки, "");
		ЗаписьЖурналаРегистрации(
			ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.Номенклатура, , ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

Функция ОбработатьСтроковоеЗначениеСтавкиНДС(СтавкаНДС)
	
	ПрочитаннаяСтавка = "";
	Для Сч = 1 По СтрДлина(СтавкаНДС) Цикл
		ОчереднойСимвол = Сред(СтавкаНДС, Сч, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ОчереднойСимвол) Или ОчереднойСимвол = "%" Тогда
			ПрочитаннаяСтавка = ПрочитаннаяСтавка + ОчереднойСимвол;
		КонецЕсли;
	КонецЦикла;
	Если Не ЗначениеЗаполнено(ПрочитаннаяСтавка) Тогда
		ПрочитаннаяСтавка = "Без НДС";
	КонецЕсли;
	
	Возврат ПрочитаннаяСтавка;
	
КонецФункции

#КонецОбласти

#Область Имена

Функция ИмяФорматаТекстовогоФайла()
	
	Возврат "csv";
	
КонецФункции

Функция ИмяФорматаЭлектронныхТаблиц()
	
	Возврат "xls";
	
КонецФункции

Функция ИмяРеквизитыОрганизации()
	
	Возврат "Реквизиты";
	
КонецФункции

Функция ИмяКонтрагенты()
	
	Возврат "Контрагенты";
	
КонецФункции

Функция ИмяТипыДокументов()
	
	Возврат "ТипыДокументов";
	
КонецФункции

Функция ИмяДокументыДвиженияДенег()
	
	Возврат "Деньги";
	
КонецФункции

Функция ИмяВходящиеДокументы()
	
	Возврат "Входящие_документы";
	
КонецФункции

Функция ИмяИсходящиеДокументы()
	
	Возврат "Исходящие_документы";
	
КонецФункции

Функция ИмяОперацииПроведенияДокументов()
	
	Возврат "ПроведениеДокументов";
	
КонецФункции

Функция ИмяНоменклатура()
	
	Возврат "Номенклатура";
	
КонецФункции

Функция ИмяКассоваяКнига()
	
	Возврат "КассоваяКнига";
	
КонецФункции

Функция ИмяРазделаПокупки()
	
	Возврат ИмяДокументы() + "_Покупка";
	
КонецФункции

Функция ИмяРазделаПродажи()
	
	Возврат ИмяДокументы() + "_Продажа";
	
КонецФункции

Функция ИмяКолонкиИдентифицирующейРазделПокупки()
	
	Возврат "ПризнакПокупки";
	
КонецФункции

Функция ИмяКолонкиИдентифицирующейНоменклатуру()
	
	Возврат "НаименованиеНоменклатуры";
	
КонецФункции

Функция ИмяОбластиНоменклатура()
	
	Возврат "Справочник";
	
КонецФункции

Функция ИмяДокументы()
	
	Возврат "Документы";
	
КонецФункции

Функция ИмяРеквизитаДляРасчетныхСчетов()
	
	Возврат "РасчетныйСчет";
	
КонецФункции

Функция ОбозначениеРеквизитаДляРасчетныхСчетовВФайле()
	
	Возврат "р/с в банках:";
	
КонецФункции

Функция РазделительРасчетныхСчетов()
	
	Возврат "р/с";
	
КонецФункции

Функция ИмяРеквизитаНомераРасчетногоСчета()
	
	Возврат "НомерСчета";
	
КонецФункции

Функция ИмяРеквизитаНаименованияБанка()
	
	Возврат "НаименованиеБанка";
	
КонецФункции

Функция ИмяРеквизитаБИКБанка()
	
	Возврат "БИК";
	
КонецФункции

Функция ИмяРеквизитаКоррСчетБанка()
	
	Возврат "КоррСчетБанка";
	
КонецФункции

Функция ИмяМакетаВидыОперацийКассовыхДокументов()
	
	Возврат "ВидыОперацийКассовыхДокументовПоСчетамУчета";
	
КонецФункции

Функция ИмяДокументаПКО()
	
	Возврат "ПриходныйКассовыйОрдер";
	
КонецФункции

Функция ИмяДокументаРКО()
	
	Возврат "РасходныйКассовыйОрдер";
	
КонецФункции

Функция ТипДенегБанк()
	
	Возврат "банк";
	
КонецФункции

Функция ТипДенегКасса()
	
	Возврат "касса";
	
КонецФункции

Функция ИмяМакетаВидыОперацийДвиженияДенег()
	
	Возврат "ВидыОперацийДвиженияДенег";
	
КонецФункции

Функция ИмяОперацииУплатыПенейШтрафовПоНалогам()
	
	Возврат "Пени, штрафы по налогам и взносам";
	
КонецФункции

Функция ИмяДокументаСчетФактура()
	
	Возврат "СчетФактураВыданный";
	
КонецФункции

Функция ИмяДокументаОтчетОРозничныхПродажах()
	
	Возврат "ОтчетОРозничныхПродажах";
	
КонецФункции

Функция НаименованиеСлужебногоФизЛица()
	
	Возврат "Служебный";
	
КонецФункции

Функция ИмяВидаНоменклатурыТовар()
	
	Возврат "товар";
	
КонецФункции

Функция ИмяВидаНоменклатурыУслуга()
	
	Возврат "услуга";
	
КонецФункции

Функция ИмяРеквизита1С()
	
	Возврат "ИмяРеквизита1С";
	
КонецФункции

Функция ИмяРеквизитаВФайле()
	
	Возврат "ИмяРеквизитаВФайле";
	
КонецФункции

Функция ИмяОдностраничногоФорматаКассовойКниги()
	
	Возврат "Одностраничный";
	
КонецФункции

Функция ИмяДвухстраничногоФорматаКассовойКниги()
	
	Возврат "Двухстраничный";
	
КонецФункции

Функция НаименованиеСкладаНТТ()
	
	Возврат НСтр("ru = 'Торговая точка'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

Функция ТаблицаСопоставления(ИмяМакета)
	
	Макет = ПолучитьМакет(ИмяМакета);
	ОбластьКолонки = Макет.ПолучитьОбласть("КолонкиТаблицы");
	
	ТаблицаСопоставления = Новый ТаблицаЗначений;
	Для НомерКолонки = 1 По ОбластьКолонки.ШиринаТаблицы Цикл
		ТаблицаСопоставления.Колонки.Добавить(ОбластьКолонки.Область(1, НомерКолонки).Текст, Новый ОписаниеТипов("Строка"));
	КонецЦикла;
	
	ОбластьПараметры = Макет.ПолучитьОбласть("ЗначенияТаблицы");
	Для НомерСтроки = 1 По ОбластьПараметры.ВысотаТаблицы Цикл
		
		НовСтрока = ТаблицаСопоставления.Добавить();
		Для НомерКолонки = 1 По ОбластьПараметры.ШиринаТаблицы Цикл
			НовСтрока[НомерКолонки - 1] = ОбластьПараметры.Область(НомерСтроки, НомерКолонки).Текст;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаСопоставления;
	
КонецФункции

Функция ЗагруженныеОбъектыРаздела(ЗагружаемыеДанные, ИмяРаздела)
	
	МассивОбъектов = Новый Массив;
	ОбъектыРаздела = ЗагружаемыеДанные.НайтиСтроки(Новый Структура("РазделДанных", ИмяРаздела));
	Для Каждого ОбъектЗагрузки Из ОбъектыРаздела Цикл
		МассивОбъектов.Добавить(ОбъектЗагрузки.ИмяРеквизитаФормы);
	КонецЦикла;
	
	Возврат СтрСоединить(МассивОбъектов,",");
	
КонецФункции

Процедура ДобавитьСтрокуВОписаниеФайлов(Описание, Имя, Заголовок)
	
	НоваяСтрока = Описание.Добавить();
	НоваяСтрока.ИмяФайла = Имя;
	НоваяСтрока.Заголовок = Заголовок;
	НоваяСтрока.ПутьКФайлу = "";
	
КонецПроцедуры

Функция МаксимальныйНомерСтрокиДляПоискаЗаголовка()
	
	Возврат 15;
	
КонецФункции

Функция МаксимальныйНомерКолонкиДляПоискаЗаголовка()
	
	Возврат 30;
	
КонецФункции

Функция ЭтоПоддерживаемаяЭлектроннаяТаблица(Расширение)
	
	Расширения = Новый Массив;
	Расширения.Добавить("xls");
	Расширения.Добавить("xlsx");
	Возврат Расширения.Найти(Расширение) <> Неопределено;
	
КонецФункции

Функция НоваяТаблицаДанных(СоставДанных)
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	Для Каждого ОписаниеРеквизита Из СоставДанных Цикл
		Если ТаблицаДанных.Колонки.Найти(ОписаниеРеквизита[ИмяРеквизита1С()]) = Неопределено Тогда
			Колонка = ТаблицаДанных.Колонки.Добавить(ОписаниеРеквизита[ИмяРеквизита1С()]);
		КонецЕсли;
	КонецЦикла;
	ТаблицаДанных.Колонки.Добавить("КоличествоОбъектов", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10)));
	Возврат ТаблицаДанных;
	
КонецФункции

Процедура УдалитьОтсутствующиеПроверяемыеКолонкиВСоставеДанных(СоставДанных)
	
	ПроверяемыеКолонки = ИменаПроверяемыхКолонокДляДокументовПокупкиПродажи();
	Для Каждого ИмяКолонки Из ПроверяемыеКолонки Цикл
		СтрокаСоставаДанных = СоставДанных.Найти(ИмяКолонки, ИмяРеквизита1С());
		Если СтрокаСоставаДанных <> Неопределено И СтрокаСоставаДанных.Значение = Неопределено Тогда
			СоставДанных.Удалить(СтрокаСоставаДанных);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция РеквизитыОбязательныеКЗаполнению(ИмяФайла)
	
	Если ИмяФайла = ИмяНоменклатура() Тогда
		Возврат РеквизитыНоменклатурыОбязательныеКЗаполнению();
	ИначеЕсли ИмяФайла = ИмяКонтрагенты() Тогда
		Возврат РеквизитыКонтрагентовОбязательныеКЗаполнению();
	ИначеЕсли ИмяФайла = ИмяКассоваяКнига() Тогда
		Возврат РеквизитыКассовойКнигиОбязательныеКЗаполнению();
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
КонецФункции

Функция РазбитьНаПодстрокиБезУчетаРазделителейВнутриКавычек(Строка, Подстроки, Уровень = 0)
	
	Если Подстроки = Неопределено Тогда
		Подстроки = Новый Массив;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Строка) Тогда
		Возврат Подстроки;
	Иначе
		ОткрывающаяКавычка = СтрНайти(Строка, """");
		Если ОткрывающаяКавычка = 0 Тогда
			Результат = Строка;
		Иначе
			Результат = Лев(Строка, ОткрывающаяКавычка - 2);
		КонецЕсли;
		Если Не ОткрывающаяКавычка = 1 Тогда
			НовыеПодстроки = СтрРазделить(Результат, ";", Истина);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Подстроки, НовыеПодстроки, Ложь);
		КонецЕсли;
	КонецЕсли;
	Если ОткрывающаяКавычка = 0 Тогда
		Возврат Подстроки;
	КонецЕсли;
	Строка = Сред(Строка, ОткрывающаяКавычка + 1);
	Для Сч = 1 По СтрДлина(Строка) Цикл
		Если КодСимвола(Строка, Сч) = КодСимволаДвойнойКавычки() Тогда // Нашли кавычку "
			Если КодСимвола(Строка, Сч + 1) = КодСимволаДвойнойКавычки() Тогда
				// Две подряд кавычки "" внутри строки означают одинарную кавычку
				// это не закрывающая кавычка, которую мы ищем
				Сч = Сч + 1;
			Иначе
				Результат = Лев(Строка, Сч - 1);
				Подстроки.Добавить(Результат);
				ОставшаясяСтрока = Сред(Строка, Сч + 2);
				Возврат РазбитьНаПодстрокиБезУчетаРазделителейВнутриКавычек(
					ОставшаясяСтрока, Подстроки, Уровень + 1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Подстроки.Добавить(Строка);
	Возврат Подстроки;
	
КонецФункции

Функция ДобавитьСоответствие(Таблица, ИмяРеквизита1С, ИмяРеквизитаВФайле = "")
	
	Строка = Таблица.Добавить();
	Строка[ИмяРеквизита1С()] = ИмяРеквизита1С;
	Строка[ИмяРеквизитаВФайле()] = ИмяРеквизитаВФайле;
	Возврат Строка;
	
КонецФункции

Функция УдалитьКавычки(Знач Значение)
	
	// Убираем первый и последний символ, если это кавычки
	Если СтрНачинаетсяС(Значение, """") И СтрЗаканчиваетсяНа(Значение, """") Тогда
		Значение = Сред(Значение, 2);
		Значение = Сред(Значение, 1, СтрДлина(Значение) - 1);
	КонецЕсли;
	
	Значение = СтрЗаменить(Значение, """""", """");
	
	Возврат Значение;
	
КонецФункции

Процедура ЗаполнитьЗначениеВНайденнойСтроке(Таблица, ИмяРеквизита, ИмяКолонки, Значение)
	
	НайденнаяСтрока = Таблица.Найти(ИмяРеквизита, ИмяКолонки);
	Если НайденнаяСтрока <> Неопределено Тогда
		НайденнаяСтрока.Значение = Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьСтрокуРеквизитаДляНесколькихФорматов(
	Реквизиты, ИмяРеквизита1С, ЗначениеТекстовыйФормат = "", ЗначениеФорматЭлектроннойТаблицы = "")
	
	Строка = Реквизиты.Добавить();
	Строка[ИмяРеквизита1С()] = ИмяРеквизита1С;
	Строка[ИмяРеквизитаВФайле() + ИмяФорматаТекстовогоФайла()] = ЗначениеТекстовыйФормат;
	Строка[ИмяРеквизитаВФайле() + ИмяФорматаЭлектронныхТаблиц()] = ЗначениеФорматЭлектроннойТаблицы;
	
КонецПроцедуры

Функция ДатаИзСтроки(ДатаСтрокой)
	
	ЗначениеДаты = ОбщегоНазначенияБПКлиентСервер.ПривестиСтрокуКДате(ДатаСтрокой);
	Если ЗначениеЗаполнено(ЗначениеДаты) Тогда
		Возврат ЗначениеДаты;
	КонецЕсли;
	
	МассивДата = СтрРазделить(ДатаСтрокой, " ");
	// Массив должен содержать три элемента - число, месяц, год
	Если МассивДата.Количество() < 3 Тогда
		Возврат ЗначениеДаты;
	КонецЕсли;
	
	СоответствиеМесяцев = СоответствиеМесяцев();
	НомерМесяца = СоответствиеМесяцев.Получить(МассивДата[1]);
	Если НомерМесяца <> Неопределено Тогда
		МассивДата[1] = НомерМесяца;
		ДатаСтрокой = СтрСоединить(МассивДата, ".");
		
		ЗначениеДаты = ОбщегоНазначенияБПКлиентСервер.ПривестиСтрокуКДате(ДатаСтрокой);
	КонецЕсли;
	
	Возврат ЗначениеДаты;
	
КонецФункции

Функция ОбработатьЧисловойРеквизит(ИсходнаяСтрока)
	
	Если ТипЗнч(ИсходнаяСтрока) = Тип("Число") Тогда
		Возврат ИсходнаяСтрока;
	КонецЕсли;
	ДлинаСтроки = СтрДлина(ИсходнаяСтрока);
	ЧастиСтроки = Новый Массив;
	// Отбираем только цифры, точки и запятые
	Для Сч = 1 По ДлинаСтроки Цикл
		ОчереднойСимвол = Сред(ИсходнаяСтрока, Сч, 1);
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ОчереднойСимвол)
			Или СимволЯвляетсяТочкойИлиЗапятой(ОчереднойСимвол) Тогда
			ЧастиСтроки.Добавить(ОчереднойСимвол);
		ИначеЕсли СимволПробела(ОчереднойСимвол) Тогда
			Продолжить;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ЧислоСтрокой = СтрСоединить(ЧастиСтроки);
	Результат = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЧислоСтрокой);
	Если Результат = Неопределено Тогда
		Возврат 0;
	Иначе
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

Функция СоответствиеМесяцев()

	Соответствие = Новый Соответствие;
	Соответствие.Вставить("января",1);
	Соответствие.Вставить("февраля",2);
	Соответствие.Вставить("марта",3);
	Соответствие.Вставить("апреля",4);
	Соответствие.Вставить("мая",5);
	Соответствие.Вставить("июня",6);
	Соответствие.Вставить("июля",7);
	Соответствие.Вставить("августа",8);
	Соответствие.Вставить("сентября",9);
	Соответствие.Вставить("октября",10);
	Соответствие.Вставить("ноября",11);
	Соответствие.Вставить("декабря",12);
	
	Возврат Соответствие;
	
КонецФункции

Функция СимволЯвляетсяТочкойИлиЗапятой(Символ)
	
	Код = КодСимвола(Символ);
	// 44 и 46 - коды символов точки и запятой
	Возврат Код = 44 Или Код = 46;
	
КонецФункции

Функция КодСимволаДвойнойКавычки()
	
	Возврат 34;
	
КонецФункции

Функция СимволПробела(Символ)
	
	Возврат КодСимвола(Символ) = 32;
	
КонецФункции

#КонецОбласти

#КонецЕсли
