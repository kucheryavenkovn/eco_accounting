
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресКлючейПоказателей = Параметры.АдресКлючейПоказателей;
	
	Если Параметры.Свойство("СтруктураДоходовВычетов") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.СтруктураДоходовВычетов);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СуммаВычета = РассчитатьСуммуВычета(ЭтотОбъект);
	Если СуммаВычета = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Заполните хотя бы одно поле'"));
		Возврат;
	КонецЕсли;
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("Вид", ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.Социальный"));
	СтруктураОтвета.Вставить("Информация",              Строка(СтруктураОтвета.Вид));
	СтруктураОтвета.Вставить("ОбучениеСвое",            ОбучениеСвое);
	СтруктураОтвета.Вставить("ОбучениеДетей",           ОбучениеДетей);
	СтруктураОтвета.Вставить("ДорогостоящееЛечение",    ДорогостоящееЛечение);
	СтруктураОтвета.Вставить("ЛечениеМедикаменты",      ЛечениеМедикаменты);
	СтруктураОтвета.Вставить("Медикаменты",             Медикаменты);
	СтруктураОтвета.Вставить("ОбычноеЛечение",          ОбычноеЛечение);
	СтруктураОтвета.Вставить("МедицинскоеСтрахование",  МедицинскоеСтрахование);
	СтруктураОтвета.Вставить("Пожертвования",           Пожертвования);
	СтруктураОтвета.Вставить("СтраховыеВзносыНаПенсию", СтраховыеВзносыНаПенсию);
	СтруктураОтвета.Вставить("ОценкаКвалификации",      ОценкаКвалификации);
	
	СтруктураОтвета.Вставить("СуммаВычета", СуммаВычета);
	
	Закрыть(СтруктураОтвета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ЛечениеМедикаменты.Видимость =
		ЕстьКлючПоказателя("СоциальныйВычетЛечениеМедикаменты", Форма.АдресКлючейПоказателей);
	
	Элементы.Медикаменты.Видимость =
		ЕстьКлючПоказателя("СоциальныйВычетМедикаменты", Форма.АдресКлючейПоказателей);
	
	Элементы.ОбычноеЛечение.Видимость =
		ЕстьКлючПоказателя("СоциальныйВычетОбычноеЛечение", Форма.АдресКлючейПоказателей);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьСуммуВычета(Форма)
	
	Возврат Форма.ОбучениеСвое + Форма.ОбучениеДетей
		+ Форма.ДорогостоящееЛечение + Форма.ЛечениеМедикаменты + Форма.МедицинскоеСтрахование
		+ Форма.ОбычноеЛечение + Форма.Медикаменты + Форма.Пожертвования
		+ Форма.СтраховыеВзносыНаПенсию + Форма.ОценкаКвалификации;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьКлючПоказателя(Знач ИмяКлюча, Знач АдресКлючейПоказателей)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.ЕстьКлючПоказателя(ИмяКлюча, АдресКлючейПоказателей);
	
КонецФункции

#КонецОбласти
