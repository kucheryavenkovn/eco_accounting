#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонтекстныйВызов = Параметры.КонтекстныйВызов;
	ВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	// Значение по умолчанию всегда Истина, если через контекстный вызов не передано иное значение
	Если Параметры.КонтекстныйВызов Тогда
		ИспользоватьВычет = Параметры.ПрименятьВычет;
	Иначе
		ИспользоватьВычет = Истина;
	КонецЕсли;
	
	// Загрузим список возможных стандартных вычетов на налогоплательщика
	// В форме показываем по возрастанию значений
	РазмерыВычетов = РазмерыВычетов(ВыбраннаяФорма);
	
	Элементы.РазмерВычета.СписокВыбора.Очистить();
	Для Каждого ТекущийВычет Из РазмерыВычетов Цикл
		
		ПредставлениеЭлемента = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';%1 рубль;;%1 рубля;%1 рублей;'"), ТекущийВычет.Значение);
		Элементы.РазмерВычета.СписокВыбора.Добавить(ТекущийВычет.Значение, ПредставлениеЭлемента);
		
	КонецЦикла;
	
	Элементы.РазмерВычета.СписокВыбора.СортироватьПоЗначению();
	
	// Заполнить месяцы в таблице Стандартных вычетов
	НачальноеЗаполнениеСтандартныеВычеты();
	
	Если Параметры.Свойство("СтруктураДоходовВычетов") И Не Параметры.КонтекстныйВызов Тогда
		// Восстановим реквизиты формы.
		ДанныеФормы = Параметры.СтруктураДоходовВычетов.ДанныеФормы;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеФормы);
		// Преобразование размера вычета для предыдущих версий помощника заполнения
		// (ранее РазмерВычета был строковым значением по шаблону "Вычет%1")
		Если ДанныеФормы.Свойство("РазмерВычета") И ТипЗнч(ДанныеФормы.РазмерВычета) = Тип("Строка") Тогда
			РазмерВычета = Число(СтрЗаменить(ДанныеФормы.РазмерВычета, "Вычет", ""));
		КонецЕсли;
		
		// Восстановим суммы вычетов.
		СуммаВычетаПоМесяцам = Неопределено;
		
		// До 2020 г. данные о вычете хранились в виде структуры: наиманование месяца и сумма вычета
		Если ДанныеФормы.Свойство("СуммаВычетаПоМесяцам", СуммаВычетаПоМесяцам)
			И ТипЗнч(СуммаВычетаПоМесяцам) = Тип("Структура") Тогда
			
			Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
				СтрокаВычета.СуммаВычета1 = СуммаВычетаПоМесяцам[СтрокаВычета.Месяц1];
				СтрокаВычета.СуммаВычета2 = СуммаВычетаПоМесяцам[СтрокаВычета.Месяц2];
			КонецЦикла;
			
		ИначеЕсли ЗначениеЗаполнено(СуммаВычетаПоМесяцам) Тогда
			
			Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
				СтрокаВычета.СуммаВычета1 = СуммаВычетаИзСохраненныхДанных(СуммаВычетаПоМесяцам, СтрокаВычета.НомерМесяца1);
				СтрокаВычета.СуммаВычета2 = СуммаВычетаИзСохраненныхДанных(СуммаВычетаПоМесяцам, СтрокаВычета.НомерМесяца2);
			КонецЦикла;
			
		КонецЕсли;
		
		ИспользоватьВычет = СтандартныйВычет.Итог("СуммаВычета1") + СтандартныйВычет.Итог("СуммаВычета2") > 0;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	УправлениеФормой(ЭтотОбъект);
	
	КлючСохраненияПоложенияОкна = СтрШаблон("%1%2", ВычетПрименяется, КонтекстныйВызов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьВычетПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерВычетаПриИзменении(Элемент)
	
	УстановитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетПрименяетсяПриИзменении(Элемент)
	
	Если ВычетПрименяется = "Год" Тогда
		УстановитьЗначенияПоУмолчанию();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураРезультата = Новый Структура;
	// Информация для формы помощника.
	СтруктураРезультата.Вставить("СуммаВычета", 0);
	СтруктураРезультата.Вставить("Вид", 
		ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.СтандартныйНаНалогоплательщика"));
	СтруктураРезультата.Вставить("Информация", НСтр("ru='Стандартный вычет на налогоплательщика'"));
	
	СуммаВычетаПоМесяцам = Новый Массив;
	ВычетыПоМесяцам = Новый Массив;
	
	РазмерыВычетов = РазмерыВычетов(ВыбраннаяФорма);
	
	// Данные для декларации.
	СтруктураДанныхДекларации = Новый Структура;
	Для Каждого ТекущийВычет Из РазмерыВычетов Цикл
		СтруктураДанныхДекларации.Вставить(ИмяПоляСуммаВычета(ТекущийВычет.Значение), 0);
	КонецЦикла;
	
	Если ИспользоватьВычет Тогда
		
		Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
			
			ИмяПоля = ИмяПоляСуммаВычета(СтрокаВычета.СуммаВычета1);
			Если ЗначениеЗаполнено(СтрокаВычета.НомерМесяца1) Тогда
				
				СтруктураЗначения = Новый Структура("НомерМесяца, СуммаВычета, ВидВычета");
				СтруктураЗначения.НомерМесяца = СтрокаВычета.НомерМесяца1;
				СтруктураЗначения.СуммаВычета = СтрокаВычета.СуммаВычета1;
				СтруктураЗначения.ВидВычета   = ИдентификаторВычетаПоЗначению(РазмерыВычетов, СтрокаВычета.СуммаВычета1);
				СуммаВычетаПоМесяцам.Добавить(СтруктураЗначения);
				
				Если ЗначениеЗаполнено(СтрокаВычета.СуммаВычета1) Тогда
					СтруктураДанныхДекларации[ИмяПоля] = СтруктураДанныхДекларации[ИмяПоля] + СтрокаВычета.СуммаВычета1;
					СтруктураРезультата.СуммаВычета = СтруктураРезультата.СуммаВычета + СтрокаВычета.СуммаВычета1;
				КонецЕсли;
				
			КонецЕсли;
			
			ИмяПоля = ИмяПоляСуммаВычета(СтрокаВычета.СуммаВычета2);
			Если ЗначениеЗаполнено(СтрокаВычета.НомерМесяца2) Тогда
				
				СтруктураЗначения = Новый Структура("НомерМесяца, СуммаВычета, ВидВычета");
				СтруктураЗначения.НомерМесяца = СтрокаВычета.НомерМесяца2;
				СтруктураЗначения.СуммаВычета = СтрокаВычета.СуммаВычета2;
				СтруктураЗначения.ВидВычета   = ИдентификаторВычетаПоЗначению(РазмерыВычетов, СтрокаВычета.СуммаВычета2);
				СуммаВычетаПоМесяцам.Добавить(СтруктураЗначения);
				
				Если ЗначениеЗаполнено(СтрокаВычета.СуммаВычета2) Тогда
					СтруктураДанныхДекларации[ИмяПоля] = СтруктураДанныхДекларации[ИмяПоля] + СтрокаВычета.СуммаВычета2;
					СтруктураРезультата.СуммаВычета = СтруктураРезультата.СуммаВычета + СтрокаВычета.СуммаВычета2;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		СтруктураРезультата.Вставить("СуммаВычета", 0);
	КонецЕсли;
	
	СтруктураРезультата.Вставить("ДанныеДекларации", СтруктураДанныхДекларации);
	СтруктураРезультата.Вставить("СведенияОВычетах", ИтоговаяТаблицаВычетов(РазмерыВычетов));
	
	// Данные формы для восстановления.
	СтруктураДанныхФормы = Новый Структура;
	СтруктураДанныхФормы.Вставить("СуммаВычетаПоМесяцам", СуммаВычетаПоМесяцам);
	СтруктураДанныхФормы.Вставить("ВычетПрименяется",     ВычетПрименяется);
	СтруктураДанныхФормы.Вставить("РазмерВычета",         РазмерВычета);
	СтруктураРезультата.Вставить("ДанныеФормы",           СтруктураДанныхФормы);
	
	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДобавитьМесяцы(НомерМесяца1, НомерМесяца2 = Неопределено)
	
	СтрокаВычета = СтандартныйВычет.Добавить();
	СтрокаВычета.Месяц1 = ОбщегоНазначенияБПКлиентСервер.ПредставлениеМесяца(НомерМесяца1);
	СтрокаВычета.НомерМесяца1 = НомерМесяца1;
	
	Если НомерМесяца2 <> Неопределено Тогда
		СтрокаВычета.Месяц2 = ОбщегоНазначенияБПКлиентСервер.ПредставлениеМесяца(НомерМесяца2);
		СтрокаВычета.НомерМесяца2 = НомерМесяца2;
	КонецЕсли;
	
	Возврат СтрокаВычета;
	
КонецФункции

&НаКлиенте
Функция ИмяПоляСуммаВычета(СуммаВычета)
	
	Возврат СтрШаблон("СуммаВычета%1", Формат(СуммаВычета, "ЧГ=0"));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторВычетаПоЗначению(РазмерыВычетов, Значение)
	
	Идентификатор = "";
	Для Каждого ТекущийВычет Из РазмерыВычетов Цикл
		Если Значение = ТекущийВычет.Значение Тогда
			Идентификатор = ТекущийВычет.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Идентификатор;
	
КонецФункции

&НаСервере
Функция ИтоговаяТаблицаВычетов(РазмерыВычетов)
	
	Если Не ИспользоватьВычет Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВычетыПоПорядку = Новый ТаблицаЗначений;
	ВычетыПоПорядку.Колонки.Добавить("НомерМесяца", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	ВычетыПоПорядку.Колонки.Добавить("СуммаВычета", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Для Каждого ТекущаяСтрока Из СтандартныйВычет Цикл
		
		НоваяСтрока = ВычетыПоПорядку.Добавить();
		НоваяСтрока.НомерМесяца = ТекущаяСтрока.НомерМесяца1;
		НоваяСтрока.СуммаВычета = ТекущаяСтрока.СуммаВычета1;
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.НомерМесяца2) Тогда
			НоваяСтрока = ВычетыПоПорядку.Добавить();
			НоваяСтрока.НомерМесяца = ТекущаяСтрока.НомерМесяца2;
			НоваяСтрока.СуммаВычета = ТекущаяСтрока.СуммаВычета2;
		КонецЕсли;
		
	КонецЦикла;
	
	ВычетыПоПорядку.Сортировать("НомерМесяца");
	НомерСтроки = 0;
	ДанныеРезультат = Новый Массив;
	КоличествоСтрок = ВычетыПоПорядку.Количество();
	
	Пока НомерСтроки < КоличествоСтрок Цикл
		
		ТекущаяСтрока = ВычетыПоПорядку[НомерСтроки];
		
		СуммаВычета = 0;
		Для СледующийНомерСтроки = НомерСтроки + 1 По КоличествоСтрок - 1 Цикл
			
			Если ТекущаяСтрока.СуммаВычета <> ВычетыПоПорядку[СледующийНомерСтроки].СуммаВычета Тогда
				Прервать;
			КонецЕсли;
			
			СуммаВычета = СуммаВычета + ВычетыПоПорядку[СледующийНомерСтроки].СуммаВычета;
			
		КонецЦикла;
		
		СтруктураЗначения = Новый Структура("НомерМесяца, СуммаВычета, ВидВычета");
		ЗаполнитьЗначенияСвойств(СтруктураЗначения, ТекущаяСтрока);
		Если Не ИспользоватьВычет Тогда
			СтруктураЗначения.СуммаВычета = 0;
		Иначе
			СтруктураЗначения.ВидВычета = ИдентификаторВычетаПоЗначению(РазмерыВычетов, СтруктураЗначения.СуммаВычета);
		КонецЕсли;
		
		ДанныеРезультат.Добавить(СтруктураЗначения);
		НомерСтроки = СледующийНомерСтроки;
		
	КонецЦикла;
	
	Возврат ДанныеРезультат;
	
КонецФункции

&НаСервере
Процедура НачальноеЗаполнениеСтандартныеВычеты()
	
	КоличествоСтрокВКолонке = 6;
	ДанныеВычета = Неопределено;
	
	ПараметрыВыполнения = УчетНДФЛПредпринимателя.НовыйПараметрыВычета();
	ЗаполнитьЗначенияСвойств(ПараметрыВыполнения, Параметры);
	ПараметрыВыполнения.ВыбраннаяФорма = ВыбраннаяФорма;
	ПараметрыВыполнения.ИмяОбъектаМетаданных = "РегистрСведений.ИПСтандартныеВычетыНаНалогоплательщика";
	
	Если КонтекстныйВызов Тогда
		ПараметрыВыполнения.КоличествоМесяцев = Месяц(Параметры.КонецПериода) - Месяц(Параметры.НачалоПериода) + 1;
	Иначе
		ПараметрыВыполнения.НачалоПериода     = НачалоГода(Параметры.Период);
		ПараметрыВыполнения.КонецПериода      = КонецГода(Параметры.Период);
		ПараметрыВыполнения.КоличествоМесяцев = 12;
	КонецЕсли;
	
	ДанныеВычета = УчетНДФЛПредпринимателя.ДанныеВычетаПоМесяцамЗаПериод(ПараметрыВыполнения);
	
	ЗаполнитьСумму = ЗначениеЗаполнено(ДанныеВычета);
	
	Если ЗаполнитьСумму Тогда
		СуммаВычета = ДанныеВычета[0].СуммаВычета;
		КоличествоМесяцевВычета = ДанныеВычета.Количество();
	Иначе
		
		СуммаВычета = 0;
		Если КонтекстныйВызов Тогда
			КоличествоМесяцевВычета = Месяц(Параметры.КонецПериода) - Месяц(Параметры.НачалоПериода) + 1;
		Иначе
			КоличествоМесяцевВычета = 12;
		КонецЕсли;
		
	КонецЕсли;
	
	Если КонтекстныйВызов Тогда
		ИспользоватьВычет = ЗаполнитьСумму И ДанныеВычета.Итог("СуммаВычета") <> 0;
	Иначе
		ИспользоватьВычет = Истина;
	КонецЕсли;
	
	СуммаВычетаИзменяется = Ложь;
	КоличествоИтераций = Мин(КоличествоСтрокВКолонке, КоличествоМесяцевВычета);
	
	Для НомерМесяца = 1 По КоличествоИтераций Цикл
		
		УчитыватьВторойМесяц = КоличествоМесяцевВычета >= НомерМесяца + КоличествоСтрокВКолонке;
		Если УчитыватьВторойМесяц Тогда
			
			НоваяСтрока = ДобавитьМесяцы(НомерМесяца, НомерМесяца + КоличествоСтрокВКолонке);
			
			Если ЗаполнитьСумму Тогда
				НоваяСтрока.СуммаВычета2 = ДанныеВычета[НомерМесяца + КоличествоСтрокВКолонке - 1].СуммаВычета;
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока = ДобавитьМесяцы(НомерМесяца);
			
		КонецЕсли;
		
		Если ЗаполнитьСумму Тогда
			НоваяСтрока.СуммаВычета1 = ДанныеВычета[НомерМесяца - 1].СуммаВычета;
		КонецЕсли;
		
		Если СуммаВычета <> НоваяСтрока.СуммаВычета1 Тогда
			
			СуммаВычетаИзменяется = Истина;
			
		ИначеЕсли УчитыватьВторойМесяц И СуммаВычета <> НоваяСтрока.СуммаВычета2 Тогда
			
			СуммаВычетаИзменяется = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если СуммаВычетаИзменяется И ЗаполнитьСумму Тогда
		ВычетПрименяется = "НесколькоМесяцевВГоду";
	Иначе
		ВычетПрименяется = "Год";
		ОпределитьСуммуВычетаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьСуммуВычетаПоУмолчанию()
	
	ВычетПрименяется = "Год";
	
	СуммаСредняя = (СтандартныйВычет.Итог("СуммаВычета1") + СтандартныйВычет.Итог("СуммаВычета2")) / 12;
	
	Если Элементы.РазмерВычета.СписокВыбора.НайтиПоЗначению(СуммаСредняя) <> Неопределено Тогда
		РазмерВычета = СуммаСредняя;
	Иначе
		РазмерВычета = Элементы.РазмерВычета.СписокВыбора[0].Значение;
	КонецЕсли;
	
	УстановитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазмерыВычетов(ВыбраннаяФорма)
	
	Возврат Отчеты.РегламентированныйОтчет3НДФЛ.РазмерыВычетов(ВыбраннаяФорма).СтандартныйВычетНаНалогоплательщика;
	
КонецФункции

&НаСервереБезКонтекста
Функция СуммаВычетаИзСохраненныхДанных(МассивСтруктур, НомерМесяца)
	
	СуммаВычета = 0;
	
	Для Каждого ТекущиеДанные Из МассивСтруктур Цикл
		
		Если ТекущиеДанные.НомерМесяца = НомерМесяца Тогда
			СуммаВычета = ТекущиеДанные.СуммаВычета;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаВычета;
	
КонецФункции

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	ВычетПрименяется = "Год";
	ОпределитьСуммуВычетаПоУмолчанию();
	УстановитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСуммуВычета(Форма)
	
	Сумма = Форма.РазмерВычета;
	
	Для Каждого СтрокаВычета Из Форма.СтандартныйВычет Цикл
		СтрокаВычета.СуммаВычета1 = Сумма;
		СтрокаВычета.СуммаВычета2 = Сумма;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ИспользоватьВычет.Видимость = Форма.КонтекстныйВызов;
	
	Если Форма.КонтекстныйВызов Тогда
		Элементы.ВычетПрименяется.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Иначе
		Элементы.ВычетПрименяется.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЕсли;
	
	Если Форма.ВычетПрименяется = "Год" Тогда
		Элементы.СтраницыПрименениеВычета.ТекущаяСтраница = Элементы.СтраницаВесьГод;
	Иначе
		Элементы.СтраницыПрименениеВычета.ТекущаяСтраница = Элементы.СтраницаНесколькоМесяцевВГоду;
	КонецЕсли;
	
	Элементы.ВычетПрименяется.Доступность = Форма.ИспользоватьВычет;
	Элементы.СтраницыПрименениеВычета.Доступность = Форма.ИспользоватьВычет;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтандартныйВычетСуммаВычета2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СтандартныйВычет.Месяц2", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
КонецПроцедуры

#КонецОбласти