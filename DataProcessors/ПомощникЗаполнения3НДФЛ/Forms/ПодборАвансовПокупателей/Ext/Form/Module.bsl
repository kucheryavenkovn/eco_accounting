
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьАвансыПокупателей(Параметры.Организация, Параметры.Период);
	УстановитьУсловноеОформление(Параметры.Документы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАвансыПокупателей

&НаКлиенте
Процедура АвансыПокупателейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура АвансыПокупателейВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	ТекущиеДанные = Элементы.АвансыПокупателей.ТекущиеДанные;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ДокументОплаты",    ТекущиеДанные.ДокументОплаты);
	РезультатВыбора.Вставить("Контрагент",        ТекущиеДанные.Контрагент);
	РезультатВыбора.Вставить("НазначениеПлатежа", ТекущиеДанные.НазначениеПлатежа);
	РезультатВыбора.Вставить("ДатаДокумента",     ТекущиеДанные.ДатаДокумента);
	РезультатВыбора.Вставить("Сумма",             ТекущиеДанные.Сумма);
	
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьАвансыПокупателей(Организация, Период)
	
	ПараметрыРасчетаАвансов = УчетДоходовИРасходовПредпринимателя.НовыйПараметрыРасчетаАвансовПокупателей();
	ПараметрыРасчетаАвансов.Организация = Организация;
	ПараметрыРасчетаАвансов.Период = Период;
	ПараметрыРасчетаАвансов.ПоказыватьУдаленныеСтроки = Ложь;
	ПараметрыРасчетаАвансов.ЗаполнитьПоДаннымУчета = Истина;
	
	ТаблицаАвансов = УчетДоходовИРасходовПредпринимателя.АвансыПокупателей(ПараметрыРасчетаАвансов);
	
	Если ТаблицаАвансов <> Неопределено Тогда
		АвансыПокупателей.Загрузить(ТаблицаАвансов);
	КонецЕсли;

	СуммаВсего = АвансыПокупателей.Итог("Сумма");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление(ВыбранныеДокументы)

	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыПокупателейКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыПокупателейДокументОплаты");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыПокупателейСумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыПокупателейНазначениеПлатежа");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АвансыПокупателейДатаДокумента");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"АвансыПокупателей.ДокументОплаты", ВидСравненияКомпоновкиДанных.ВСписке, ВыбранныеДокументы);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);

КонецПроцедуры

#КонецОбласти
