
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Организация", ОтборОрганизация, ЗначениеЗаполнено(ОтборОрганизация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Контрагент", ОтборКонтрагент, ЗначениеЗаполнено(ОтборКонтрагент));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяОрганизация           = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОтборОрганизация              = ОсновнаяОрганизация;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОтборОрганизация = ОбщегоНазначенияБПКлиент.ИзменитьОтборПоОсновнойОрганизации(Список, ,Параметр);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПровестиДокумент(ПараметрКоманды)
	
	ДокументОбъект = ПараметрКоманды.ПолучитьОбъект();
	Попытка
		Если НЕ ДокументОбъект.ПроверитьЗаполнение() Тогда 
			Возврат Ложь;
		КонецЕсли;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;

КонецФункции

&НаСервере
Функция ПолучитьПараметрыДокумента(Ссылка)
	
	МетаданныеДокумента	= Ссылка.Метаданные();
	
	ЕстьРучнаяКорректировка	= ОбщегоНазначения.ЕстьРеквизитОбъекта("РучнаяКорректировка", МетаданныеДокумента);
	
	ИменаРеквизитов	= "Проведен, ПометкаУдаления";
	Если ЕстьРучнаяКорректировка Тогда
		ИменаРеквизитов	= ИменаРеквизитов + ", РучнаяКорректировка";
	КонецЕсли;
	
	Результат	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	Если НЕ ЕстьРучнаяКорректировка Тогда
		Результат.Вставить("РучнаяКорректировка", Ложь);
	КонецЕсли;
	
	Результат.Вставить("РегОперация",	БухгалтерскийУчетКлиентСерверПереопределяемый.ЭтоРегламентнаяОперация(Ссылка));
	Результат.Вставить("ОперацияБух",	ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОперацияБух"));
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьДвиженияДокумента(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если НЕ ТекДанные = Неопределено Тогда
		ПараметрыДокумента = ПолучитьПараметрыДокумента(ТекДанные.Ссылка);
		
		Если НЕ ПараметрыДокумента.ПометкаУдаления И НЕ ПараметрыДокумента.ОперацияБух И
			НЕ ПараметрыДокумента.РегОперация И НЕ ПараметрыДокумента.РучнаяКорректировка И
			НЕ ПараметрыДокумента.Проведен Тогда
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Вставить(0, КодВозвратаДиалога.Да, "Провести");
			Кнопки.Вставить(1, КодВозвратаДиалога.Нет, "Отмена");
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ПараметрыВыполненияКоманды", Новый Структура("Источник", ТекДанные.Ссылка));
			ДополнительныеПараметры.Вставить("ПараметрКоманды", ТекДанные.Ссылка);
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередПросмотромСледуетПровестиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед просмотром проводок документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
		Иначе
			ПараметрыФормы = Новый Структура("ДокументДвижений", ТекДанные.Ссылка);
			ОткрытьФорму("Обработка.КорректировкаДвижений.Форма", 
				ПараметрыФормы, 
				ЭтаФорма, 
				ТекДанные.Ссылка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередПросмотромСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = ДополнительныеПараметры.ПараметрКоманды;
	ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
	
	ДокументПроведен = ПровестиДокумент(ПараметрКоманды);
	Если ДокументПроведен Тогда
		ОповеститьОбИзменении(ПараметрКоманды);
		Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", ПараметрКоманды));
	Иначе
		Сообщить(НСтр("ru = 'Не удалось провести документ'"), СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДокументДвижений", ПараметрКоманды);
	ОткрытьФорму("Обработка.КорректировкаДвижений.Форма", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрКоманды);

КонецПроцедуры	
