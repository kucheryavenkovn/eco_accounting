
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Обработчик обновления 3.0.78
// Функция для обработчика обновления.
// Обрабатывает остатки по регистру НДС предъявленный реализация 0, в которых не заполнено измерение Поставщик
// и заполняет значением из регистра НДС предъявленный 
Процедура ЗаполнитьПоставщикаПриОбновлении() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Остатки.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Остатки.СчетУчетаНДС КАК СчетУчетаНДС,
	|	НДСПредъявленныйРеализация0Остатки.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ОстаткиПоСФ
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			,
	|			Организация В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						НастройкиУчетаНДС.Организация КАК Организация
	|					ИЗ
	|						РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|					ГДЕ
	|						НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19 = ИСТИНА)
	|				И (Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|					ИЛИ Поставщик = НЕОПРЕДЕЛЕНО)) КАК НДСПредъявленныйРеализация0Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоСФ.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(НДСПредъявленный.Поставщик) КАК Поставщик
	|ИЗ
	|	ОстаткиПоСФ КАК ОстаткиПоСФ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|		ПО (НДСПредъявленный.СчетФактура = ОстаткиПоСФ.СчетФактура)
	|ГДЕ
	|	НДСПредъявленный.Активность = ИСТИНА
	|	И НЕ(НДСПредъявленный.Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ИЛИ НДСПредъявленный.Поставщик = НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоСФ.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСПредъявленныйРеализация0Обороты.Регистратор КАК Регистратор,
	|	НДСПредъявленныйРеализация0Обороты.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Обороты.Состояние КАК Состояние,
	|	НДСПредъявленныйРеализация0Обороты.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДСПредъявленныйРеализация0Обороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленныйРеализация0Обороты.СчетУчетаНДС КАК СчетУчетаНДС
	|ПОМЕСТИТЬ ОстаткиПоРегистраторам
	|ИЗ
	|	ОстаткиПоСФ КАК ОстаткиПоСФ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленныйРеализация0.Обороты(, , Регистратор, ) КАК НДСПредъявленныйРеализация0Обороты
	|		ПО ОстаткиПоСФ.СчетФактура = НДСПредъявленныйРеализация0Обороты.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОстаткиПоРегистраторам.Регистратор КАК Регистратор
	|ИЗ
	|	ОстаткиПоРегистраторам КАК ОстаткиПоРегистраторам";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ДанныеПоСФ = РезультатыЗапроса[1].Выгрузить();
	ДанныеПоРегистраторам = РезультатыЗапроса[3].Выгрузить();
	
	Для каждого Строка Из ДанныеПоРегистраторам Цикл
		НаборЗаписейНДСПредъявленныйРеализация0 = РегистрыНакопления.НДСПредъявленныйРеализация0.СоздатьНаборЗаписей();
		НаборЗаписейНДСПредъявленныйРеализация0.Отбор.Регистратор.Установить(Строка.Регистратор);
		НаборЗаписейНДСПредъявленныйРеализация0.Прочитать();
		
		Для каждого СтрокаЗаписей Из НаборЗаписейНДСПредъявленныйРеализация0 Цикл
			СтруктураОтбора = Новый Структура();
			СтруктураОтбора.Вставить("СчетФактура", СтрокаЗаписей.СчетФактура);
			ДанныеПоПоставщику = ДанныеПоСФ.НайтиСтроки(СтруктураОтбора);
			Если ДанныеПоПоставщику.Количество() > 0 Тогда
				СтрокаПоставщик = ДанныеПоПоставщику[0];
				СтрокаЗаписей.Поставщик = СтрокаПоставщик.Поставщик;
			КонецЕсли;
		КонецЦикла;
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНДСПредъявленныйРеализация0, Истина);
		Исключение
			
			ШаблонСообщения = НСтр("ru = 'Не выполнено обновление записей регистра накопления ""НДС предъявленый реализация 0""
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыНакопления.НДСПредъявленныйРеализация0,,
			ТекстСообщения);
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления 3.0.78
// Функция для обработчика обновления.
// Обрабатывает движения по регистру НДС предъявленный реализация 0, в которых не заполнено измерение Поставщик
// и заполняет значением из регистра НДС предъявленный 
Процедура ЗаполнитьПоставщикаПриОбновленииОтложенно(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Ключ = "НеПрименяетсяПоставщикДляРегистраНДСПредъявленныйРеализация0";
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХранилищеДанных.Данные КАК Данные,
	|	ХранилищеДанных.Владелец КАК Владелец
	|ИЗ
	|	РегистрСведений.ХранилищеДанных КАК ХранилищеДанных
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ХранилищеДанных.Владелец) = ТИП(Справочник.Организации)";
	

	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	МассивОрганизацийДляИсключения = Новый Массив();
	Пока РезультатЗапроса.Следующий() Цикл
		Если ЗначениеЗаполнено(РезультатЗапроса.Данные) Тогда
			СохраненныеДанные = РезультатЗапроса.Данные.Получить();
			Если ЗначениеЗаполнено(СохраненныеДанные) И СохраненныеДанные.Свойство(Ключ) Тогда
				МассивОрганизацийДляИсключения.Добавить(РезультатЗапроса.Владелец);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	НДСПредъявленныйРеализация0.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ОстаткиПоСФ
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0 КАК НДСПредъявленныйРеализация0
	|ГДЕ
	|	НДСПредъявленныйРеализация0.Организация В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				НастройкиУчетаНДС.Организация КАК Организация
	|			ИЗ
	|				РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
	|			ГДЕ
	|				НастройкиУчетаНДС.РаздельныйУчетНДСНаСчете19 = ИСТИНА)
	|	И НЕ НДСПредъявленныйРеализация0.Организация В (&ОрганизацииИсключения)
	|	И (НДСПредъявленныйРеализация0.Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ НДСПредъявленныйРеализация0.Поставщик = НЕОПРЕДЕЛЕНО)
	|	И НДСПредъявленныйРеализация0.Активность = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоСФ.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(НДСПредъявленный.Поставщик) КАК Поставщик,
	|	ОстаткиПоСФ.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ОстаткиПоСФСПоставщиком
	|ИЗ
	|	ОстаткиПоСФ КАК ОстаткиПоСФ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|		ПО (НДСПредъявленный.СчетФактура = ОстаткиПоСФ.СчетФактура)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоСФ.СчетФактура,
	|	ОстаткиПоСФ.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоСФСПоставщиком.СчетФактура КАК СчетФактура,
	|	ОстаткиПоСФСПоставщиком.Поставщик КАК Поставщик,
	|	ОстаткиПоСФСПоставщиком.Регистратор КАК Регистратор
	|ИЗ
	|	ОстаткиПоСФСПоставщиком КАК ОстаткиПоСФСПоставщиком
	|ГДЕ
	|	НЕ(ОстаткиПоСФСПоставщиком.Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ИЛИ ОстаткиПоСФСПоставщиком.Поставщик = НЕОПРЕДЕЛЕНО)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоСФСПоставщиком.Регистратор КАК Регистратор
	|ИЗ
	|	ОстаткиПоСФСПоставщиком КАК ОстаткиПоСФСПоставщиком
	|ГДЕ
	|	НЕ(ОстаткиПоСФСПоставщиком.Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ИЛИ ОстаткиПоСФСПоставщиком.Поставщик = НЕОПРЕДЕЛЕНО)";
	
	Запрос.УстановитьПараметр("ОрганизацииИсключения", МассивОрганизацийДляИсключения);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ДанныеПоСФ = РезультатыЗапроса[2].Выгрузить();
	ДанныеПоРегистраторам = РезультатыЗапроса[3].Выгрузить();
	
	Если ДанныеПоСФ.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из ДанныеПоРегистраторам Цикл
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НДСПредъявленныйРеализация0.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Строка.Регистратор);
			Блокировка.Заблокировать();
			
			НаборЗаписейНДСПредъявленныйРеализация0 = РегистрыНакопления.НДСПредъявленныйРеализация0.СоздатьНаборЗаписей();
			НаборЗаписейНДСПредъявленныйРеализация0.Отбор.Регистратор.Установить(Строка.Регистратор);
			НаборЗаписейНДСПредъявленныйРеализация0.Прочитать();
			Для каждого СтрокаЗаписей Из НаборЗаписейНДСПредъявленныйРеализация0 Цикл
				
				СтруктураОтбора = Новый Структура();
				СтруктураОтбора.Вставить("СчетФактура", СтрокаЗаписей.СчетФактура);
				СтруктураОтбора.Вставить("Регистратор", СтрокаЗаписей.Регистратор);
				ДанныеПоПоставщику = ДанныеПоСФ.НайтиСтроки(СтруктураОтбора);
				Если ДанныеПоПоставщику.Количество() > 0 Тогда
					СтрокаПоставщик = ДанныеПоПоставщику[0];
					СтрокаЗаписей.Поставщик = СтрокаПоставщик.Поставщик;
				КонецЕсли;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНДСПредъявленныйРеализация0, Истина);
			
			ЗафиксироватьТранзакцию()
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не выполнено обновление записей регистра накопления ""НДС предъявленый реализация 0""
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыНакопления.НДСПредъявленныйРеализация0,,
			ТекстСообщения);
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

// Обработчик обновления 3.0.83
// Процедура для обработчика обновления.
// В процессе выполнения процедуры определяется для каких организация необходимо
// использовать измерение "Поставщик" в регистре "НДС предъявленный, реализация 0"
// Если по организации нет остатков по регистру "НДС Предъявленный, реализация 0" с незаполненным Поставщиком
// значит предыдущее обновление прошло успешно
// В ином случае, в регистре сведений Хранилище данных будет сохранен
// признак НеПрименяетсяПоставщикДляРегистраНДСПредъявленныйРеализация0
//
Процедура ЗаполнитьПризнакНеПрименяетсяПоставщикДляРегистраНДСПредъявленныйРеализация0() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСПредъявленныйРеализация0Остатки.СчетФактура КАК СчетФактура,
	|	НДСПредъявленныйРеализация0Остатки.Организация КАК Организация
	|ПОМЕСТИТЬ ОстаткиПоСФ
	|ИЗ
	|	РегистрНакопления.НДСПредъявленныйРеализация0.Остатки(
	|			,
	|			Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ИЛИ Поставщик = НЕОПРЕДЕЛЕНО) КАК НДСПредъявленныйРеализация0Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоСФ.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(НДСПредъявленный.Поставщик) КАК Поставщик,
	|	ОстаткиПоСФ.Организация КАК Организация
	|ПОМЕСТИТЬ ОборотыСПоставщиком
	|ИЗ
	|	ОстаткиПоСФ КАК ОстаткиПоСФ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|		ПО (НДСПредъявленный.СчетФактура = ОстаткиПоСФ.СчетФактура)
	|ГДЕ
	|	НДСПредъявленный.Активность = ИСТИНА
	|	И НЕ(НДСПредъявленный.Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ИЛИ НДСПредъявленный.Поставщик = НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоСФ.СчетФактура,
	|	ОстаткиПоСФ.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыСПоставщиком.Организация КАК Организация
	|ИЗ
	|	ОборотыСПоставщиком КАК ОборотыСПоставщиком";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого Строка Из РезультатЗапроса Цикл
		ОбщегоНазначенияБП.ЗаписатьДанныеВХранилище(Строка.Организация, Истина,
			"НеПрименяетсяПоставщикДляРегистраНДСПредъявленныйРеализация0");
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
