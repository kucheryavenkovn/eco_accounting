#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	Регистратор = Отбор.Регистратор.Значение;
	Организация = ЭтотОбъект[0].ГоловнаяОрганизация;
	
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		Если СтрокаНабора.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			Если Не ЗначениеЗаполнено(СтрокаНабора.ДокументОснование) Тогда
				СтрокаНабора.ДокументОснование = Регистратор;
			КонецЕсли;
		ИначеЕсли СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.ЗачетАвансовыхПлатежей Тогда	
			Если Не ЗначениеЗаполнено(СтрокаНабора.ДокументОснование) Тогда
				СтрокаНабора.ДокументОснование = Регистратор;
			КонецЕсли;
		ИначеЕсли СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.ВозвращеноНалоговымАгентом Тогда	
			СтрокаНабора.КрайнийСрокУплаты = '00010101'
		КонецЕсли;
	КонецЦикла;
	
	УчетНДФЛ.ПроставитьКрайнийСрокУплаты(ЭтотОбъект, Организация);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли