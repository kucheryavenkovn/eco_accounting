
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ШаблонЗаголовка = НСтр("ru='Присоединенные файлы: %1'");
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла",  ПараметрКоманды);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ПараметрыВыполненияКоманды.Источник.ТолькоПросмотр);
	ПараметрыФормы.Вставить("ПростаяФорма", Истина);
	ПараметрыФормы.Вставить("ЗаголовокФормы", ТекстЗаголовка);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотКлиентПовтИсп =
			ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияС1СДокументооборотКлиентПовтИсп");
		Если МодульИнтеграцияС1СДокументооборотКлиентПовтИсп.ИспользоватьПрисоединенныеФайлы1СДокументооборота() Тогда
			МодульИнтеграцияС1СДокументооборотКлиент =
				ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияС1СДокументооборотКлиент");
			ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение",
				ЭтотОбъект,
				Новый Структура("ПараметрыФормы, ПараметрыВыполненияКоманды", ПараметрыФормы, ПараметрыВыполненияКоманды));
			МодульИнтеграцияС1СДокументооборотКлиент.ПроверитьПодключение(ОписаниеОповещения,,,Истина);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	МодульИнтеграцияС1СДокументооборотВызовСервера =
		ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияС1СДокументооборотВызовСервера");
	Если Результат И МодульИнтеграцияС1СДокументооборотВызовСервера.
			ИспользоватьПрисоединенныеФайлы1СДокументооборотаДляОбъекта(Параметры.ПараметрыФормы.ВладелецФайла) Тогда
		ИмяФормы = "Обработка.ИнтеграцияС1СДокументооборот.Форма.ПрисоединенныеФайлы";
	Иначе
		ИмяФормы = "Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормы,
		Параметры.ПараметрыФормы,
		Параметры.ПараметрыВыполненияКоманды.Источник,
		Параметры.ПараметрыВыполненияКоманды.Уникальность,
		Параметры.ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

