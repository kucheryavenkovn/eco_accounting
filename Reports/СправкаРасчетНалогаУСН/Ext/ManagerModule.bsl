#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийИНалоговыйУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты, "");
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление","РасчетНалогаУСН", "Расчет налога УСН"));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Ложь);
	Результат.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("БУ");
	НаборПоказателей.Добавить("НУ");
	НаборПоказателей.Добавить("ПР");
	НаборПоказателей.Добавить("ВР");
	
	Возврат НаборПоказателей;
	
КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	НачалоПериода = НачалоГода(ПараметрыОтчета.НачалоПериода);
	КонецПериода  = КонецКвартала(ПараметрыОтчета.КонецПериода);
	
	ЧастиЗаголовка = Новый Массив;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		ЧастиЗаголовка.Добавить(
			НСтр("ru = 'Расчет торгового сбора, уменьшающего налог УСН за'"));
	Иначе
		ЭтоКонецГода = КонецПериода = КонецГода(КонецПериода);
		
		ЧастиЗаголовка.Добавить(НСтр("ru = 'Справка-расчет'"));
		ЧастиЗаголовка.Добавить(?(ЭтоКонецГода, НСтр("ru = 'налога УСН'"), НСтр("ru = 'авансового платежа по налогу УСН'")));
		ЧастиЗаголовка.Добавить(НСтр("ru = 'за'"));
	КонецЕсли;
	
	ЧастиЗаголовка.Добавить(
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина));
	
	ТекстЗаголовка = СтрСоединить(ЧастиЗаголовка, " ");
	
	Если ПараметрыОтчета.НалоговыйПериодРасширен Тогда
		СправкиРасчеты.ДополнитьСловоСсылкойНаПримечание(ТекстЗаголовка);
	КонецЕсли;
	
	Возврат ТекстЗаголовка;
	
КонецФункции

Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьЗаголовок        = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьОписаниеНастроек = Макет.ПолучитьОбласть("ОписаниеНастроек");
	ОбластьОрганизация      = Макет.ПолучитьОбласть("Организация");
	
	//Организация
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(ПараметрыОтчета.Организация);
	ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекстОрганизация;
	Результат.Вывести(ОбластьОрганизация);
	
	//Заголовок
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = "" + ПолучитьТекстЗаголовка(ПараметрыОтчета);
	Результат.Вывести(ОбластьЗаголовок);
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "Заголовок";
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		
		// Раздвинем заголовок
		Результат.Область("R1:R" + Результат.ВысотаТаблицы).ШиринаКолонки = 140;
		
		// Добавим отступ таблицы
		ОбластьЗаголовок.Параметры.ЗаголовокОтчета = "";
		Результат.Вывести(ОбластьЗаголовок);
		
	КонецЕсли;
	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоКвартала(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецКвартала(ПараметрыОтчета.КонецПериода));
		
		ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоГода(ПараметрыОтчета.КонецПериода), ПараметрыОтчета.КонецПериода);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПредставлениеПериода", ПредставлениеПериода);
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
	ПараметрыОтчета.ПоказательНУ = Истина;
	
	ОтображатьТорговыйСбор = ПараметрыОтчета.УменьшатьНалогНаТорговыйСбор;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетНалогаУСН" Тогда
	
		ИмяГруппировки = ?(ПараметрыОтчета.ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы,
			"Доходы", "ДоходыМинусРасходы");
		Если ОтображатьТорговыйСбор Тогда
			ИмяГруппировки = ИмяГруппировки + "ПлательщикТорговогоСбора";
		КонецЕсли;
		
		МассивКолонок = ИменаКолонокСправкиРасчета(ПараметрыОтчета.ОбъектНалогообложения, ОтображатьТорговыйСбор);
		
		Таблица = БухгалтерскиеОтчеты.НайтиПоИмени(КомпоновщикНастроек.Настройки.Структура,"ТаблицаОбъектНалогообложения");
		ГруппировкаОбъектНалогообложения   = БухгалтерскиеОтчеты.НайтиПоИмени(Таблица.Строки,"Объект"+ИмяГруппировки);
		
		Группа = ГруппировкаОбъектНалогообложения.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
		Группа.Расположение = РасположениеПоляКомпоновкиДанных.ОтдельнаяКолонка;
		
		Для Каждого ИмяКолонки Из МассивКолонок Цикл
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(Группа, "" + ИмяКолонки);
		КонецЦикла;
		
		ГруппировкаОбъектНалогообложения.Использование = Истина;
	
	КонецЕсли;
	
	// Для плательщика торгового сбора на УСН-доходы выведем расчет суммы вычитаемого из налога сбора
	Если ОтображатьТорговыйСбор Или ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		
		// Заголовок таблицы
		ГруппировкаВычитаемыйСборЗаголовок = БухгалтерскиеОтчеты.НайтиПоИмени(КомпоновщикНастроек.Настройки.Структура, "ВычитаемыйТорговыйСборЗаголовок");
		ГруппировкаВычитаемыйСборЗаголовок.Использование = (ПараметрыОтчета.КлючТекущегоВарианта = "РасчетНалогаУСН");
		
		// Таблица
		Таблица = БухгалтерскиеОтчеты.НайтиПоИмени(КомпоновщикНастроек.Настройки.Структура,"ТаблицаРасчетВычитаемогоТорговогоСбора");
		Таблица.Использование = Истина;
		
		ГруппировкаВычитаемыйСбор = БухгалтерскиеОтчеты.НайтиПоИмени(Таблица.Строки, "ВычитаемыйТорговыйСбор");
		
		Группа = ГруппировкаВычитаемыйСбор.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
		Группа.Расположение = РасположениеПоляКомпоновкиДанных.ОтдельнаяКолонка;
		
		МассивКолонок = ИменаКолонокВычетаТорговогоСбора();
		
		Для Каждого ИмяКолонки Из МассивКолонок Цикл
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(Группа, "" + ИмяКолонки);
		КонецЦикла;
		
		ГруппировкаВычитаемыйСбор.Использование = Истина;
		
		// Сноска в заголовке таблицы
		СноскаВЗаголовкеТаблицы = "";
		
		Если ПараметрыОтчета.НалоговыйПериодРасширен Тогда
			НомерПримечания = 2; // Первое примечание по расширенному налоговому периоду уже отображено в заголовке отчета.
		Иначе
			НомерПримечания = 1;
		КонецЕсли;
		
		СправкиРасчеты.ДополнитьСловоСсылкойНаПримечание(СноскаВЗаголовкеТаблицы, НомерПримечания);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "СноскаУменьшениеНалогаНаТорговыйСбор", СноскаВЗаголовкеТаблицы);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	// Добавим примечания, если требуются.
	СчетчикПримечаний = 0;
	
	Если ПараметрыОтчета.НалоговыйПериодРасширен Тогда
		
		СправкиРасчеты.ДобавитьПримечание(
			Результат,
			ПояснениеРасширенныйНалоговыйПериод(ПараметрыОтчета.ДатаНачалаДеятельности, ПараметрыОтчета.КонецПериода),
			СчетчикПримечаний);
		
	КонецЕсли;
	
	Если ПараметрыОтчета.УменьшатьНалогНаТорговыйСбор Тогда
		
		СправкиРасчеты.ДобавитьПримечание(Результат, ПояснениеУменьшениеНалогаНаТорговыйСбор(), СчетчикПримечаний);
		
	КонецЕсли;
	
	// Установим параметры таблицы.
	Результат.ФиксацияСверху = 0;
	Результат.ФиксацияСлева  = 0;
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИменаКолонокСправкиРасчета(ОбъектНалогообложения, ОтображатьТорговыйСбор = Ложь)
	
	МассивИмен = Новый Массив;
	
	Если ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда
		
		МассивИмен.Добавить("Доходы");
		МассивИмен.Добавить("СтавкаНалогаУСН");
		МассивИмен.Добавить("НалогВсего");
		МассивИмен.Добавить("ПроцентУменьшенияНалога");
		МассивИмен.Добавить("РасходыУменьшающиеНалог");
		МассивИмен.Добавить("УменьшениеНалогаФакт");
		Если ОтображатьТорговыйСбор Тогда
			МассивИмен.Добавить("ТорговыйСборУменьшающийНалог");
		КонецЕсли;
		МассивИмен.Добавить("НалогИсчисленныйВсего");
		МассивИмен.Добавить("АвансовыеПлатежи");
		МассивИмен.Добавить("НалогКУплате");
		МассивИмен.Добавить("НалогКУменьшению");
		
	ИначеЕсли ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы Тогда
		
		МассивИмен.Добавить("Доходы");
		МассивИмен.Добавить("Расходы");
		МассивИмен.Добавить("ПеренесенныеУбытки");
		МассивИмен.Добавить("НалоговаяБаза");
		МассивИмен.Добавить("СтавкаНалогаУСН");
		МассивИмен.Добавить("НалогИсчисленныйВсего");
		МассивИмен.Добавить("МинимальныйНалог");
		МассивИмен.Добавить("АвансовыеПлатежи");
		МассивИмен.Добавить("НалогКУплате");
		МассивИмен.Добавить("НалогКУменьшению");
		
	КонецЕсли;
	
	Возврат МассивИмен;
	
КонецФункции

Функция ИменаКолонокВычетаТорговогоСбора()
	
	МассивИмен = Новый Массив;
	
	МассивИмен.Добавить("Доходы");
	МассивИмен.Добавить("СтавкаНалогаУСН");
	МассивИмен.Добавить("НалогВсего");
	МассивИмен.Добавить("ПроцентУменьшенияНалога");
	МассивИмен.Добавить("РасходыУменьшающиеНалог");
	МассивИмен.Добавить("УменьшениеНалогаФакт");
	МассивИмен.Добавить("НалогИсчисленныйВсего");
	МассивИмен.Добавить("УплаченныйТорговыйСбор");
	МассивИмен.Добавить("ТорговыйСборУменьшающийНалог");
	
	Возврат МассивИмен;
	
КонецФункции

Функция ПояснениеРасширенныйНалоговыйПериод(ДатаРегистрации, КонецПериодаОтчета)
	
	ШаблонПояснения = НСтр("ru = '%1 рассчитывается за %2 период с даты регистрации %3 по %4 (п.2 ст. 55 НК РФ)'");
	
	ЭтоНалоговыйПериод = КонецКвартала(КонецПериодаОтчета) = КонецГода(КонецПериодаОтчета);
	
	ФорматнаяСтрокаПериода = "ДФ=dd.MM.yyyy";
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПояснения,
		?(ЭтоНалоговыйПериод, НСтр("ru = 'Налог'"), НСтр("ru = 'Авансовый платеж'")),
		?(ЭтоНалоговыйПериод, НСтр("ru = 'налоговый'"), НСтр("ru = 'отчетный'")),
		Формат(ДатаРегистрации, ФорматнаяСтрокаПериода),
		Формат(КонецКвартала(КонецПериодаОтчета), ФорматнаяСтрокаПериода));
	
КонецФункции

Функция ПояснениеУменьшениеНалогаНаТорговыйСбор()
	
	Возврат НСтр("ru = 'Торговый сбор уменьшает только налог, рассчитанный по облагаемой торговым сбором деятельности (п. 8 ст. 346.21 НК РФ)'");
	
КонецФункции

#КонецОбласти

#КонецЕсли