
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЦвет(знач ЦветСтрока)
	
	Если Не ЗначениеЗаполнено(ЦветСтрока) Тогда
		Возврат Новый Цвет(255, 255, 255);
	Иначе
		Ном = Найти(ЦветСтрока, ",");
		КрасныйСтрока = Лев(ЦветСтрока, Ном - 1);
		Строка = Сред(ЦветСтрока, Ном + 1);
		Красный = Число(КрасныйСтрока);
		Ном = Найти(Строка, ",");
		ЗеленыйСтрока = Лев(Строка, Ном - 1);
		Строка = Сред(Строка, Ном + 1);
		Зеленый = Число(ЗеленыйСтрока);
		Синий = Число(Строка);
		Возврат Новый Цвет(Красный, Зеленый, Синий);
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Запись   = Форма.Запись;
	
	Элементы.Цвет.ЦветФона = ПолучитьЦвет(Запись.Цвет);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписанНовыйЦвет");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

&НаКлиенте
Процедура ЦветНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураЦвет = Новый Структура("Красный, Зеленый, Синий", 0, 0, 0);
	Если ЗначениеЗаполнено(Запись.Цвет) Тогда
		НачальныйЦвет = ПолучитьЦвет(Запись.Цвет); 
		СтруктураЦвет.Красный = НачальныйЦвет.Красный;
		СтруктураЦвет.Зеленый = НачальныйЦвет.Зеленый;
		СтруктураЦвет.Синий = НачальныйЦвет.Синий;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ЦветОкончаниеВыбора", ЭтаФорма);
	ОткрытьФорму("РегистрСведений.АР_ЦветаРаскраскиПомещений.Форма.ФормаВыбораЦвета", СтруктураЦвет, ЭтаФорма,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветОкончаниеВыбора(ВыбранныйЦвет, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЦвет <> Неопределено Тогда
		Запись.Цвет = Строка(ВыбранныйЦвет.Красный) + "," + Строка(ВыбранныйЦвет.Зеленый) + "," + Строка(ВыбранныйЦвет.Синий);
		Модифицированность = Истина;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры
