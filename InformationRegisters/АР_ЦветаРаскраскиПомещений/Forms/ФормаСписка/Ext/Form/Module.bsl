
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЦвет(знач ЦветСтрока)
	
	Если Не ЗначениеЗаполнено(ЦветСтрока) Тогда
		Возврат Новый Цвет(255, 255, 255);
	Иначе
		Ном = Найти(ЦветСтрока, ",");
		КрасныйСтрока = Лев(ЦветСтрока, Ном - 1);
		Строка = Сред(ЦветСтрока, Ном + 1);
		Красный = Число(КрасныйСтрока);
		Ном = Найти(Строка, ",");
		ЗеленыйСтрока = Лев(Строка, Ном - 1);
		Строка = Сред(Строка, Ном + 1);
		Зеленый = Число(ЗеленыйСтрока);
		Синий = Число(Строка);
		Возврат Новый Цвет(Красный, Зеленый, Синий);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура НастроитьУсловноеОформление()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЦветаРаскраскиПомещений.Цвет
	|ИЗ
	|	РегистрСведений.АР_ЦветаРаскраскиПомещений КАК ЦветаРаскраскиПомещений";
	Выборка = Запрос.Выполнить().Выбрать();
	Список.УсловноеОформление.Элементы.Очистить();
	Пока Выборка.Следующий() Цикл
		
		НовыйЭлемент = Список.УсловноеОформление.Элементы.Добавить();
		НовыйЭлемент.Использование = Истина;
		
		ЭлементОтбора = НовыйЭлемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Цвет");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Выборка.Цвет;
		
		НовоеПоле = НовыйЭлемент.Поля.Элементы.Добавить();
		НовоеПоле.Использование = Истина;
		НовоеПоле.Поле = Новый ПолеКомпоновкиДанных("Цвет");
		
		Оформление = НовыйЭлемент.Оформление;
		
		ПараметрЦветФона = Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ЦветФона"));
		ПараметрЦветФона.Использование = Истина;
		ПараметрЦветФона.Значение = ПолучитьЦвет(Выборка.Цвет);
		
		ПараметрТекст = Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Текст"));
		ПараметрТекст.Использование = Истина;
		ПараметрТекст.Значение = "";
		
	КонецЦикла;
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанНовыйЦвет" Тогда
		НастроитьУсловноеОформление();
	КонецЕсли;
	
КонецПроцедуры
