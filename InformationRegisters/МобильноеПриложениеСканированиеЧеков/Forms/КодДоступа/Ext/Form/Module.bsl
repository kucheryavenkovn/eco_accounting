// Для параметра Пользователь подразумевается определяемый тип ПользовательШиныМобильныхПриложений

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	
	МенеджерЗаписи = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.Пользователь = Параметры.Пользователь;
	Если ЗначениеЗаполнено(МенеджерЗаписи.Пользователь) Тогда
		МенеджерЗаписи.Прочитать();
	Иначе
		// Пользователь может идентифицироваться ссылкой на справочник Пользователи
		// или ссылкой на справочник ФизическиеЛица.
		// В регистр пишется преимущественно физическое лицо.
		Пользователь = Пользователи.ТекущийПользователь();
		МенеджерЗаписи.Пользователь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ФизическоеЛицо");
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Пользователь = Пользователь;
			МенеджерЗаписи.Прочитать();
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(МенеджерЗаписи.КодДоступа) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПредставлениеСотрудника = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ПредставлениеПользователя(
		МенеджерЗаписи.Пользователь);
	
	ПредставлениеКодаДоступа = МенеджерЗаписи.ПредставлениеКодаДоступа;
	
	КартинкаQRКод = КартинкаQRКод(МенеджерЗаписи.КодДоступа);
	РасположитьQRКод(QRКод, КартинкаQRКод, ПредставлениеКодаДоступа);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура РасположитьQRКод(ТабличныйДокумент, Картинка, Текст)
	
	// Нельзя просто так взять и поместить картинку на форме.
	// Использовать небиблиотечные картинки можно только для рисунка табличного документа
	
	// Табличный документ держит размеры в миллиметрах, а картинка - в пикселях.
	// Пересчитаем одно в другое, используем супрематический метод
	
	Эталон = Справочники.ПодключаемоеОборудование.ПолучитьМакет("МакетДляОпределенияКоэффициентовЕдиницИзмерения");
	
	ПлотностьВысоты = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100; // мм / пикс
	ПлотностьШирины = Эталон.Рисунки.Квадрат100Пикселей.Ширина / 100; // мм / пикс
	
	ШиринаМм = Картинка.Ширина() * ПлотностьШирины;
	ВысотаМм = Картинка.Высота() * ПлотностьВысоты;
	
	Размер = (ШиринаМм + ВысотаМм) / 2; // Средняя
	
	Рисунок = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
	Рисунок.Картинка = Картинка;
	Рисунок.Ширина = Размер;
	Рисунок.Высота = Размер;
	Рисунок.Линия  = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
	
	Рисунок.Расположить(ТабличныйДокумент.Область());
	
	// Текст с содержимым QR-кода
	ВыводТекста = Новый ТабличныйДокумент;
	Область = ВыводТекста.Область(1, 1, 1, 1);
	Область.Шрифт      = Новый Шрифт(Область.Шрифт, , 26);
	Область.ЦветТекста = WebЦвета.Черный;
	Отступ = 6;
	Область.Текст = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов(" ", Отступ) + Текст;
	
	ТабличныйДокумент.Вывести(ВыводТекста);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КартинкаQRКод(Значение)
	
	РазмерКода = 400; // См. картинку QR_MobileAccounting_Android
	
	Возврат Новый Картинка(ГенерацияШтрихкодаВызовСервера.ДанныеQRКода(Значение, 3, РазмерКода));
	
КонецФункции

#КонецОбласти
