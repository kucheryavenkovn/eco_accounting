#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Количество() = 1 Тогда
		Запись = Получить(0);
		
		Если Запись.ВидЗаписи = Перечисления.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("РегистрационныйЗнак");
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификационныйНомер");
			МассивНепроверяемыхРеквизитов.Добавить("Марка");
			МассивНепроверяемыхРеквизитов.Добавить("ПостановкаНаУчетВНалоговомОргане");
			МассивНепроверяемыхРеквизитов.Добавить("КодВидаТранспортногоСредства");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяБаза");
			МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияНалоговойБазы");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяСтавка");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяЛьгота");
			МассивНепроверяемыхРеквизитов.Добавить("ЭкологическийКласс");
			МассивНепроверяемыхРеквизитов.Добавить("ВидЗаписи");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.РегистрацияТранспортныхСредств.ПроверитьЗначениеОпцииВедетсяУчетПлатежейВПлатон(ЭтотОбъект, Отказ, Замещение);
	
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетПоПутевымЛистам") Тогда
		СоздатьТранспортныеСредства();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьТранспортныеСредства()
	
	// Создание новых транспортных средств осуществляется только при постановке ОС на учет.
	// При снятии с регистрации или при удалении записи нет смысла создавать элемент справочника.
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыВыгрузки = Новый Массив;
	РеквизитыВыгрузки.Добавить("ОсновноеСредство");
	РеквизитыВыгрузки.Добавить("ИдентификационныйНомер");
	РеквизитыВыгрузки.Добавить("Марка");
	РеквизитыВыгрузки.Добавить("РегистрационныйЗнак");
	РеквизитыВыгрузки.Добавить("Организация");
	РеквизитыВыгрузки.Добавить("ВидЗаписи");
	
	ДанныеРегистрации = Выгрузить(, СтрСоединить(РеквизитыВыгрузки, ","));
	ОтборЗаписей = Новый Структура;
	ОтборЗаписей.Вставить("ВидЗаписи", Перечисления.ВидЗаписиОРегистрации.Регистрация);
	
	ОсновныеСредства = ДанныеРегистрации.НайтиСтроки(ОтборЗаписей);
	
	ГруппыОС = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДанныеРегистрации.ВыгрузитьКолонку("ОсновноеСредство"), "ГруппаОС");
	ТипТранспортныеСредства = Перечисления.ГруппыОС.ТранспортныеСредства;
	
	ТранспортныеСредства = Справочники.ТранспортныеСредства.НовыйТаблицаТранспортныеСредства();
	
	Для Каждого ТекущаяСтрока Из ОсновныеСредства Цикл
		
		Если Не ПустаяСтрока(ТекущаяСтрока.ИдентификационныйНомер)
			И ГруппыОС[ТекущаяСтрока.ОсновноеСредство].ГруппаОС = ТипТранспортныеСредства Тогда
			
			СтрокаТранспортныеСредства = ТранспортныеСредства.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТранспортныеСредства, ТекущаяСтрока);
			СтрокаТранспортныеСредства.ОформляютсяПутевыеЛисты = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТранспортныеСредства) Тогда
		
		Попытка
			
			Справочники.ТранспортныеСредства.СоздатьТранспортныеСредства(ТранспортныеСредства);
			
		Исключение
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать транспортное средство по причине:
				|%1.'"), ОписаниеОшибки());
			ИмяСобытия = НСтр("ru = 'Данные.Добавление'", ОбщегоНазначения.КодОсновногоЯзыка());
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств,, ТекстОшибки);
			
			ВызватьИсключение НСтр("ru = 'Не удалось создать транспортное средство.'");
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли