#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возвращает сумму вычета по заданным параметрам
//
// Параметры:
//   ПараметрыВычета - Структура с ключами:
//     * ВидВычета   - ПеречислениеСсылка.СтандартныеВычетыНаНалогоплательщика
//     * НомерМесяца - Число.
//   РазмерыВычетов  - Структура данных вычета (см. Отчеты.РегламентированныйОтчет3НДФЛ.РазмерыВычетов())
//
// Возвращаемое значение:
//   Число
//
Функция РазмерТекущегоВычета(ПараметрыВычета, РазмерыВычетов) Экспорт
	
	СуммаВычета = РазмерыВычетов.СтандартныйВычетНаНалогоплательщика.Получить(ПараметрыВычета.ВидВычета);
	
	Возврат СуммаВычета;
	
КонецФункции

Функция ДанныеВычетаЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВычетыНаНалогоплательщика.Период КАК Период,
	|	0 КАК НомерМесяца,
	|	ВычетыНаНалогоплательщика.Организация КАК Организация,
	|	ВычетыНаНалогоплательщика.ВидВычета КАК ВидВычета
	|ИЗ
	|	РегистрСведений.ИПСтандартныеВычетыНаНалогоплательщика.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК ВычетыНаНалогоплательщика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВычетыНаНалогоплательщика.Период,
	|	МЕСЯЦ(ВычетыНаНалогоплательщика.Период),
	|	ВычетыНаНалогоплательщика.Организация,
	|	ВычетыНаНалогоплательщика.ВидВычета
	|ИЗ
	|	РегистрСведений.ИПСтандартныеВычетыНаНалогоплательщика КАК ВычетыНаНалогоплательщика
	|ГДЕ
	|	ВычетыНаНалогоплательщика.Организация = &Организация
	|	И ВычетыНаНалогоплательщика.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерМесяца");
	
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли