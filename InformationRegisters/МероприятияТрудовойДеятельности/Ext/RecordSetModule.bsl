#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// 1. Записывется набор записей (движения документа).
	// 2. Он сравнивается с предыдущим набором (предыдущими движениями).
	// 3. Те записи из предыдущего набора которых нет в новом и которые учатвовали в обмене.
	//    с ПФР (МероприятияТрудовойДеятельностиПереданные) - записываются в регистр МероприятияТрудовойДеятельностиПрочие.
	// 4. Для записей новго набора у которых не назначен идентификатор мероприятия ищутся полные
	//    аналоги в регистре сведнгий МероприятияТрудовойДеятельностиПрочие.
	// 5. Для записей не имеющих идентификатора мероприятия ищется аналог из предыдущих движений,
	//    если находится используется идентификатор из предыдущих движений.
	// 6. Если в предыддущих движениях не нашлось аналога - ищется аналог из числа найденных
	//    в МероприятияТрудовойДеятельностиПрочие, если находится используется идентификатор записи
	//    из Прочих, а сама запись в прочих удаляется.
	// 7. Если идентифкатор так и не удалось заполнить назначется новый.

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НовыйНабор", Выгрузить());
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ДатаОтмены", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ПустойИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельности.Организация КАК Организация,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельности.Отменено КАК Отменено,
		|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник,
		|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
		|	МероприятияТрудовойДеятельности.ВидМероприятия КАК ВидМероприятия,
		|	МероприятияТрудовойДеятельности.Сведения КАК Сведения,
		|	МероприятияТрудовойДеятельности.Подразделение КАК Подразделение,
		|	МероприятияТрудовойДеятельности.Должность КАК Должность,
		|	МероприятияТрудовойДеятельности.РазрядКатегория КАК РазрядКатегория,
		|	МероприятияТрудовойДеятельности.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
		|	МероприятияТрудовойДеятельности.ТрудоваяФункция КАК ТрудоваяФункция,
		|	МероприятияТрудовойДеятельности.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
		|	МероприятияТрудовойДеятельности.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	МероприятияТрудовойДеятельности.СерияДокументаОснования КАК СерияДокументаОснования,
		|	МероприятияТрудовойДеятельности.НомерДокументаОснования КАК НомерДокументаОснования,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольнения КАК ОснованиеУвольнения,
		|	МероприятияТрудовойДеятельности.ДатаС КАК ДатаС,
		|	МероприятияТрудовойДеятельности.ДатаПо КАК ДатаПо,
		|	МероприятияТрудовойДеятельности.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	МероприятияТрудовойДеятельности.ДатаОтмены КАК ДатаОтмены,
		|	МероприятияТрудовойДеятельности.Регистратор.Дата КАК РегистраторДата,
		|	МероприятияТрудовойДеятельности.ПредставлениеДолжности КАК ПредставлениеДолжности,
		|	МероприятияТрудовойДеятельности.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
		|	МероприятияТрудовойДеятельности.ОписаниеДолжности КАК ОписаниеДолжности
		|ПОМЕСТИТЬ ВТТекущийНабор
		|ИЗ
		|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|ГДЕ
		|	МероприятияТрудовойДеятельности.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельности.Организация КАК Организация,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельности.Отменено КАК Отменено,
		|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник,
		|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
		|	МероприятияТрудовойДеятельности.ВидМероприятия КАК ВидМероприятия,
		|	МероприятияТрудовойДеятельности.Сведения КАК Сведения,
		|	МероприятияТрудовойДеятельности.Подразделение КАК Подразделение,
		|	МероприятияТрудовойДеятельности.Должность КАК Должность,
		|	МероприятияТрудовойДеятельности.РазрядКатегория КАК РазрядКатегория,
		|	МероприятияТрудовойДеятельности.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
		|	МероприятияТрудовойДеятельности.ТрудоваяФункция КАК ТрудоваяФункция,
		|	МероприятияТрудовойДеятельности.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
		|	МероприятияТрудовойДеятельности.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	МероприятияТрудовойДеятельности.СерияДокументаОснования КАК СерияДокументаОснования,
		|	МероприятияТрудовойДеятельности.НомерДокументаОснования КАК НомерДокументаОснования,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольнения КАК ОснованиеУвольнения,
		|	МероприятияТрудовойДеятельности.ДатаС КАК ДатаС,
		|	МероприятияТрудовойДеятельности.ДатаПо КАК ДатаПо,
		|	МероприятияТрудовойДеятельности.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	МероприятияТрудовойДеятельности.ДатаОтмены КАК ДатаОтмены,
		|	МероприятияТрудовойДеятельности.ПредставлениеДолжности КАК ПредставлениеДолжности,
		|	МероприятияТрудовойДеятельности.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
		|	МероприятияТрудовойДеятельности.ОписаниеДолжности КАК ОписаниеДолжности
		|ПОМЕСТИТЬ ВТНовыйНабор
		|ИЗ
		|	&НовыйНабор КАК МероприятияТрудовойДеятельности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущийНабор.ИдМероприятия КАК ИдМероприятия
		|ИЗ
		|	ВТТекущийНабор КАК ТекущийНабор";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущийНабор.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТекущийНабор.Организация КАК Организация,
			|	ТекущийНабор.ИдМероприятия КАК ИдМероприятия,
			|	ТекущийНабор.Отменено КАК Отменено,
			|	ТекущийНабор.Сотрудник КАК Сотрудник,
			|	ТекущийНабор.ДатаМероприятия КАК ДатаМероприятия,
			|	ТекущийНабор.ВидМероприятия КАК ВидМероприятия,
			|	ТекущийНабор.Сведения КАК Сведения,
			|	ТекущийНабор.Подразделение КАК Подразделение,
			|	ТекущийНабор.Должность КАК Должность,
			|	ТекущийНабор.РазрядКатегория КАК РазрядКатегория,
			|	ТекущийНабор.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
			|	ТекущийНабор.ТрудоваяФункция КАК ТрудоваяФункция,
			|	ТекущийНабор.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
			|	ТекущийНабор.ДатаДокументаОснования КАК ДатаДокументаОснования,
			|	ТекущийНабор.СерияДокументаОснования КАК СерияДокументаОснования,
			|	ТекущийНабор.НомерДокументаОснования КАК НомерДокументаОснования,
			|	ТекущийНабор.ОснованиеУвольнения КАК ОснованиеУвольнения,
			|	ТекущийНабор.ДатаС КАК ДатаС,
			|	ТекущийНабор.ДатаПо КАК ДатаПо,
			|	ТекущийНабор.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
			|	ТекущийНабор.ДатаОтмены КАК ДатаОтмены,
			|	ТекущийНабор.ПредставлениеДолжности КАК ПредставлениеДолжности,
			|	ТекущийНабор.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
			|	ТекущийНабор.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
			|	ТекущийНабор.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
			|	ТекущийНабор.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
			|	ТекущийНабор.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
			|	ТекущийНабор.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
			|	ТекущийНабор.ОписаниеДолжности КАК ОписаниеДолжности,
			|	ВЫБОР
			|		КОГДА НовыйНабор.ФизическоеЛицо ЕСТЬ NULL
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК УдаляемоеДвижение
			|ПОМЕСТИТЬ ВТИзменения
			|ИЗ
			|	ВТТекущийНабор КАК ТекущийНабор
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыйНабор КАК НовыйНабор
			|		ПО ТекущийНабор.ФизическоеЛицо = НовыйНабор.ФизическоеЛицо
			|			И ТекущийНабор.Организация = НовыйНабор.Организация
			|			И ТекущийНабор.Сотрудник = НовыйНабор.Сотрудник
			|			И ТекущийНабор.ДатаМероприятия = НовыйНабор.ДатаМероприятия
			|			И ТекущийНабор.ВидМероприятия = НовыйНабор.ВидМероприятия
			|			И ТекущийНабор.Сведения = НовыйНабор.Сведения
			|			И ТекущийНабор.Подразделение = НовыйНабор.Подразделение
			|			И ТекущийНабор.Должность = НовыйНабор.Должность
			|			И ТекущийНабор.РазрядКатегория = НовыйНабор.РазрядКатегория
			|			И ТекущийНабор.КодПоРееструДолжностей = НовыйНабор.КодПоРееструДолжностей
			|			И ТекущийНабор.ТрудоваяФункция = НовыйНабор.ТрудоваяФункция
			|			И (ВЫБОР
			|				КОГДА ТекущийНабор.НаименованиеДокументаОснования = """"
			|					ТОГДА ИСТИНА
			|				КОГДА НовыйНабор.НаименованиеДокументаОснования = """"
			|					ТОГДА ИСТИНА
			|				КОГДА ТекущийНабор.НаименованиеДокументаОснования = НовыйНабор.НаименованиеДокументаОснования
			|					ТОГДА ИСТИНА
			|				ИНАЧЕ ЛОЖЬ
			|			КОНЕЦ)
			|			И ТекущийНабор.ДатаДокументаОснования = НовыйНабор.ДатаДокументаОснования
			|			И ТекущийНабор.СерияДокументаОснования = НовыйНабор.СерияДокументаОснования
			|			И ТекущийНабор.НомерДокументаОснования = НовыйНабор.НомерДокументаОснования
			|			И ТекущийНабор.ОснованиеУвольнения = НовыйНабор.ОснованиеУвольнения
			|			И ТекущийНабор.ДатаС = НовыйНабор.ДатаС
			|			И ТекущийНабор.ДатаПо = НовыйНабор.ДатаПо
			|			И ТекущийНабор.ЯвляетсяСовместителем = НовыйНабор.ЯвляетсяСовместителем
			|			И ТекущийНабор.ДатаОтмены = НовыйНабор.ДатаОтмены
			|			И ТекущийНабор.ПредставлениеДолжности = НовыйНабор.ПредставлениеДолжности
			|			И ТекущийНабор.ПредставлениеПодразделения = НовыйНабор.ПредставлениеПодразделения
			|			И ТекущийНабор.ОснованиеУвольненияТекстОснования = НовыйНабор.ОснованиеУвольненияТекстОснования
			|			И ТекущийНабор.ОснованиеУвольненияСтатья = НовыйНабор.ОснованиеУвольненияСтатья
			|			И ТекущийНабор.ОснованиеУвольненияЧасть = НовыйНабор.ОснованиеУвольненияЧасть
			|			И ТекущийНабор.ОснованиеУвольненияПункт = НовыйНабор.ОснованиеУвольненияПункт
			|			И ТекущийНабор.ОснованиеУвольненияПодПункт = НовыйНабор.ОснованиеУвольненияПодПункт
			|			И ТекущийНабор.ОписаниеДолжности = НовыйНабор.ОписаниеДолжности
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Изменения.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Изменения.Организация КАК Организация,
			|	Изменения.ИдМероприятия КАК ИдМероприятия,
			|	Изменения.Сотрудник КАК Сотрудник,
			|	Изменения.ДатаМероприятия КАК ДатаМероприятия,
			|	Изменения.ВидМероприятия КАК ВидМероприятия,
			|	Изменения.Сведения КАК Сведения,
			|	Изменения.Подразделение КАК Подразделение,
			|	Изменения.Должность КАК Должность,
			|	Изменения.РазрядКатегория КАК РазрядКатегория,
			|	Изменения.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
			|	Изменения.ТрудоваяФункция КАК ТрудоваяФункция,
			|	Изменения.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
			|	Изменения.ДатаДокументаОснования КАК ДатаДокументаОснования,
			|	Изменения.СерияДокументаОснования КАК СерияДокументаОснования,
			|	Изменения.НомерДокументаОснования КАК НомерДокументаОснования,
			|	Изменения.ОснованиеУвольнения КАК ОснованиеУвольнения,
			|	Изменения.ДатаС КАК ДатаС,
			|	Изменения.ДатаПо КАК ДатаПо,
			|	Изменения.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
			|	Изменения.ДатаОтмены КАК ДатаОтменыПереданного,
			|	Изменения.ПредставлениеДолжности КАК ПредставлениеДолжности,
			|	Изменения.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
			|	Изменения.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
			|	Изменения.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
			|	Изменения.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
			|	Изменения.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
			|	Изменения.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
			|	Изменения.ОписаниеДолжности КАК ОписаниеДолжности,
			|	ВЫБОР
			|		КОГДА Изменения.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА &ДатаОтмены
			|		ИНАЧЕ Изменения.ДатаОтмены
			|	КОНЕЦ КАК ДатаОтмены,
			|	Изменения.Отменено КАК Отменено
			|ПОМЕСТИТЬ ВТУдаляемыеПереданные
			|ИЗ
			|	ВТИзменения КАК Изменения
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
			|		ПО Изменения.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
			|			И Изменения.Отменено = МероприятияТрудовойДеятельностиПереданные.Отменено
			|ГДЕ
			|	Изменения.УдаляемоеДвижение
			|	И НЕ МероприятияТрудовойДеятельностиПереданные.ИдМероприятия ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Изменения.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Изменения.Организация КАК Организация,
			|	Изменения.ИдМероприятия КАК ИдМероприятия,
			|	Изменения.Сотрудник КАК Сотрудник,
			|	Изменения.ДатаМероприятия КАК ДатаМероприятия,
			|	Изменения.ВидМероприятия КАК ВидМероприятия,
			|	Изменения.Сведения КАК Сведения,
			|	Изменения.Подразделение КАК Подразделение,
			|	Изменения.Должность КАК Должность,
			|	Изменения.РазрядКатегория КАК РазрядКатегория,
			|	Изменения.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
			|	Изменения.ТрудоваяФункция КАК ТрудоваяФункция,
			|	Изменения.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
			|	Изменения.ДатаДокументаОснования КАК ДатаДокументаОснования,
			|	Изменения.СерияДокументаОснования КАК СерияДокументаОснования,
			|	Изменения.НомерДокументаОснования КАК НомерДокументаОснования,
			|	Изменения.ОснованиеУвольнения КАК ОснованиеУвольнения,
			|	Изменения.ДатаС КАК ДатаС,
			|	Изменения.ДатаПо КАК ДатаПо,
			|	Изменения.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
			|	Изменения.ДатаОтмены КАК ДатаОтмены,
			|	Изменения.ПредставлениеДолжности КАК ПредставлениеДолжности,
			|	Изменения.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
			|	Изменения.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
			|	Изменения.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
			|	Изменения.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
			|	Изменения.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
			|	Изменения.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
			|	Изменения.ОписаниеДолжности КАК ОписаниеДолжности,
			|	ИСТИНА КАК Отменено
			|ИЗ
			|	ВТУдаляемыеПереданные КАК Изменения
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Изменения.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Изменения.Организация КАК Организация,
			|	Изменения.ИдМероприятия КАК ИдМероприятия,
			|	Изменения.Отменено КАК Отменено,
			|	Изменения.Сотрудник КАК Сотрудник,
			|	Изменения.ДатаМероприятия КАК ДатаМероприятия,
			|	Изменения.ВидМероприятия КАК ВидМероприятия,
			|	Изменения.Сведения КАК Сведения,
			|	Изменения.Подразделение КАК Подразделение,
			|	Изменения.Должность КАК Должность,
			|	Изменения.РазрядКатегория КАК РазрядКатегория,
			|	Изменения.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
			|	Изменения.ТрудоваяФункция КАК ТрудоваяФункция,
			|	Изменения.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
			|	Изменения.ДатаДокументаОснования КАК ДатаДокументаОснования,
			|	Изменения.СерияДокументаОснования КАК СерияДокументаОснования,
			|	Изменения.НомерДокументаОснования КАК НомерДокументаОснования,
			|	Изменения.ОснованиеУвольнения КАК ОснованиеУвольнения,
			|	Изменения.ДатаС КАК ДатаС,
			|	Изменения.ДатаПо КАК ДатаПо,
			|	Изменения.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
			|	Изменения.ДатаОтмены КАК ДатаОтмены,
			|	Изменения.ПредставлениеДолжности КАК ПредставлениеДолжности,
			|	Изменения.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
			|	Изменения.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
			|	Изменения.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
			|	Изменения.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
			|	Изменения.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
			|	Изменения.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
			|	Изменения.ОписаниеДолжности КАК ОписаниеДолжности
			|ИЗ
			|	ВТИзменения КАК Изменения
			|ГДЕ
			|	НЕ Изменения.УдаляемоеДвижение";
	
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		ПрежниеИдентификаторы = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
		
		Выборка = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выбрать();
		Если Выборка.Количество() > 0 Тогда
			
			Пока Выборка.Следующий() Цикл
				
				Запись = РегистрыСведений.МероприятияТрудовойДеятельностиПрочие.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
				Запись.Записать();
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		ПрежниеИдентификаторы = Неопределено;
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияТрудовойДеятельностиПрочие.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельностиПрочие.Организация КАК Организация,
		|	МероприятияТрудовойДеятельностиПрочие.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельностиПрочие.Отменено КАК Отменено,
		|	МероприятияТрудовойДеятельностиПрочие.Сотрудник КАК Сотрудник,
		|	МероприятияТрудовойДеятельностиПрочие.ДатаМероприятия КАК ДатаМероприятия,
		|	МероприятияТрудовойДеятельностиПрочие.ВидМероприятия КАК ВидМероприятия,
		|	МероприятияТрудовойДеятельностиПрочие.Сведения КАК Сведения,
		|	МероприятияТрудовойДеятельностиПрочие.Подразделение КАК Подразделение,
		|	МероприятияТрудовойДеятельностиПрочие.Должность КАК Должность,
		|	МероприятияТрудовойДеятельностиПрочие.РазрядКатегория КАК РазрядКатегория,
		|	МероприятияТрудовойДеятельностиПрочие.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
		|	МероприятияТрудовойДеятельностиПрочие.ТрудоваяФункция КАК ТрудоваяФункция,
		|	МероприятияТрудовойДеятельностиПрочие.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
		|	МероприятияТрудовойДеятельностиПрочие.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	МероприятияТрудовойДеятельностиПрочие.СерияДокументаОснования КАК СерияДокументаОснования,
		|	МероприятияТрудовойДеятельностиПрочие.НомерДокументаОснования КАК НомерДокументаОснования,
		|	МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольнения КАК ОснованиеУвольнения,
		|	МероприятияТрудовойДеятельностиПрочие.ДатаС КАК ДатаС,
		|	МероприятияТрудовойДеятельностиПрочие.ДатаПо КАК ДатаПо,
		|	МероприятияТрудовойДеятельностиПрочие.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	МероприятияТрудовойДеятельностиПрочие.ДатаОтмены КАК ДатаОтмены,
		|	МероприятияТрудовойДеятельностиПрочие.ПредставлениеДолжности КАК ПредставлениеДолжности,
		|	МероприятияТрудовойДеятельностиПрочие.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
		|	МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
		|	МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
		|	МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
		|	МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
		|	МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
		|	МероприятияТрудовойДеятельностиПрочие.ОписаниеДолжности КАК ОписаниеДолжности
		|ИЗ
		|	ВТНовыйНабор КАК НовыйНабор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПрочие КАК МероприятияТрудовойДеятельностиПрочие
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
		|			ПО МероприятияТрудовойДеятельностиПрочие.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
		|				И (МероприятияТрудовойДеятельностиПереданные.Отменено)
		|		ПО НовыйНабор.ФизическоеЛицо = МероприятияТрудовойДеятельностиПрочие.ФизическоеЛицо
		|			И НовыйНабор.Организация = МероприятияТрудовойДеятельностиПрочие.Организация
		|			И НовыйНабор.Сотрудник = МероприятияТрудовойДеятельностиПрочие.Сотрудник
		|			И НовыйНабор.ДатаМероприятия = МероприятияТрудовойДеятельностиПрочие.ДатаМероприятия
		|			И НовыйНабор.ВидМероприятия = МероприятияТрудовойДеятельностиПрочие.ВидМероприятия
		|			И НовыйНабор.Сведения = МероприятияТрудовойДеятельностиПрочие.Сведения
		|			И НовыйНабор.Подразделение = МероприятияТрудовойДеятельностиПрочие.Подразделение
		|			И НовыйНабор.Должность = МероприятияТрудовойДеятельностиПрочие.Должность
		|			И НовыйНабор.РазрядКатегория = МероприятияТрудовойДеятельностиПрочие.РазрядКатегория
		|			И НовыйНабор.КодПоРееструДолжностей = МероприятияТрудовойДеятельностиПрочие.КодПоРееструДолжностей
		|			И НовыйНабор.ТрудоваяФункция = МероприятияТрудовойДеятельностиПрочие.ТрудоваяФункция
		|			И НовыйНабор.НаименованиеДокументаОснования = МероприятияТрудовойДеятельностиПрочие.НаименованиеДокументаОснования
		|			И НовыйНабор.ДатаДокументаОснования = МероприятияТрудовойДеятельностиПрочие.ДатаДокументаОснования
		|			И НовыйНабор.СерияДокументаОснования = МероприятияТрудовойДеятельностиПрочие.СерияДокументаОснования
		|			И НовыйНабор.НомерДокументаОснования = МероприятияТрудовойДеятельностиПрочие.НомерДокументаОснования
		|			И НовыйНабор.ОснованиеУвольнения = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольнения
		|			И НовыйНабор.ДатаС = МероприятияТрудовойДеятельностиПрочие.ДатаС
		|			И НовыйНабор.ДатаПо = МероприятияТрудовойДеятельностиПрочие.ДатаПо
		|			И НовыйНабор.ЯвляетсяСовместителем = МероприятияТрудовойДеятельностиПрочие.ЯвляетсяСовместителем
		|			И НовыйНабор.ПредставлениеДолжности = МероприятияТрудовойДеятельностиПрочие.ПредставлениеДолжности
		|			И НовыйНабор.ПредставлениеПодразделения = МероприятияТрудовойДеятельностиПрочие.ПредставлениеПодразделения
		|			И НовыйНабор.ОснованиеУвольненияТекстОснования = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияТекстОснования
		|			И НовыйНабор.ОснованиеУвольненияСтатья = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияСтатья
		|			И НовыйНабор.ОснованиеУвольненияЧасть = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияЧасть
		|			И НовыйНабор.ОснованиеУвольненияПункт = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияПункт
		|			И НовыйНабор.ОснованиеУвольненияПодПункт = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияПодПункт
		|			И НовыйНабор.ОписаниеДолжности = МероприятияТрудовойДеятельностиПрочие.ОписаниеДолжности
		|ГДЕ
		|	НовыйНабор.ИдМероприятия = &ПустойИдентификатор
		|	И НЕ НовыйНабор.Отменено
		|	И МероприятияТрудовойДеятельностиПереданные.ИдМероприятия ЕСТЬ NULL";
	
	ПрочиеМероприятия = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Если Не ЗначениеЗаполнено(Запись.ИдМероприятия) Тогда
			
			СтруктураПоиска = Новый Структура(
				"ФизическоеЛицо,
				|Организация,
				|Сотрудник,
				|ДатаМероприятия,
				|ВидМероприятия,
				|Сведения,
				|Подразделение,
				|Должность,
				|РазрядКатегория,
				|КодПоРееструДолжностей,
				|ТрудоваяФункция,
				|НаименованиеДокументаОснования,
				|ДатаДокументаОснования,
				|СерияДокументаОснования,
				|НомерДокументаОснования,
				|ОснованиеУвольнения,
				|ДатаС,
				|ДатаПо,
				|ДатаОтмены,
				|ЯвляетсяСовместителем,
				|Отменено,
				|ПредставлениеДолжности,
				|ПредставлениеПодразделения,
				|ОснованиеУвольненияТекстОснования,
				|ОснованиеУвольненияСтатья,
				|ОснованиеУвольненияЧасть,
				|ОснованиеУвольненияПункт,
				|ОснованиеУвольненияПодПункт,
				|ОписаниеДолжности");
			
			Если ПрежниеИдентификаторы <> Неопределено Тогда
				
				// Заполнение идентификаторов мероприятий, регистрировавшихся ранее
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, Запись);
				НайденныеЗаписи = ПрежниеИдентификаторы.НайтиСтроки(СтруктураПоиска);
				Если НайденныеЗаписи.Количество() > 0 Тогда
					Запись.ИдМероприятия = НайденныеЗаписи[0].ИдМероприятия;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Запись.ИдМероприятия) Тогда
				
				// Заполнение идентификаторов мероприятий, регистрировавшихся ранее
				Если Не ЗначениеЗаполнено(Запись.ДатаОтмены) Тогда
					СтруктураПоиска.Удалить("ДатаОтмены");
					СтруктураПоиска.Удалить("Отменено");
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, Запись);
				НайденныеЗаписи = ПрочиеМероприятия.НайтиСтроки(СтруктураПоиска);
				Если НайденныеЗаписи.Количество() > 0 Тогда
					
					Запись.ИдМероприятия = НайденныеЗаписи[0].ИдМероприятия;
					
					Запись = РегистрыСведений.МероприятияТрудовойДеятельностиПрочие.СоздатьМенеджерЗаписи();
					Запись.ФизическоеЛицо = НайденныеЗаписи[0].ФизическоеЛицо;
					Запись.Организация = НайденныеЗаписи[0].Организация;
					Запись.ИдМероприятия = НайденныеЗаписи[0].ИдМероприятия;
					Запись.Отменено = НайденныеЗаписи[0].Отменено;
					Запись.Удалить();
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Запись.ИдМероприятия) Тогда
				// Назначение идентификаторов новым мероприятиям
				Запись.ИдМероприятия = Новый УникальныйИдентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли