#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

Процедура СформироватьНаборыЗаписейМероприятияТрудовойДеятельности(ПараметрыОбновления = Неопределено) Экспорт
	
	Если ПараметрыОбновления = Неопределено Или Не ПараметрыОбновления.Свойство("РегистраторыДвижений") Тогда
		
		РегистраторыДвижений = Новый Массив;
		Если ПараметрыОбновления <> Неопределено Тогда
			ПараметрыОбновления.Вставить("РегистраторыДвижений", РегистраторыДвижений);
		КонецЕсли;
		
		РегистраторыДвижений.Добавить("КадровыйПеревод");
		РегистраторыДвижений.Добавить("ПриемНаРаботу");
		РегистраторыДвижений.Добавить("Увольнение");
		
		// Мероприятия, введенные в СЗВ_ТД обрабатываем в последнюю очередь
		РегистраторыДвижений.Добавить("РегистрацияТрудовойДеятельности");
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Ссылка.Организация КАК Организация,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Сотрудник КАК ФизическоеЛицо,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ДатаМероприятия КАК ДатаМероприятия,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ВидМероприятия КАК ВидМероприятия,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Сведения КАК Сведения,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Должность КАК Должность,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.РазрядКатегория КАК РазрядКатегория,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Подразделение КАК Подразделение,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ТрудоваяФункция КАК ТрудоваяФункция,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ДатаДокументаОснования КАК ДатаДокументаОснования,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.НомерДокументаОснования КАК НомерДокументаОснования,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.СерияДокументаОснования КАК СерияДокументаОснования,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОснованиеУвольнения КАК ОснованиеУвольнения,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ДатаС КАК ДатаС,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ДатаПо КАК ДатаПо,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ИдМероприятия КАК ИдМероприятия,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.СотрудникЗаписи КАК Сотрудник,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Ссылка КАК Ссылка,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Ссылка.Дата КАК Дата,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.НомерСтроки КАК НомерСтроки,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ПредставлениеДолжности КАК ПредставлениеДолжности,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ОписаниеДолжности КАК ОписаниеДолжности
			|ИЗ
			|	Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.Мероприятия КАК СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия
			|ГДЕ
			|	СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.Ссылка.Проведен
			|	И СведенияОТрудовойДеятельностиРаботниковСЗВ_ТДМероприятия.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)";
		
		МероприятияТрудовойДеятельности = Запрос.Выполнить().Выгрузить();
		МероприятияТрудовойДеятельности.Колонки.Добавить("Учтено", Новый ОписаниеТипов("Булево"));
		МероприятияТрудовойДеятельности.Индексы.Добавить("ФизическоеЛицо,Сотрудник,Организация,ДатаМероприятия,ВидМероприятия");
		
		Если ПараметрыОбновления <> Неопределено Тогда
			ПараметрыОбновления.Вставить("МероприятияТрудовойДеятельности", МероприятияТрудовойДеятельности);
		КонецЕсли;
		
	Иначе
		РегистраторыДвижений = ПараметрыОбновления.РегистраторыДвижений;
		МероприятияТрудовойДеятельности = ПараметрыОбновления.МероприятияТрудовойДеятельности;
	КонецЕсли;
	
	Если ПараметрыОбновления = Неопределено Тогда
		ОбрабатываемыеРегистраторы = ОбщегоНазначения.СкопироватьРекурсивно(РегистраторыДвижений);
	Иначе
		Если РегистраторыДвижений.Количество() = 0 Тогда
			ОбрабатываемыеРегистраторы = Новый Массив;
		Иначе
			ОбрабатываемыеРегистраторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РегистраторыДвижений[0]);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ИмяРегистратора Из ОбрабатываемыеРегистраторы Цикл
		
		Документы[ИмяРегистратора].СформироватьНаборыЗаписейМероприятияТрудовойДеятельности(МероприятияТрудовойДеятельности, ПараметрыОбновления);
		Если ПараметрыОбновления = Неопределено Тогда
			РегистраторыДвижений.Удалить(0);
		ИначеЕсли ПараметрыОбновления.ОбработкаЗавершена Тогда
			
			РегистраторыДвижений.Удалить(0);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если РегистраторыДвижений.Количество() = 0 Тогда
		Документы.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.СформироватьНаборыЗаписейМероприятияТрудовойДеятельностиПереданные(ПараметрыОбновления);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗаписиМероприятий(Мероприятия, МероприятияТрудовойДеятельности, РеквизитыПроверки = "") Экспорт
	
	Для Каждого Мероприятие Из Мероприятия Цикл
		
		СтруктураПоиска = Новый Структура("ФизическоеЛицо,Сотрудник,Организация,ДатаМероприятия,ВидМероприятия");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Мероприятие);
		
		ИменаДополнительныхРеквизитов = СтрРазделить(РеквизитыПроверки, ",");
		НайденныеМероприятия = МероприятияТрудовойДеятельности.НайтиСтроки(СтруктураПоиска);
		Для Каждого НайденноеМероприятие Из НайденныеМероприятия Цикл
			
			Если НайденноеМероприятие.Учтено Тогда
				Продолжить;
			КонецЕсли;
			
			Если НачалоДня(Мероприятие.ДатаДокументаОснования) <> НайденноеМероприятие.ДатаДокументаОснования
				Или ЗначениеЗаполнено(Мероприятие.НаименованиеДокументаОснования)
					И ЗначениеЗаполнено(НайденноеМероприятие.НаименованиеДокументаОснования)
					И Мероприятие.НаименованиеДокументаОснования <> НайденноеМероприятие.НаименованиеДокументаОснования Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			МероприятиеНайдено = Истина;
			Для Каждого ИмяРеквизита Из ИменаДополнительныхРеквизитов Цикл
				
				Если Мероприятие[ИмяРеквизита] <> НайденноеМероприятие[ИмяРеквизита] Тогда
					МероприятиеНайдено = Ложь;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если МероприятиеНайдено Тогда
				
				ЗаполнитьЗначенияСвойств(Мероприятие, НайденноеМероприятие);
				НайденноеМероприятие.Учтено = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(Мероприятие.ИдМероприятия) Тогда
			Мероприятие.ИдМероприятия = Новый УникальныйИдентификатор();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВосстановлениеПереданныхИдентификаторов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияТрудовойДеятельности.Регистратор КАК Регистратор,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельностиПрочие.ИдМероприятия КАК ИдМероприятияПрочие
		|ПОМЕСТИТЬ ВТМероприятияКЗаменеИдентификаторов
		|ИЗ
		|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
		|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПрочие КАК МероприятияТрудовойДеятельностиПрочие
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданныеПрочие
		|			ПО МероприятияТрудовойДеятельностиПрочие.ИдМероприятия = МероприятияТрудовойДеятельностиПереданныеПрочие.ИдМероприятия
		|				И (НЕ МероприятияТрудовойДеятельностиПереданныеПрочие.Отменено)
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданныеПрочиеОтмененные
		|			ПО МероприятияТрудовойДеятельностиПрочие.ИдМероприятия = МероприятияТрудовойДеятельностиПереданныеПрочиеОтмененные.ИдМероприятия
		|				И (МероприятияТрудовойДеятельностиПереданныеПрочиеОтмененные.Отменено)
		|		ПО МероприятияТрудовойДеятельности.ФизическоеЛицо = МероприятияТрудовойДеятельностиПрочие.ФизическоеЛицо
		|			И МероприятияТрудовойДеятельности.Организация = МероприятияТрудовойДеятельностиПрочие.Организация
		|			И МероприятияТрудовойДеятельности.Сотрудник = МероприятияТрудовойДеятельностиПрочие.Сотрудник
		|			И МероприятияТрудовойДеятельности.ДатаМероприятия = МероприятияТрудовойДеятельностиПрочие.ДатаМероприятия
		|			И МероприятияТрудовойДеятельности.ВидМероприятия = МероприятияТрудовойДеятельностиПрочие.ВидМероприятия
		|			И МероприятияТрудовойДеятельности.Сведения = МероприятияТрудовойДеятельностиПрочие.Сведения
		|			И МероприятияТрудовойДеятельности.Подразделение = МероприятияТрудовойДеятельностиПрочие.Подразделение
		|			И МероприятияТрудовойДеятельности.Должность = МероприятияТрудовойДеятельностиПрочие.Должность
		|			И МероприятияТрудовойДеятельности.РазрядКатегория = МероприятияТрудовойДеятельностиПрочие.РазрядКатегория
		|			И МероприятияТрудовойДеятельности.КодПоРееструДолжностей = МероприятияТрудовойДеятельностиПрочие.КодПоРееструДолжностей
		|			И МероприятияТрудовойДеятельности.ТрудоваяФункция = МероприятияТрудовойДеятельностиПрочие.ТрудоваяФункция
		|			И (ВЫБОР
		|				КОГДА МероприятияТрудовойДеятельности.НаименованиеДокументаОснования = """"
		|					ТОГДА ИСТИНА
		|				КОГДА МероприятияТрудовойДеятельностиПрочие.НаименованиеДокументаОснования = """"
		|					ТОГДА ИСТИНА
		|				КОГДА МероприятияТрудовойДеятельности.НаименованиеДокументаОснования = МероприятияТрудовойДеятельностиПрочие.НаименованиеДокументаОснования
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ)
		|			И МероприятияТрудовойДеятельности.ДатаДокументаОснования = МероприятияТрудовойДеятельностиПрочие.ДатаДокументаОснования
		|			И МероприятияТрудовойДеятельности.СерияДокументаОснования = МероприятияТрудовойДеятельностиПрочие.СерияДокументаОснования
		|			И МероприятияТрудовойДеятельности.НомерДокументаОснования = МероприятияТрудовойДеятельностиПрочие.НомерДокументаОснования
		|			И МероприятияТрудовойДеятельности.ОснованиеУвольнения = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольнения
		|			И МероприятияТрудовойДеятельности.ДатаС = МероприятияТрудовойДеятельностиПрочие.ДатаС
		|			И МероприятияТрудовойДеятельности.ДатаПо = МероприятияТрудовойДеятельностиПрочие.ДатаПо
		|			И МероприятияТрудовойДеятельности.ЯвляетсяСовместителем = МероприятияТрудовойДеятельностиПрочие.ЯвляетсяСовместителем
		|			И МероприятияТрудовойДеятельности.ПредставлениеДолжности = МероприятияТрудовойДеятельностиПрочие.ПредставлениеДолжности
		|			И МероприятияТрудовойДеятельности.ПредставлениеПодразделения = МероприятияТрудовойДеятельностиПрочие.ПредставлениеПодразделения
		|			И МероприятияТрудовойДеятельности.ОснованиеУвольненияТекстОснования = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияТекстОснования
		|			И МероприятияТрудовойДеятельности.ОснованиеУвольненияСтатья = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияСтатья
		|			И МероприятияТрудовойДеятельности.ОснованиеУвольненияЧасть = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияЧасть
		|			И МероприятияТрудовойДеятельности.ОснованиеУвольненияПункт = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияПункт
		|			И МероприятияТрудовойДеятельности.ОснованиеУвольненияПодПункт = МероприятияТрудовойДеятельностиПрочие.ОснованиеУвольненияПодПункт
		|			И МероприятияТрудовойДеятельности.ОписаниеДолжности = МероприятияТрудовойДеятельностиПрочие.ОписаниеДолжности
		|ГДЕ
		|	НЕ МероприятияТрудовойДеятельности.Регистратор В (&МассивОбновленных)
		|	И НЕ МероприятияТрудовойДеятельности.Отменено
		|	И МероприятияТрудовойДеятельностиПереданные.ИдМероприятия ЕСТЬ NULL
		|	И НЕ МероприятияТрудовойДеятельностиПрочие.ИдМероприятия ЕСТЬ NULL
		|	И МероприятияТрудовойДеятельностиПереданныеПрочиеОтмененные.ИдМероприятия ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	МероприятияКЗаменеИдентификаторов.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторыКОбновлению
		|ИЗ
		|	ВТМероприятияКЗаменеИдентификаторов КАК МероприятияКЗаменеИдентификаторов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегистраторыКОбновлению.Регистратор КАК Регистратор
		|ИЗ
		|	ВТРегистраторыКОбновлению КАК РегистраторыКОбновлению";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияКЗаменеИдентификаторов.Регистратор КАК Регистратор,
		|	МероприятияКЗаменеИдентификаторов.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияКЗаменеИдентификаторов.ИдМероприятияПрочие КАК ИдМероприятияПрочие
		|ИЗ
		|	ВТРегистраторыКОбновлению КАК РегистраторыКОбновлению
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМероприятияКЗаменеИдентификаторов КАК МероприятияКЗаменеИдентификаторов
		|		ПО РегистраторыКОбновлению.Регистратор = МероприятияКЗаменеИдентификаторов.Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
	
	Выборка = Запрос.Выполнить().Выбрать();;
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		МассивОбновленных.Добавить(Выборка.Регистратор);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.МероприятияТрудовойДеятельности.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.МероприятияТрудовойДеятельности.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		
		ТаблицаНабора = НаборЗаписей.Выгрузить();
		
		Пока Выборка.Следующий() Цикл
			
			СтрокиНабора = ТаблицаНабора.НайтиСтроки(Новый Структура("ИдМероприятия,Отменено", Выборка.ИдМероприятия, Ложь));
			Для Каждого СтрокаНабора Из СтрокиНабора Цикл
				
				СтрокаНабора.ИдМероприятия = Выборка.ИдМероприятияПрочие;
				
				НаборПрочих = РегистрыСведений.МероприятияТрудовойДеятельностиПрочие.СоздатьНаборЗаписей();
				НаборПрочих.Отбор.ФизическоеЛицо.Установить(СтрокаНабора.ФизическоеЛицо);
				НаборПрочих.Отбор.Организация.Установить(СтрокаНабора.Организация);
				НаборПрочих.Прочитать();
				
				ТаблицаНабораПрочих = НаборПрочих.Выгрузить();
				СтрокиНабораПрочих = ТаблицаНабораПрочих.НайтиСтроки(Новый Структура("ИдМероприятия", Выборка.ИдМероприятияПрочие));
				СтрокиСНовымИдМероприятия = ТаблицаНабораПрочих.НайтиСтроки(Новый Структура("ИдМероприятия", Выборка.ИдМероприятия));
				Для Каждого СтрокаНабораПрочих Из СтрокиНабораПрочих Цикл
					
					Если СтрокиСНовымИдМероприятия.Количество() > 0 Тогда
						ТаблицаНабораПрочих.Удалить(СтрокаНабораПрочих);
					Иначе
						СтрокаНабораПрочих.ИдМероприятия = Выборка.ИдМероприятия;
					КонецЕсли;
					
				КонецЦикла;
				
				НаборПрочих.Загрузить(ТаблицаНабораПрочих);
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборПрочих);
				
			КонецЦикла;
			
		КонецЦикла;
		
		НаборЗаписей.Загрузить(ТаблицаНабора);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
