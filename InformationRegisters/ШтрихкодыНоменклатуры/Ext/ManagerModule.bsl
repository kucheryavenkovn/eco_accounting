#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция вычисляет контрольный символ кода EAN
//
// Параметры:
//  Штрихкод     - штрихкод (без контрольной цифры)
//  Тип          - тип штрихкода: 13 - EAN13, 8 - EAN8
//
// Возвращаемое значение:
//  Контрольный символ штрихкода
//
Функция КонтрольныйСимволEAN(ШтрихКод, Тип) Экспорт

	Четн   = 0;
	Нечетн = 0;

	КоличествоИтераций = ?(Тип = 13, 6, 4);

	Для Индекс = 1 По КоличествоИтераций Цикл
		Если (Тип = 8) и (Индекс = КоличествоИтераций) Тогда
		Иначе
			Четн   = Четн   + Сред(ШтрихКод, 2 * Индекс, 1);
		КонецЕсли;
		Нечетн = Нечетн + Сред(ШтрихКод, 2 * Индекс - 1, 1);
	КонецЦикла;

	Если Тип = 13 Тогда
		Четн = Четн * 3;
	Иначе
		Нечетн = Нечетн * 3;
	КонецЕсли;

	КонтЦифра = 10 - (Четн + Нечетн) % 10;

	Возврат ?(КонтЦифра = 10, "0", Строка(КонтЦифра));

КонецФункции

Функция НоменклатураПоШтрихкоду(Знач Штрихкод, ТочноеСоответствие = Истина) Экспорт
	Если ПустаяСтрока(Штрихкод) Тогда
		Возврат НовыйТаблицаНоменклатурыПоШтрихкоду()
	КонецЕсли;
	
	МассивШтрихкодов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СокрЛП(Штрихкод));
	
	Возврат НоменклатураПоШтрихкодам(МассивШтрихкодов, ТочноеСоответствие);
КонецФункции

// Возвращает таблицу сведений о номенклатуре по переданным штрихкодам
// 
// Параметры:
//   СписокШтрихкодов - Массив - список штрикходов для которых надо найти номенклатуру
//   ТочноеСоответствие - Булево - ИСТИНА если нужно искать по точному соотвествию
// Возвращаемое значение:
// - ТаблицаЗначений
//   * Номенклатура         - СправочникСсылка.Номенклатура
//   * Штрихкод             - Строка - Штрихкод номенклатуры
//   * Представление        - Представление номенклатуры
//   * МаркируемаяПродукция - Булево - Признак маркируемой продукции
//   * ТабачнаяПродукция    - Булево - Признак табачной продукции
//   * ОбувнаяПродукция     - Булево - Признак обувной продукции
//   * АлкогольнаяПродукция - Булево - Признак алкогольной продукции

Функция НоменклатураПоШтрихкодам(СписокШтрихкодов, ТочноеСоответствие) Экспорт
	
	ТаблицаИсточник = Новый ТаблицаЗначений;
	ТаблицаИсточник.Колонки.Добавить("Штрихкод", ОбщегоНазначения.ОписаниеТипаСтрока(200));
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ТаблицаИсточник, СписокШтрихкодов, "Штрихкод");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаНоменклатура", ТаблицаИсточник);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаНоменклатура.Штрихкод КАК Штрихкод
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	&ТаблицаНоменклатура КАК ТаблицаНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	ИСТИНА КАК АлкогольнаяПродукция,
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.ВидПродукции.Маркируемый, ЛОЖЬ)) КАК Маркируемая
	|ПОМЕСТИТЬ ВТ_АлкогольнаяПродукция
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТ_Номенклатура.Штрихкод КАК ИсходныйКод,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура.Наименование КАК ПредставлениеНоменклатура,
	|	ЕСТЬNULL(СправочникНоменклатура.ТабачнаяПродукция, ЛОЖЬ) КАК ТабачнаяПродукция,
	|	ЕСТЬNULL(СправочникНоменклатура.ОбувнаяПродукция, ЛОЖЬ) КАК ОбувнаяПродукция,
	|	ЕСТЬNULL(СправочникНоменклатура.ЛегкаяПромышленность, ЛОЖЬ) КАК ЛегкаяПромышленность,
	|	ЕСТЬNULL(СправочникНоменклатура.МолочнаяПродукция, ЛОЖЬ) КАК МолочнаяПродукция,
	|	ЕСТЬNULL(СправочникНоменклатура.Шины, ЛОЖЬ) КАК Шины,
	|	ЕСТЬNULL(СправочникНоменклатура.Фотоаппараты, ЛОЖЬ) КАК Фотоаппараты,
	|	ЕСТЬNULL(СправочникНоменклатура.Духи, ЛОЖЬ) КАК Духи,
	|	ЕСТЬNULL(СправочникНоменклатура.Велосипеды, ЛОЖЬ) КАК Велосипеды,
	|	ЕСТЬNULL(СправочникНоменклатура.КреслаКоляски, ЛОЖЬ) КАК КреслаКоляски,
	|	ЕСТЬNULL(ВТ_АлкогольнаяПродукция.АлкогольнаяПродукция, ЛОЖЬ) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ВТ_АлкогольнаяПродукция.Маркируемая, ЛОЖЬ) КАК МаркируемаяАлкогольнаяПродукция
	|ИЗ
	|	ВТ_Номенклатура КАК ВТ_Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО (&Шаблон)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО (ШтрихкодыНоменклатуры.Номенклатура = СправочникНоменклатура.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АлкогольнаяПродукция КАК ВТ_АлкогольнаяПродукция
	|		ПО (ШтрихкодыНоменклатуры.Номенклатура = ВТ_АлкогольнаяПродукция.Номенклатура)
	|ГДЕ
	|	НЕ СправочникНоменклатура.Ссылка ЕСТЬ NULL";
	
	Если ТочноеСоответствие Тогда
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&Шаблон", "ШтрихкодыНоменклатуры.Штрихкод ПОДОБНО ВТ_Номенклатура.Штрихкод");
	Иначе
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&Шаблон", "ШтрихкодыНоменклатуры.Штрихкод ПОДОБНО ВТ_Номенклатура.Штрихкод + ""%""");
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	ШрифтВыделения         = Новый Шрифт(,,Истина);
	ЦветВыделения          = ЦветаСтиля.ЦветУспешногоПоиска;
	
	ТаблицаНоменклатурыПоШтрихкоду = НовыйТаблицаНоменклатурыПоШтрихкоду();
	Пока РезультатЗапроса.Следующий() Цикл
		ФорматВыделяемаяСтрока = Новый ФорматированнаяСтрока(РезультатЗапроса.ИсходныйКод, ШрифтВыделения, ЦветВыделения);
		
		ПредставлениеСтроки = Новый Массив;
		ПредставлениеСтроки.Добавить(ФорматВыделяемаяСтрока);
		ПредставлениеСтроки.Добавить(Новый ФорматированнаяСтрока(Прав(РезультатЗапроса.Штрихкод, СтрДлина(РезультатЗапроса.Штрихкод) - СтрДлина(РезультатЗапроса.ИсходныйКод))));
		ПредставлениеСтроки.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(" (%1)", РезультатЗапроса.ПредставлениеНоменклатура)));
		
		СтрокаНоменклатураПоШтрихкоду = ТаблицаНоменклатурыПоШтрихкоду.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаНоменклатураПоШтрихкоду, РезультатЗапроса);
		
		СтрокаНоменклатураПоШтрихкоду.Представление = Новый ФорматированнаяСтрока(ПредставлениеСтроки);
		
		СтрокаНоменклатураПоШтрихкоду.МаркируемаяПродукция = РезультатЗапроса.ТабачнаяПродукция 
			ИЛИ РезультатЗапроса.ОбувнаяПродукция 
			ИЛИ РезультатЗапроса.МаркируемаяАлкогольнаяПродукция;
	КонецЦикла;
	
	Возврат ТаблицаНоменклатурыПоШтрихкоду;
КонецФункции


// Функция осуществляет формирование штрихкода EAN13 для
// штучного товара
//
// Возвращаемое значение:
//  Строка
//
Функция СформироватьШтрихкодEAN13(Номенклатура) Экспорт

	ПрефиксШтучногоТовара = "0";
	ПрефиксВнутреннегоШтрихкода = "01";
	
	Код = Мин(ПоследнийКодШтучногоТовара(ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода, 13) + 1,
		МаксимальныйКодШтучногоТовара(13));

	НовыйШтрихкод = ПолучитьШтрихкодПоКоду(Код, ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода, 13);
	
	ЗаписьРегистра = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Штрихкод = НовыйШтрихкод;
	ЗаписьРегистра.Номенклатура = Номенклатура;
	ЗаписьРегистра.Записать(Истина);
	
	Возврат НовыйШтрихкод;
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйТаблицаНоменклатурыПоШтрихкоду()
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура",              Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры.Ресурсы.Номенклатура.Тип);
	ТаблицаНоменклатуры.Колонки.Добавить("Штрихкод",                  Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры.Измерения.Штрихкод.Тип);
	
	// Представление для формы выбора
	ТаблицаНоменклатуры.Колонки.Добавить("Представление",             Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("ФорматированнаяСтрока"))));
	
	// Представления для форм ГОСИС
	ТаблицаНоменклатуры.Колонки.Добавить("ПредставлениеНоменклатура", Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Строка"))));
	
	// Признак маркируемой продукции любого вида
	ТаблицаНоменклатуры.Колонки.Добавить("МаркируемаяПродукция",      Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	
	ТаблицаНоменклатуры.Колонки.Добавить("ТабачнаяПродукция",         Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("ОбувнаяПродукция",          Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("ЛегкаяПромышленность",      Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("МолочнаяПродукция",         Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("Шины",                      Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("Фотоаппараты",              Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("Духи",                      Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("Велосипеды",                Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	ТаблицаНоменклатуры.Колонки.Добавить("КреслаКоляски",             Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
		
	// Признак алкогольной продукции (как маркируемой так и нет)
	ТаблицаНоменклатуры.Колонки.Добавить("АлкогольнаяПродукция",      Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип("Булево"))));
	
	Возврат ТаблицаНоменклатуры;
КонецФункции

Функция ПоследнийКодШтучногоТовара(ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода, EAN) Экспорт
	
	Запрос = Новый Запрос;
	
	Если EAN = 8 Тогда
		Запрос.Текст = "ВЫБРАТЬ
		|	МАКСИМУМ(ПОДСТРОКА(ШтрихкодыНоменклатуры.Штрихкод, 2, 6)) КАК Код
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод ПОДОБНО &ШаблонШтрихкод";
		ШаблонШтрихкод = "2_______";
	ИначеЕсли EAN = 13 Тогда
		Запрос.Текст = "ВЫБРАТЬ
		|	МАКСИМУМ(ПОДСТРОКА(ШтрихкодыНоменклатуры.Штрихкод, 5, 8)) КАК Код
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод ПОДОБНО &ШаблонШтрихкод";
		ШаблонШтрихкод = СтрШаблон("2%1%2_________", ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода);
	Иначе
		ВызватьИсключение НСтр("ru = 'Недопустимая длина EAN. Ожидается 8 или 13.'");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ШаблонШтрихкод", ШаблонШтрихкод);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
	Результат = ОписаниеТипаЧисла.ПривестиЗначение(Выборка.Код);
	
	Возврат Результат;
	
КонецФункции 

Функция ПолучитьШтрихкодПоКоду(Код, ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода, EAN) Экспорт
	
	Если EAN = 8 Тогда
		Штрихкод = СтрШаблон("2%1", Формат(Код, "ЧЦ=6; ЧВН=; ЧГ="));
	ИначеЕсли EAN = 13 Тогда
		Штрихкод = СтрШаблон("2%1%2%3", ПрефиксШтучногоТовара, ПрефиксВнутреннегоШтрихкода, Формат(Код, "ЧЦ=8; ЧВН=; ЧГ="));
	Иначе
		ВызватьИсключение НСтр("ru = 'Недопустимая длина EAN. Ожидается 8 или 13.'")
	КонецЕсли;
	Результат = СтрШаблон("%1%2", Штрихкод, КонтрольныйСимволEAN(ШтрихКод, EAN));
	
	Возврат Результат;

КонецФункции

Функция МаксимальныйКодШтучногоТовара(EAN) Экспорт
	
	Если EAN = 13 Тогда
		Возврат 99999999;
	КонецЕсли;
	
	Если EAN = 8 Тогда
		Возврат 999999;
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'Недопустимая длина EAN. Ожидается 8 или 13.'");
	
КонецФункции


#КонецОбласти

#КонецЕсли