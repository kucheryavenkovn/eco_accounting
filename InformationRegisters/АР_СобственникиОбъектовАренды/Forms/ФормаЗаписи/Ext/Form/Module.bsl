
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если ЗначениеЗаполнено(Запись.Собственник) Тогда
		ЗаполнитьСобственников(Запись.Собственник);
	КонецЕсли;
	
	Если Параметры.Ключ.Пустой() Тогда 
		Запись.Собственник = "";
		Запись.Период      = ТекущаяДата();
	КонецЕсли;
	
	ПризнакОдинСобственник = Истина;
	Если Собственники.Количество() = 0 Тогда 
		НоваяСтрока = Собственники.Добавить();
		НоваяСтрока.Доля = 100;
	ИначеЕсли Собственники.Количество() > 1 Тогда
		ПризнакОдинСобственник = Ложь;	
	КонецЕсли;
	
	УстановитьВидимостьСтраниц();
			
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если ПризнакОдинСобственник Тогда
		Если НЕ ЗначениеЗаполнено(Собственники[0].Собственник) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Поле ""Собственник"" не заполнено", , "Собственник", "Собственник", Отказ);
		КонецЕсли;
	Иначе
		НомерСтроки = 0;
		Для Каждого СтрокаТаблицы из Собственники Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Собственник) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнена колонка ""Собственник"" в строке " + (НомерСтроки + 1) + " списка ""Собственники""'"), , "Собственники[" + Формат(НомерСтроки, "ЧГ=0") + "].Собственник", "Собственники", Отказ);
			КонецЕсли;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Собственники.Итог("Доля") > 100 и НЕ ПризнакОдинСобственник Тогда
		ТекстСообщения = НСтр("ru='Сумма долей превышает 100%!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Собственники", "Элементы", Отказ);
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.Собственник) Тогда
		ТекущийОбъект.Собственник =	Справочники.АР_СобственникиОбъектовНедвижимости.ПолучитьСсылку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьСобственника");
	
	Если ЗначениеЗаполнено(Запись.Собственник) Тогда
		ЗаполнитьСобственников(Запись.Собственник);
	КонецЕсли;
	
	ПризнакОдинСобственник = ?(Собственники.Количество() > 1, Ложь, Истина);
	
	УстановитьВидимостьСтраниц();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбработатьПерезаполнениеСобственников(ТекущийОбъект);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПризнакОдинСобственникПриИзменении(Элемент)
	
	Если Собственники.Количество() > 1 и ПризнакОдинСобственник Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПризнакОдинСобственникПослеЗакрытияВопроса", ЭтаФорма);
		ТекстВопроса = НСтр("ru='Все строки кроме первой будут удалены. Продолжить?'"); 
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);

		Возврат;
	КонецЕсли;
	
	ОбработатьСтрокиТаблицыСобственники();
	УстановитьВидимостьСтраниц();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПринципалаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Собственники.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", Собственники[0].Собственник));
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент, ЭтаФорма,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПринципалаПриИзменении(Элемент)
	
	Если НЕ Собственники.Количество() = 1 Тогда 
		Возврат;
	КонецЕсли;
		
	Если НЕ ДоговорСКомитентом(Собственники[0].АгентскийДоговор) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для собственника выбран договор, не являющийся договором с принципалом",, "АгентскийДоговор", "");	
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСобственники

&НаКлиенте
Процедура СобственникиДоговорПринципалаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Собственники.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		
	Если НЕ ДоговорСКомитентом(ТекущаяСтрока.АгентскийДоговор) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для собственника выбран договор, не являющийся договором с принципалом",, "Собственники[" + Формат(Элементы.Собственники.ТекущаяСтрока, "ЧГ=0") + "].АгентскийДоговор", "");	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСобственников(сСобственник)
	ДанныеОСобственниках = ПолучитьСписокСобственников(сСобственник);
	Собственники.Загрузить(ДанныеОСобственниках);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокСобственников(сСобственник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Ссылка КАК Ссылка,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.НомерСтроки КАК НомерСтроки,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Собственник КАК Собственник,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Доля КАК Доля,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.НомерЗаписиВЕГРП КАК НомерЗаписиВЕГРП,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.ДатаЗаписиВЕГРП КАК ДатаЗаписиВЕГРП,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскаяСхема КАК АгентскаяСхема,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскийДоговор КАК АгентскийДоговор
		|ИЗ
		|	Справочник.АР_СобственникиОбъектовНедвижимости.Собственники КАК АР_СобственникиОбъектовНедвижимостиСобственники
		|ГДЕ
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Ссылка = &Собственник";
	
	Запрос.УстановитьПараметр("Собственник", сСобственник);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗ = РезультатЗапроса.Выгрузить();
	
	Возврат ТЗ;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьСтраниц()
	
	Элементы.Страницы.ТекущаяСтраница = ?(ПризнакОдинСобственник, Элементы.СтраницаСтрока, Элементы.СтраницаТаблица);
	 
КонецПроцедуры

&НаСервере
Процедура ОбработатьСтрокиТаблицыСобственники()
	
	Если ПризнакОдинСобственник Тогда 
		КоличествоСтрок = Собственники.Количество();
		
		Пока КоличествоСтрок > 1 Цикл
			Собственники.Удалить(Собственники[КоличествоСтрок - 1]);
			КоличествоСтрок = КоличествоСтрок - 1;
		КонецЦикла;
		
		Если Собственники.Количество() = 0 Тогда 
			НоваяСтрока = Собственники.Добавить();
		КонецЕсли;
		
		Если Собственники.Количество() = 1 Тогда 
			Собственники[0].Доля = 100;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакОдинСобственникПослеЗакрытияВопроса(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ПризнакОдинСобственник = Ложь;
		Возврат;
	КонецЕсли;

	ОбработатьСтрокиТаблицыСобственники();
	УстановитьВидимостьСтраниц();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПерезаполнениеСобственников(ТекущийОбъект)	
	
	СправочникОбъект = ТекущийОбъект.Собственник.ПолучитьОбъект();
	Если СправочникОбъект = Неопределено Тогда 
		СправочникОбъект = Справочники.АР_СобственникиОбъектовНедвижимости.СоздатьЭлемент();
		СправочникОбъект.УстановитьСсылкуНового(ТекущийОбъект.Собственник);
	КонецЕсли;
		
	СправочникОбъект.Наименование = "Собственники объекта недвижимости " + ?(ЗначениеЗаполнено(ТекущийОбъект.ОбъектАренды), ТекущийОбъект.ОбъектАренды, "<все объекты>") + " на " + Формат(ТекущийОбъект.Период, "ДФ=dd.MM.yyyy");
	СправочникОбъект.Собственники.Загрузить(Собственники.Выгрузить());
	СправочникОбъект.Записать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоговорСКомитентом(Договор)

	ДоговорСКомитентом = (ЗначениеЗаполнено(Договор) И Договор.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);	
	
	Возврат ДоговорСКомитентом;
	
КонецФункции

#КонецОбласти




