// Форма предназначена для редактирования настроек в режиме интеграции с банком.
// В полном интерфейсе настройки находятся в общей форме НалогиИОтчеты.

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	
	Если Организация.Пустая() Тогда
		Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	// Запрет изменения элементов, если у пользователя нет прав настройки антикризисных мер
	ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов);
	Элементы.ДеятельностьОтнесенаКПострадавшимОтКоронавируса.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.МерыУстойчивогоРазвития.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.НеРаботаетВНерабочиеДни.ТолькоПросмотр = ТолькоПросмотр;
	
	// Настройки антикризисных мер
	МенеджерЗаписи = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.Прочитать();
	МерыУстойчивогоРазвития = МенеджерЗаписи.МерыУстойчивогоРазвития;
	НеРаботаетВНерабочиеДни = Не МенеджерЗаписи.РаботаетВНерабочиеДни;
	ДеятельностьОтнесенаКПострадавшимОтКоронавируса = МенеджерЗаписи.ДеятельностьОтнесенаКПострадавшимОтКоронавируса;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДеятельностьОтнесенаКПострадавшимОтКоронавирусаПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ОповеститьОбИзмененииНастройкиПродленияСроковНалоговОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура МерыУстойчивогоРазвитияПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ОповеститьОбИзмененииНастройкиПродленияСроковНалоговОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура НеРаботаетВНерабочиеДниПриИзменении(Элемент)
	
	ЗаписатьИзмененияАнтикризис();
	ОповеститьОбИзмененииНастройкиПродленияСроковНалоговОтчетов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверьтеДеятельностьОтнесенаКПострадавшимОтКоронавируса(Команда)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОсновнойКодОКВЭД = ОсновнойКодОКВЭД(Организация);
	Иначе
		ОсновнойКодОКВЭД = "";
	КонецЕсли;
	
	ОбщегоНазначенияБПКлиент.ОткрытьПроверкуДеятельностиНаКоронавирус(ОсновнойКодОКВЭД);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьИзмененияАнтикризис()
	
	МенеджерЗаписи = РегистрыСведений.НастройкиПродленияСроковНалоговОтчетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	
	МенеджерЗаписи.Прочитать(); // чтобы не затереть настройки, которые здесь не изменяются
	Если Не МенеджерЗаписи.Выбран() Тогда
		// Если запись не существует, то поле Организация сброшено
		МенеджерЗаписи.Организация = Организация;
	КонецЕсли;
	
	МенеджерЗаписи.МерыУстойчивогоРазвития = МерыУстойчивогоРазвития;
	МенеджерЗаписи.РаботаетВНерабочиеДни = Не НеРаботаетВНерабочиеДни;
	МенеджерЗаписи.ДеятельностьОтнесенаКПострадавшимОтКоронавируса = ДеятельностьОтнесенаКПострадавшимОтКоронавируса;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииНастройкиПродленияСроковНалоговОтчетов()
	
	// Используется для разных настроек, связанных с антикризисными мерами:
	// - изменение сроков
	// - освобождение от налогов
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Организация);
	
	Оповестить("Запись_НастройкиПродленияСроковНалоговОтчетов", ПараметрыОповещения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновнойКодОКВЭД(Знач Организация)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОсновнойКодОКВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "КодОКВЭД2");
	КонецЕсли;
	
	Возврат ОсновнойКодОКВЭД;
	
КонецФункции

#КонецОбласти