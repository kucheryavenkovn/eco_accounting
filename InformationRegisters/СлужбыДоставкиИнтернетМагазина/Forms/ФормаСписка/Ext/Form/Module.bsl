#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиПодключения = Параметры.НастройкиПодключения;
	Организация = Параметры.Организация;
	
	Элементы.ФормаЗагрузитьИзИнтернетМагазина.Видимость = ЗначениеЗаполнено(НастройкиПодключения)
		И (НастройкиПодключения.CMSИнтернетМагазина = Перечисления.CMSИнтернетМагазина.Bitrix
			ИЛИ НастройкиПодключения.CMSИнтернетМагазина = Перечисления.CMSИнтернетМагазина.UMI);
		
	УстановитьУсловноеОФормление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		Отказ = Истина;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Организация", Организация));
		ОткрытьФорму("РегистрСведений.СлужбыДоставкиИнтернетМагазина.ФормаЗаписи", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗагрузитьИзИнтернетМагазина(Команда)
	ОбменСИнтернетМагазиномВызовСервера.ЗаполнитьСпособыДоставкиИнтернетМагазина(НастройкиПодключения, Организация);
	Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура УстановитьУсловноеОФормление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СпособДоставки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.СпособДоставки", ВидСравненияКомпоновкиДанных.НеЗаполнено);	
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

#КонецОбласти


