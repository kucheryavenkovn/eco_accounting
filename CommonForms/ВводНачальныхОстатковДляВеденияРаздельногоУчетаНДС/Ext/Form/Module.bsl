#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Организация = Параметры.Организация;
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
	
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаНДС)Тогда
		ДоступнаАктуализацияОстатков = Истина;
	Иначе
		ДоступнаАктуализацияОстатков = Ложь;
		ЭтаФорма.Элементы.НеАктуальныеОстатки.Заголовок = НСтр(
		"ru = 'Данные остатков по раздельному учету неактуальны. Для актуализации остатков обратитесь к администратору'"); 
	КонецЕсли;
	
	ЭтаФорма.Элементы.ФормаАктуализироватьОстатки.Доступность = ДоступнаАктуализацияОстатков;
		
	ПараметрыРУ = УчетНДСРаздельныйФормированиеНачальныхОстатков.ДатаОстатков(Организация);
	Если ПараметрыРУ.ВедетсяРаздельныйУчетНаСчете19 = Истина Тогда
		ДатаВключенияРУ = ПараметрыРУ.Дата;
	КонецЕсли;
	
	
	ЕстьПодразделения = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();
	ЭтаФорма.Элементы.СписокДокументовПодразделениеОрганизации.Видимость = ЕстьПодразделения;
	
	НужнаАктуализация = РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.НеобходимоАктуализироватьОстаткиПоРаздельномуУчету(Организация,
																															ДатаВключенияРУ);
	УжеЗаполнялисьОстатки = РегистрыСведений.ВыполнениеРегламентныхОперацийНДС.УжеЗаполнялисьОстаткиПоРУ(Организация, ДатаВключенияРУ);
	Элементы.ГруппаБаннер.Видимость = НужнаАктуализация И УжеЗаполнялисьОстатки;
	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбрабротка = Ложь;
	
	ОтборДокумента = Новый Структура ("Ключ", Элемент.ТекущиеДанные.ДокументОперации);
	Если ТипЗнч(Элемент.ТекущиеДанные.ДокументОперации) = Тип("ДокументСсылка.ВводНачальныхОстатков") Тогда
		ОткрытьФорму("Документ.ВводНачальныхОстатков.ФормаОбъекта",ОтборДокумента, ,ЭтаФорма.УникальныйИдентификатор);
	ИначеЕсли ТипЗнч(Элемент.ТекущиеДанные.ДокументОперации) = Тип("ДокументСсылка.ОперацияБух") Тогда 
		ОткрытьФорму("Документ.ОперацияБух.ФормаОбъекта", ОтборДокумента, ,ЭтаФорма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура АктуализироватьОстатки(Команда)
	
	Результат = ВыполнитьОбработкиПоПереходуНаРаздельныйУчет(Организация, ДатаВключенияРУ);
	
	Если Результат.Статус = "Выполнено" Тогда
		
		Элементы.СписокДокументов.Обновить();
		Элементы.ГруппаБаннер.Видимость = Ложь;
		Оповестить("ОстаткиАктуализированы");
		
	Иначе
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("КнопкаВыполнитьЗавершение", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат,
						ОповещениеОЗавершении, ПараметрыОжидания);

	КонецЕсли;

 КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат)
		Или Не Результат.Свойство("Статус") Тогда
		
		Элементы.СписокДокументов.Обновить();

		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СохранитьИнформациюОбОшибкеВЖурналРегистрации(Результат.ПодробноеПредставлениеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Отменено" Тогда
		
		Элементы.СписокДокументов.Обновить();
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		Элементы.ГруппаБаннер.Видимость = Ложь;
		Элементы.СписокДокументов.Обновить();
		Оповестить("ОстаткиАктуализированы");

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьОбработкиПоПереходуНаРаздельныйУчет(Организация, Дата)
	
	УчетНДСРаздельныйФормированиеНачальныхОстатков.ОтменитьФормированиеНачальныхОстатковДляВеденияРаздельногоУчетаНДС(
																									Организация, Дата );

	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Организация", Организация);
	ПараметрыПроцедуры.Вставить("Период", Дата);
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение остатков по раздельному учету'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"УчетНДСРаздельныйФормированиеНачальныхОстатков.СформироватьОстатки",
		ПараметрыПроцедуры, ПараметрыВыполнения);

КонецФункции

&НаСервере
Процедура СохранитьИнформациюОбОшибкеВЖурналРегистрации(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Формирование остатков по раздельному учету НДС'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);

КонецПроцедуры

#КонецОбласти


