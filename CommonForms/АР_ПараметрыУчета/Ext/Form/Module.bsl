
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Значения реквизитов формы
	НастройкаСроковОплаты = АР_ОбщиеПроцедуры.ПолучитьНастройкуСроковОплатыПоУмолчанию();
	АР_НастройкаСроковОплаты = ?(ЗначениеЗаполнено(НастройкаСроковОплаты), "Открыть настройки", "Задать настройки");
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "АР_ИзмененыНастройкиСроковОплаты" Тогда
		ПрочитатьНастройкиСроковОплатыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АР_ИспользоватьСреднийМесяцПриИзменении(Элемент)
	
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура АР_ВестиУчетТарифицируемыхУслугПоСчетчикамПриИзменении(Элемент)
	
	Если Не НаборКонстант.АР_ВестиУчетТарифицируемыхУслугПоСчетчикам Тогда
		НаборКонстант.АР_ИспользоватьНесколькоСчетчиковДляУслуги = Ложь;
	КонецЕсли;
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура АР_СреднийМесяцПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_СохранятьДокументWordПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ИспользоватьГрафикИзмененияСтавокВДоговорахАрендыПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ИспользоватьНесколькоСчетчиковДляУслугиПриИзменении(Элемент)
	
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
	ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	
КонецПроцедуры

&НаКлиенте
Процедура АР_ПриРасчетеПениСрокОплатыРабочийДеньПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ОбрабатыватьШаблоныИПланировкиНаКлиентеПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ТипОбъектаАрендыПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ПериодНачисленияАренднойПлатыПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ОсновнаяУслугаПоАрендеПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ОсновнаяУслугаДляНачисленияДепозитаПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ОсновнойСпособНачисленияАрендойПлатыПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ОсновнойПроизводственныйКалендарьАрендыПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_СпособУчетаНДСПриНачисленииПенейПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_ТипЦенПлановойСебестоимостиМатериаловПриИзменении(Элемент)
	ПриИзмененииРеквизитаНаКлиенте(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АР_НастройкаСроковОплатыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.АР_НастройкаСроковОплатыПоУмолчанию",, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы      = Форма.Элементы;
	НаборКонстант = Форма.НаборКонстант;
	
	Форма.Элементы.АР_СреднийМесяц.Доступность = Форма.НаборКонстант.АР_ИспользоватьСреднийМесяц;
	Форма.Элементы.АР_ИспользоватьНесколькоСчетчиковДляУслуги.Доступность = Форма.НаборКонстант.АР_ВестиУчетТарифицируемыхУслугПоСчетчикам;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРеквизитаНаКлиенте(Элемент)
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
	Оповестить("ИзменениеНастроекПараметровУчета");
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат "";
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 14)) = НРег("НаборКонстант.") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 15);
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = НаборКонстант[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
		
		Если КонстантаИмя = "АР_ВестиУчетТарифицируемыхУслугПоСчетчикам" Тогда
			Если НЕ КонстантаЗначение Тогда
				Константы.АР_ИспользоватьНесколькоСчетчиковДляУслуги.Установить(Ложь);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаКлиенте
Процедура Декорация3Нажатие(Элемент)
	
	ОткрытьФорму("Справочник.АР_НапоминанияУведомленияПоСобытиям.ФормаСписка");
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиСроковОплатыНаСервере()
	
	НастройкаСроковОплаты = АР_ОбщиеПроцедуры.ПолучитьНастройкуСроковОплатыПоУмолчанию();
	АР_НастройкаСроковОплаты = ?(НастройкаСроковОплаты <> Неопределено, "Открыть настройки", "Задать настройки");
	
КонецПроцедуры

#КонецОбласти