#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
	ПравоРедактированияНастроек = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиСистемыНалогообложения);
	Элементы.ПредупреждениеЕНВДУказатьДействующуюСистему.Видимость = ПравоРедактированияНастроек;
	Элементы.ПредупреждениеЕНВДОбратитесьКСотруднику.Видимость = НЕ ПравоРедактированияНастроек;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиСистемыНалогообложения" Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ОрганизацииСистемаНалогообложенияПредставление" Тогда
		
		ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
		ПараметрыОткрытия.Заголовок = НСтр("ru = 'Настройки налогов и отчетов'");
		ПараметрыОткрытия.ИмяФормы = "ОбщаяФорма.НалогиИОтчеты";
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Элемент.ТекущиеДанные.Организация);
		
		ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьФормуНаСервере()
	
	Организации.Очистить();
	
	ОрганизацииПрименяющиеТолькоЕНВД = УчетЕНВД.ОрганизацииПрименяющиеТолькоЕНВД(ОбщегоНазначенияБП.ТекущаяДатаНаСервере());
	
	Если ОрганизацииПрименяющиеТолькоЕНВД.Количество() = 0 Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОК;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОрганизации;
	КонецЕсли;
	
	Для каждого СтрокаВыборки Из ОрганизацииПрименяющиеТолькоЕНВД Цикл
		НоваяСтрока = Организации.Добавить();
		НоваяСтрока.Организация = СтрокаВыборки.Организация;
		НоваяСтрока.СистемаНалогообложенияПредставление = НСтр("ru = 'Только ЕНВД (до конца 2020 года)'");
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
