
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	УстановитьВидимостьДекорации1СОтчетность();
	
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Запланируем обновление на завтра
	ПодключитьОбработчикОжидания("Подключаемый_ЕжедневноеОбновление", ИнтервалОбновленияДанных(ТекущаяДата()), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеНастроекЭДООрганизации" ИЛИ ИмяСобытия = "ОбновитьУчетнуюЗапись" Тогда
		УстановитьВидимостьДекорации1СОтчетность();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОтчетностьПоЭТК" Тогда
		СтандартнаяОбработка = Ложь;
		Открыть1СОтчетность();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "1СОтчетность" Тогда
		СтандартнаяОбработка = Ложь;
		ЗаявлениеНаПодключениеОткрытьМастер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	Период = ТекущаяДатаСеанса();
	
	ПредставлениеТекущейДаты = Формат(Период, "Л=ru_RU; ДФ='d MMMM, dddd'");
	
	Заголовок = СтрШаблон(НСтр("ru = 'Сегодня: %1'"), ПредставлениеТекущейДаты);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЕжедневноеОбновление()
	
	ОбновитьЗаголовокФормы();
	
	// Запланируем обновление на завтра
	ПодключитьОбработчикОжидания("Подключаемый_ЕжедневноеОбновление", ИнтервалОбновленияДанных(ТекущаяДата()), Истина);
	
КонецПроцедуры

&НаКлиенте
Функция ИнтервалОбновленияДанных(ДатаОбновленияДанных)
	
	Возврат (КонецДня(ДатаОбновленияДанных) + 1) - ДатаОбновленияДанных + 59;
	
КонецФункции

&НаКлиенте
Процедура Открыть1СОтчетность()
	
	ОткрытьФорму("ОбщаяФорма.РегламентированнаяОтчетность",
		,
		,
		"1С-Отчетность",
		);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеНаПодключениеОткрытьМастер()
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(
		Организация, ЭтотОбъект, , , , , Истина);
	
КонецПроцедуры

#Область ПоддержкаСервиса

&НаКлиенте
Процедура ОбращенияВПоддержкуНажатие(Элемент)
	
	ИнформационныйЦентрКлиент.ОткрытьОбращенияВСлужбуПоддержки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦентрИдейНажатие(Элемент)
	
	ИнформационныйЦентрКлиент.ОткрытьЦентрИдей();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДекорации1СОтчетность()
	
	ЭлектронныйДокументооборотДоступен = ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(,,Истина);
	Элементы.ДекорацияОтчетностьВПенсионныйФонд1СОтчетность.Видимость = НЕ ЭлектронныйДокументооборотДоступен;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
