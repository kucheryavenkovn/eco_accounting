#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресСпискаВидовДеятельности) Тогда
		ЗаполнитьВидыДеятельности(Параметры.АдресСпискаВидовДеятельности);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.КодВидаДеятельности) Тогда
		УстановитьТекущуюСтроку(Параметры.КодВидаДеятельности);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НайтиОчистка(Элемент, СтандартнаяОбработка)
	
	Элементы.ВидыДеятельности.ОтборСтрок = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ПустаяСтрока(Текст) Тогда
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Текст) Тогда
			Фильтр = Новый ФиксированнаяСтруктура("Код", Текст);
		Иначе
			Фильтр = Новый ФиксированнаяСтруктура("Наименование", Текст);
		КонецЕсли;
		Элементы.ВидыДеятельности.ОтборСтрок = Фильтр;
	Иначе
		Элементы.ВидыДеятельности.ОтборСтрок = Неопределено;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВидыДеятельности

&НаКлиенте
Процедура ВидыДеятельностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьВидДеятельности();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьВидДеятельности();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВидыДеятельности(АдресСпискаВидовДеятельности)
	
	ВидыДеятельности.Очистить();
	
	СписокВидовДеятельности = ПолучитьИзВременногоХранилища(АдресСпискаВидовДеятельности);
	Если СписокВидовДеятельности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДлинаКода = 6;
	
	Если ТипЗнч(СписокВидовДеятельности) = Тип("ТаблицаЗначений") Тогда
		Для Каждого РеквизитыВидаДеятельности Из СписокВидовДеятельности Цикл
			НоваяСтрока = ВидыДеятельности.Добавить();
			НоваяСтрока.Код = Лев(СокрЛ(РеквизитыВидаДеятельности.Код), ДлинаКода);
			НоваяСтрока.Наименование = РеквизитыВидаДеятельности.Наименование;
			НоваяСтрока.ПолныйКод = РеквизитыВидаДеятельности.Код;
		КонецЦикла;
	ИначеЕсли ТипЗнч(СписокВидовДеятельности) = Тип("СписокЗначений") Тогда
		Для Каждого РеквизитыВидаДеятельности Из СписокВидовДеятельности Цикл
			НоваяСтрока = ВидыДеятельности.Добавить();
			НоваяСтрока.Код = Лев(СокрЛ(РеквизитыВидаДеятельности.Значение), ДлинаКода);
			НоваяСтрока.Наименование = РеквизитыВидаДеятельности.Представление;
			НоваяСтрока.ПолныйКод = РеквизитыВидаДеятельности.Значение;
		КонецЦикла;
	ИначеЕсли ТипЗнч(СписокВидовДеятельности) = Тип("Массив") Тогда
		Для Каждого РеквизитыВидаДеятельности Из СписокВидовДеятельности Цикл
			НоваяСтрока = ВидыДеятельности.Добавить();
			НоваяСтрока.Код = Лев(СокрЛ(РеквизитыВидаДеятельности.Код), ДлинаКода);
			НоваяСтрока.Наименование = РеквизитыВидаДеятельности.Наименование;
			НоваяСтрока.ПолныйКод = РеквизитыВидаДеятельности.Код;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтроку(КодВидаДеятельности)
	
	НайденныеСтроки = ВидыДеятельности.НайтиСтроки(Новый Структура("ПолныйКод", КодВидаДеятельности));
	Если НайденныеСтроки.Количество() <> 0 Тогда
		Элементы.ВидыДеятельности.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидДеятельности()

	ВыбраннаяСтрока = Элементы.ВидыДеятельности.ТекущиеДанные;
	Если ВыбраннаяСтрока <> Неопределено И Не ПустаяСтрока(ВыбраннаяСтрока.ПолныйКод) Тогда
		ОповеститьОВыборе(ВыбраннаяСтрока.ПолныйКод);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти