
&НаКлиенте
Процедура Заполнить(Команда)
	
	СтруктураПараметрвов = Новый Структура("Реквизит, Значение", Реквизит, ЗначениеКолонки);
	Закрыть(СтруктураПараметрвов);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ИмяДокумента", ИмяДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ЭтоПостояннаяЧасть", ЭтоПостояннаяЧасть);
	
	СписокВыбора = Элементы.Колонка.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если ИмяДокумента = "АР_ЗаключениеДоговораАренды"
		ИЛИ Параметры.ИмяДокумента = "АР_ДополнительноеСоглашение" Тогда
		СписокВыбора.Добавить("Услуга", "Услуга");
		СписокВыбора.Добавить("СпособНачисления", "Способ начисления");
		СписокВыбора.Добавить("Ставка", "Ставка");
		СписокВыбора.Добавить("ДатаНачалаСрокаАренды", "Дата начала");
		СписокВыбора.Добавить("ДатаОкончанияСрокаАренды", "Дата окончания");
	ИначеЕсли ИмяДокумента = "АР_ИзменениеСтатусовОбъектов" Тогда
		СписокВыбора.Добавить("Статус", "Статус");
		СписокВыбора.Добавить("ДатаНачала", "Дата начала");
		СписокВыбора.Добавить("ДатаОкончания", "Дата окончания");
	КонецЕсли;
	
	ЭлементСписока = СписокВыбора.Получить(0);
	Реквизит = ЭлементСписока.Значение;
	Колонка = ЭлементСписока.Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Реквизит) Тогда
		КолонкаОбработкаВыбора(Элементы.Колонка, Реквизит, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КолонкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = "Услуга" Тогда
		Элементы.ЗначениеКолонки.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ИначеЕсли ВыбранноеЗначение = "СпособНачисления" Тогда
		Элементы.ЗначениеКолонки.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.АР_СпособыНачисленияАренднойПлаты");
	ИначеЕсли ВыбранноеЗначение = "Статус" Тогда
		Элементы.ЗначениеКолонки.ОграничениеТипа = Новый ОписаниеТипов("ПеречислениеСсылка.АР_СтатусыОбъектовАренды");
	ИначеЕсли ВыбранноеЗначение = "Ставка" Тогда
		Если ЭтоПостояннаяЧасть Тогда
			КвЧисла = Новый КвалификаторыЧисла(15, 2);
		Иначе
			КвЧисла = Новый КвалификаторыЧисла(20, 7);
		КонецЕсли;
		Элементы.ЗначениеКолонки.ОграничениеТипа = Новый ОписаниеТипов("Число", КвЧисла);
	ИначеЕсли Найти(ВыбранноеЗначение, "Дата") > 0 Тогда
		Элементы.ЗначениеКолонки.ОграничениеТипа = Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата));
	КонецЕсли;
	ЗначениеКолонки = Элементы.ЗначениеКолонки.ОграничениеТипа.ПривестиЗначение(ЗначениеКолонки);

	ЭлементСписка = Элементы.Колонка.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Реквизит = ВыбранноеЗначение;
	Колонка = ЭлементСписка.Представление;
	
	УстановитьПараметрыВыбора();

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбора()
	
	Если Реквизит = "Услуга" Тогда
		НовыеПараметры = Новый Массив();
		НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.Услуга", Истина));
		НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.АР_ПостояннаяЧастьОплаты", ЭтоПостояннаяЧасть));
	Иначе	
		НовыеПараметры = Новый Массив();
	КонецЕсли;
	Элементы.ЗначениеКолонки.ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметры);
	
КонецПроцедуры