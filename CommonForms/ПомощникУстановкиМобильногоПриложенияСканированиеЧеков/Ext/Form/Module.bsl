#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяПрограммы = ОбщегоНазначенияБП.ПредставлениеПрограммыВинительныйПадеж();
	
	Элементы.ДекорацияОписание2.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Элементы.ДекорацияОписание2.Заголовок,
		ИмяПрограммы);
		
	АдресСтраницы = "https://apps.apple.com/ru/app/id1513613578";
	Если НЕ СтраницаОпубликована(АдресСтраницы) Тогда
		Элементы.ДекорацияШаг1Заголовок.Заголовок = СтрЗаменить(
			Элементы.ДекорацияШаг1Заголовок.Заголовок, 
			НСтр("ru=' или App Store'"),
			"");
		Элементы.ГруппаПриложениеВAppStoreФреш.Видимость = Ложь;
	КонецЕсли;
		
	Элементы.ПодключениеСотрудниковОписание.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Элементы.ПодключениеСотрудниковОписание.Заголовок,
		ИмяПрограммы);
		
	Пользователь = Пользователи.ТекущийПользователь();
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("КодДоступа", "");
	ПараметрыТекста.Вставить("Логин",      "");
	
	Если Пользователь = Пользователи.СсылкаНеуказанногоПользователя() Тогда
		ПараметрыТекста.Логин = НСтр("ru = 'адрес электронной почты'");
	Иначе
		ПараметрыТекста.Логин = СтрШаблон(НСтр("ru = 'логин <b>%1</b> и пароль'"), ИмяПользователя());
	КонецЕсли;
	
	МожноПодключатьПользователей = Ложь;
	ШаблонТекста = НСтр("ru = 'Для входа в мобильное приложение используйте свой [Логин]'");
	
	КодДоступа = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.КодДоступа();
	
	Если КодДоступа <> Неопределено Тогда
		
		МожноПодключатьПользователей = Истина;
		ШаблонТекста = НСтр(
			"ru = 'Для входа в мобильное приложение используйте
             |- код доступа <a href=''КодДоступа''>[КодДоступа]</a>
             |или
             |- свой [Логин]'");
		ПараметрыТекста.КодДоступа = КодДоступа.Представление;
		
	КонецЕсли;
		
	СтрокаСТегами = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста);
	Элементы.ДекорацияШаг2Заголовок.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(СтрокаСТегами);
	
	Элементы.ПодключениеПодотчетныхЛиц.Видимость = МожноПодключатьПользователей;
	
	Элементы.ГруппаБаннерУсловияИспользования.Видимость = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ВРег("ПомощникУстановкиМобильногоПриложенияСканированиеЧеков"),
		ВРег("УсловияИспользования"),
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияAndroidНажатие(Элемент)
	
	АдресСтраницы = "https://play.google.com/store/apps/details?id=com.e1c.ReceiptScanner";
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияiOSНажатие(Элемент)
	
	АдресСтраницы = "https://apps.apple.com/ru/app/id1513613578";
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияШаг2ЗаголовокОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "КодДоступа" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("РегистрСведений.МобильноеПриложениеСканированиеЧеков.Форма.КодДоступа", , ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключениеСотрудниковНастройкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Пользователи" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФорму("РегистрСведений.МобильноеПриложениеСканированиеЧеков.Форма.Пользователи", , ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БаннерУсловияИспользованияЗакрытьНажатие(Элемент)
	
	Элементы.ГруппаБаннерУсловияИспользования.Видимость = Ложь;
	СохранитьОтключениеВидимостиБаннераУсловияИспользования();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьОтключениеВидимостиБаннераУсловияИспользования()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ВРег("ПомощникУстановкиМобильногоПриложенияСканированиеЧеков"), 
		ВРег("УсловияИспользования"), 
		Ложь);
	
КонецПроцедуры

&НаСервере
Функция СтраницаОпубликована(АдресСтраницы)
	
	URI = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСтраницы);
	HTTPСоединение = Новый HTTPСоединение(URI.Хост, URI.Порт,,,,3, ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение());
	GetЗапрос = Новый HTTPЗапрос(URI.ПутьНаСервере);
	Попытка
		Результат = HTTPСоединение.Получить(GetЗапрос);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Результат.КодСостояния <> 404;
	
КонецФункции

#КонецОбласти