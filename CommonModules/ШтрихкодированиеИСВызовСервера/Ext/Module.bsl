#Область СлужебныйПрограммныйИнтерфейс

// Выполняет обработку штрихкода и возвращает результат этой обработки.
//
// Параметры:
//  ВходящиеДанные - Структура - Данные штрихкода.
//  ПараметрыСканирования - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  КэшированныеЗначения - Структура - Содержит поля кэшируемых значений.
//  УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор, по которому будут помещены данные по обработанным
//                                                      штрихкодам в хранилище.
// Возвращаемое значение:
//  Структура - (См. ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода).
Функция ОбработатьШтрихкод(ВходящиеДанные, ПараметрыСканирования, КэшированныеЗначения, УникальныйИдентификатор) Экспорт
	
	ДанныеШтрихкода = ОбщегоНазначения.СкопироватьРекурсивно(ВходящиеДанные);
	ШтрихкодированиеИСКлиентСервер.ДекодироватьШтрихкодДанныхBase64(ДанныеШтрихкода);
	
	СписокДанныхШтрихкодов = Новый Массив;
	СписокДанныхШтрихкодов.Добавить(ДанныеШтрихкода);
	
	РезультатыОбработки = ШтрихкодированиеИС.ОбработатьШтрихкоды(
		СписокДанныхШтрихкодов, ПараметрыСканирования, КэшированныеЗначения, УникальныйИдентификатор);
	
	ДанныеШтрихкода = Неопределено;
	Для Каждого КлючЗНачение Из РезультатыОбработки Цикл
		ДанныеШтрихкода = КлючЗНачение.Значение;
	КонецЦикла;
	
	Возврат ДанныеШтрихкода;
	
КонецФункции

// Добавляет серию в элемент справочника "ШтрихкодыУпаковок".
//
// Параметры:
//  РезультатОбработки - Структура - (См. ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода).
//  Серия - ОпределяемыйТип.СерияНоменклатуры - Серия.
// Возвращаемое значение:
// (См. ШтрихкодированиеИС.ИнициализироватьДанныеШтрихкода).
Функция ОбработатьДанныеШтрихкодаПослеВыбораСерии(РезультатОбработки, РезультатВыбора) Экспорт
	
	ДанныеШтрихкода = РезультатОбработки.ДанныеШтрихкода;
	ДанныеШтрихкода.Серия                   = РезультатВыбора.ДанныеВыбора.Серия;
	ДанныеШтрихкода.ДополнительныеПараметры = РезультатВыбора;
	ДанныеШтрихкода.ТребуетсяВыборСерии     = Ложь;
	
	Если ЗначениеЗаполнено(ДанныеШтрихкода.ШтрихкодУпаковки) Тогда
		
		НовыеРеквизиты = Новый Структура;
		НовыеРеквизиты.Вставить("Серия", РезультатВыбора.ДанныеВыбора.Серия);
		
		Справочники.ШтрихкодыУпаковокТоваров.ИзменитьШтрихкодУпаковки(
			ДанныеШтрихкода.ШтрихкодУпаковки, НовыеРеквизиты);
		
	КонецЕсли;
	
	Возврат ДанныеШтрихкода;
	
КонецФункции

// Возвращает вид продукции по коду GTIN.
//
// Параметры:
//  GTIN - Строка - Код формата GTIN.
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
Функция ВидПродукцииПоGTIN(GTIN) Экспорт

	ШтрихкодыEAN = Новый Массив;
	ШтрихкодEAN = ШтрихкодированиеИС.ШтрихкодEANИзGTIN(GTIN);
	ШтрихкодыEAN.Добавить(ШтрихкодEAN);

	ДанныеПоШтрихкодам = ШтрихкодированиеИС.ДанныеПоШтрихкодамEAN(ШтрихкодыEAN);
	Если ДанныеПоШтрихкодам.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат ДанныеПоШтрихкодам[0].ВидПродукции;

КонецФункции

// Очищает отложенные коды маркировки.
// 
// Параметры:
//  АдресХранилища - Строка - Адрес временного хранилища.
Процедура ОчиститьОтложенныеКодыМаркировки(АдресХранилища) Экспорт

	Если Не ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		Возврат;
	КонецЕсли;
	
	КэшМаркируемойПродукции = ПолучитьИзВременногоХранилища(АдресХранилища);
	КэшМаркируемойПродукции.ОтложенныеКодыМаркировки.Очистить();
	
КонецПроцедуры

// Получает данные для уточнения сведений у пользователя из кэша по адресу.
// 
// Параметры:
//  АдресВременногоХранилища - Строка - Адрес кэша обработки кодов маркировки.
// Возвращаемое значение:
//  Неопределено - Структура - 
Функция ДанныеДляУточненияСведенийПользователя(АдресВременногоХранилища) Экспорт
	
	Если Не ЭтоАдресВременногоХранилища(АдресВременногоХранилища) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеКэша = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Возврат ДанныеКэша.ДанныеДляУточненияСведенийПользователя;
	
КонецФункции

Процедура ОбработатьУточнениеДанныхДляФормыПроверкиИПодбора(РезультатВыбора, РезультатОбработки, ПараметрыСканирования, КэшированныеЗначения) Экспорт
	
	ШтрихкодированиеИС.ОбработатьУточнениеДанныхДляФормыПроверкиИПодбора(РезультатВыбора, РезультатОбработки, ПараметрыСканирования, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ОбработатьГрупповоеУточнениеДанных(СохраненныйВыбор, ОбновляемыеШтрихкодыУпаковок, ПараметрыСканирования) Экспорт
	
	ГрупповаяОбработкаШтрихкодовИС.ОбработатьУточнениеДанных(СохраненныйВыбор, ОбновляемыеШтрихкодыУпаковок, ПараметрыСканирования);
	
КонецПроцедуры

#Область РазобратьНормализоватьКодМаркировки

// Функция обертка, для вызова с клиента
Функция РазобратьКодМаркировки(Знач ДанныеДляРазбора, ВидыПродукции = Неопределено, ПримечаниеКРезультатуРазбора = Неопределено) Экспорт
	Возврат ШтрихкодированиеИССлужебный.РазобратьКодМаркировки(ДанныеДляРазбора, ВидыПродукции, ПримечаниеКРезультатуРазбора);
КонецФункции

// Функция обертка, для вызова с клиента
Функция НормализоватьКодМаркировки(РезультатРазбора, ВидПродукции, ПараметрыНормализации = Неопределено) Экспорт
	Возврат ШтрихкодированиеИССлужебный.НормализоватьКодМаркировки(РезультатРазбора, ВидПродукции, ПараметрыНормализации);
КонецФункции

// Функция обертка, для вызова с клиента
Функция ПараметрыНормализацииКодаМаркировки() Экспорт
	Возврат ШтрихкодированиеИССлужебный.ПараметрыНормализацииКодаМаркировки();
КонецФункции

#КонецОбласти

#КонецОбласти
