#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.165";
	Обработчик.Процедура = "КабинетСотрудника.УстановитьЗначениеПоказыватьПриглашениеКабинетСотрудника";
	Обработчик.РежимВыполнения = "Монопольно";
		
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.РегламентныеЗадания

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользуетсяСервисКабинетСотрудника;
	Настройка.ДоступноВАвтономномРабочемМесте = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПодключениеСервисаКабинетСотрудника;
	Настройка.ДоступноВАвтономномРабочемМесте = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.РегламентныеЗадания

// СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий

// См. ОчередьЗаданийПереопределяемый.ПриПолученииСпискаШаблонов.
//
Процедура ПриПолученииСпискаШаблоновОчередиЗаданий(Шаблоны) Экспорт
	
	Шаблоны.Добавить(Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника.Имя);
	Шаблоны.Добавить(Метаданные.РегламентныеЗадания.ПодключениеСервисаКабинетСотрудника.Имя);
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПодключениеСервисаКабинетСотрудника.ИмяМетода);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий

Процедура ПриЗаписиПубликуемогоОбъекта(Объект) Экспорт
	
	ЗначенияКонтролируемыхРеквизитов = Неопределено;
	Объект.ДополнительныеСвойства.Свойство("ПрежниеДанныеПубликуемогоОбъекта", ЗначенияКонтролируемыхРеквизитов);
	Если Не КонтролируемыеРеквизитыИзменились(ЗначенияКонтролируемыхРеквизитов, Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектПубликации", Объект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации КАК ОбъектПубликации
	|ИЗ
	|	РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъектыКабинетСотрудника
	|ГДЕ
	|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации = &ОбъектПубликации";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПредметПубликации 	= Объект.Ссылка;
		МенеджерЗаписи.ВерсияДанных 		= Строка(Новый УникальныйИдентификатор);
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает значение для ФО ПоказыватьПриглашениеКабинетСотрудника
//	Параметры:
//		ЭтоБазовая - булево;
//		ВыполняетсяНачальнаяНастройка - булево;
//		ИспользуетсяСервисКабинетСотрудника - булево.
Процедура УстановитьЗначениеПоказыватьПриглашениеКабинетСотрудника(Знач ЭтоБазовая = Неопределено, 
																   Знач ВыполняетсяНачальнаяНастройка = Неопределено,
																   Знач ИспользуетсяСервисКабинетСотрудника = Неопределено) Экспорт
															   
	Если ВыполняетсяНачальнаяНастройка = Неопределено Тогда 
		 Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.НачальнаяНастройкаПрограммы") Тогда
			 Модуль = ОбщегоНазначения.ОбщийМодуль("НачальнаяНастройкаПрограммы");
			 ВыполняетсяНачальнаяНастройка = Модуль.ВыполняетсяНачальнаяНастройкаПрограммы();
		 Иначе
			 ВыполняетсяНачальнаяНастройка = Ложь;
		 КонецЕсли;
	КонецЕсли;
	
	ЭтоБазовая = ?(ЭтоБазовая <> Неопределено, ЭтоБазовая, ЗарплатаКадры.ЭтоБазоваяВерсияКонфигурации());
	ИспользуетсяСервисКабинетСотрудника = ?(ИспользуетсяСервисКабинетСотрудника <> Неопределено, ИспользуетсяСервисКабинетСотрудника,
																								 ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника"));
	ПоказыватьПриглашениеКабинетаСотрудника = НЕ (ЭтоБазовая ИЛИ ВыполняетсяНачальнаяНастройка ИЛИ ИспользуетсяСервисКабинетСотрудника);
	
	Если ПоказыватьПриглашениеКабинетаСотрудника <> Константы.ПоказыватьПриглашениеКабинетСотрудника.Получить() Тогда
		Константы.ПоказыватьПриглашениеКабинетСотрудника.Установить(ПоказыватьПриглашениеКабинетаСотрудника);
	КонецЕсли;
КонецПроцедуры

// См. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки
Процедура ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы) Экспорт
	
	Типы.Добавить(Метаданные.РегистрыСведений.ВерсииЗаявокКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.СостояниеОтложенногоПодключенияКСервису);
	Типы.Добавить(Метаданные.РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.НастройкиСервисаКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.ОбъектыДляУдаленияИзСервисаКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.ПравилаПубликацииКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.РасчетныеЛисткиКабинетСотрудника);
	Типы.Добавить(Метаданные.РегистрыСведений.СотрудникиДляОбновленияПубликуемыхОбъектов);
	Типы.Добавить(Метаданные.РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ);
	Типы.Добавить(Метаданные.РегистрыСведений.ФизическиеЛицаКабинетСотрудника);
	Типы.Добавить(Метаданные.БизнесПроцессы.ЗаявкаКабинетСотрудника);
	Типы.Добавить(Метаданные.Константы.ИспользуетсяСервисКабинетСотрудника);
	Типы.Добавить(Метаданные.Константы.ПоказыватьПриглашениеКабинетСотрудника);
	Типы.Добавить(Метаданные.Константы.РегистрироватьВЖурналеСобытийЗапросы);
	Типы.Добавить(Метаданные.Справочники.ШаблоныОтветовНаЗаявкиКабинетСотрудника);
	
	КабинетСотрудникаВнутренний.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСКлючамиПриложения

Функция ЕстьКлючиПриложения()
	УстановитьПривилегированныйРежим(Истина);
	Возврат (КлючиПриложенияИзБезопасногоХранилища() <> Неопределено);
КонецФункции

Функция КлючиПриложенияИзБезопасногоХранилища() Экспорт
	
	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		"1СКабинетСотрудника.Приложение.Ключи",
		"ИдентификаторКлиента,СекретКлиента");

	Если ДанныеВБезопасномХранилище.ИдентификаторКлиента <> Неопределено
		И ДанныеВБезопасномХранилище.СекретКлиента <> Неопределено Тогда
		Возврат Новый Структура(
			"ИдентификаторКлиента,СекретКлиента",
			ДанныеВБезопасномХранилище.ИдентификаторКлиента,
			ДанныеВБезопасномХранилище.СекретКлиента);
	КонецЕсли;
		
	Возврат Неопределено;

КонецФункции

Процедура СохранитьКлючиПриложенияВБезопасномХранилище(ИдентификаторКлиента, СекретКлиента) Экспорт

	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище("1СКабинетСотрудника.Приложение.Ключи",ИдентификаторКлиента, "ИдентификаторКлиента");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище("1СКабинетСотрудника.Приложение.Ключи",СекретКлиента, "СекретКлиента");
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеТокенаАутентификации

Функция ТокенАутентификацииИзБезопасногоХранилища()
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеВБезопасномХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		"1СКабинетСотрудника.Приложение.Токен",
		"Токен,СрокГодности");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеВБезопасномХранилище.Токен <> Неопределено
		И ДанныеВБезопасномХранилище.СрокГодности <> Неопределено Тогда
		Возврат ДанныеВБезопасномХранилище.Токен;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция СохранитьТокенАутентификацииВБезопасномХранилище(Токен, СрокГодности)
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище("1СКабинетСотрудника.Приложение.Токен",Токен, "Токен");
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище("1СКабинетСотрудника.Приложение.Токен",СрокГодности, "СрокГодности");
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

Функция ПолучитьНовыйТокенАутентификации(Соединение)
	
	Результат = Неопределено;
	
	Если Не ЕстьКлючиПриложения() Тогда
		ОписаниеОшибки = НСтр("ru = 'Не заданы ключи приложения.'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	КлючиПриложения = КлючиПриложенияИзБезопасногоХранилища();
	
	СтрокаBase64 = Base64Строка(
	ПолучитьДвоичныеДанныеИзСтроки(СтрШаблон("%1:%2", КлючиПриложения.ИдентификаторКлиента, КлючиПриложения.СекретКлиента)));
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ПС, "");
	СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ВК, "");
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", ИмяКлиентскогоПриложения());
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Authorization", СтрШаблон("Basic %1", СтрокаBase64));
	Запрос = Новый HTTPЗапрос("/auth/oidc/token", Заголовки);
	Запрос.УстановитьТелоИзСтроки("grant_type=client_credentials");
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	Если Ответ.КодСостояния = 200 Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		РезультатЧтенияJSON = ПрочитатьJSON(ЧтениеJSON);
		Если РезультатЧтенияJSON.Свойство("id_token") Тогда
			Результат = РезультатЧтенияJSON["id_token"];
			СрокГодности = ТекущаяДатаСеанса() + 3600;
			СохранитьТокенАутентификацииВБезопасномХранилище(Результат, СрокГодности);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить токен аутентификации.
				|Сервер вернул код:%1
				|%2'"), Ответ.КодСостояния, Ответ.ПолучитьТелоКакСтроку());
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ИспользованиеAPI

Функция ПараметрыПодключения(ИмяСобытияЖР)
	
	НастройкиСервиса = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	СтруктураАдресаПриложения = ОбщегоНазначенияКлиентСервер.СтруктураURI(НастройкиСервиса.АдресПриложения);
	
	Результат = Новый Структура;
	Результат.Вставить("АдресПриложения", 			НастройкиСервиса.АдресПриложения);
	Результат.Вставить("СтруктураАдресаПриложения", СтруктураАдресаПриложения);
	Результат.Вставить("Соединение",				НовоеHTTPСоединение(СтруктураАдресаПриложения));
	Результат.Вставить("ШаблоныРесурсов",			ОписаниеAPIСервиса());
	Результат.Вставить("Ошибки",					Новый Массив);
	Результат.Вставить("БылиОшибки",				Ложь);
	Результат.Вставить("ИмяСобытияЖР",				ИмяСобытияЖР);
	Результат.Вставить("ВестиПротокол",				Константы.РегистрироватьВЖурналеСобытийЗапросы.Получить());
	Возврат Результат;
	
КонецФункции

Функция ИмяКлиентскогоПриложения()
	
	Возврат "1C Enterprise 8.3";
	
КонецФункции

Функция HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, ИмяМетода, СтрокаТела = "", ИмяФайла = "")
	
	Соединение = ПараметрыПодключения.Соединение;

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("User-Agent", ИмяКлиентскогоПриложения());
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		Заголовки.Вставить("Content-Type", "application/octet-stream");
	ИначеЕсли ЗначениеЗаполнено(СтрокаТела) Тогда
		Заголовки.Вставить("Content-Type", "application/json");
	КонецЕсли;
	
	ТокенАутентификации = ТокенАутентификацииИзБезопасногоХранилища();
	
	Если Не ЗначениеЗаполнено(ТокенАутентификации) Тогда
		ТокенАутентификации = ПолучитьНовыйТокенАутентификации(Соединение);
	КонецЕсли;
	Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", ТокенАутентификации));
	
	Запрос = Новый HTTPЗапрос("/" + ПараметрыПодключения.СтруктураАдресаПриложения.ПутьНаСервере + РесурсСервиса, Заголовки);
	Если ЗначениеЗаполнено(ИмяФайла) Тогда
		Запрос.УстановитьИмяФайлаТела(ИмяФайла);
	ИначеЕсли ЗначениеЗаполнено(СтрокаТела) Тогда
		Запрос.УстановитьТелоИзСтроки(СтрокаТела);
	КонецЕсли;
	
	Если ПараметрыПодключения.ВестиПротокол Тогда
		ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыПодключения, ИмяМетода, Запрос);
	КонецЕсли;
	
	Попытка
		Ответ = Соединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
	Исключение
		ЗаписатьОшибкуВызова(ПараметрыПодключения.ИмяСобытияЖР, ПараметрыПодключения.СтруктураАдресаПриложения, ИмяМетода, Запрос, , ОписаниеОшибки());
		ВызватьИсключение;
	КонецПопытки;
	
	Если ПараметрыПодключения.ВестиПротокол Тогда
		ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыПодключения, ИмяМетода, Запрос, Ответ);
	КонецЕсли;
	
	Если Ответ.КодСостояния = 401 Тогда
		ТокенАутентификации = ПолучитьНовыйТокенАутентификации(Соединение);
		Запрос.Заголовки.Вставить("Authorization", СтрШаблон("Bearer %1", ТокенАутентификации));
		Попытка
			Соединение = НовоеHTTPСоединение(ПараметрыПодключения.СтруктураАдресаПриложения);
			Если ПараметрыПодключения.ВестиПротокол Тогда
				ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыПодключения, ИмяМетода, Запрос);
			КонецЕсли;
			Ответ = Соединение.ВызватьHTTPМетод(ИмяМетода, Запрос);
		Исключение
			ЗаписатьОшибкуВызова(ПараметрыПодключения.ИмяСобытияЖР, ПараметрыПодключения.Сервер, ИмяМетода, Запрос, , ОписаниеОшибки());
			ВызватьИсключение;
		КонецПопытки;
		Если ПараметрыПодключения.ВестиПротокол Тогда
			ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыПодключения, ИмяМетода, Запрос, Ответ);
		КонецЕсли;
	КонецЕсли;
	
		
	Если Ответ.КодСостояния >= 300
		И Не (Ответ.КодСостояния = 404 И ИмяМетода = "DELETE") Тогда
		ЗаписатьОшибкуВызова(ПараметрыПодключения.ИмяСобытияЖР, ПараметрыПодключения.СтруктураАдресаПриложения, ИмяМетода, Запрос, Ответ);
		Соединение = НовоеHTTPСоединение(ПараметрыПодключения.СтруктураАдресаПриложения);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ИдентификаторПриложенногоФайла(ПараметрыПодключения, Знач ПриложенныйФайл, Расширение)
	
	Если ТипЗнч(ПриложенныйФайл) = Тип("ХранилищеЗначения") Тогда
		ПриложенныйФайл = ПриложенныйФайл.Получить();
	КонецЕсли;
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
	Если ТипЗнч(ПриложенныйФайл) = Тип("ТабличныйДокумент") Тогда
		ПриложенныйФайл.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.PDF);
	ИначеЕсли ТипЗнч(ПриложенныйФайл) = Тип("СправочникСсылка.ФизическиеЛицаПрисоединенныеФайлы") Тогда
		ДвоичныеДанныеВложения = РаботаСФайлами.ДвоичныеДанныеФайла(ПриложенныйФайл);
		ДвоичныеДанныеВложения.Записать(ИмяВременногоФайла);
	Иначе
		Картинка = Новый Картинка(ПриложенныйФайл);
		Картинка.Записать(ИмяВременногоФайла);
	КонецЕсли;
	Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсФайлы(), "POST",, ИмяВременногоФайла);
	УдалитьФайлы(ИмяВременногоФайла);
	Если Ответ.КодСостояния >= 300 Тогда
		ВызватьИсключение НСтр("ru = 'Произошла ошибка при отправке файлов в сервис. Подробности в журнале регистрации.'");
	КонецЕсли;
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
	
	Возврат ОбъектОтвета.Получить("fileID");
	
КонецФункции

Функция ТекстОшибкиОбработкиЗапроса()
	
	Возврат НСтр("ru = 'Произошла ошибка при обработке запроса сервисом.'");
	
КонецФункции

Процедура ЗаписатьОшибкуВызова(ИмяСобытияЖР, СтруктураАдресаПриложения, Метод, HTTPЗапрос, HTTPОтвет = Неопределено, ОписаниеОшибки = Неопределено)
	
	ЭтоПередачаФайла = (СтрНайти(HTTPЗапрос.АдресРесурса, РесурсФайлы()) > 0);
	
	АдресСервера = СтрШаблон( "%1://%2", СтруктураАдресаПриложения.Схема, СтруктураАдресаПриложения.ИмяСервера);
	
	ЗапросТекст = Метод + " " + АдресСервера + HTTPЗапрос.АдресРесурса+ Символы.ПС;
	Для Каждого Заголовок Из HTTPЗапрос.Заголовки Цикл
		ЗначениеЗаголовка = Заголовок.Значение;
		Если Заголовок.Ключ = "Authorization" Тогда
			ЗначениеЗаголовка = "*";
		КонецЕсли;
		ЗапросТекст = ЗапросТекст + Символы.ПС + Заголовок.Ключ + ": " + ЗначениеЗаголовка;
	КонецЦикла;
	
	Если HTTPОтвет = Неопределено Тогда
		ОтветТекст = НСтр("ru = 'Не удалось получить ответ от сервера.'");
	Иначе
		ОтветТекст = СтрШаблон(НСтр("ru = 'Сервер вернул код состояния: %1'"), HTTPОтвет.КодСостояния);
		Если Не ЭтоПередачаФайла Тогда
			ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Если ЗначениеЗаполнено(ТелоОтвета) Тогда
				ОтветТекст = ОтветТекст + Символы.ПС + Символы.ПС + ТелоОтвета;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ОписаниеОшибки = Неопределено Тогда
		ОтветТекст = ОтветТекст + Символы.ПС + ОписаниеОшибки;
	КонецЕсли;

	Комментарий = НСтр(
	"ru = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ОтветТекст);
	ЗаписьЖурналаРегистрации(ИмяСобытияЖР, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
	
КонецПроцедуры

Функция НазваниеГруппыСобытийЖР()
	
	Возврат НСтр("ru = '1С:Кабинет сотрудника'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция ИмяСобытияЖРПолучениеИзменений()
	
	Возврат СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Получение изменений'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецФункции

Функция ИмяСобытияЖРПередачаИзменений()
	
	Возврат СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Публикация изменений'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецФункции

Функция ИмяСобытияЖРУдалениеДанных()
	
	Возврат СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Удаление данных'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецФункции

Функция ИмяСобытияЖРРегистрацияИзменений()
	
	Возврат СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Регистрация изменений'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецФункции

Функция ИмяСобытияЖРПубликацияРасчетныхЛистов()
	
	Возврат СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Публикация расчетных листов'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецФункции

Функция ЗаполнитьПараметрыШаблонаURL(Знач ШаблонСтроки, Знач Параметры)
	Результат = ШаблонСтроки;
	Для Каждого Параметр Из Параметры Цикл
		Результат = СтрЗаменить(Результат, "{" + Параметр.Ключ + "}", Параметр.Значение);
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область ОписаниеAPI

Функция ОписаниеAPIСервиса()
	
	ОписаниеAPI = Новый Соответствие;
	ОписаниеAPI.Вставить("ФизическиеЛица",									"/api/persons/{ID}");
	ОписаниеAPI.Вставить("ФизическиеЛица_Запросы_Изменения",				"/api/persons/information-change-requests/updates?version={version}&limit={limit}");
	ОписаниеAPI.Вставить("Организации",										"/api/employers/{ID}");
	ОписаниеAPI.Вставить("СтруктураПредприятия",							"/api/divisions/{ID}");
	ОписаниеAPI.Вставить("Должности",										"/api/positions/{ID}");
	ОписаниеAPI.Вставить("ШтатноеРасписание",								"/api/stafflist-positions/{ID}");
	ОписаниеAPI.Вставить("Сотрудники",										"/api/employees/{ID}");
	ОписаниеAPI.Вставить("ИнформацияОбОтпусках",							"/api/vacations/summaries/{ID}");
	ОписаниеAPI.Вставить("ПрименяемыеВычеты",								"/api/tax-deductions/{personID}/{employerID}");
	ОписаниеAPI.Вставить("СоставныеЧастиЗарплаты",							"/api/payslip-component-types/{ID}");
	ОписаниеAPI.Вставить("РасчетныеЛисты",									"/api/payslips/{personID}/{month}");
	ОписаниеAPI.Вставить("Справки2НДФЛ",									"/api/forms2NDFL/{ID}");
	ОписаниеAPI.Вставить("Справки2НДФЛ_Запросы_Изменения",					"/api/forms2NDFL/requests/updates?version={version}&limit={limit}");
	ОписаниеAPI.Вставить("Справки2НДФЛ_Запросы_Статусы",					"/api/forms2NDFL/requests/{requestID}/status/?status={status}");
	ОписаниеAPI.Вставить("ОтветыНаЗапросыСправок2НДФЛ",						"/api/forms2NDFL/responses/{ID}");
	ОписаниеAPI.Вставить("ЗаявленияНаОтпуск_Запросы_Изменения",				"/api/vacations/requests/updates?version={version}&limit={limit}");
	ОписаниеAPI.Вставить("СправкиСРаботы",									"/api/employment-certificates/{ID}");
	ОписаниеAPI.Вставить("СправкиСРаботы_Запросы_Изменения",				"/api/employment-certificates/requests/updates?version={version}&limit={limit}");
	ОписаниеAPI.Вставить("Файлы",											"/api/files/{ID}");
	ОписаниеAPI.Вставить("СправкиОбОстаткеОтпуска_Запросы_Изменения",		"/api/vacations/balance-reports/requests/updates?version={version}&limit={limit}");
	ОписаниеAPI.Вставить("ЗаявленияНаНалоговыеВычеты_Запросы_Изменения",	"/api/tax-deductions/requests/updates?version={version}&limit={limit}");
	ОписаниеAPI.Вставить("Отсутствия_Изменения",							"/api/absences/updates?version={version}&limit={limit}");
	Возврат ОписаниеAPI;
	
КонецФункции

Функция РесурсAPI()
	
	Возврат "/api";
	
КонецФункции

Функция РесурсФизическиеЛица()
	
	Возврат РесурсAPI() + "/persons";
	
КонецФункции

Функция РесурсОрганизации()
	
	Возврат РесурсAPI() + "/employers";
	
КонецФункции

Функция РесурсСтруктураПредприятия()
	
	Возврат РесурсAPI() + "/divisions";
	
КонецФункции

Функция РесурсДолжности()
	
	Возврат РесурсAPI() + "/positions";
	
КонецФункции

Функция РесурсШтатноеРасписание()
	
	Возврат РесурсAPI() + "/stafflist-positions";
	
КонецФункции

Функция РесурсСотрудники()
	
	Возврат РесурсAPI() + "/employees";
	
КонецФункции

Функция РесурсИнформацияОбОтпуске()
	
	Возврат РесурсAPI() + "/vacations/summaries";
	
КонецФункции

Функция РесурсПрименяемыеВычеты()
	
	Возврат РесурсAPI() + "/tax-deductions";
	
КонецФункции

Функция РесурсЗаявленияНаНалоговыеВычеты()
	
	Возврат РесурсПрименяемыеВычеты() + "/requests";
	
КонецФункции

Функция РесурсСоставныеЧастиЗарплаты()
	
	Возврат РесурсAPI() + "/payslip-component-types";
	
КонецФункции

Функция РесурсРасчетныеЛисты()
	
	Возврат РесурсAPI() + "/payslips";
	
КонецФункции

Функция РесурсЗапросыИзмененияЛичныхДанных()
	
	Возврат РесурсФизическиеЛица() + "/information-change-requests";
	
КонецФункции

Функция РесурсСправки2НДФЛ()
	
	Возврат РесурсAPI() + "/forms2NDFL";
	
КонецФункции

Функция РесурсЗапросыСправок2НДФЛ()
	
	Возврат РесурсСправки2НДФЛ() + "/requests";
	
КонецФункции

Функция РесурсОтветыНаЗапросыСправок2НДФЛ()
	
	Возврат РесурсСправки2НДФЛ() + "/responses";
	
КонецФункции

Функция РесурсСправкиСМестаРаботы()
	
	Возврат РесурсAPI() + "/employment-certificates";
	
КонецФункции

Функция РесурсЗапросыСправокСРаботы()
	
	Возврат РесурсСправкиСМестаРаботы() + "/requests";
	
КонецФункции

Функция РесурсЗаявленияНаОтпуск()
	
	Возврат РесурсAPI() + "/vacations/requests";
	
КонецФункции

Функция РесурсСправкиОбОстаткеОтпуска()
	
	Возврат РесурсAPI() + "/vacations/balance-reports";
	
КонецФункции

Функция РесурсЗапросыСправокОбОстаткеОтпуска()
	
	Возврат РесурсСправкиОбОстаткеОтпуска() + "/requests";
	
КонецФункции

Функция РесурсОтсутствия()
	
	Возврат РесурсAPI() + "/absences";
	
КонецФункции

Функция РесурсФайлы()
	
	Возврат РесурсAPI() + "/files";
	
КонецФункции

Функция РесурсБудущиеИзмененияОстатковОтпусков()
	
	Возврат РесурсAPI() + "/vacations/unused-days-predicted";
	
КонецФункции

#КонецОбласти

#Область КонструкторыСоответствияПолей

Функция ОписаниеПолейОрганизаций()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",		"Организация");
	СоответствиеПолей.Вставить("name",		"Наименование");
	СоответствиеПолей.Вставить("taxID",		"ИНН");
	СоответствиеПолей.Вставить("isBranch",	"ОбособленноеПодразделение");
	СоответствиеПолей.Вставить("parentOrganizationID", "ГоловнаяОрганизация");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейСтруктурыПредприятия()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",				"СтруктураПредприятия");
	СоответствиеПолей.Вставить("name",				"Наименование");
	СоответствиеПолей.Вставить("code",				"Код");
	СоответствиеПолей.Вставить("parentDivisionID",	"Родитель");
	СоответствиеПолей.Вставить("headID",			"ФизическоеЛицоРуководителя");
	СоответствиеПолей.Вставить("priority",			"Порядок");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейДолжностей()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",		"Должность");
	СоответствиеПолей.Вставить("name",		"Наименование");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейШтатногоРасписания()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",			"ПозицияШтатногоРасписания");
	СоответствиеПолей.Вставить("name",			"Наименование");
	СоответствиеПолей.Вставить("employerID",	"Организация");
	СоответствиеПолей.Вставить("divisionID",	"МестоВСтруктуреПредприятия");
	СоответствиеПолей.Вставить("positionID",	"Должность");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейФизическихЛиц()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",				"ФизическоеЛицо");
	СоответствиеПолей.Вставить("lastName",			"Фамилия");
	СоответствиеПолей.Вставить("firstName",			"Имя");
	СоответствиеПолей.Вставить("patronymic",		"Отчество");
	СоответствиеПолей.Вставить("initials",			"Инициалы");
	СоответствиеПолей.Вставить("gender",			"Пол");
	СоответствиеПолей.Вставить("birthDate",			"ДатаРождения");
	СоответствиеПолей.Вставить("taxID",				"ИНН");
	СоответствиеПолей.Вставить("insuranceNumber",	"СтраховойНомерПФР");
	СоответствиеПолей.Вставить("birthPlace",		"МестоРождения");
	СоответствиеПолей.Вставить("phoneNumber",		"ТелефонРабочийПредставление");
	СоответствиеПолей.Вставить("mobilePhoneNumber",	"ТелефонМобильныйПредставление");
	СоответствиеПолей.Вставить("eMail",				"EMailПредставление");
	СоответствиеПолей.Вставить("identityDocument",	ОписаниеПолейДокументВид());
	СоответствиеПолей.Вставить("registrationAddress", "АдресПоПропискеПредставление");
	СоответствиеПолей.Вставить("residentialAddress", "АдресМестаПроживанияПредставление");
	
	Возврат СоответствиеПолей;

КонецФункции

Функция ОписаниеПолейДокументВид()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("identityDocumentType",	"ДокументВид");
	СоответствиеПолей.Вставить("serias",				"ДокументСерия");
	СоответствиеПолей.Вставить("number",				"ДокументНомер");
	СоответствиеПолей.Вставить("issueDate",				"ДокументДатаВыдачи");
	СоответствиеПолей.Вставить("issuingAuthority",		"ДокументКемВыдан");
	СоответствиеПолей.Вставить("issuingAuthorityID",	"ДокументКодПодразделения");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейСотрудников()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",					"Сотрудник");
	СоответствиеПолей.Вставить("personID",				"ФизическоеЛицо");
	СоответствиеПолей.Вставить("employmentDate",		"РабочееМестоПериодРегистрации");
	СоответствиеПолей.Вставить("staffListPositionID",	"ДолжностьПоШтатномуРасписанию");
	СоответствиеПолей.Вставить("employerID",			"Организация");
	СоответствиеПолей.Вставить("divisionID",			"МестоВСтруктуреПредприятия");
	СоответствиеПолей.Вставить("positionID",			"Должность");
	СоответствиеПолей.Вставить("transferDate",			"РабочееМестоПериодРегистрации");
	СоответствиеПолей.Вставить("employmentType",		"ВидЗанятости");
	СоответствиеПолей.Вставить("wageRate",				ОписаниеПолейТарифнойСтавки());
	Возврат СоответствиеПолей;

КонецФункции

Функция ОписаниеПолейТарифнойСтавки()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("value",			"ТарифнаяСтавка");
	СоответствиеПолей.Вставить("presentation",	"ПоказательТарифнойСтавки");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейИнформацииОбОтпусках()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("personID",			"ФизическоеЛицо");
	СоответствиеПолей.Вставить("daysNotUsed",		"ОстатокОтпуска");
	СоответствиеПолей.Вставить("previousVacation",	ОписаниеПолейИнформацииОПредыдущемОтпуске());
	СоответствиеПолей.Вставить("nextVacation",		ОписаниеПолейИнформацииОСледующемОтпуске());
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейИнформацииОПредыдущемОтпуске()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("startDate",		"ДатаНачалаПредыдущегоОтпуска");
	СоответствиеПолей.Вставить("endDate",		"ДатаОкончанияПредыдущегоОтпуска");
	СоответствиеПолей.Вставить("days",			"КоличествоДнейПредыдущегоОтпуска");
	СоответствиеПолей.Вставить("note",	"");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейИнформацииОСледующемОтпуске()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("startDate",		"ДатаНачалаСледующегоОтпуска");
	СоответствиеПолей.Вставить("endDate",		"ДатаОкончанияСледующегоОтпуска");
	СоответствиеПолей.Вставить("days",			"КоличествоДнейСледующегоОтпуска");
	СоответствиеПолей.Вставить("description",	"");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейСправокСРаботы()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",			"ЗаявкаСсылка");
	СоответствиеПолей.Вставить("requestID",		"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("employerID",	"Организация");
	СоответствиеПолей.Вставить("personID",		"ФизическоеЛицо");
	СоответствиеПолей.Вставить("attachment",	ОписаниеПолейВложения());
	СоответствиеПолей.Вставить("note",			"Комментарий");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейВложения()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("name",		"НаименованиеВложения");
	СоответствиеПолей.Вставить("extension",	"РасширениеВложения");
	СоответствиеПолей.Вставить("fileID ",	"ИдентификаторВложения");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейВходящийФайл()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("name",		"НаименованиеФайла");
	СоответствиеПолей.Вставить("extension",	"РасширениеФайла");
	СоответствиеПолей.Вставить("size",		"РазмерФайла");
	СоответствиеПолей.Вставить("ID",		"ИдентификаторФайла");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейЗапросовСправокНДФЛ()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",			"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("version",		"Версия");
	СоответствиеПолей.Вставить("dateCreated",	"ДатаСоздания");
	СоответствиеПолей.Вставить("personID",		"ФизическоеЛицо");
	СоответствиеПолей.Вставить("purpose",		"Назначение");
	СоответствиеПолей.Вставить("periodType",	"ТипПериода");
	СоответствиеПолей.Вставить("taxYear",		"НалоговыйПериод");
	СоответствиеПолей.Вставить("proofPeriod",	"КоличествоМесяцев");
	СоответствиеПолей.Вставить("note",			"Комментарий");
	СоответствиеПолей.Вставить("employerID",	"Организация");
	Возврат СоответствиеПолей;

КонецФункции

Функция ОписаниеПолейОтсутствия()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",			"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("version",		"Версия");
	СоответствиеПолей.Вставить("dateCreated",	"ДатаСоздания");
	СоответствиеПолей.Вставить("personID",		"ФизическоеЛицо");
	СоответствиеПолей.Вставить("reason",		"Причина");
	СоответствиеПолей.Вставить("startDate",		"ДатаНачала");
	СоответствиеПолей.Вставить("endDate",		"ДатаОкончания");
	СоответствиеПолей.Вставить("note",			"Комментарий");
	СоответствиеПолей.Вставить("attachments",	"Вложения");
	СоответствиеПолей.Вставить("allDay",		"ВесьДень");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейЗаявленийНаОтпуск()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",				"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("version",			"Версия");
	СоответствиеПолей.Вставить("dateCreated",		"ДатаСоздания");
	СоответствиеПолей.Вставить("personID",			"ФизическоеЛицо");
	СоответствиеПолей.Вставить("startDate",			"ДатаНачала");
	СоответствиеПолей.Вставить("endDate",			"ДатаОкончания");
	СоответствиеПолей.Вставить("communicationType",	"СпособСвязи");
	СоответствиеПолей.Вставить("unpaid",			"ЗаСвойСчет");
	СоответствиеПолей.Вставить("approved",			"Согласован");
	СоответствиеПолей.Вставить("note",				"Комментарий");
	СоответствиеПолей.Вставить("deputyInfos",		"");
	СоответствиеПолей.Вставить("originalRequestID",	"");
	СоответствиеПолей.Вставить("signatories",		"");
	СоответствиеПолей.Вставить("attachments",		"Вложения");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейЗапросаСправокСРаботы()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",			"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("version",		"Версия");
	СоответствиеПолей.Вставить("dateCreated",	"ДатаСоздания");
	СоответствиеПолей.Вставить("personID",		"ФизическоеЛицо");
	СоответствиеПолей.Вставить("employerID",	"Организация");
	СоответствиеПолей.Вставить("purpose",		"НазначениеСтрокой");
	СоответствиеПолей.Вставить("note",			"Комментарий");
	СоответствиеПолей.Вставить("attachments",	"Вложения");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейЗапросаИзмененияЛичныхДанных()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ID",						"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("version",					"Версия");
	СоответствиеПолей.Вставить("dateCreated",				"ДатаСоздания");
	СоответствиеПолей.Вставить("personID",					"ФизическоеЛицо");
	СоответствиеПолей.Вставить("employerID",				"Организация");
	СоответствиеПолей.Вставить("changeName",				"ИзменитьФИО");
	СоответствиеПолей.Вставить("lastName",					"Фамилия");
	СоответствиеПолей.Вставить("firstName",					"Имя");
	СоответствиеПолей.Вставить("patronymic",				"Отчество");
	СоответствиеПолей.Вставить("changeIdentityDocument",	"ИзменитьДокумент");
	СоответствиеПолей.Вставить("identityDocument",			ОписаниеПолейДокументВид());
	СоответствиеПолей.Вставить("changePersonalPhone",		"ИзменитьНомерТелефона");
	СоответствиеПолей.Вставить("personalPhone",				"ЛичныйНомерТелефона");
	СоответствиеПолей.Вставить("changeAddress",				"ИзменитьАдрес");
	СоответствиеПолей.Вставить("registrationAddress",		"АдресРегистрации");
	СоответствиеПолей.Вставить("residentialAddress",		"АдресМестаПроживания");
	СоответствиеПолей.Вставить("note",						"Комментарий");
	СоответствиеПолей.Вставить("attachments",				"Вложения");
	Возврат СоответствиеПолей;
	
КонецФункции

Функция ОписаниеПолейСправокОбОстаткеОтпуска()
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("requestID",		"ИдентификаторЗапроса");
	СоответствиеПолей.Вставить("attachment",	ОписаниеПолейВложения());
	Возврат СоответствиеПолей;
	
КонецФункции

Функция НовоеОписаниеПоля(ИмяПоляСервиса, ИмяПоляКонфигурации, ТипПоля, ОписаниеПолей = Неопределено)
	
	ОписаниеПоля = Новый Соответствие;
	ОписаниеПоля.Вставить("ИмяПоляСервиса", ИмяПоляСервиса);
	ОписаниеПоля.Вставить("ИмяПоляКонфигурации", ИмяПоляКонфигурации);
	ОписаниеПоля.Вставить("ТипПоля", ТипПоля);
	ОписаниеПоля.Вставить("ОписаниеПолей", ОписаниеПолей);
	Возврат ОписаниеПоля;
	
КонецФункции

Функция ОписаниеПолейПрименяемыеНалоговыеВычеты()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("employerID", "Организация", Тип("СправочникСсылка.Организации")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personID", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("deductions", "ИнформацияОВычетах", Тип("ТаблицаЗначений"), ОписаниеПолейИнформацияОПримененииВычета()));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейБудущиеИзмененияОстатковОтпуска()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personID", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("components", "Остатки", Тип("ТаблицаЗначений"), ОписаниеПолейСтрокаБудущихИзмененийОстатковОтпуска()));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейСтрокаБудущихИзмененийОстатковОтпуска()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("date", "Период", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("daysNotUsed", "КоличествоДней", Тип("Число")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейИнформацияОПримененииВычета()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("startDate", "ДатаНачала", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("endDate", "ДатаОкончания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("amount", "РазмерВычета", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "ОписаниеВычета", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейОтветовНаЗапросыСправок2НДФЛ()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("requestID", "ИдентификаторЗапроса", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("employerID", "Организация", Тип("СправочникСсылка.Организации")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personID", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("forms2NDFL", "Справки2НДФЛ", Тип("ТаблицаЗначений"), ОписаниеПолейСправок2НДФЛ()));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейСправок2НДФЛ()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("ID", "Справка2НДФЛ", Тип("ДокументСсылка.СправкаНДФЛ")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("taxYear", "НалоговыйПериод", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("incomeAmount", "СуммаДохода", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("taxAmount", "СуммаНалога", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "Комментарий", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("dateCreated", "ДатаСоздания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("attachment", "Вложение", Неопределено, ОписаниеПолейПриложенныйФайл()));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейЗапросаСправокОбОстаткеОтпуска()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("ID", "ИдентификаторЗапроса", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("version", "Версия", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("dateCreated", "ДатаСоздания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personID", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейЗаявленийНаНалоговыеВычеты()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("ID", "ИдентификаторЗапроса", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("version", "Версия", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("dateCreated", "ДатаСоздания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personID", "ФизическоеЛицо", Тип("СправочникСсылка.ФизическиеЛица")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("employerID", "Организация", Тип("СправочникСсылка.Организации")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("applyMonth", "МесяцПрименения", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("personalDeduction", "ЭтоЛичныйВычет", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("childTaxDeductions", "ВычетыНаДетей", Тип("ТаблицаЗначений"), ОписаниеПолейВычетаНаДетей()));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("taxAuthorityNotices", "УведомленияИзНалоговой", Тип("ТаблицаЗначений"), ОписаниеПолейУведомленияИзНалоговой()));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("attachments", "Вложения", Тип("ТаблицаЗначений"), ОписаниеПолейЗагружаемыйФайл()));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "Комментарий", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейВычетаНаДетей()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("childSeniority", "СтаршинствоРебенка", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("childDisability", "РебенокИнвалид", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("singleParent", "РодительОдиночка", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("guardian", "ЗаявительОпекун", Тип("Булево")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("endDate", "ДатаОкончания", Тип("Дата")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("attachments", "Вложения", Тип("ТаблицаЗначений"), ОписаниеПолейЗагружаемыйФайл()));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "Комментарий", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейУведомленияИзНалоговой()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("amount", "Размер", Тип("Число")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("attachment", "Вложение", Неопределено, ОписаниеПолейЗагружаемыйФайл()));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("note", "Комментарий", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейПриложенныйФайл()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("name", "НаименованиеФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("extension", "РасширениеФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("size", "РазмерФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("fileID", "ИдентификаторФайла", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция ОписаниеПолейЗагружаемыйФайл()
	
	ОписаниеПолей = Новый Массив;
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("name", "НаименованиеФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("extension", "РасширениеФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("size", "РазмерФайла", Тип("Строка")));
	ОписаниеПолей.Добавить(НовоеОписаниеПоля("ID", "ИдентификаторФайла", Тип("Строка")));
	Возврат ОписаниеПолей;
	
КонецФункции

Функция НовоеОписаниеТиповПоля(ИмяПоля)
	
	ТипПоля = Новый ОписаниеТипов("Строка");
	Если ИмяПоля = "ФизическоеЛицо" Тогда
		ТипПоля = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
	ИначеЕсли ИмяПоля = "Организация" Тогда
		ТипПоля = Новый ОписаниеТипов("СправочникСсылка.Организации");
	ИначеЕсли ИмяПоля = "ИзменитьФИО"
			Или ИмяПоля = "ИзменитьДокумент"
			Или ИмяПоля = "ИзменитьНомерТелефона"
			Или ИмяПоля = "ИзменитьАдрес"
			Или ИмяПоля = "ЗаСвойСчет"
			Или ИмяПоля = "ВесьДень" Тогда
		ТипПоля = Новый ОписаниеТипов("Булево");
	ИначеЕсли ИмяПоля = "Версия"
			Или ИмяПоля = "ЭтоЛичныйВычет"
			Или ИмяПоля = "НалоговыйПериод" Тогда
		ТипПоля = Новый ОписаниеТипов("Число");
	ИначеЕсли ИмяПоля = "Вложения" Тогда
		ТипПоля = Новый ОписаниеТипов("ТаблицаЗначений");
	ИначеЕсли ИмяПоля = "ДатаСоздания"
			Или ИмяПоля = "ДатаНачала"
			Или ИмяПоля = "ДатаОкончания"
			Или ИмяПоля = "МесяцПрименения"
			Или ИмяПоля = "ДокументДатаВыдачи" Тогда
		ТипПоля = Новый ОписаниеТипов("Дата");
	КонецЕсли;
	Возврат ТипПоля;
	
КонецФункции

#КонецОбласти

#Область ФункцииПреобразованияПеречислений

Функция ПолФизическогоЛица(Пол)
	
	Результат = Неопределено;
	
	Если Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской") Тогда
		Результат = "male";
	ИначеЕсли Пол = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский") Тогда
		Результат = "female";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидЗанятостиСотрудника(ВидЗанятости)
	
	Результат = Неопределено;
	
	Если ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы Тогда
		Результат = "mainWork";
	ИначеЕсли ВидЗанятости = Перечисления.ВидыЗанятости.Совместительство Тогда
		Результат = "extraWorkExternal";
	ИначеЕсли ВидЗанятости = Перечисления.ВидыЗанятости.ВнутреннееСовместительство Тогда
		Результат = "extraWorkInternal";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидДокументаФизическогоЛица(ВидДокумента)
	
	Результат = "other";
	
	Если ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ") Тогда
		Результат = "passport";
	ИначеЕсли ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ЗагранпаспортРФ") Тогда
		Результат = "foreignPassport";
	ИначеЕсли ВидДокумента = ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ВоенныйБилет") Тогда
		Результат = "militaryIdentityCard";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеДанныхИБ

#Область ФункцииПолученияДанных

Функция ДанныеФизическихЛиц(ПараметрыПодключения, МассивОтбора)
	
	Результат = НовыйРезультатПолученияДанных();
	
	СоответствиеПолей = ОписаниеПолейФизическихЛиц();
	ВыбираемыеПоля = ВыбираемыеПоля(СоответствиеПолей);
	
	КадровыеДанные = СтрСоединить(ВыбираемыеПоля, ", ");
	ТаблицаДанных = КадровыйУчет.КадровыеДанныеФизическихЛиц(Ложь, МассивОтбора, КадровыеДанные);
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.МестоРождения = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(СтрокаТЗ.МестоРождения);
		Если ЗначениеЗаполнено(СтрокаТЗ.ТелефонРабочийПредставление) И СтрДлина(СтрокаТЗ.ТелефонРабочийПредставление) > 20 Тогда
			СтрокаТЗ.ТелефонРабочийПредставление = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТЗ.ТелефонМобильныйПредставление) И СтрДлина(СтрокаТЗ.ТелефонМобильныйПредставление) > 20 Тогда
			СтрокаТЗ.ТелефонМобильныйПредставление = "";
		КонецЕсли;
	КонецЦикла;
	
	Вложения = КабинетСотрудникаВнутренний.ФотографииФизическихЛиц(ТаблицаДанных.ВыгрузитьКолонку("ФизическоеЛицо"));
	Если ЗначениеЗаполнено(Вложения) Тогда
		СоответствиеПолей.Вставить("picture", ОписаниеПолейВложения());
		ДополнитьТаблицуКолонкамиВложения(ТаблицаДанных);
		ВыгрузитьВложенияВСервис(ПараметрыПодключения, ТаблицаДанных, Вложения, "ФизическоеЛицо", "Фотография", "jpg");
	КонецЕсли;
	
	Результат.ВыгрузитьДанные = ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеДолжностей(ПараметрыПодключения, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейДолжностей();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОтбора", МассивОтбора);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Должности.Ссылка КАК Должность,
	|	Должности.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Должности КАК Должности
	|ГДЕ
	|	Должности.Ссылка В(&СписокОтбора)";
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);

КонецФункции

Функция ДанныеОрганизаций(ПараметрыПодключения, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейОрганизаций();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокОтбора", МассивОтбора);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	Организации.Наименование КАК Наименование,
	|	Организации.ИНН КАК ИНН,
	|	Организации.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Организации.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&СписокОтбора)";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеСтруктурыПредприятия(ПараметрыПодключения, МассивОтбора)
	
	Результат = НовыйРезультатПолученияДанных();
	
	СоответствиеПолей = ОписаниеПолейСтруктурыПредприятия();
		
	ТаблицаДанных = КабинетСотрудникаВнутренний.ДанныеСтруктурыПредприятия(МассивОтбора);
	
	Возврат  ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеШтатногоРасписания(ПараметрыПодключения, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейШтатногоРасписания();
	ТаблицаДанных = КабинетСотрудникаВнутренний.ДанныеШтатногоРасписания(МассивОтбора);
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);

КонецФункции

Функция ДанныеСотрудников(ПараметрыПодключения, МассивОтбора)
	
	Результат = НовыйРезультатПолученияДанных();
	
	СоответствиеПолей = ОписаниеПолейСотрудников();
	ВыбираемыеПоля = ВыбираемыеПоля(СоответствиеПолей);
	
	КадровыеДанные = СтрСоединить(ВыбираемыеПоля, ", ");
	ТаблицаДанных = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, МассивОтбора, КадровыеДанные);
	
	Результат.ВыгрузитьДанные = ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
	Возврат Результат;

КонецФункции

Функция ДанныеОбОтпусках(МассивОтбора)
	
	Результат = Новый Структура("ИнформацияОбОтпусках,ОстаткиОтпусков");
	
	ДанныеПоОтпускам = КабинетСотрудникаВнутренний.ДанныеПоОтпускам(МассивОтбора);
	
	Результат.ИнформацияОбОтпусках = ТаблицаДанныхВМассив(ДанныеПоОтпускам.ИнформацияОбОтпусках, ОписаниеПолейИнформацииОбОтпусках());
	Результат.ОстаткиОтпусков = МассивИзТаблицы(ДанныеПоОтпускам.ОстаткиОтпусков, ОписаниеПолейБудущиеИзмененияОстатковОтпуска());
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеОВычетах(ПараметрыПодключения, МассивОтбора)
	
	ОписаниеПолей = ОписаниеПолейПрименяемыеНалоговыеВычеты();
	ТаблицаДанных = ПрименяемыеВычеты(МассивОтбора, ТекущаяДатаСеанса());
	
	ФизическиеЛицаСВычетами  = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаДанных, "ФизическоеЛицо", Истина);
	
	Результат = Новый Структура("Данные,ФизическиеЛицаБезВычетов");
	Результат.Данные = МассивИзТаблицы(ТаблицаДанных, ОписаниеПолей);
	Результат.ФизическиеЛицаБезВычетов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(МассивОтбора, ФизическиеЛицаСВычетами);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеРасчетныхЛистов(Организация, МесяцРасчетныхЛистов, СписокФизическихЛиц)
	
	Данные = Отчеты.АнализНачисленийИУдержаний.ДанныеРасчетныхЛистков(СписокФизическихЛиц, Организация, НачалоМесяца(МесяцРасчетныхЛистов), КонецМесяца(МесяцРасчетныхЛистов));
	Если Не ЗначениеЗаполнено(Данные.ДанныеРасчетныхЛистков) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеРасчетныхЛистов = Данные.ДанныеРасчетныхЛистков.ДанныеРасчетныхЛистков;
	РасчетныеЛистыДокументы = КраткиеРасчетныеЛистыДокументы(Данные.ДанныеРасчетныхЛистков);
	
	ПерваяПоловинаМесяца = Ложь;
	ДанныеРасчетныхЛистов.Колонки.Добавить("ID",						Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	ДанныеРасчетныхЛистов.Колонки.Добавить("section",					Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	ДанныеРасчетныхЛистов.Колонки.Добавить("specificSalaryComponent",	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	ДанныеРасчетныхЛистов.Колонки.Добавить("accrualType",				Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	ДанныеРасчетныхЛистов.Колонки.Добавить("priority",					Новый ОписаниеТипов("Число"));
	ДанныеРасчетныхЛистов.Колонки.Добавить("name",						Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100)));
	ДанныеРасчетныхЛистов.Колонки.Добавить("amount",					Новый ОписаниеТипов("Число"));
	Идентификаторы = ИдентификаторыСоставнойЧастиЗарплаты();
	НачисленияТарифнойСтавки = РасчетЗарплаты.НачисленияТарифнойСтавки();
	СтрокиКУдалению = Новый Массив;
	Для Каждого СтрокаДанных Из ДанныеРасчетныхЛистов Цикл
		Если Не ЗначениеЗаполнено(СтрокаДанных.ВидРасчета) И Не ЗначениеЗаполнено(СтрокаДанных.Группа) Тогда
			СтрокиКУдалению.Добавить(СтрокаДанных);
			Продолжить;;
		КонецЕсли;
		ОбработатьСтрокуДанных(СтрокаДанных, НачисленияТарифнойСтавки);
	КонецЦикла;
	
	Для каждого СтрокаДанных Из СтрокиКУдалению Цикл
		ДанныеРасчетныхЛистов.Удалить(СтрокаДанных);
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеРасчетныхЛистов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеРасчетныхЛистов.Сотрудник КАК Сотрудник,
	|	ДанныеРасчетныхЛистов.amount КАК amount,
	|	ДанныеРасчетныхЛистов.priority КАК priority,
	|	ДанныеРасчетныхЛистов.accrualType КАК accrualType,
	|	ДанныеРасчетныхЛистов.section КАК section,
	|	ДанныеРасчетныхЛистов.specificSalaryComponent КАК specificSalaryComponent,
	|	ДанныеРасчетныхЛистов.name КАК name,
	|	ДанныеРасчетныхЛистов.ID КАК ID
	|ПОМЕСТИТЬ ВТДанныеСИдентификаторами
	|ИЗ
	|	&ДанныеРасчетныхЛистов КАК ДанныеРасчетныхЛистов
	|ГДЕ
	|	НЕ ДанныеРасчетныхЛистов.ID = """"
	|	И НЕ ДанныеРасчетныхЛистов.Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|	И НЕ ДанныеРасчетныхЛистов.amount = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеСИдентификаторами.ID КАК ID,
	|	ВТДанныеСИдентификаторами.name КАК name,
	|	ВТДанныеСИдентификаторами.section КАК section,
	|	ВТДанныеСИдентификаторами.specificSalaryComponent КАК specificSalaryComponent,
	|	ВТДанныеСИдентификаторами.accrualType КАК accrualType,
	|	МИНИМУМ(ВТДанныеСИдентификаторами.priority) КАК priority
	|ИЗ
	|	ВТДанныеСИдентификаторами КАК ВТДанныеСИдентификаторами
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеСИдентификаторами.ID,
	|	ВТДанныеСИдентификаторами.name,
	|	ВТДанныеСИдентификаторами.section,
	|	ВТДанныеСИдентификаторами.specificSalaryComponent,
	|	ВТДанныеСИдентификаторами.accrualType
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеСИдентификаторами.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДанныеСИдентификаторами.Сотрудник КАК Сотрудник,
	|	ВТДанныеСИдентификаторами.ID КАК ID,
	|	ВТДанныеСИдентификаторами.amount КАК amount
	|ИЗ
	|	ВТДанныеСИдентификаторами КАК ВТДанныеСИдентификаторами
	|ИТОГИ ПО
	|	ФизическоеЛицо");
	Запрос.УстановитьПараметр("ДанныеРасчетныхЛистов", ДанныеРасчетныхЛистов);
	Запрос.УстановитьПараметр("Идентификаторы", Идентификаторы);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	СоставныеЧастиЗарплаты = Новый Массив;
	Выборка = РезультатЗапроса[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		СоставнаяЧастьЗарплаты = Новый Структура;
		Для Каждого КолонкаРезультатаЗапроса Из РезультатЗапроса[1].Колонки Цикл
			Если ЗначениеЗаполнено(Выборка[КолонкаРезультатаЗапроса.Имя]) Тогда
				СоставнаяЧастьЗарплаты.Вставить(КолонкаРезультатаЗапроса.Имя, Выборка[КолонкаРезультатаЗапроса.Имя]);
			КонецЕсли;
		КонецЦикла;
		СоставныеЧастиЗарплаты.Добавить(СоставнаяЧастьЗарплаты);
	КонецЦикла;
	ИнформацияОЗарплате = Новый Массив;
	ВыборкаФизЛицо = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФизЛицо.Следующий() Цикл
		Компоненты = Новый Массив;
		Выборка = ВыборкаФизЛицо.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураКомпонента = Новый Структура;
			СтруктураКомпонента.Вставить("employerID", Организация);
			СтруктураКомпонента.Вставить("employeeID", Выборка.Сотрудник);
			СтруктураКомпонента.Вставить("componentID", Выборка.ID);
			СтруктураКомпонента.Вставить("amount", Выборка.amount);
			Компоненты.Добавить(СтруктураКомпонента);
		КонецЦикла;
		Структура = Новый Структура;
		Структура.Вставить("personID", ВыборкаФизЛицо.ФизическоеЛицо);
		Структура.Вставить("month", МесяцРасчетныхЛистов);
		Структура.Вставить("isFirstHalf", ПерваяПоловинаМесяца);
		Структура.Вставить("components", Компоненты);
		ИнформацияОЗарплате.Добавить(Структура);
	КонецЦикла;
	Результат = Новый Структура;
	Результат.Вставить("СоставныеЧастиЗарплаты", СоставныеЧастиЗарплаты);
	Результат.Вставить("ИнформацияОЗарплате", ИнформацияОЗарплате);
	Результат.Вставить("РасчетныеЛистыДокументы", РасчетныеЛистыДокументы);
	Возврат Результат;
	
КонецФункции

Функция КраткиеРасчетныеЛистыДокументы(ДанныеРасчетныхЛистковСтруктура)
	
	Результат = Новый Соответствие;
	
	ДанныеРасчетныхЛистков = ДанныеРасчетныхЛистковСтруктура.ДанныеРасчетныхЛистков;
	
	Для Каждого ОбщиеДанныеРасчетногоЛистка Из ДанныеРасчетныхЛистковСтруктура.ОбщиеДанныеРасчетныхЛистков Цикл
		
		ФизическоеЛицо = ОбщиеДанныеРасчетногоЛистка.Ключ;
		
		ДокументРезультат = КраткийРасчетныйЛист(ОбщиеДанныеРасчетногоЛистка.Значение, ДанныеРасчетныхЛистков.Скопировать(Новый Структура("ФизическоеЛицо", ФизическоеЛицо)));
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			Результат.Вставить(ФизическоеЛицо, ДокументРезультат);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция КраткийРасчетныйЛист(ОбщиеДанныеРасчетногоЛистка, ДанныеРасчетногоЛистка)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.РазмерСтраницы = "A6";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_РасчетныйЛистокДляСервисаКабинетСотрудника");
	
	ОбластьШапки = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапки.Параметры.Заполнить(ОбщиеДанныеРасчетногоЛистка);
	ДокументРезультат.Вывести(ОбластьШапки);
	
	ВыводимыеГруппы = Новый Массив;
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо"));
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено"));
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано"));
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Выплачено"));
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы"));
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно"));
	ВыводимыеГруппы.Добавить(ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо"));
	
	Для Каждого Группа Из ВыводимыеГруппы Цикл
		
		ДанныеГруппы = ДанныеРасчетногоЛистка.Скопировать(Новый Структура("Группа", Группа));
		Если ДанныеГруппы.Количество() > 0 Тогда
			
			Если Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано")
				Или Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Выплачено") Тогда
				ДанныеГруппы.ЗаполнитьЗначения(Неопределено, "Сотрудник");
				ДанныеГруппы.Свернуть("ФизическоеЛицо,Сотрудник,Группа,ВидРасчета,РегистраторВыплаты,ПриоритетВидаРасчета,ПериодДействия", "Сумма");
			КонецЕсли;
			
			ДанныеВычисляемыеНаХоду = Новый Структура("Сумма", 0);
			Если Группа <> ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы")
				И Группа <> ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно") Тогда
				ДанныеВычисляемыеНаХоду.Сумма = ДанныеГруппы.Итог("Сумма");
			КонецЕсли;
			
			Если Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо")
				Или Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо") Тогда
				
				Если ДанныеВычисляемыеНаХоду.Сумма = 0 Тогда
					Продолжить;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо")
				Или Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо") Тогда
				
				Если ДанныеВычисляемыеНаХоду.Сумма > 0 Тогда
					ОбластьГруппы = Макет.ПолучитьОбласть("ДолгПредприятия");
				Иначе
					ОбластьГруппы = Макет.ПолучитьОбласть("ДолгРаботника");
				КонецЕсли;
				
			ИначеЕсли Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено") Тогда
				ОбластьГруппы = Макет.ПолучитьОбласть("Начислено");
			ИначеЕсли Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано") Тогда
				ОбластьГруппы = Макет.ПолучитьОбласть("Удержано");
			ИначеЕсли Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Выплачено") Тогда
				ОбластьГруппы = Макет.ПолучитьОбласть("Выплачено");
			ИначеЕсли Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы") Тогда
				ОбластьГруппы = Макет.ПолучитьОбласть("Льготы");
			ИначеЕсли Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно") Тогда
				ОбластьГруппы = Макет.ПолучитьОбласть("Справочно");
			ИначеЕсли Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо") Тогда
				ОбластьГруппы = Макет.ПолучитьОбласть("КонечноеСальдо");
			КонецЕсли;
			
			ОбластьГруппы.Параметры.Заполнить(ДанныеВычисляемыеНаХоду);
			ДокументРезультат.Вывести(ОбластьГруппы);
			
			Если Группа <> ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо")
				И Группа <> ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо") Тогда
				
				ПредставлениеРабочегоМеста = "";
				Для Каждого СтрокаДанных Из ДанныеГруппы Цикл
					
					Если Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено") Тогда
						
						Если Не ПустаяСтрока(ПредставлениеРабочегоМеста)
							И ПредставлениеРабочегоМеста <> СтрокаДанных.ПредставлениеРабочегоМеста Тогда
							
							ОбластьПредставлениеРабочегоМеста = Макет.ПолучитьОбласть("ПредставлениеРабочегоМеста");
							ОбластьПредставлениеРабочегоМеста.Параметры.Заполнить(СтрокаДанных);
							ДокументРезультат.Вывести(ОбластьПредставлениеРабочегоМеста);
							
						КонецЕсли;
						
						ПредставлениеРабочегоМеста = СтрокаДанных.ПредставлениеРабочегоМеста;
						
					КонецЕсли;
					
					ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
					ОбластьСтроки.Параметры.Заполнить(СтрокаДанных);
					ДокументРезультат.Вывести(ОбластьСтроки);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОбщиеДанныеРасчетногоЛистка.ТекстоваяИнформация) Тогда
		ОбластьШапки = Макет.ПолучитьОбласть("ТекстоваяИнформация");
		ОбластьШапки.Параметры.Заполнить(ОбщиеДанныеРасчетногоЛистка);
		ДокументРезультат.Вывести(ОбластьШапки);
	КонецЕсли;
	
	Возврат ДокументРезультат;
	
КонецФункции

Функция ИдентификаторЗначенияПеречисления(Знач Значение)
	
	Если Не Метаданные.Перечисления.Содержит(Значение.Метаданные()) Тогда
		ВызватьИсключение НСтр("ru = 'Значение не является значением перечисления.'");
	КонецЕсли;
	
	Значение = ЗначениеВСтрокуВнутр(Значение);
	Значение = Сред(Значение, СтрНайти(Значение, ":") + 1, 32);
	Значение =
		Лев(Значение, 8)+ "-"
		+ Сред(Значение, 9, 4) + "-"
		+ Сред(Значение, 13, 4) + "-"
		+ Сред(Значение, 17, 4) + "-"
		+ Прав(Значение, 12);
	
	Возврат Значение;
	
КонецФункции

Процедура ОбработатьСтрокуДанных(СтрокаДанных, НачисленияТарифнойСтавки)
	
	Если СтрокаДанных.ВидРасчета = Неопределено Тогда
		СтрокаДанных.ID = ИдентификаторЗначенияПеречисления(СтрокаДанных.Группа);
		СтрокаДанных.ПриоритетВидаРасчета = 0;
	ИначеЕсли ОбщегоНазначения.ЭтоПланВидовРасчета(СтрокаДанных.ВидРасчета.Метаданные()) Тогда
		СтрокаДанных.ID = Строка(СтрокаДанных.ВидРасчета.УникальныйИдентификатор());
		СтрокаДанных.ПриоритетВидаРасчета = СтрокаДанных.ВидРасчета.РеквизитДопУпорядочивания;
	ИначеЕсли ОбщегоНазначения.ЭтоПеречисление(СтрокаДанных.ВидРасчета.Метаданные()) Тогда
		СтрокаДанных.ID = ИдентификаторЗначенияПеречисления(СтрокаДанных.ВидРасчета);
		СтрокаДанных.ПриоритетВидаРасчета = 99;
	КонецЕсли;
	СтрокаДанных.name = ?(СтрокаДанных.ВидРасчета = Неопределено, Строка(СтрокаДанных.Группа), Строка(СтрокаДанных.ВидРасчета));
	
	Если СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.НачальноеСальдо") Тогда
		СтрокаДанных.section = "incomingBalance";
		Если СтрокаДанных.Сумма < 0 Тогда
			СтрокаДанных.specificSalaryComponent = "incomingEmployerDebt";
			СтрокаДанных.name = НСтр("ru = 'Долг сотрудника на начало'");
			СтрокаДанных.ID = "2f576597-ec81-482e-a504-202004ac75c4";
		Иначе
			СтрокаДанных.specificSalaryComponent = "incomingEmployeeDebt";
			СтрокаДанных.name = НСтр("ru = 'Долг предприятия на начало'");
			СтрокаДанных.ID = "a51bf7db-9fd5-451e-a3e4-7c4613b0a11b";
		КонецЕсли;
	ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено") Тогда
		СтрокаДанных.section = "accruals";
		СтрокаДанных.accrualType = ТипНачисления(НачисленияТарифнойСтавки, СтрокаДанных.Группа, СтрокаДанных.ВидРасчета);
		СтрокаДанных.specificSalaryComponent = "";
		Если СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа")
			Или СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги") Тогда
			СтрокаДанных.specificSalaryComponent = "civilContractAcrual";
		ИначеЕсли СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.КомпенсацияЗаЗадержкуЗарплаты") Тогда
			СтрокаДанных.specificSalaryComponent = "salaryDelayCompensation";
		ИначеЕсли СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности")
			Или СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриРожденииРебенка")
			Или СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребение")
			Или СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребениеСотруднику") Тогда
			СтрокаДанных.specificSalaryComponent = "allowance";
		КонецЕсли;
	ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано") Тогда
		СтрокаДанных.section = "deductions";
		Если СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПроцентыПоЗайму") Тогда
			СтрокаДанных.specificSalaryComponent = "loanInterestRepayment";
		ИначеЕсли СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.ПогашениеЗаймаИзЗарплаты") Тогда
			СтрокаДанных.specificSalaryComponent = "loanRepayment";
		ИначеЕсли СтрокаДанных.ВидРасчета = ПредопределенноеЗначение("Перечисление.ВидыОсобыхНачисленийИУдержаний.НДФЛ") Тогда
			СтрокаДанных.specificSalaryComponent = "tax";
		Иначе
			СтрокаДанных.specificSalaryComponent = "";
		КонецЕсли;
	ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Выплачено") Тогда
		СтрокаДанных.section = "payouts";
		СтрокаДанных.specificSalaryComponent = "";
		Если ТипЗнч(СтрокаДанных.РегистраторВыплаты) = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк") Тогда
			СтрокаДанных.specificSalaryComponent = "bankPayment";
			СтрокаДанных.name = НСтр("ru = 'Выплата на карту'");
			СтрокаДанных.ID = "dd587525-ff57-4ebc-9ef7-5c0f9d515d08";
		Иначе
			СтрокаДанных.specificSalaryComponent = "cashPayment";
			СтрокаДанных.name = НСтр("ru = 'Выплата наличными'");
			СтрокаДанных.ID = "2a1c604e-8c6a-4b6c-a8d9-e984984f1c57";
		КонецЕсли;
	ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.КонечноеСальдо") Тогда
		СтрокаДанных.section = "outgoingBalance";
		Если СтрокаДанных.Сумма < 0 Тогда
			СтрокаДанных.specificSalaryComponent = "outgoingEmployerDebt";
			СтрокаДанных.name = НСтр("ru = 'Долг сотрудника на конец'");
			СтрокаДанных.ID = "a110f6ae-17cf-4f8a-8545-6c86f3d45ac6";
		Иначе
			СтрокаДанных.specificSalaryComponent = "outgoingEmployeeDebt";
			СтрокаДанных.name = НСтр("ru = 'Долг предприятия на конец'");
			СтрокаДанных.ID = "375922cb-9d6a-4614-be5b-46e2cd2e1f12";
		КонецЕсли;
	ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно") Тогда
		СтрокаДанных.section = "additionalInfo";
		СтрокаДанных.specificSalaryComponent = "";
	ИначеЕсли СтрокаДанных.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы") Тогда
		СтрокаДанных.section = "benefits";
		СтрокаДанных.specificSalaryComponent = "";
	Иначе
		
		ИдентификаторНеизвестногоЗначения = "";
		Если Метаданные.Перечисления.Содержит(СтрокаДанных.Группа.Метаданные()) Тогда
			ИдентификаторНеизвестногоЗначения = ИдентификаторЗначенияПеречисления(СтрокаДанных.Группа);
		КонецЕсли;
			
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Данные расчетных листов содержат неизвестное значение группы начисления удержания выплаты.
											|Неизвестное значение: %1
											|Тип значения: %2
											|Идентификатор: %3'"), СтрокаДанных.Группа, ТипЗнч(СтрокаДанных.Группа), ИдентификаторНеизвестногоЗначения);
		
	КонецЕсли;
	СтрокаДанных.priority =
		(Перечисления.ГруппыНачисленияУдержанияВыплаты.Индекс(СтрокаДанных.Группа) + 1 )* 10000000
		+ ?(СтрокаДанных.ПриоритетВидаРасчета = Неопределено, 0, Число(СтрокаДанных.ПриоритетВидаРасчета));
	СтрокаДанных.amount = ?(СтрокаДанных.Сумма = Неопределено, 0, Число(СтрокаДанных.Сумма));
	
КонецПроцедуры

Функция ИдентификаторыСоставнойЧастиЗарплаты()
	
	Идентификаторы = Новый ТаблицаЗначений;
	Идентификаторы.Колонки.Добавить("section",					Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	Идентификаторы.Колонки.Добавить("specificSalaryComponent",	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	Идентификаторы.Колонки.Добавить("accrualType",				Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	Идентификаторы.Колонки.Добавить("ID",						Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "incomingBalance";
	НоваяСтрока.specificSalaryComponent = "incomingEmployerDebt";
	НоваяСтрока.ID = "2f576597-ec81-482e-a504-202004ac75c4";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "incomingBalance";
	НоваяСтрока.specificSalaryComponent = "incomingEmployeeDebt";
	НоваяСтрока.ID = "a51bf7db-9fd5-451e-a3e4-7c4613b0a11b";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "accruals";
	НоваяСтрока.specificSalaryComponent = "";
	НоваяСтрока.accrualType = "permanent";
	НоваяСтрока.ID = "192e35e6-5299-46a2-b0ab-af177d5521be";

	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "accruals";
	НоваяСтрока.specificSalaryComponent = "";
	НоваяСтрока.accrualType = "variable";
	НоваяСтрока.ID = "65f38e09-fb7c-4612-9326-9cd96e517333";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "accruals";
	НоваяСтрока.specificSalaryComponent = "civilContractAcrual";
	НоваяСтрока.ID = "28005886-0042-442a-94b5-5155eac0a3dd";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "accruals";
	НоваяСтрока.specificSalaryComponent = "salaryDelayCompensation";
	НоваяСтрока.ID = "057ca56e-0a6f-4708-96cb-772af62f1242";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "accruals";
	НоваяСтрока.specificSalaryComponent = "allowance";
	НоваяСтрока.ID = "91c006f8-0baa-4608-a449-4b56a95446bc";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "deductions";
	НоваяСтрока.specificSalaryComponent = "";
	НоваяСтрока.ID = "7787f7d9-826a-4dca-9e54-59185c73a1c6";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "deductions";
	НоваяСтрока.specificSalaryComponent = "loanInterestRepayment";
	НоваяСтрока.ID = "24d582cb-d3c2-4892-8187-30c32122a93c";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "deductions";
	НоваяСтрока.specificSalaryComponent = "loanRepayment";
	НоваяСтрока.ID = "28faac1b-0651-4953-bff9-d84ea990e333";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "deductions";
	НоваяСтрока.specificSalaryComponent = "tax";
	НоваяСтрока.ID = "52832021-8803-45a7-8212-804c51d7745e";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "payouts";
	НоваяСтрока.specificSalaryComponent = "";
	НоваяСтрока.ID = "7a8c743a-abb2-4ea5-bc3c-dd323c37530d";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "outgoingBalance";
	НоваяСтрока.specificSalaryComponent = "outgoingEmployerDebt";
	НоваяСтрока.ID = "a110f6ae-17cf-4f8a-8545-6c86f3d45ac6";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "outgoingBalance";
	НоваяСтрока.specificSalaryComponent = "outgoingEmployeeDebt";
	НоваяСтрока.ID = "375922cb-9d6a-4614-be5b-46e2cd2e1f12";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "additionalInfo";
	НоваяСтрока.specificSalaryComponent = "";
	НоваяСтрока.ID = "30efdd89-0845-42ec-b88b-fd0db9e44efc";
	
	НоваяСтрока = Идентификаторы.Добавить();
	НоваяСтрока.section = "benefits";
	НоваяСтрока.specificSalaryComponent = "";
	НоваяСтрока.ID = "2001554d-6c89-4a3c-b905-4d31914d2b16";
	
	Возврат Идентификаторы;
	
КонецФункции

Функция ТипНачисления(НачисленияТарифнойСтавки, Группа, Начисление)
	
	Результат = "";
	Если Группа = ПредопределенноеЗначение("Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено") Тогда
		Результат = ?(НачисленияТарифнойСтавки.Найти(Начисление) = Неопределено, "variable", "permanent");
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция ДанныеОтветовНаЗапросыСправок2НДФЛ(ПараметрыПодключения, ЗаявкиКОбработке)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заявки", ЗаявкиКОбработке);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаКабинетСотрудника.Предмет КАК Ссылка,
	|	ЗаявкаКабинетСотрудника.ИдентификаторЗаявки КАК ИдентификаторЗаявки,
	|	ЗаявкаКабинетСотрудника.Организация КАК Организация,
	|	ЗаявкаКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗаявкаКабинетСотрудника.ОтветПоЗаявке КАК ОтветПоЗаявке
	|ПОМЕСТИТЬ ВТСсылки
	|ИЗ
	|	БизнесПроцесс.ЗаявкаКабинетСотрудника КАК ЗаявкаКабинетСотрудника
	|ГДЕ
	|	ЗаявкаКабинетСотрудника.Ссылка В(&Заявки)
	|	И ЗаявкаКабинетСотрудника.Предмет ССЫЛКА Документ.СправкаНДФЛ
	|	И ВЫРАЗИТЬ(ЗаявкаКабинетСотрудника.Предмет КАК Документ.СправкаНДФЛ).Проведен";
	Запрос.Выполнить();
	
	УчетНДФЛ.СоздатьВТДанныеСправок2НДФЛДляПубликации(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Заявки.ИдентификаторЗаявки КАК ИдентификаторЗапроса,
	|	Заявки.Организация КАК Организация,
	|	Заявки.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТСсылки КАК Заявки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заявки.ИдентификаторЗаявки КАК ИдентификаторЗапроса,
	|	Заявки.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Заявки.Организация КАК Организация,
	|	Заявки.Ссылка КАК Справка2НДФЛ,
	|	Заявки.ОтветПоЗаявке КАК Комментарий,
	|	ДанныеСправок2НДФЛ.ДатаСоздания КАК ДатаСоздания,
	|	ДанныеСправок2НДФЛ.НалоговыйПериод КАК НалоговыйПериод,
	|	ДанныеСправок2НДФЛ.СуммаДохода КАК СуммаДохода,
	|	ДанныеСправок2НДФЛ.СуммаНалога КАК СуммаНалога
	|ИЗ
	|	ВТСсылки КАК Заявки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСправок2НДФЛДляПубликации КАК ДанныеСправок2НДФЛ
	|		ПО Заявки.Ссылка = ДанныеСправок2НДФЛ.Ссылка";
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатыЗапроса.ВГраница();
	
	ШаблонНаименованияФайла = НСтр("ru = '%1 Справка 2НФДЛ %2.%3'");
	РасширениеФайла = "pdf";
	ТаблицаДанных = РезультатыЗапроса[КоличествоРезультатов - 1].Выгрузить();
	ТаблицаДанных.Колонки.Добавить("Справки2НДФЛ", Новый ОписаниеТипов("ТаблицаЗначений"));
	ВыборкаДанныеСправок = РезультатыЗапроса[КоличествоРезультатов].Выбрать();
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		Отбор = Новый Структура("ИдентификаторЗапроса, Организация, ФизическоеЛицо");
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		СтрокаТаблицы.Справки2НДФЛ = НоваяТаблицаИзОписания(ОписаниеПолейСправок2НДФЛ(), "ИмяПоляКонфигурации");
		Пока ВыборкаДанныеСправок.НайтиСледующий(Отбор) Цикл
			НоваяСтрока = СтрокаТаблицы.Справки2НДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДанныеСправок);
			
			ТабДок = Документы.СправкаНДФЛ.СформироватьПечатнуюФорму2НДФЛ(
						ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыборкаДанныеСправок.Справка2НДФЛ),
						Новый СписокЗначений);
			
			НоваяСтрока.ИдентификаторФайла = ИдентификаторПриложенногоФайла(ПараметрыПодключения, ТабДок, РасширениеФайла);
			НоваяСтрока.НаименованиеФайла = СтрШаблон(ШаблонНаименованияФайла,
				ВыборкаДанныеСправок.ФизическоеЛицо, Формат(ВыборкаДанныеСправок.НалоговыйПериод, "ЧГ=0"), РасширениеФайла);
			НоваяСтрока.РасширениеФайла = РасширениеФайла;
		КонецЦикла;
		ВыборкаДанныеСправок.Сбросить();
	КонецЦикла;
	
	Возврат МассивИзТаблицы(ТаблицаДанных, ОписаниеПолейОтветовНаЗапросыСправок2НДФЛ());
	
КонецФункции

Функция ДанныеСправокСРаботы(ПараметрыПодключения, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейСправокСРаботы();
	ШаблонНаименования = НСтр("ru = '%1 Справка с места работы.%2'");
	ТаблицаДанных = ТаблицаДанныхЗаявокСПриложеннымиФайлами(ПараметрыПодключения, МассивОтбора, ШаблонНаименования);
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ДанныеСправокОбОстаткеОтпуска(ПараметрыПодключения, МассивОтбора)
	
	СоответствиеПолей = ОписаниеПолейСправокОбОстаткеОтпуска();
	ШаблонНаименования = НСтр("ru = '%1 Справка об остатках отпусков.%2'");
	ТаблицаДанных = ТаблицаДанныхЗаявокСПриложеннымиФайлами(ПараметрыПодключения, МассивОтбора, ШаблонНаименования);
	Возврат ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей);
	
КонецФункции

Функция ПрименяемыеВычеты(СписокФизическихЛиц, ДатаАктуальности)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РазмерВычетовНДФЛСрезПоследних.КодВычета КАК КодВычета,
	|	РазмерВычетовНДФЛСрезПоследних.Размер КАК Размер
	|ИЗ
	|	РегистрСведений.РазмерВычетовНДФЛ.СрезПоследних(&ДатаАктуальности, ) КАК РазмерВычетовНДФЛСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
	РазмерыВычетов = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		РазмерыВычетов.Вставить(Выборка.КодВычета, Выборка.Размер);
	КонецЦикла;

	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИмущественныеВычетыНДФЛ.ПрименятьВычетыС) КАК Период,
	|	ИмущественныеВычетыНДФЛ.Организация КАК Организация,
	|	ИмущественныеВычетыНДФЛ.Сотрудник КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТПоследниеПериодыИмущественныхВычетов
	|ИЗ
	|	Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ КАК ИмущественныеВычетыНДФЛ
	|ГДЕ
	|	ИмущественныеВычетыНДФЛ.Проведен
	|	И ИмущественныеВычетыНДФЛ.ПрименятьВычетыС МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИмущественныеВычетыНДФЛ.Сотрудник В(&СписокФизическихЛиц)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИмущественныеВычетыНДФЛ.Организация,
	|	ИмущественныеВычетыНДФЛ.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации) КАК МесяцРегистрации,
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация КАК Организация
	|ПОМЕСТИТЬ ВТПоследнийМесяцВычетовНаДетей
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
	|ГДЕ
	|	СтандартныеВычетыНаДетейНДФЛ.ДействуетДо >= &ДатаНачала
	|	И СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо В(&СписокФизическихЛиц)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация,
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МесяцРегистрации,
	|	Организация,
	|	ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИмущественныеВычетыНДФЛ.Организация КАК Организация,
	|	ИмущественныеВычетыНДФЛ.Сотрудник КАК ФизическоеЛицо,
	|	ИмущественныеВычетыНДФЛ.ПрименятьВычетыС КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ИмущественныеВычетыНДФЛ.ПрименятьВычетыС, ГОД) КАК ДатаОкончания,
	|	ИмущественныеВычетыНДФЛ.РасходыНаСтроительствоПриобретение КАК РасходыНаСтроительствоПриобретение,
	|	ИмущественныеВычетыНДФЛ.ПроцентыПоКредитам КАК ПроцентыПоКредитам,
	|	ИмущественныеВычетыНДФЛ.ПроцентыПриПерекредитовании КАК ПроцентыПриПерекредитовании,
	|	ИмущественныеВычетыНДФЛ.РасходыНаСвоеОбучение КАК РасходыНаСвоеОбучение,
	|	ИмущественныеВычетыНДФЛ.РасходыНаОбучениеДетей КАК РасходыНаОбучениеДетей,
	|	ИмущественныеВычетыНДФЛ.РасходыНаЛечение КАК РасходыНаЛечение,
	|	ИмущественныеВычетыНДФЛ.СтраховыеВзносыНаМедУслуги КАК СтраховыеВзносыНаМедУслуги,
	|	ИмущественныеВычетыНДФЛ.РасходыНаДорогостоящееЛечение КАК РасходыНаДорогостоящееЛечение,
	|	ИмущественныеВычетыНДФЛ.ВзносыНаДобровольноеСтрахованиеЖизни КАК ВзносыНаДобровольноеСтрахованиеЖизни
	|ИЗ
	|	ВТПоследниеПериодыИмущественныхВычетов КАК ПоследниеПериодыИмущественныхВычетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ КАК ИмущественныеВычетыНДФЛ
	|		ПО ПоследниеПериодыИмущественныхВычетов.Период = ИмущественныеВычетыНДФЛ.ПрименятьВычетыС
	|			И ПоследниеПериодыИмущественныхВычетов.Организация = ИмущественныеВычетыНДФЛ.Организация
	|			И ПоследниеПериодыИмущественныхВычетов.ФизическоеЛицо = ИмущественныеВычетыНДФЛ.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация КАК Организация,
	|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации КАК ДатаНачала,
	|	СтандартныеВычетыНаДетейНДФЛ.ДействуетДо КАК ДатаОкончания,
	|	СтандартныеВычетыНаДетейНДФЛ.КодВычета КАК КодВычета,
	|	СтандартныеВычетыНаДетейНДФЛ.КоличествоДетей КАК КоличествоДетей,
	|	ВидыВычетовНДФЛ.ПолноеНаименование КАК ПолноеНаименование
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоследнийМесяцВычетовНаДетей КАК ПоследнийМесяцВычетовНаДетей
	|		ПО СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации = ПоследнийМесяцВычетовНаДетей.МесяцРегистрации
	|			И СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация = ПоследнийМесяцВычетовНаДетей.Организация
	|			И СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо = ПоследнийМесяцВычетовНаДетей.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыВычетовНДФЛ КАК ВидыВычетовНДФЛ
	|		ПО СтандартныеВычетыНаДетейНДФЛ.КодВычета = ВидыВычетовНДФЛ.Ссылка
	|ГДЕ
	|	СтандартныеВычетыНаДетейНДФЛ.КоличествоДетей > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтандартныеВычетыНаДетейНДФЛ.КодВычета.Код,
	|	ДатаОкончания");
	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(ДатаАктуальности));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецГода(ДатаАктуальности));
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	УстановитьПривилегированныйРежим(Истина);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("РазмерВычета", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10)));
	Таблица.Колонки.Добавить("ОписаниеВычета", Новый ОписаниеТипов("Строка"));
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.РасходыНаСтроительствоПриобретение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаСтроительствоПриобретение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на жилье'");
		КонецЕсли;
		
		Если Выборка.ПроцентыПоКредитам > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.ПроцентыПоКредитам;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Проценты по кредитам'");
		КонецЕсли;
		
		Если Выборка.ПроцентыПриПерекредитовании > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.ПроцентыПриПерекредитовании;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Проценты при перекредитовании'");
		КонецЕсли;
		
		Если Выборка.РасходыНаСвоеОбучение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаСвоеОбучение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на свое обучение'");
		КонецЕсли;
		
		Если Выборка.РасходыНаОбучениеДетей > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаОбучениеДетей;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на обучение детей'");
		КонецЕсли;
		
		Если Выборка.РасходыНаЛечение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаЛечение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на лечение'");
		КонецЕсли;
		
		Если Выборка.СтраховыеВзносыНаМедУслуги > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.СтраховыеВзносыНаМедУслуги;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Страховые взносы на медицинские услуги'");
		КонецЕсли;
		
		Если Выборка.РасходыНаДорогостоящееЛечение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаДорогостоящееЛечение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на дорогостоящее лечение'");
		КонецЕсли;
		
		Если Выборка.РасходыНаСтроительствоПриобретение > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.РасходыНаСтроительствоПриобретение;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Расходы на жилье'");
		КонецЕсли;
		
		Если Выборка.ВзносыНаДобровольноеСтрахованиеЖизни > 0 Тогда
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РазмерВычета = Выборка.ВзносыНаДобровольноеСтрахованиеЖизни;
			НоваяСтрока.ОписаниеВычета = НСтр("ru = 'Взносы на добровольное страхование жизни'");
		КонецЕсли;
	КонецЦикла;
		
	Выборка = РезультатыЗапроса[3].Выбрать();
	Пока Выборка.Следующий() Цикл
		РазмерВычета = РазмерыВычетов[Выборка.КодВычета];
		ТекстРубли = НСтр("ru = 'р'");
		ОписаниеВычета = СтрШаблон("%1 %2. %3", РазмерВычета, ТекстРубли, Выборка.ПолноеНаименование);
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.РазмерВычета = Выборка.КоличествоДетей;
		НоваяСтрока.ОписаниеВычета = ОписаниеВычета;
	КонецЦикла;
	
	Результат = Таблица.Скопировать(, "Организация, ФизическоеЛицо");
	Результат.Свернуть("Организация, ФизическоеЛицо");
	Результат.Колонки.Добавить("ИнформацияОВычетах", Новый ОписаниеТипов("ТаблицаЗначений"));
	Для Каждого СтрокаТаблицы  Из Результат Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("Организация", СтрокаТаблицы.Организация);
		Отбор.Вставить("ФизическоеЛицо", СтрокаТаблицы.ФизическоеЛицо);
		НайденныеСтроки = Таблица.НайтиСтроки(Отбор);
		ТаблицаИнформацияОВычетах = НоваяТаблицаИнформацияОВычетах();
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НоваяСтрока = ТаблицаИнформацияОВычетах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
		КонецЦикла;
		СтрокаТаблицы.ИнформацияОВычетах = ТаблицаИнформацияОВычетах;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НоваяТаблицаИнформацияОВычетах()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("РазмерВычета", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("ОписаниеВычета", Новый ОписаниеТипов("Строка"));
	Возврат Результат;
	
КонецФункции

Функция ТаблицаДанныхЗаявокСПриложеннымиФайлами(ПараметрыПодключения, МассивОтбора, ШаблонНаименования)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заявки", МассивОтбора);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаКабинетСотрудника.Ссылка КАК ЗаявкаСсылка,
	|	ЗаявкаКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗаявкаКабинетСотрудника.Организация КАК Организация,
	|	ЗаявкаКабинетСотрудника.ОтветПоЗаявке КАК Комментарий,
	|	ЗаявкаКабинетСотрудника.ФайлОтвета КАК ФайлОтвета,
	|	ЗаявкаКабинетСотрудника.ИдентификаторЗаявки КАК ИдентификаторЗапроса,
	|	ЗаявкаКабинетСотрудника.ФайлОтвета.Расширение КАК ФайлОтветаРасширение
	|ИЗ
	|	БизнесПроцесс.ЗаявкаКабинетСотрудника КАК ЗаявкаКабинетСотрудника
	|ГДЕ
	|	ЗаявкаКабинетСотрудника.Ссылка В(&Заявки)
	|	И ЗаявкаКабинетСотрудника.ФайлОтвета <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛицаПрисоединенныеФайлы.ПустаяСсылка)";
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	ДополнитьТаблицуКолонкамиВложения(ТаблицаДанных);
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СтрокаТЗ.ИдентификаторВложения 	= ИдентификаторПриложенногоФайла(ПараметрыПодключения, СтрокаТЗ.ФайлОтвета, СтрокаТЗ.ФайлОтветаРасширение);
		СтрокаТЗ.РасширениеВложения 	= СтрокаТЗ.ФайлОтветаРасширение;
		СтрокаТЗ.НаименованиеВложения 	= СтрШаблон(ШаблонНаименования, Строка(СтрокаТЗ.ФизическоеЛицо), СтрокаТЗ.ФайлОтветаРасширение);
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

Функция НовыйРезультатПолученияДанных()
	
	Результат = Новый Структура;
	Результат.Вставить("ВыгрузитьДанные", Новый Массив);
	Результат.Вставить("УдалитьДанные", Новый Массив);
	Возврат Результат;
	
КонецФункции

Функция НоваяТаблицаИзОписания(ОписаниеПолей, СвойствоИменКолонок)
	
	Результат = Новый ТаблицаЗначений;
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		Если ОписаниеПоля["ТипПоля"] = Неопределено Тогда
			ДобавитьКолонкиТаблицыПоОписанию(Результат, ОписаниеПоля["ОписаниеПолей"]);
		Иначе
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ОписаниеПоля["ТипПоля"]);
			Результат.Колонки.Добавить(ОписаниеПоля[СвойствоИменКолонок], Новый ОписаниеТипов(МассивТипов));
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция МассивИзТаблицы(ТаблицаДанных, ОписаниеПолей)
	
	Результат = Новый Массив;
	Для Каждого СтрокаТаблицыДанных Из ТаблицаДанных Цикл
		Результат.Добавить(СтруктураИзСтрокиТаблицы(СтрокаТаблицыДанных, ОписаниеПолей));
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция СтруктураИзСтрокиТаблицы(СтрокаТаблицыДанных, ОписаниеПолей)
	
	Результат = Новый Структура;
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		Если ОписаниеПоля["ТипПоля"] = Неопределено Тогда
			Значение = СтруктураИзСтрокиТаблицы(СтрокаТаблицыДанных, ОписаниеПоля["ОписаниеПолей"]);
		ИначеЕсли ОписаниеПоля["ТипПоля"] = Тип("ТаблицаЗначений") Тогда
			Значение = ЗначениеПоИмениПоля(СтрокаТаблицыДанных, ОписаниеПоля["ИмяПоляКонфигурации"]);
			Если ЗначениеЗаполнено(Значение) Тогда
				Значение = МассивИзТаблицы(Значение, ОписаниеПоля["ОписаниеПолей"]);
			КонецЕсли;
		Иначе
			Значение = ЗначениеПоИмениПоля(СтрокаТаблицыДанных, ОписаниеПоля["ИмяПоляКонфигурации"]);
		КонецЕсли;
		Если ЗначениеЗаполнено(Значение)
				Или ТипЗнч(Значение) = Тип("Число") Тогда
			Результат.Вставить(ОписаниеПоля["ИмяПоляСервиса"], Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ВыбираемыеПоля(СоответствиеПолей)
	
	ВыбираемыеПоля = Новый Массив;
	Для Каждого КлючИЗначение Из СоответствиеПолей Цикл
		Поле = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(Поле) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Поле) = Тип("Соответствие") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВыбираемыеПоля, ВыбираемыеПоля(Поле), Истина);
		ИначеЕсли ВыбираемыеПоля.Найти(Поле) = Неопределено Тогда
			ВыбираемыеПоля.Добавить(Поле);
		КонецЕсли;
	КонецЦикла;
	Возврат ВыбираемыеПоля;
	
КонецФункции

Функция ТаблицаДанныхВМассив(ТаблицаДанных, СоответствиеПолей)
	
	МассивЭлементов = Новый Массив;
	Для Каждого СтрокаТаблицыДанных Из ТаблицаДанных Цикл
		МассивЭлементов.Добавить(СтрокаТаблицыДанныхВСтруктуру(СтрокаТаблицыДанных, СоответствиеПолей));
	КонецЦикла;
	Возврат МассивЭлементов;
	
КонецФункции

Функция СтрокаТаблицыДанныхВСтруктуру(СтрокаТаблицыДанных, СоответствиеПолей)
	
	Результат = Новый Структура;
	Для Каждого КлючИЗначение Из СоответствиеПолей Цикл
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Соответствие") Тогда
			Значение = СтрокаТаблицыДанныхВСтруктуру(СтрокаТаблицыДанных, КлючИЗначение.Значение);
		Иначе
			Значение = ЗначениеПоИмениПоля(СтрокаТаблицыДанных, КлючИЗначение.Значение);
		КонецЕсли;
		Если ЗначениеЗаполнено(Значение) Тогда
			Результат.Вставить(КлючИЗначение.Ключ, Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция ЗначениеПоИмениПоля(СтрокаТаблицыДанных, Поле)
	
	Если Не ЗначениеЗаполнено(Поле) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ВладелецСтроки = СтрокаТаблицыДанных.Владелец();
	Если ВладелецСтроки.Колонки.Найти(Поле) = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат СтрокаТаблицыДанных[Поле];
	
КонецФункции

Процедура ДополнитьТаблицуКолонкамиВложения(ТаблицаДанных)
	
	ТаблицаДанных.Колонки.Добавить("НаименованиеВложения",	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(150)));
	ТаблицаДанных.Колонки.Добавить("РасширениеВложения",	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(10)));
	ТаблицаДанных.Колонки.Добавить("ИдентификаторВложения",	Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	
КонецПроцедуры

#КонецОбласти

#Область Публикация

// Публикация расчетных листков из формы интеграции с сервисом.
Процедура ОпубликоватьРасчетныеЛистыЗаМесяцВФоне(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Месяц", ПараметрыПроцедуры.Месяц);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОНЕЦПЕРИОДА(&Месяц, ДЕНЬ) КАК Период,
	|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации КАК Сотрудник
	|ПОМЕСТИТЬ ВТСотрудникиКПубликации
	|ИЗ
	|	РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъектыКабинетСотрудника
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|		ПО ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации = НачисленияУдержанияПоСотрудникам.Сотрудник
	|			И (НАЧАЛОПЕРИОДА(НачисленияУдержанияПоСотрудникам.Период, МЕСЯЦ) = &Месяц)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника КАК ОшибкиЗаполнения
	|			ПО Сотрудники.ФизическоеЛицо = ОшибкиЗаполнения.ОбъектПубликации
	|		ПО ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации = Сотрудники.Ссылка
	|ГДЕ
	|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации ССЫЛКА Справочник.Сотрудники
	|	И ОшибкиЗаполнения.ЕстьОшибки ЕСТЬ NULL";
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиКПубликации") Тогда
		ВызватьИсключение НСтр("ru = 'Нет данных для выгрузки.'");
	КонецЕсли;
	
	ОписательВТ = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиКПубликации");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВТ, Ложь, "Организация");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КадровыеДанные.Организация КАК Организация,
	|	КадровыеДанные.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	КадровыеДанные.Организация,
	|	КадровыеДанные.ФизическоеЛицо";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
		
		Организация = Выборка.Организация;
		
		СписокФизическихЛиц = Новый Массив;
		Пока Выборка.Следующий() Цикл
			СписокФизическихЛиц.Добавить(Выборка.ФизическоеЛицо);
		КонецЦикла;
		
		ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПубликацияРасчетныхЛистов());
		
		ВыгрузитьРасчетныеЛисты(ПараметрыПодключения,
			Выборка.Организация,
			ПараметрыПроцедуры.Месяц,
			СписокФизическихЛиц);
		
		Если ЗначениеЗаполнено(ПараметрыПодключения.Ошибки) Тогда
			ВызватьИсключение НСтр("ru = 'При выгрузке данных в сервис личных кабинетов произошли ошибки.'");
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ОпубликоватьРасчетныеЛистыВФоне(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПубликацияРасчетныхЛистов());
	ВыгрузитьРасчетныеЛисты(ПараметрыПодключения,
		ПараметрыПроцедуры.Организация,
		ПараметрыПроцедуры.МесяцРасчетныхЛистов,
		ПараметрыПроцедуры.СписокФизическихЛиц);
		
	Если ЗначениеЗаполнено(ПараметрыПодключения.Ошибки) Тогда
		ВызватьИсключение НСтр("ru = 'При выгрузке данных в сервис личных кабинетов произошли ошибки.'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпубликоватьОтветственноеЛицоФоновоеЗадание(Параметры, АдресХранилища) Экспорт

	Результат = Новый Структура("СообщениеОбОшибке");
	
	БылиОшибки = ОпубликоватьОтветственноеЛицо(Параметры.Ответственный);
	Если БылиОшибки Тогда
		Результат.СообщениеОбОшибке = НСтр("ru='Не удалось опубликовать ответственное лицо.'");
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

Функция ОпубликоватьОтветственноеЛицо(Ответственный) Экспорт

	// Регистрируем ответственного к публикации.
	МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаКабинетСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ФизическоеЛицо = Ответственный;
	МенеджерЗаписи.Записать();
	
	ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПередачаИзменений());
	РезультатВыгрузки = РезультатВыгрузкиФизическихЛиц(ПараметрыПодключения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ответственный));
	Если РезультатВыгрузки.БылиОшибки Тогда
		МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПредметПубликации = Ответственный;
		МенеджерЗаписи.БылаОшибкаПриПубликации = Истина;
		МенеджерЗаписи.ОписаниеОшибки = РезультатВыгрузки.НеВыгружено[0].Значение;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
	Возврат РезультатВыгрузки.БылиОшибки;

КонецФункции

Процедура ОпубликоватьИзменения(ПараметрыПодключения, БылиОшибки)
	
	ОпубликоватьЗарегистрированныеИзменения(ПараметрыПодключения, БылиОшибки);
	ОпубликоватьПрочиеИзменения(ПараметрыПодключения, БылиОшибки);
	
КонецПроцедуры

Процедура ОпубликоватьЗарегистрированныеИзменения(ПараметрыПодключения, БылиОшибки)
	
	ОтложенноеОбновлениеСписковПубликуемыхОбъектов();
	
	// Таблица с данными зарегистрированными к отправке.
	// Колонки таблицы: ПредметПубликации, ВерсияДанных, Публикуется, ТипДанных
	ТаблицаИзменений = ИзмененияДляПубликации();
	Если ТаблицаИзменений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИзменений.Индексы.Добавить("ТипДанных,Публикуется");
	Отбор = Новый Структура("ТипДанных,Публикуется");
	Отбор.Публикуется = Истина;
	ТипыДанных = ТипыПубликуемыхДанных();
	
	Результат = НовыйРезультатВыгрузки();
	
	Отбор.ТипДанных = ТипыДанных["ФизическиеЛица"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиФизическихЛиц(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	Отбор.ТипДанных = ТипыДанных["Организации"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиОрганизаций(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	Отбор.ТипДанных = ТипыДанных["СтруктураПредприятия"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиСтруктурыПредприятия(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	Отбор.ТипДанных = ТипыДанных["Должности"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиДолжностей(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	Отбор.ТипДанных = ТипыДанных["ШтатноеРасписание"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиШтатногоРасписания(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	Отбор.ТипДанных = ТипыДанных["Сотрудники"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиСотрудников(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	Отбор.ТипДанных = ТипыДанных["ЗаявкаКабинетСотрудника"];
	ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
	Если ТаблицаДляВыгрузки.Количество()>0 Тогда
		РезультатВыгрузки = РезультатВыгрузкиЗаявок(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ПредметПубликации"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
		Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
	КонецЕсли;
	
	ОтменитьРегистрациюИзмененийПоРезультатамПубликации(ТаблицаИзменений, Результат);
	
	БылиОшибки = Результат.БылиОшибки;
	
КонецПроцедуры

Процедура ОпубликоватьПрочиеИзменения(ПараметрыПодключения, БылиОшибки)
	
	Отбор = Новый Структура("Публикуется");
	Отбор.Публикуется = Истина;
	
	// Сведения о вычетах НДФЛ.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Вычеты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Вычеты.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА ФизическиеЛица.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется
	|ИЗ
	|	РегистрСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ КАК Вычеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛица
	|		ПО Вычеты.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника КАК ОшибкиЗаполнения
	|		ПО Вычеты.ФизическоеЛицо = ОшибкиЗаполнения.ОбъектПубликации
	|ГДЕ
	|	ОшибкиЗаполнения.ЕстьОшибки ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Результат = НовыйРезультатВыгрузки();
		
		ТаблицаИзменений = РезультатЗапроса.Выгрузить();
		ТаблицаИзменений.Индексы.Добавить("Публикуется");
		ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
		Если ТаблицаДляВыгрузки.Количество()>0 Тогда
			РезультатВыгрузки = РезультатВыгрузкиПрименяемыхВычетов(ПараметрыПодключения, ТаблицаДляВыгрузки.ВыгрузитьКолонку("ФизическоеЛицо"));
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
			// Физические лица по которым заказали публикацию вычетов, но они не имеют вычетов.
			ФизическиеЛицаБезВычетов = РезультатВыгрузки.ФизическиеЛицаБезВычетов;
			// Включим этих физических лиц в список выгруженных.
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, ФизическиеЛицаБезВычетов);
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
			Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
		КонецЕсли;
		
		Если ТаблицаИзменений.Количество() > 0 Тогда
			
			НачатьТранзакцию();
			Попытка
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных = ТаблицаИзменений;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ФизическоеЛицо", "ФизическоеЛицо");
				Блокировка.Заблокировать();
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("ТаблицаИзменений", ТаблицаИзменений);
				Запрос.УстановитьПараметр("Выгружено", Результат.Выгружено);
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ТаблицаИзменений.ФизическоеЛицо КАК ФизическоеЛицо,
				|	ТаблицаИзменений.ВерсияДанных КАК ВерсияДанных,
				|	ТаблицаИзменений.Публикуется КАК Публикуется
				|ПОМЕСТИТЬ ВТОтменитьИзменения
				|ИЗ
				|	&ТаблицаИзменений КАК ТаблицаИзменений
				|ГДЕ
				|	(ТаблицаИзменений.ФизическоеЛицо В (&Выгружено)
				|			ИЛИ НЕ ТаблицаИзменений.Публикуется)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ОтменитьИзменения.ФизическоеЛицо КАК ФизическоеЛицо
				|ИЗ
				|	ВТОтменитьИзменения КАК ОтменитьИзменения
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ КАК ФизическиеЛица
				|		ПО ОтменитьИзменения.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
				|			И (ОтменитьИзменения.ВерсияДанных = ФизическиеЛица.ВерсияДанных
				|				ИЛИ НЕ ОтменитьИзменения.Публикуется)";
				
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьМенеджерЗаписи();
					МенеджерЗаписи.ФизическоеЛицо = Выборка.ФизическоеЛицо;
					МенеджерЗаписи.Удалить();
				КонецЦикла;
				
				Если Результат.БылиОшибки Тогда
					Для Каждого Ошибка Из Результат.НеВыгружено Цикл
						МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьМенеджерЗаписи();
						МенеджерЗаписи.ФизическоеЛицо = Ошибка.Ключ;
						МенеджерЗаписи.Прочитать();
						МенеджерЗаписи.БылаОшибкаПриПубликации = Истина;
						МенеджерЗаписи.ОписаниеОшибки = Ошибка.Значение;
						МенеджерЗаписи.Записать();
					КонецЦикла;
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				ЗаписатьОшибкуВыгрузкиИзменений("", ОписаниеОшибки());
				Результат.БылиОшибки = Истина;
			КонецПопытки;
			
		КонецЕсли;
		
		БылиОшибки = ?(Результат.БылиОшибки, Истина, БылиОшибки);
		
	КонецЕсли;
	
	РезультатЗапроса = КабинетСотрудникаВнутренний.ДанныеДляОбновленияПубликацииПравНаОтпуск();
	Если РезультатЗапроса <> Неопределено И Не РезультатЗапроса.Пустой() Тогда
		
		Результат = НовыйРезультатВыгрузки();
		
		ТаблицаИзменений = РезультатЗапроса.Выгрузить();
		ТаблицаИзменений.Индексы.Добавить("Публикуется");
		ТаблицаДляВыгрузки = ТаблицаИзменений.Скопировать(Отбор);
		Если ТаблицаДляВыгрузки.Количество()>0 Тогда
			
			Данные = ДанныеОбОтпусках(ТаблицаДляВыгрузки.ВыгрузитьКолонку("Сотрудник"));
			
			РезультатВыгрузки = РезультатВыгрузкиИнформацииОбОтпусках(ПараметрыПодключения, Данные.ИнформацияОбОтпусках);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено);
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
			Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
			
			РезультатВыгрузки = РезультатВыгрузкиОстатковОтпусковБудущихПериодов(ПараметрыПодключения, Данные.ОстаткиОтпусков);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат.Выгружено, РезультатВыгрузки.Выгружено, Истина);
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Результат.НеВыгружено, РезультатВыгрузки.НеВыгружено, Истина);
			Результат.БылиОшибки = ?(РезультатВыгрузки.БылиОшибки, Истина, Результат.БылиОшибки);
			
		КонецЕсли;
		
		КабинетСотрудникаВнутренний.ОтменитьРегистрациюСотрудникиДляОбновленияПубликацииПравНаОтпуск(ТаблицаИзменений, Результат);
		БылиОшибки = ?(Результат.БылиОшибки, Истина, БылиОшибки);
		
	КонецЕсли;

КонецПроцедуры

Функция ИзмененияДляПубликации()

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации КАК ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных КАК ВерсияДанных
	|ПОМЕСТИТЬ ВТВсеИзменения
	|ИЗ
	|	РегистрСведений.ИзмененияДляСервисаКабинетСотрудника КАК ВсеИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации КАК ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА ПубликуемыеОбъекты.ОбъектПубликации ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется
	|ПОМЕСТИТЬ ВТИзмененияПрочихОбъектов
	|ИЗ
	|	ВТВсеИзменения КАК ВсеИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО ВсеИзменения.ПредметПубликации = ПубликуемыеОбъекты.ОбъектПубликации
	|ГДЕ
	|	НЕ ВсеИзменения.ПредметПубликации ССЫЛКА Справочник.ФизическиеЛица
	|	И НЕ ВсеИзменения.ПредметПубликации ССЫЛКА БизнесПроцесс.ЗаявкаКабинетСотрудника";
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТВсеИзменения") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Изменения.ПредметПубликации КАК ПредметПубликации,
	|	Изменения.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР
	|		КОГДА ПубликуемыеОбъекты.ФизическоеЛицо ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Публикуется,
	|	ТИПЗНАЧЕНИЯ(Изменения.ПредметПубликации) КАК ТипДанных
	|ИЗ
	|	ВТВсеИзменения КАК Изменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО Изменения.ПредметПубликации = ПубликуемыеОбъекты.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника КАК ОшибкиЗаполнения
	|		ПО Изменения.ПредметПубликации = ОшибкиЗаполнения.ОбъектПубликации
	|ГДЕ
	|	Изменения.ПредметПубликации ССЫЛКА Справочник.ФизическиеЛица
	|	И ОшибкиЗаполнения.ЕстьОшибки ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных,
	|	ИСТИНА,
	|	ТИПЗНАЧЕНИЯ(ВсеИзменения.ПредметПубликации)
	|ИЗ
	|	ВТВсеИзменения КАК ВсеИзменения
	|ГДЕ
	|	ВсеИзменения.ПредметПубликации ССЫЛКА БизнесПроцесс.ЗаявкаКабинетСотрудника
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных,
	|	ВсеИзменения.Публикуется,
	|	ТИПЗНАЧЕНИЯ(ВсеИзменения.ПредметПубликации)
	|ИЗ
	|	ВТИзмененияПрочихОбъектов КАК ВсеИзменения
	|ГДЕ
	|	НЕ ВсеИзменения.ПредметПубликации ССЫЛКА Справочник.Сотрудники
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеИзменения.ПредметПубликации,
	|	ВсеИзменения.ВерсияДанных,
	|	ВсеИзменения.Публикуется,
	|	ТИПЗНАЧЕНИЯ(ВсеИзменения.ПредметПубликации)
	|ИЗ
	|	ВТИзмененияПрочихОбъектов КАК ВсеИзменения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника КАК ОшибкиЗаполнения
	|			ПО Сотрудники.ФизическоеЛицо = ОшибкиЗаполнения.ОбъектПубликации
	|		ПО ВсеИзменения.ПредметПубликации = Сотрудники.Ссылка
	|ГДЕ
	|	ВсеИзменения.ПредметПубликации ССЫЛКА Справочник.Сотрудники
	|	И ОшибкиЗаполнения.ЕстьОшибки ЕСТЬ NULL";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ОтменитьРегистрациюИзмененийПоРезультатамПубликации(ТаблицаИзменений, РезультатВыгрузки)

	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляСервисаКабинетСотрудника");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ТаблицаИзменений;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ПредметПубликации", "ПредметПубликации");
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаИзменений", ТаблицаИзменений);
		Запрос.УстановитьПараметр("Выгружено", РезультатВыгрузки.Выгружено);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаИзменений.ПредметПубликации КАК ПредметПубликации,
		|	ТаблицаИзменений.ВерсияДанных КАК ВерсияДанных,
		|	ТаблицаИзменений.Публикуется КАК Публикуется
		|ПОМЕСТИТЬ ВТОтменитьИзменения
		|ИЗ
		|	&ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	(ТаблицаИзменений.ПредметПубликации В (&Выгружено)
		|			ИЛИ НЕ ТаблицаИзменений.Публикуется)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтменитьИзменения.ПредметПубликации КАК ПредметПубликации
		|ИЗ
		|	ВТОтменитьИзменения КАК ОтменитьИзменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляСервисаКабинетСотрудника КАК ИзмененияДляСервисаКабинетСотрудника
		|		ПО ОтменитьИзменения.ПредметПубликации = ИзмененияДляСервисаКабинетСотрудника.ПредметПубликации
		|			И (ОтменитьИзменения.ВерсияДанных = ИзмененияДляСервисаКабинетСотрудника.ВерсияДанных
		|				ИЛИ НЕ ОтменитьИзменения.Публикуется)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.ПредметПубликации = Выборка.ПредметПубликации;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		Если РезультатВыгрузки.БылиОшибки Тогда
			Для Каждого Ошибка Из РезультатВыгрузки.НеВыгружено Цикл
				МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.ПредметПубликации = Ошибка.Ключ;
				МенеджерЗаписи.Прочитать();
				МенеджерЗаписи.БылаОшибкаПриПубликации = Истина;
				МенеджерЗаписи.ОписаниеОшибки = Ошибка.Значение;
				МенеджерЗаписи.Записать();
			КонецЦикла;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписатьОшибкуВыгрузкиИзменений("", ОписаниеОшибки());
		РезультатВыгрузки.БылиОшибки = Истина;
	КонецПопытки;

КонецФункции

Процедура ОпубликоватьУдалениеДанных(ПараметрыПодключения, БылиОшибки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыДляУдаления.ОбъектДляУдаления КАК ОбъектДляУдаления,
	|	ТИПЗНАЧЕНИЯ(ОбъектыДляУдаления.ОбъектДляУдаления) КАК ТипДанных
	|ИЗ
	|	РегистрСведений.ОбъектыДляУдаленияИзСервисаКабинетСотрудника КАК ОбъектыДляУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТипыДанных = ТипыПубликуемыхДанных();
	РесурсСервисаПоТипуДанных = Новый Соответствие;
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["ФизическиеЛица"], 		РесурсФизическиеЛица());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["Организации"], 			РесурсОрганизации());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["Должности"], 			РесурсДолжности());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["Сотрудники"], 			РесурсСотрудники());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["ШтатноеРасписание"], 	РесурсШтатноеРасписание());
	РесурсСервисаПоТипуДанных.Вставить(ТипыДанных["СтруктураПредприятия"], 	РесурсСтруктураПредприятия());
	
	УдаленныеОбъекты = Новый Массив;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		РесурсСервиса = РесурсСервисаПоТипуДанных[Выборка.ТипДанных];
		Если РесурсСервиса <> Неопределено Тогда
			Если РезультатУдаленияОбъектаИзСервиса(ПараметрыПодключения, РесурсСервиса, Выборка.ОбъектДляУдаления) Тогда
				УдаленныеОбъекты.Добавить(Выборка.ОбъектДляУдаления);
			Иначе
				БылиОшибки = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("УдаленныеОбъекты", УдаленныеОбъекты);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыДляУдаления.ОбъектДляУдаления КАК ОбъектДляУдаления
	|ИЗ
	|	РегистрСведений.ОбъектыДляУдаленияИзСервисаКабинетСотрудника КАК ОбъектыДляУдаления
	|ГДЕ
	|	НЕ ОбъектыДляУдаления.ОбъектДляУдаления В (&УдаленныеОбъекты)";
	НаборЗаписей = РегистрыСведений.ОбъектыДляУдаленияИзСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ТипыПубликуемыхДанных()

	ОписаниеТиповДанных = Новый Соответствие;
	ОписаниеТиповДанных.Вставить("ФизическиеЛица", 			Тип("СправочникСсылка.ФизическиеЛица"));
	ОписаниеТиповДанных.Вставить("Организации", 			Тип("СправочникСсылка.Организации"));
	ОписаниеТиповДанных.Вставить("Должности", 				Тип("СправочникСсылка.Должности"));
	ОписаниеТиповДанных.Вставить("Сотрудники", 				Тип("СправочникСсылка.Сотрудники"));
	ОписаниеТиповДанных.Вставить("ЗаявкаКабинетСотрудника", Тип("БизнесПроцессСсылка.ЗаявкаКабинетСотрудника"));
	ОписаниеТиповДанных.Вставить("ШтатноеРасписание", 	 	КабинетСотрудникаВнутренний.ТипШтатноеРасписание());
	ОписаниеТиповДанных.Вставить("СтруктураПредприятия", 	КабинетСотрудникаВнутренний.ТипСтруктураПредприятия());
	
	Возврат ОписаниеТиповДанных;
	
КонецФункции

Функция НовыйРезультатВыгрузки()
	
	Результат = Новый Структура;
	Результат.Вставить("Выгружено", Новый Массив);
	Результат.Вставить("НеВыгружено", Новый Соответствие);
	Результат.Вставить("БылиОшибки", Ложь);
	Возврат Результат;
	
КонецФункции

Функция СформироватьJSON(Значение)
	
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON;
	НастройкиСериализацииJSON.ВариантЗаписиДаты = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	ИмяФайла = "";
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON( ,Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, Значение, НастройкиСериализацииJSON, "ПреобразованиеJSON", ОбщегоНазначения.ОбщийМодуль("КабинетСотрудника"));
	Результат = ЗаписьJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Функция ПреобразованиеJSON(Знач Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Результат = Неопределено;

	Если ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ПолФизическогоЛица") Тогда
		Результат = ПолФизическогоЛица(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ВидыЗанятости") Тогда
		Результат = ВидЗанятостиСотрудника(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.ВидыДокументовФизическихЛиц") Тогда
		Результат = ВидДокументаФизическогоЛица(Значение);
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение))
			Или Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение))
			Или БизнесПроцессы.ТипВсеСсылки().СодержитТип(ТипЗнч(Значение)) Тогда
		Если Не Значение.Пустая() Тогда
			Результат = Строка(Значение.УникальныйИдентификатор());
		КонецЕсли;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьОшибкуВыгрузкиИзменений(ТипИзменений, ОписаниеОшибки) Экспорт
	
	ТекстОшибки = НСтр(
	"ru = 'Произошла ошибка при выгрузке %1
	|Описание ошибки:
	|%2'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, ТипИзменений, ОписаниеОшибки);
	ЗаписьЖурналаРегистрации(ИмяСобытияЖРПередачаИзменений(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	
КонецПроцедуры

Функция РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, Знач РесурсСервиса, Знач Данные, ПолеКлюча, РезультатВыгрузки = Неопределено)

	Если РезультатВыгрузки = Неопределено Тогда
		РезультатВыгрузки = НовыйРезультатВыгрузки();
	КонецЕсли;
	
	Если Не ТипЗнч(Данные) = Тип("Массив") Тогда
		ВызватьИсключение НСтр("ru = 'Произошла ошибка при выгрузки коллекции в сервис. Ожидается массив.'");
	КонецЕсли;
	
	РазмерПакета = 100;
	КоличествоЭлементов = Данные.Количество();
	
	Если КоличествоЭлементов > РазмерПакета Тогда
		ДанныеПакета = Новый Массив;
		Пока РазмерПакета > 0 Цикл
			ДанныеПакета.Добавить(Данные[0]);
			Данные.Удалить(0);
			РазмерПакета = РазмерПакета - 1;
		КонецЦикла;
		РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, ПолеКлюча, РезультатВыгрузки);
	Иначе
		ДанныеПакета = Данные;
	КонецЕсли;
	
	СтрокаТела = СформироватьJSON(ДанныеПакета);
	Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, "PUT", СтрокаТела);

	Если Ответ.КодСостояния = 200 Или Ответ.КодСостояния = 204 Тогда
		Для Каждого СтрокаДанных Из ДанныеПакета Цикл
			РезультатВыгрузки.Выгружено.Добавить(СтрокаДанных[ПолеКлюча]);
		КонецЦикла;
	ИначеЕсли Ответ.КодСостояния = 400 Тогда
		РезультатВыгрузки.БылиОшибки = Истина;
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
		
		Результат = ОбъектОтвета["result"];
		Если Не Результат = Неопределено Тогда
			Для Каждого ЭлементРезультат Из Результат Цикл
				КлючОбъекта = ДанныеПакета[Число(ЭлементРезультат["position"]) - 1][ПолеКлюча];
				РезультатВыгрузки.Выгружено.Добавить(КлючОбъекта);
			КонецЦикла;
		КонецЕсли;
		
		Ошибки = ОбъектОтвета["errors"];
		Если Не Ошибки = Неопределено Тогда
			Для Каждого ЭлементОшибки Из Ошибки Цикл
				КлючОбъекта = ДанныеПакета[Число(ЭлементОшибки["position"]) - 1][ПолеКлюча];
				РезультатВыгрузки.НеВыгружено.Вставить(КлючОбъекта, ОписаниеОшибкиВыгрузки(ЭлементОшибки));
			КонецЦикла;
		КонецЕсли;
	Иначе
		ВызватьИсключение ТекстОшибкиОбработкиЗапроса();
	КонецЕсли;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ОписаниеОшибкиВыгрузки(ОбъектОшибка)
	
	Попытка
		Результат = НСтр("ru = 'Тип ошибки: %1
						|Код ошибки: %2
						|Описание: %3
						|Подробно:'");
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Результат, ОбъектОшибка["error"]["type"], ОбъектОшибка["error"]["code"], ОбъектОшибка["error"]["description"]);
	
		Для Каждого КлючЗначение Из ОбъектОшибка["error"]["value"] Цикл
			Результат = Результат + Символы.ПС + КлючЗначение.Ключ + ": " + КлючЗначение.Значение;
		КонецЦикла;
	Исключение
		Результат = НСтр("ru = 'Неизвестное описание ошибки.'");
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция РезультатУдаленияОбъектаИзСервиса(ПараметрыПодключения, Знач РесурсСервиса, Ссылка)
	
	РесурсСервиса = СтрШаблон("%1/%2", РесурсСервиса , Строка(Ссылка.УникальныйИдентификатор()));
	
	Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, "DELETE");
	Если Ответ.КодСостояния = 204 Или Ответ.КодСостояния = 404 Тогда
		Возврат Истина;
	ИначеЕсли Ответ.КодСостояния = 400 Тогда
		Возврат Ложь;
	Иначе
		ВызватьИсключение ТекстОшибкиОбработкиЗапроса();
	КонецЕсли;
	
КонецФункции

Процедура ВыгрузитьВложенияВСервис(ПараметрыПодключения, ТаблицаДанных, Вложения, ИмяПоляВладельца, НазваниеВложения, Расширение)
	
	РесурсСервиса = ЗаполнитьПараметрыШаблонаURL(ПараметрыПодключения.ШаблоныРесурсов.Получить("Файлы"), Новый Структура("ID", ""));
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		ХранилищеВложения = Вложения.Получить(СтрокаТаблицы[ИмяПоляВладельца]);
		Если Не ЗначениеЗаполнено(ХранилищеВложения) Тогда
			Продолжить;
		КонецЕсли;
		Вложение = ХранилищеВложения.Получить();
		Если ТипЗнч(Вложение) <> Тип("ДвоичныеДанные") Тогда
			Возврат;
		КонецЕсли;
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
		Картинка = Новый Картинка(Вложение);
		Картинка.Записать(ИмяВременногоФайла);
		Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, "POST",, ИмяВременногоФайла);
		УдалитьФайлы(ИмяВременногоФайла);
		Если Ответ.КодСостояния >= 300 Тогда
			ВызватьИсключение НСтр("ru = 'Произошла ошибка при отправке файлов в сервис. Подробности в журнале регистрации.'");
		КонецЕсли;
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		ОбъектОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
		СтрокаТаблицы.ИдентификаторВложения = ОбъектОтвета.Получить("fileID");
		СтрокаТаблицы.РасширениеВложения = Расширение;
		СтрокаТаблицы.НаименованиеВложения = Строка(СтрокаТаблицы[ИмяПоляВладельца]) + " " + НазваниеВложения + "." + Расширение;
	КонецЦикла;
	
КонецПроцедуры

Функция СтатусЗапросаУстановлен(ПараметрыПодключения, Знач РесурсСервиса, ИдентификаторЗапроса, Статус)
	
	Результат = Истина;
	РесурсСервиса = РесурсСервиса + "/" + ИдентификаторЗапроса + "/status?status=" + Статус;
	Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, "PUT");
	Если Ответ.КодСостояния >= 300 Тогда
		Результат = Ложь;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

#Область ПроцедурыВыгрузкиОбъектовПоТипам

Функция РезультатВыгрузкиФизическихЛиц(ПараметрыПодключения, Список)
	
	Результат = НовыйРезультатВыгрузки();
	РесурсСервиса = РесурсФизическиеЛица();
	Данные = ДанныеФизическихЛиц(ПараметрыПодключения, Список);
	
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные.ВыгрузитьДанные, "ID");
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиОрганизаций(ПараметрыПодключения, Список)
	
	РесурсСервиса = РесурсОрганизации();
	Данные = ДанныеОрганизаций(ПараметрыПодключения, Список);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, "ID");
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиСтруктурыПредприятия(ПараметрыПодключения, Список)
	
	РесурсСервиса = РесурсСтруктураПредприятия();
	Данные = ДанныеСтруктурыПредприятия(ПараметрыПодключения, Список);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, "ID");
		
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиДолжностей(ПараметрыПодключения, Список)
	
	РесурсСервиса = РесурсДолжности();
	Данные = ДанныеДолжностей(ПараметрыПодключения, Список);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, "ID");
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиШтатногоРасписания(ПараметрыПодключения, Список)

	РесурсСервиса = РесурсШтатноеРасписание();
	Данные = ДанныеШтатногоРасписания(ПараметрыПодключения, Список);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, "ID");
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиСотрудников(ПараметрыПодключения, Список)
	
	РесурсСервиса = РесурсСотрудники();
	Данные = ДанныеСотрудников(ПараметрыПодключения, Список);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные.ВыгрузитьДанные, "ID");
	
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиИнформацииОбОтпусках(ПараметрыПодключения, Данные)
	
	РесурсСервиса = РесурсИнформацияОбОтпуске();
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, "personID");
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиОстатковОтпусковБудущихПериодов(ПараметрыПодключения, Данные)
	
	РесурсСервиса = РесурсБудущиеИзмененияОстатковОтпусков();
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, Данные, "personID");
	Возврат Результат;
	
КонецФункции

Функция РезультатВыгрузкиПрименяемыхВычетов(ПараметрыПодключения, Список)
	
	РесурсСервиса = РесурсПрименяемыеВычеты();
	СведенияОВычетах = ДанныеОВычетах(ПараметрыПодключения, Список);
	Результат = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, СведенияОВычетах.Данные, "personID");
	Результат.Вставить("ФизическиеЛицаБезВычетов", СведенияОВычетах.ФизическиеЛицаБезВычетов);
	
	Возврат Результат;
	
КонецФункции

Процедура ВыгрузитьРасчетныеЛисты(ПараметрыПодключения, Организация, МесяцРасчетныхЛистов, СписокФизическихЛиц)
	
	МесяцРасчетныхЛистов = НачалоМесяца(МесяцРасчетныхЛистов);
	Данные = ДанныеРасчетныхЛистов(Организация, МесяцРасчетныхЛистов, СписокФизическихЛиц);
	
	Если Не ЗначениеЗаполнено(Данные) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
		ПараметрыПодключения,
		РесурсСоставныеЧастиЗарплаты(),
		Данные.СоставныеЧастиЗарплаты,
		"ID");
	
	Если РезультатВыгрузки.БылиОшибки Тогда
		ВызватьИсключение НСтр("ru = 'При выгрузке составных частей зарплаты возникли ошибки. Подробности в журнале регистрации.'");
	КонецЕсли;

	ШаблонНаименованияФайла = НСтр("ru = '%1 Расчетный лист %2.%3'");
	РасширениеФайла = "pdf";
	Для Каждого СтруктураИнформацияОЗарплате Из Данные.ИнформацияОЗарплате Цикл
		РасчетныйЛистДокумент = Данные.РасчетныеЛистыДокументы.Получить(СтруктураИнформацияОЗарплате.personID);
		Если РасчетныйЛистДокумент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураПриложенныйФайл = Новый Структура;
		СтруктураПриложенныйФайл.Вставить("fileID",
			ИдентификаторПриложенногоФайла(ПараметрыПодключения, РасчетныйЛистДокумент, РасширениеФайла));
		СтруктураПриложенныйФайл.Вставить("name", СтрШаблон(ШаблонНаименованияФайла,
			СтруктураИнформацияОЗарплате.personID, Формат(МесяцРасчетныхЛистов, "ДФ='MMMM yyyy'"), РасширениеФайла));
		СтруктураПриложенныйФайл.Вставить("extension", РасширениеФайла);
		СтруктураИнформацияОЗарплате.Вставить("attachment", СтруктураПриложенныйФайл);
	КонецЦикла;
	
	РезультатВыгрузки = РезультатВыгрузкиКоллекцииВСервис(
		ПараметрыПодключения,
		РесурсРасчетныеЛисты(),
		Данные.ИнформацияОЗарплате,
		"personID");
		
	Успешно = Новый Массив;
		
	Для Каждого ЭлементДанных Из Данные.ИнформацияОЗарплате Цикл
		ЗаписатьРезультатВыгрузкиРасчетныхЛистов(
			Организация,
			ЭлементДанных.personID,
			МесяцРасчетныхЛистов,
			Перечисления.СостоянияРасчетныхЛистковКабинетСотрудника.Опубликован);
		Успешно.Добавить(ЭлементДанных.personID);
	КонецЦикла;
	
	Для Каждого ФизическоеЛицо Из СписокФизическихЛиц Цикл
		Если Не Успешно.Найти(ФизическоеЛицо) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьРезультатВыгрузкиРасчетныхЛистов(
			Организация,
			ФизическоеЛицо,
			МесяцРасчетныхЛистов,
			Перечисления.СостоянияРасчетныхЛистковКабинетСотрудника.ОшибкаОбработки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьРезультатВыгрузкиРасчетныхЛистов(Организация, ФизическоеЛицо, МесяцРасчетныхЛистов, СостояниеПубликации)
	
	МенеджерЗаписи = РегистрыСведений.РасчетныеЛисткиКабинетСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.ФизическоеЛицо = ФизическоеЛицо;
	МенеджерЗаписи.Месяц = МесяцРасчетныхЛистов;
	МенеджерЗаписи.СостояниеПубликации = СостояниеПубликации;
	МенеджерЗаписи.Ответственный = Пользователи.ТекущийПользователь();
	МенеджерЗаписи.ДатаПубликации = ТекущаяДатаСеанса();
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

Функция РезультатВыгрузкиЗаявок(ПараметрыПодключения, Список)
	
	Результат = НовыйРезультатВыгрузки();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокЗаявок", Список);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявкаКабинетСотрудника.Ссылка КАК Ссылка,
	|	ЗаявкаКабинетСотрудника.ТипЗаявки КАК ТипЗаявки,
	|	ЗаявкаКабинетСотрудника.ИдентификаторЗаявки КАК ИдентификаторЗаявки,
	|	ЗаявкаКабинетСотрудника.СостояниеЗаявки КАК СостояниеЗаявки
	|ИЗ
	|	БизнесПроцесс.ЗаявкаКабинетСотрудника КАК ЗаявкаКабинетСотрудника
	|ГДЕ
	|	ЗаявкаКабинетСотрудника.Ссылка В(&СписокЗаявок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипЗаявки";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИдентификаторыВыгруженныхЗаявок = Новый Массив;
	ИдентификаторыЗаявок = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ТипЗаявки") Цикл
		
		ТипЗаявки = Выборка.ТипЗаявки;
		
		ДанныеДляВыгрузки = Новый ТаблицаЗначений;
		ДанныеДляВыгрузки.Колонки.Добавить("requestID");
		
		СостоянияЗаявок = Новый Соответствие;
		ЗаявкиКОбработке = Новый Массив;
		Пока Выборка.Следующий() Цикл
			ЗаявкиКОбработке.Добавить(Выборка.Ссылка);
			НоваяСтрока = ДанныеДляВыгрузки.Добавить();
			НоваяСтрока.requestID = Выборка.ИдентификаторЗаявки;
			СостоянияЗаявок.Вставить(Выборка.ИдентификаторЗаявки, Выборка.СостояниеЗаявки);
			ИдентификаторыЗаявок.Вставить(Выборка.ИдентификаторЗаявки, Выборка.Ссылка);
		КонецЦикла;
		
		ВыгружатьФайлы = Истина;
		Если ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных Тогда
			ВыгружатьФайлы = Ложь;
			РесурсСервиса = РесурсЗапросыИзмененияЛичныхДанных();
			РесурсСтатусовСервиса = РесурсСервиса;
		ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ Тогда
			ДанныеДляВыгрузки = ДанныеОтветовНаЗапросыСправок2НДФЛ(ПараметрыПодключения, ЗаявкиКОбработке);
			РесурсСервиса = РесурсОтветыНаЗапросыСправок2НДФЛ();
			РесурсСтатусовСервиса = РесурсЗапросыСправок2НДФЛ();
		ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы Тогда
			ДанныеДляВыгрузки = ДанныеСправокСРаботы(ПараметрыПодключения, ЗаявкиКОбработке);
			РесурсСервиса = РесурсСправкиСМестаРаботы();
			РесурсСтатусовСервиса = РесурсЗапросыСправокСРаботы();
		ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск Тогда
			ВыгружатьФайлы = Ложь;
			РесурсСтатусовСервиса = РесурсЗаявленияНаОтпуск();
		ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия Тогда
			ВыгружатьФайлы = Ложь;
			РесурсСтатусовСервиса = РесурсОтсутствия();
		ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска Тогда
			ДанныеДляВыгрузки = ДанныеСправокОбОстаткеОтпуска(ПараметрыПодключения, ЗаявкиКОбработке);
			РесурсСервиса = РесурсСправкиОбОстаткеОтпуска();
			РесурсСтатусовСервиса = РесурсЗапросыСправокОбОстаткеОтпуска();
		ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты Тогда
			ВыгружатьФайлы = Ложь;
			РесурсСтатусовСервиса = РесурсЗаявленияНаНалоговыеВычеты();
		Иначе
			Продолжить;
		КонецЕсли;
		ИдентификаторыЗаявокСФайлами = Новый Соответствие;
		Если ВыгружатьФайлы Тогда
			РезультатВыгрузкиЗаявок = РезультатВыгрузкиКоллекцииВСервис(ПараметрыПодключения, РесурсСервиса, ДанныеДляВыгрузки, "requestID");
			Для каждого ЭлементМассива Из РезультатВыгрузкиЗаявок.Выгружено Цикл
				ИдентификаторыЗаявокСФайлами.Вставить(ЭлементМассива, Истина);
			КонецЦикла;
		КонецЕсли;
		Для Каждого ЭлементДанных Из ДанныеДляВыгрузки Цикл
			Если ВыгружатьФайлы И ИдентификаторыЗаявокСФайлами[ЭлементДанных.requestID] = Неопределено Тогда
				Результат.БылиОшибки = Истина;
				Продолжить;
			КонецЕсли;
			СтатусВыполнения = СостоянияЗаявок.Получить(ЭлементДанных.requestID);
			Если СтатусВыполнения = Перечисления.СостоянияЗаявокКабинетСотрудника.Отказ Тогда
				СтатусВыполнения = "rejected";
			Иначе
				СтатусВыполнения = "completed";
			КонецЕсли;
			Если СтатусЗапросаУстановлен(ПараметрыПодключения, РесурсСтатусовСервиса, ЭлементДанных.requestID, СтатусВыполнения) Тогда
				ИдентификаторыВыгруженныхЗаявок.Добавить(ЭлементДанных.requestID);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Для каждого Идентификатор Из ИдентификаторыВыгруженныхЗаявок Цикл
		ОпубликованнаяЗаявка = ИдентификаторыЗаявок[Идентификатор];
		Если ОпубликованнаяЗаявка <> Неопределено Тогда
			Результат.Выгружено.Добавить(ОпубликованнаяЗаявка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Загрузка

#Область ПроцедурыЗагрузкиИОбработкиИзменений

Процедура ЗагрузитьИзмененияИзСервиса(ПараметрыПодключения, БылиОшибки = Ложь)
	
	ТипыОбрабатываемыхЗаявок = КабинетСотрудникаВнутренний.ТипыОбрабатываемыхЗаявок();
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("Справки2НДФЛ_Запросы_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьЗапросыСправокНДФЛ(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("ЗаявленияНаОтпуск_Запросы_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьЗаявленияНаОтпуск(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("СправкиСРаботы_Запросы_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьЗапросыСправокСРаботы(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("ФизическиеЛица_Запросы_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьЗапросыИзмененияЛичныхДанных(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("СправкиОбОстаткеОтпуска_Запросы_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьЗапросыСправокОбОстаткеОтпуска(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("ЗаявленияНаНалоговыеВычеты_Запросы_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьЗаявленияНаНалоговыеВычеты(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Если ТипыОбрабатываемыхЗаявок.Найти(Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия) <> Неопределено Тогда
		Попытка
			ТипЗапроса = Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия;
			ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("Отсутствия_Изменения");
			ЗапросыПользователей = ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса);
			Если ЗначениеЗаполнено(ЗапросыПользователей) Тогда
				ОбработатьОтсутствия(ПараметрыПодключения, ЗапросыПользователей);
			КонецЕсли;
		Исключение
			БылиОшибки = Истина;
			ЗаписатьОшибкуЗагрузкиИзменений(Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьЗапросыСправокНДФЛ(ПараметрыПодключения, МассивОбъектов)
	
	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ;
	СоответствиеПолей = ОписаниеПолейЗапросовСправокНДФЛ();
	ТаблицаДанных = МассивОбъектовВТаблицу(МассивОбъектов, СоответствиеПолей);
	ТаблицаДанных.Сортировать("Версия");
	СписокФизическихЛиц = ТаблицаДанных.ВыгрузитьКолонку("ФизическоеЛицо");
	
	ВыбираемыеПоля = Новый Массив;
	ВыбираемыеПоля.Добавить("ФизическоеЛицо");
	ВыбираемыеПоля.Добавить("Сотрудник");
	
	ШаблонОписания = НСтр(
	"ru = '%1 запрашивает справку 2-НДФЛ
	|Налоговый период: %2
	|Назначение: %3
	|Комментарий сотрудника: %4
	|
	|Необходимо заполнить и провести документы, связанные с данной задачей.'");
	ТаблицаДанных.Сортировать("Версия");
	ВерсияДляЗаписи = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.НалоговыйПериод) Тогда
					НалоговыйПериод = СтрокаТаблицы.НалоговыйПериод;
				Иначе
					НалоговыйПериод = Год(ТекущаяДатаСеанса());
				КонецЕсли;
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Запрос справки 2-НДФЛ для %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				ОписаниеЗадачи = СтрШаблон(ШаблонОписания, Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(НалоговыйПериод), СтрокаТаблицы.Назначение, СтрокаТаблицы.Комментарий);
				Справка = СправкаНДФЛ(СтрокаТаблицы.ФизическоеЛицо,СтрокаТаблицы.Организация, СтрокаТаблицы.Назначение, НалоговыйПериод);
				ЗаявкаКабинета.Предмет 			= Справка;
				ЗаявкаКабинета.Наименование 	= НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		= ОписаниеЗадачи;
				ЗаявкаКабинета.Организация 		= СтрокаТаблицы.Организация;
				ЗаявкаКабинета.ФизическоеЛицо 	= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Процедура ОбработатьЗаявленияНаОтпуск(ПараметрыПодключения, МассивОбъектов)
	
	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск;
	СоответствиеПолей = ОписаниеПолейЗаявленийНаОтпуск();
	ТаблицаДанных = МассивОбъектовВТаблицу(МассивОбъектов, СоответствиеПолей);
	ТаблицаДанных.Сортировать("Версия");
	ШаблонОписания = НСтр(
	"ru = '%1 Просит предоставить %2
	|Период отпуска с %3 по %4
	|Комментарий сотрудника: %5'");
	ТаблицаДанных.Сортировать("Версия");
	ВерсияДляЗаписи = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Вложения) Тогда
					ЗагрузитьФайлыЗаявки(ПараметрыПодключения, ЗаявкаКабинета, СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.Вложения);
				КонецЕсли;
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Заявление на отпуск %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				ОписаниеЗадачи = СтрШаблон(ШаблонОписания,
						Строка(СтрокаТаблицы.ФизическоеЛицо),
						?(СтрокаТаблицы.ЗаСвойСчет, НСтр("ru = 'отпуск за свой счет'"), НСтр("ru = 'ежегодный отпуск'")),
						Формат(СтрокаТаблицы.ДатаНачала, "ДЛФ=D"),
						Формат(СтрокаТаблицы.ДатаОкончания, "ДЛФ=D"),
						СтрокаТаблицы.Комментарий);
				ЗаявкаКабинета.Предмет 			= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Наименование 	= НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		= ОписаниеЗадачи;
				ЗаявкаКабинета.ФизическоеЛицо 	= СтрокаТаблицы.ФизическоеЛицо;
				КоличествоДней = КабинетСотрудникаВнутренний.КоличествоДнейОтпускаФизическогоЛица(СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.ДатаНачала, СтрокаТаблицы.ДатаОкончания);
				ЗаявкаКабинета.КоличествоДнейОтпуска = КоличествоДней;
				ЗаявкаКабинета.ДатаНачалаОтпуска 	 = СтрокаТаблицы.ДатаНачала;
				ЗаявкаКабинета.ДатаОкончанияОтпуска  = СтрокаТаблицы.ДатаОкончания;
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Процедура ОбработатьЗапросыСправокСРаботы(ПараметрыПодключения, МассивОбъектов)

	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы;
	СоответствиеПолей = ОписаниеПолейЗапросаСправокСРаботы();
	ТаблицаДанных = МассивОбъектовВТаблицу(МассивОбъектов, СоответствиеПолей);
	ТаблицаДанных.Сортировать("Версия");
	ШаблонОписания = НСтр(
	"ru = '%1 запрашивает справку с места работы
	|Организация: %2
	|Назначение: %3
	|Комментарий сотрудника: %4
	|
	|Проверьте автоматически сформированный файл или прикрепите свой и выполните задачу.'");
	ВерсияДляЗаписи = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ТаблицаДанных);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Таблица.Организация КАК Организация
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.Ссылка КАК Сотрудник
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ФизическиеЛица.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|			И ФизическиеЛица.Организация = Сотрудники.ГоловнаяОрганизация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо";
	РезультатЗапроса = Запрос.Выполнить();
	
	ФизическиеЛица = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
		МассивСотрудников = Новый Массив;
		Пока Выборка.Следующий() Цикл
			МассивСотрудников.Добавить(Выборка.Сотрудник);
		КонецЦикла;
		ФизическиеЛица.Вставить(Выборка.ФизическоеЛицо, МассивСотрудников);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Вложения) Тогда
					ЗагрузитьФайлыЗаявки(ПараметрыПодключения, ЗаявкаКабинета, СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.Вложения);
				КонецЕсли;
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Справка с места работы %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				ОписаниеЗадачи = СтрШаблон(ШаблонОписания,
						Строка(СтрокаТаблицы.ФизическоеЛицо),
						Строка(СтрокаТаблицы.Организация),
						СтрокаТаблицы.НазначениеСтрокой,
						СтрокаТаблицы.Комментарий);
				ЗаявкаКабинета.Предмет 			= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Наименование 	= НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		= ОписаниеЗадачи;
				ЗаявкаКабинета.ФизическоеЛицо 	= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Организация 		= СтрокаТаблицы.Организация;
				ЗаявкаКабинета.ФайлОтвета = ФайлСправкиСМестаРаботы(СтрокаТаблицы.ФизическоеЛицо, ФизическиеЛица[СтрокаТаблицы.ФизическоеЛицо]);
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Процедура ОбработатьЗапросыИзмененияЛичныхДанных(ПараметрыПодключения, МассивОбъектов)
	
	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных;
	СоответствиеПолей = ОписаниеПолейЗапросаИзмененияЛичныхДанных();
	ТаблицаДанных = МассивОбъектовВТаблицу(МассивОбъектов, СоответствиеПолей);
	ТаблицаДанных.Сортировать("Версия");
	ШаблонОписания = НСтр(
	"ru = '%1 сообщает об изменении личных данных
	|Новые данные:%2'");
	ВерсияДляЗаписи = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Изменение личных данных %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				Описание = "";
				Если СтрокаТаблицы.ИзменитьФИО Тогда
					Если ЗначениеЗаполнено(СтрокаТаблицы.Фамилия) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Фамилия: %1'"), СтрокаТаблицы.Фамилия);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.Имя) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Имя: %1'"), СтрокаТаблицы.Имя);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.Отчество) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Отчество: %1'"), СтрокаТаблицы.Отчество);
					КонецЕсли;
				КонецЕсли;
				Если СтрокаТаблицы.ИзменитьДокумент Тогда
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументВид) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Вид документа: %1'"), СтрокаТаблицы.ДокументВид);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументСерия) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Серия документа: %1'"), СтрокаТаблицы.ДокументСерия);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументНомер) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Номер документа: %1'"), СтрокаТаблицы.ДокументНомер);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументДатаВыдачи) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Дата выдачи документа: %1'"), СтрокаТаблицы.ДокументДатаВыдачи);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументКемВыдан) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Кем выдан документ: %1'"), СтрокаТаблицы.ДокументКемВыдан);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументКодПодразделения) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Код подразделения выдавшего документ: %1'"), СтрокаТаблицы.ДокументКодПодразделения);
					КонецЕсли;
				КонецЕсли;
				Если СтрокаТаблицы.ИзменитьНомерТелефона Тогда
					Если ЗначениеЗаполнено(СтрокаТаблицы.ЛичныйНомерТелефона) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Личный номер телефона: %1'"), СтрокаТаблицы.ЛичныйНомерТелефона);
					КонецЕсли;
				КонецЕсли;
				Если СтрокаТаблицы.ИзменитьАдрес Тогда
					Если ЗначениеЗаполнено(СтрокаТаблицы.АдресРегистрации) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Адрес регистрации: %1'"), СтрокаТаблицы.АдресРегистрации);
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаТаблицы.АдресМестаПроживания) Тогда
						Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Адрес места проживания: %1'"), СтрокаТаблицы.АдресМестаПроживания);
					КонецЕсли;
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТаблицы.Комментарий) Тогда
					Описание = Описание + Символы.ПС + СтрШаблон(НСтр("ru = 'Комментарий сотрудника: %1'"), СтрокаТаблицы.Комментарий);
				КонецЕсли;
				ОписаниеЗадачи = СтрШаблон(ШаблонОписания, Строка(СтрокаТаблицы.ФизическоеЛицо), Описание);
				Если ЗначениеЗаполнено(СтрокаТаблицы.Вложения) Тогда
					ЗагрузитьФайлыЗаявки(ПараметрыПодключения, ЗаявкаКабинета, СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.Вложения);
				КонецЕсли;
				ЗаявкаКабинета.Предмет 			= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Наименование 	= НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		= ОписаниеЗадачи;
				ЗаявкаКабинета.ФизическоеЛицо 	= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Процедура ОбработатьЗапросыСправокОбОстаткеОтпуска(ПараметрыПодключения, ОбъектыJSON)
	
	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска;
	ОписаниеПолей = ОписаниеПолейЗапросаСправокОбОстаткеОтпуска();
	ТаблицаДанных = ТаблицаИзОбъектовJSON(ОбъектыJSON, ОписаниеПолей);
	ТаблицаДанных.Сортировать("Версия");
	ШаблонОписания = НСтр(
	"ru = '%1 запрашивает справку об остатке отпуска
	|Организация: %2
	|Месяц применения: %3'
	|
	|Проверьте автоматически сформированный файл или прикрепите свой и выполните задачу.");
	ВерсияДляЗаписи = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ТаблицаДанных);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.Ссылка КАК Сотрудник
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ФизическиеЛица.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо";
	РезультатЗапроса = Запрос.Выполнить();
	ФизическиеЛица = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
		МассивСотрудников = Новый Массив;
		Пока Выборка.Следующий() Цикл
			МассивСотрудников.Добавить(Выборка.Сотрудник);
		КонецЦикла;
		ФизическиеЛица.Вставить(Выборка.ФизическоеЛицо, МассивСотрудников);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Справка об остатке отпуска %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				ОписаниеЗадачи = НаименованиеЗадачи;
				ЗаявкаКабинета.Предмет 			= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Наименование 	= НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		= ОписаниеЗадачи;
				ЗаявкаКабинета.ФизическоеЛицо 	= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.ФайлОтвета = ФайлСправкиОбОстаткеОтпусков(СтрокаТаблицы.ФизическоеЛицо, ФизическиеЛица[СтрокаТаблицы.ФизическоеЛицо]);
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Процедура ОбработатьЗаявленияНаНалоговыеВычеты(ПараметрыПодключения, ОбъектыJSON)
	
	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты;
	ОписаниеПолей = ОписаниеПолейЗаявленийНаНалоговыеВычеты();
	ТаблицаДанных = ТаблицаИзОбъектовJSON(ОбъектыJSON, ОписаниеПолей);
	ТаблицаДанных.Сортировать("Версия");
	ШаблонОписания = НСтр(
	"ru = 'Заявление на налоговые вычеты от %1
	|Организация: %2
	|Месяц применения: %3'");
	ВерсияДляЗаписи = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Вложения) Тогда
					ЗагрузитьФайлыЗаявки(ПараметрыПодключения, ЗаявкаКабинета, СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.Вложения);
				КонецЕсли;
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Заявление на налоговые вычеты %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				ОписаниеЗадачи = СтрШаблон(ШаблонОписания, Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(СтрокаТаблицы.Организация), Строка(СтрокаТаблицы.МесяцПрименения));
				ЗаявкаКабинета.Предмет 			= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Наименование 	= НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		= ОписаниеЗадачи;
				ЗаявкаКабинета.ФизическоеЛицо 	= СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Процедура ОбработатьОтсутствия(ПараметрыПодключения, МассивОбъектов)
	
	ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия;
	СоответствиеПолей = ОписаниеПолейОтсутствия();
	ТаблицаДанных = МассивОбъектовВТаблицу(МассивОбъектов, СоответствиеПолей);
	ТаблицаДанных.Сортировать("Версия");
	ШаблонОписания = НСтр(
	"ru = '%1 просит согласовать отсутствие
	|Причина: %2
	|Дата начала: %3
	|Дата окончания: %4
	|Комментарий: %5'");
	
	ШаблонОпоздания = НСтр(
	"ru = '%1 просит согласовать отсутствие
	|Причина: %2
	|Дата: %3
	|Время прихода на работу: %4
	|Комментарий: %5'");
	
	ШаблонЛичныеДела = НСтр(
	"ru = '%1 просит согласовать отсутствие
	|Причина: %2
	|Дата: %3 с %4 по %5
	|Комментарий: %6'");
	
	ШаблонЛичныеДелаВесьДень = НСтр(
	"ru = '%1 просит согласовать отсутствие
	|Причина: %2
	|Весь день: %3
	|Комментарий: %4'");
	
	ВерсияДляЗаписи = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		НачатьТранзакцию();
		Попытка
			ЗаявкаКабинета = НоваяЗаявкаКабинета(ТипЗаявки, СтрокаТаблицы.ИдентификаторЗапроса);
			Если ЗаявкаКабинета <> Неопределено Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Вложения) Тогда
					ЗагрузитьФайлыЗаявки(ПараметрыПодключения, ЗаявкаКабинета, СтрокаТаблицы.ФизическоеЛицо, СтрокаТаблицы.Вложения);
				КонецЕсли;
				НаименованиеЗадачи = СтрШаблон(НСтр("ru = 'Согласование отсутствия %1'"), Строка(СтрокаТаблицы.ФизическоеЛицо));
				ПричинаОтсутствия = ПричинаОтсутствияСервиса(СтрокаТаблицы.Причина);
				Если СтрокаТаблицы.ВесьДень Тогда
					ДатаНачала 		= Формат(СтрокаТаблицы.ДатаНачала, "ДЛФ=D");
					ДатаОкончания 	= Формат(СтрокаТаблицы.ДатаОкончания, "ДЛФ=D");
					Если ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела И ДатаНачала = ДатаОкончания Тогда
						ОписаниеЗадачи = СтрШаблон(ШаблонЛичныеДелаВесьДень,
							Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(ПричинаОтсутствия), ДатаНачала, СтрокаТаблицы.Комментарий);
					Иначе	
						ОписаниеЗадачи = СтрШаблон(ШаблонОписания,
							Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(ПричинаОтсутствия), ДатаНачала, ДатаОкончания, СтрокаТаблицы.Комментарий);
					КонецЕсли;	
				Иначе
					Если ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Опоздание Тогда
						ДатаСобытия   = Формат(СтрокаТаблицы.ДатаОкончания, "ДЛФ=D");
						ВремяВозврата = Формат(СтрокаТаблицы.ДатаОкончания, "ДЛФ=T");
						ОписаниеЗадачи = СтрШаблон(ШаблонОпоздания,
							Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(ПричинаОтсутствия), ДатаСобытия, ВремяВозврата, СтрокаТаблицы.Комментарий);
					ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела Тогда
						ДатаСобытия   = Формат(СтрокаТаблицы.ДатаНачала, "ДЛФ=D");
						ВремяНачала    = Формат(СтрокаТаблицы.ДатаНачала, "ДЛФ=T");
						ВремяОкончания = Формат(СтрокаТаблицы.ДатаОкончания, "ДЛФ=T");
						ОписаниеЗадачи = СтрШаблон(ШаблонЛичныеДела,
							Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(ПричинаОтсутствия), ДатаСобытия, ВремяНачала, ВремяОкончания,СтрокаТаблицы.Комментарий);	
					Иначе
						ОписаниеЗадачи = СтрШаблон(ШаблонОписания,
							Строка(СтрокаТаблицы.ФизическоеЛицо), Строка(ПричинаОтсутствия), СтрокаТаблицы.ДатаНачала, СтрокаТаблицы.ДатаОкончания, СтрокаТаблицы.Комментарий);
					КонецЕсли;
				КонецЕсли;
				ЗаявкаКабинета.Предмет 			 = СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.Наименование 	 = НаименованиеЗадачи;
				ЗаявкаКабинета.Содержание 		 = ОписаниеЗадачи;
				ЗаявкаКабинета.ФизическоеЛицо 	 = СтрокаТаблицы.ФизическоеЛицо;
				ЗаявкаКабинета.ПричинаОтсутствия = ПричинаОтсутствия;
				ЗаявкаКабинета.Записать();
				ЗаявкаКабинета.Старт();
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
			ВызватьИсключение;
		КонецПопытки;
		ВерсияДляЗаписи = СтрокаТаблицы.Версия;
	КонецЦикла;
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ЗаписатьВерсию(ТипЗаявки, ВерсияДляЗаписи);
	
КонецПроцедуры

Функция НоваяЗаявкаКабинета(ТипЗаявки, ИдентификаторЗаявки)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЗаявки", ИдентификаторЗаявки);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаявкаКабинетСотрудника.Ссылка КАК Ссылка
	|ИЗ
	|	БизнесПроцесс.ЗаявкаКабинетСотрудника КАК ЗаявкаКабинетСотрудника
	|ГДЕ
	|	ЗаявкаКабинетСотрудника.ИдентификаторЗаявки = &ИдентификаторЗаявки";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Заявка = БизнесПроцессы.ЗаявкаКабинетСотрудника.СоздатьБизнесПроцесс();
	Заявка.Дата 			= ТекущаяДатаСеанса();
	Заявка.ИдентификаторЗаявки = ИдентификаторЗаявки;
	Заявка.Автор 			= Справочники.Пользователи.ПустаяСсылка();
	Заявка.Важность 		= Перечисления.ВариантыВажностиЗадачи.Обычная;
	Заявка.Исполнитель 		= РольИсполнителя(ТипЗаявки);
	Заявка.СрокИсполнения 	= КонецДня(ТекущаяДатаСеанса());
	Заявка.ТипЗаявки 		= ТипЗаявки;
	Заявка.СостояниеЗаявки 	= Перечисления.СостоянияЗаявокКабинетСотрудника.Новая;
	
	Возврат Заявка;
	
КонецФункции

Функция ФайлСправкиСМестаРаботы(ФизическоеЛицо, МассивСотрудников)

	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
	ТабДок = КабинетСотрудникаВнутренний.ПечатнаяФормаСправкаСМестаРаботы(МассивСотрудников);
	ТабДок.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.PDF);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВременногоФайла));
	УдалитьФайлы(ИмяВременногоФайла);
	
	ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.ВладелецФайлов = ФизическоеЛицо;
	ПараметрыФайла.ИмяБезРасширения = НСтр("ru = 'Справка с места работы'");
	ПараметрыФайла.РасширениеБезТочки = "pdf";
	ПараметрыФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
	ПараметрыФайла.Служебный = Истина;
	
	Возврат РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
	
КонецФункции

Функция ФайлСправкиОбОстаткеОтпусков(ФизическоеЛицо, МассивСотрудников)

	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
	ТабДок = КабинетСотрудникаВнутренний.ПечатнаяФормаСправкаОбОстаткахОтпусков(МассивСотрудников);
	ТабДок.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.PDF);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВременногоФайла));
	УдалитьФайлы(ИмяВременногоФайла);
	
	ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.ВладелецФайлов = ФизическоеЛицо;
	ПараметрыФайла.ИмяБезРасширения = НСтр("ru = 'Справка об остатках отпуска'");
	ПараметрыФайла.РасширениеБезТочки = "pdf";
	ПараметрыФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
	ПараметрыФайла.Служебный = Истина;
	
	Возврат РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
	
КонецФункции

#КонецОбласти

Процедура ЗаписатьОшибкуЗагрузкиИзменений(ТипИзменений, ОписаниеОшибки)
	
	ТекстОшибки = НСтр(
	"ru = 'Произошла ошибка при загрузке %1
	|Описание ошибки:
	|%2'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, ТипИзменений, ОписаниеОшибки);
	ЗаписьЖурналаРегистрации(ИмяСобытияЖРПолучениеИзменений(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
	
КонецПроцедуры

Функция ИменаСвойствСервисаСоЗначениямиДата()
	
	Результат = Новый Массив;
	Результат.Добавить("dateCreated");
	Результат.Добавить("issueDate");
	Результат.Добавить("startDate");
	Результат.Добавить("endDate");
	Результат.Добавить("applyMonth");
	Возврат Результат;
	
КонецФункции

Функция ЗаявкиПользователей(ПараметрыПодключения, ТипЗапроса, ШаблонРесурса)
	
	ВерсияИзменений = РегистрыСведений.ВерсииЗаявокКабинетСотрудника.ВерсияИзменений(ТипЗапроса);
	Лимит = 100;
	ПараметрыULR = Новый Структура;
	ПараметрыULR.Вставить("version", Формат(ВерсияИзменений + 1, "ЧН=0; ЧГ=0"));
	ПараметрыULR.Вставить("limit", Лимит);
	РесурсСервиса = ЗаполнитьПараметрыШаблонаURL(ШаблонРесурса,ПараметрыULR);
	Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, "GET");
	Если Ответ.КодСостояния >= 300 Тогда
		ПараметрыПодключения.Ошибки.Добавить(Ответ.ПолучитьТелоКакСтроку());
		Возврат Неопределено;
	ИначеЕсли Ответ.КодСостояния = 204 Тогда
		// нет изменений
		Возврат Неопределено;
	КонецЕсли;
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
	ИменаСвойствСервисаСоЗначениямиДата = ИменаСвойствСервисаСоЗначениямиДата();
	МассивОбъектов = ПрочитатьJSON(ЧтениеJSON, Истина, ИменаСвойствСервисаСоЗначениямиДата, ФорматДатыJSON.ISO, "ВосстановлениеJSON", ОбщегоНазначения.ОбщийМодуль("КабинетСотрудника"),, ИменаСвойствСервисаСоЗначениямиДата);
	Возврат МассивОбъектов;
	
КонецФункции

Функция ВосстановлениеJSON(Знач Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Результат = Неопределено;
	
	Попытка
		Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	Исключение
		Результат = Дата(1,1,1);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция РольИсполнителя(ТипЗаявки)
	
	Если ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаНалоговыеВычеты;
	ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаЗаявленияНаОтпуск;
	ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаИзменениеЛичныхДанных;
	ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаСогласованиеОтсутствий;
	ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаСправкиНДФЛ;
	ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаСправкиОбОстаткахОтпусков;
	ИначеЕсли ТипЗаявки = Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы Тогда
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ОтветственныйЗаСправкиСМестаРаботы;
	Иначе
		РольИсполнителяЗаявки = Справочники.РолиИсполнителей.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РольИсполнителяЗаявки;
	
КонецФункции

Функция ТаблицаИзОбъектовJSON(ОбъектыJSON, ОписаниеПолей, ПрефиксПолей = "")
	
	Таблица = Новый ТаблицаЗначений;
	ДобавитьКолонкиТаблицыПоОписанию(Таблица, ОписаниеПолей, ПрефиксПолей);
	Для Каждого ОбъектJSON Из ОбъектыJSON Цикл
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьСтрокуТаблицыПоОписанию(НоваяСтрока, ОбъектJSON, ОписаниеПолей, ПрефиксПолей);
	КонецЦикла;
	Возврат Таблица;

КонецФункции

Процедура ДобавитьКолонкиТаблицыПоОписанию(Таблица, ОписаниеПолей, ПрефиксПолей = "")
	
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		Если ОписаниеПоля["ТипПоля"] = Неопределено Тогда
			ДобавитьКолонкиТаблицыПоОписанию(Таблица, ОписаниеПоля["ОписаниеПолей"], ПрефиксПолей);
		Иначе
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ОписаниеПоля["ТипПоля"]);
			Таблица.Колонки.Добавить(ПрефиксПолей + ОписаниеПоля["ИмяПоляКонфигурации"], Новый ОписаниеТипов(МассивТипов));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуТаблицыПоОписанию(СтрокаТаблицы, ОбъектJSON, ОписаниеПолей, ПрефиксПолей = "")
	
	Для Каждого ОписаниеПоля Из ОписаниеПолей Цикл
		ЗначениеJSON = ОбъектJSON.Получить(ОписаниеПоля["ИмяПоляСервиса"]);
		Если ЗначениеJSON = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ОписаниеПоля["ТипПоля"] = Неопределено Тогда
			ЗаполнитьСтрокуТаблицыПоОписанию(СтрокаТаблицы, ОбъектJSON, ОписаниеПоля["ОписаниеПолей"], ПрефиксПолей);
		Иначе
			СтрокаТаблицы[ОписаниеПоля["ИмяПоляКонфигурации"]] = ПреобразоватьЗначениеJSON(ЗначениеJSON, ОписаниеПоля, ПрефиксПолей);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПреобразоватьЗначениеJSON(ЗначениеJSON, ОписаниеПоля, ПрефиксПолей)
	
	Значение = ЗначениеJSON;
	Если ОписаниеПоля["ТипПоля"] = Тип("ТаблицаЗначений") Тогда
		Значение = ТаблицаИзОбъектовJSON(Значение, ОписаниеПоля["ОписаниеПолей"], ПрефиксПолей);
	ИначеЕсли ОписаниеПоля[ПрефиксПолей + "ИмяПоляКонфигурации"] = "ФизическоеЛицо" Тогда
		Значение = Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(Значение))
	ИначеЕсли ОписаниеПоля[ПрефиксПолей + "ИмяПоляКонфигурации"] = "Организация" Тогда
		Значение = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(Значение))
	ИначеЕсли ОписаниеПоля[ПрефиксПолей + "ИмяПоляКонфигурации"] = "Назначение" Тогда
		Значение = НазначениеСправкиНДФЛСервиса(Значение);
	КонецЕсли;
	Возврат Значение;
	
КонецФункции

Функция МассивОбъектовВТаблицу(МассивОбъектов, СоответствиеПолей)
	
	Таблица = Новый ТаблицаЗначений;
	ДобавитьКолонкиТаблицыПоСоответствию(Таблица, СоответствиеПолей);
	Для Каждого СоответствиеОбъекта Из МассивОбъектов Цикл
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьСтрокуТаблицыПоСоответствию(НоваяСтрока, СоответствиеОбъекта, СоответствиеПолей);
	КонецЦикла;
	Возврат Таблица;
	
КонецФункции

Процедура ДобавитьКолонкиТаблицыПоСоответствию(Таблица, СоответствиеПолей)
	
	Для Каждого КлючИЗначение Из СоответствиеПолей Цикл
		ИмяПоля = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ИмяПоля) = Тип("Соответствие") Тогда
			ДобавитьКолонкиТаблицыПоСоответствию(Таблица, ИмяПоля);
		Иначе
			Таблица.Колонки.Добавить(ИмяПоля, НовоеОписаниеТиповПоля(ИмяПоля));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуТаблицыПоСоответствию(СтрокаТаблицы, СоответствиеОбъекта, СоответствиеПолей)
	
	Для Каждого КлючИЗначение Из СоответствиеОбъекта Цикл
		ИмяПоля = СоответствиеПолей.Получить(КлючИЗначение.Ключ);
		Если Не ЗначениеЗаполнено(ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ИмяПоля) = Тип("Соответствие") Тогда
			ЗаполнитьСтрокуТаблицыПоСоответствию(СтрокаТаблицы, КлючИЗначение.Значение, ИмяПоля);
		Иначе
			СтрокаТаблицы[ИмяПоля] = ЗначениеИзСтрокиСервиса(КлючИЗначение.Значение, ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеИзСтрокиСервиса(СтрокаЗначения, ИмяПоля)
	
	Значение = СтрокаЗначения;
	Если ИмяПоля = "ФизическоеЛицо" Тогда
		Значение = Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаЗначения))
	ИначеЕсли ИмяПоля = "Организация" Тогда
		Значение = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрокаЗначения))
	ИначеЕсли ИмяПоля = "Назначение" Тогда
		Значение = НазначениеСправкиНДФЛСервиса(Значение);
	ИначеЕсли ИмяПоля = "Вложения" Тогда
		Значение = МассивОбъектовВТаблицу(Значение, ОписаниеПолейВходящийФайл());
	КонецЕсли;
	Возврат Значение;
	
КонецФункции

Функция СправкаНДФЛ(ФизическоеЛицо,Организация, Назначение, НалоговыйПериод)
	
	ДанныеЗаполнения = Новый Структура("Действие,Данные");
	ДанныеЗаполнения.Действие = "ОбработкаЗапросаКабинетСотрудника";
	
	Данные = Новый Структура("ФизическоеЛицо,Организация,НалоговыйПериод,СпособФормирования");
	Данные.ФизическоеЛицо 	= ФизическоеЛицо;
	Данные.Организация 		= Организация;
	Данные.НалоговыйПериод 	= НалоговыйПериод;
	Если Назначение = "Подтверждение доходов" Тогда
		Данные.СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно;
	ИначеЕсли Назначение = "Декларирование доходов" Тогда
		Данные.СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.ВРазрезеКодовОКАТО;
	КонецЕсли;
	ДанныеЗаполнения.Данные = Данные;
	
	УстановитьПривилегированныйРежим(Истина);
	ДокСправкаНДФЛ = Документы.СправкаНДФЛ.СоздатьДокумент();
	ДокСправкаНДФЛ.Заполнить(ДанныеЗаполнения);
	ДокСправкаНДФЛ.Записать();
	
	Возврат ДокСправкаНДФЛ.Ссылка;
	
КонецФункции

Функция НазначениеСправкиНДФЛСервиса(Назначение)
	
	Результат = Назначение;
	Если Назначение = "incomeProof" Тогда
		Результат = "Подтверждение доходов";
	ИначеЕсли Назначение = "declare" Тогда
		Результат = "Декларирование доходов";
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция ПричинаОтсутствияСервиса(ПричинаОтсутствия)
	
	Результат = ПричинаОтсутствия;
	Если ПричинаОтсутствия = "late" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Опоздание;
	ИначеЕсли ПричинаОтсутствия = "illness" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Болезнь;
	ИначеЕсли ПричинаОтсутствия = "vacation" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск;
	ИначеЕсли ПричинаОтсутствия = "studyLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск;
	ИначеЕсли ПричинаОтсутствия = "unpaidLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет;
	ИначеЕсли ПричинаОтсутствия = "maternityLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам;
	ИначеЕсли ПричинаОтсутствия = "parentalLeave" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоУходуЗаРебенком;
	ИначеЕсли ПричинаОтсутствия = "timeOff" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул;
	ИначеЕсли ПричинаОтсутствия = "businessTrip" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка;
	ИначеЕсли ПричинаОтсутствия = "personalReason" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела;
	ИначеЕсли ПричинаОтсутствия = "invalidChildCare" Тогда
		Результат = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Процедура ЗагрузитьФайлыЗаявки(ПараметрыПодключения, Заявка, Владелец, ТаблицаВложений)
	
	ШаблонРесурса = ПараметрыПодключения.ШаблоныРесурсов.Получить("Файлы");
	Для Каждого СтрокаТаблицы Из ТаблицаВложений Цикл
		
		РесурсСервиса = ЗаполнитьПараметрыШаблонаURL(ШаблонРесурса, Новый Структура("ID", СтрокаТаблицы.ИдентификаторФайла));
		Ответ = HTTPОтветСервиса(ПараметрыПодключения, РесурсСервиса, "GET");
		Если Не Ответ.КодСостояния = 200 Тогда
			ВызватьИсключение НСтр("ru = 'Произошла ошибка при загрузке файлов из сервиса. Подробности в журнале регистрации.'");
		КонецЕсли;
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Ответ.ПолучитьТелоКакДвоичныеДанные());
		
		ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
		ПараметрыФайла.ВладелецФайлов = Владелец;
		ПараметрыФайла.ИмяБезРасширения = СтрЗаменить(СтрокаТаблицы.НаименованиеФайла, "." + СтрокаТаблицы.РасширениеФайла, "");
		ПараметрыФайла.РасширениеБезТочки = СтрЗаменить(СтрокаТаблицы.РасширениеФайла, ".", "");
		ПараметрыФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
		ПараметрыФайла.Служебный = Истина;
		
		НоваяСтрока = Заявка.ФайлыЗаявки.Добавить();
		НоваяСтрока.Файл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСПравиламиПубликации

Процедура СоздатьВТШтатноеРасписание(МенеджерВТ) Экспорт
	
	КабинетСотрудникаВнутренний.СоздатьВТШтатноеРасписание(МенеджерВТ);
	
КонецПроцедуры

// Вызывается при сохранении настроек правил публикации.
// Выполняется полное обновление списков публикуемых объектов на основе правил публикации.
//
Процедура ОбновитьПубликуемыеОбъекты() Экспорт
	
	КабинетСотрудникаВнутренний.ОбновитьСтруктуруПредприятия();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШтатноеРасписание.Ссылка КАК Позиция,
	|	ШтатноеРасписание.Владелец КАК Организация,
	|	ШтатноеРасписание.Должность КАК Должность,
	|	ШтатноеРасписание.Подразделение КАК Подразделение
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаПубликацииКабинетСотрудника КАК Правила
	|		ПО (Правила.ОбъектПравила = ШтатноеРасписание.Ссылка)";
	// таблица значений всех публикуемых позиций
	ТаблицаПозиций = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаПозиций.Количество() = 0 Тогда
		РегистрыСведений.ФизическиеЛицаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
		РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей().Записать();
		РегистрыСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника.СоздатьНаборЗаписей().Записать();
		Возврат;
	КонецЕсли;
	
	ПубликуемыеПозиции     = ТаблицаПозиций.ВыгрузитьКолонку("Позиция");
	ПубликуемыеОрганизации = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПозиций,"Организация",Истина);
	ПубликуемыеДолжности   = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПозиций,"Должность",Истина);
	ПубликуемыеПодразделенияПредприятия = КабинетСотрудникаВнутренний.ПубликуемаяСтруктураПредприятия(ПубликуемыеПозиции);
	
	ГоловныеОрганизации = Новый Массив;
	Для каждого Организация Из ПубликуемыеОрганизации Цикл
		ГоловныеОрганизации.Добавить(ЗарплатаКадры.ГоловнаяОрганизация(Организация));
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПубликуемыеОрганизации, ГоловныеОрганизации, Истина);
	
	ИмяОпции = "ИспользоватьШтатноеРасписание";
	ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
	ВедетсяШтатноеРасписание = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	КадровыеДанныеСтрока = "ВидСобытия";
	Если ВедетсяШтатноеРасписание Тогда
		КадровыеДанныеСтрока = КадровыеДанныеСтрока + ",ДолжностьПоШтатномуРасписанию";
	Иначе
		КадровыеДанныеСтрока = КадровыеДанныеСтрока + ",Подразделение,Должность";
	КонецЕсли;
	ПараметрыПолучения.КадровыеДанные = КадровыеДанныеСтрока;
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыеДанные = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолучения);
	УстановитьПривилегированныйРежим(Ложь);

	Если Не ВедетсяШтатноеРасписание Тогда
		
		Запрос.УстановитьПараметр("КадровыеДанные", КадровыеДанные);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровыеДанные.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанные.Сотрудник КАК Сотрудник,
		|	КадровыеДанные.Подразделение КАК Подразделение,
		|	КадровыеДанные.Должность КАК Должность,
		|	КадровыеДанные.ВидСобытия КАК ВидСобытия
		|ПОМЕСТИТЬ ВТКадровыеДанные
		|ИЗ
		|	&КадровыеДанные КАК КадровыеДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанные.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанные.Сотрудник КАК Сотрудник,
		|	ШтатноеРасписание.Ссылка КАК ДолжностьПоШтатномуРасписанию,
		|	КадровыеДанные.ВидСобытия КАК ВидСобытия
		|ИЗ
		|	ВТКадровыеДанные КАК КадровыеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ПО КадровыеДанные.Подразделение = ШтатноеРасписание.Подразделение
		|			И КадровыеДанные.Должность = ШтатноеРасписание.Должность";
		
		УстановитьПривилегированныйРежим(Истина);
		КадровыеДанные = Запрос.Выполнить().Выгрузить();
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	ПозицииШР = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(ПубликуемыеПозиции);
	ПубликуемыеФизическиеЛица 	= Новый Массив;
	ПубликуемыеСотрудники 		= Новый Массив;
	Для каждого СтрокаТЗ Из КадровыеДанные Цикл
		Если ПозицииШР[СтрокаТЗ.ДолжностьПоШтатномуРасписанию] <> Неопределено Тогда
			Если СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
					Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.НачальныеДанные
					Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.ВосстановлениеВДолжности
					Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перемещение Тогда
				ПубликуемыеФизическиеЛица.Добавить(СтрокаТЗ.ФизическоеЛицо);
				ПубликуемыеСотрудники.Добавить(СтрокаТЗ.Сотрудник);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	// добавляем в список ответственное лицо
	Если ЗначениеЗаполнено(Настройки.Ответственный) Тогда
		ПубликуемыеФизическиеЛица.Добавить(Настройки.Ответственный);
	КонецЕсли;
	
	ПубликуемыеФизическиеЛица 	= ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПубликуемыеФизическиеЛица);
	ПубликуемыеСотрудники 		= ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПубликуемыеСотрудники);
	
	НаборЗаписей = РегистрыСведений.ФизическиеЛицаКабинетСотрудника.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверитьЗаполнение");
	Для каждого ФизическоеЛицо Из ПубликуемыеФизическиеЛица Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ФизическоеЛицо = ФизическоеЛицо;
	КонецЦикла;
	Таблица = НаборЗаписей.Выгрузить();
	Запрос.УстановитьПараметр("ФизическиеЛица", Таблица);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	&ФизическиеЛица КАК ФизическиеЛица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТФизическиеЛица КАК НовыеФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛица
	|		ПО НовыеФизическиеЛица.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
	|ГДЕ
	|	ФизическиеЛица.ФизическоеЛицо ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	// таблица новых публикуемых физических лиц, для регистрации публикации изменений 
	ТаблицаИзменений = РезультатЗапроса.Выгрузить();
	// записываем список публикуемых физических лиц
	НаборЗаписей.Записать();
	
	// Регистрация новых физических лиц как изменения для публикации.
	// Дополнительно регистрируем физических лиц в ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.
	Для каждого СтрокаТЗ Из ТаблицаИзменений Цикл
		
		Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
		Запись.ПредметПубликации = СтрокаТЗ.ФизическоеЛицо;
		Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		Запись.Записать();
		
		Запись = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьМенеджерЗаписи();
		Запись.ФизическоеЛицо = СтрокаТЗ.ФизическоеЛицо;
		Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		Запись.Записать();
		
	КонецЦикла;
	
	// Очищаем сведения для отложенного обновления публикуемых объектов.
	НаборЗаписей = РегистрыСведений.СотрудникиДляОбновленияПубликуемыхОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	// Все публикуемые объекты, кроме физических лиц.
	НаборЗаписей = РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей();
	Для каждого ОбъектПубликации Из ПубликуемыеСотрудники Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ОбъектПубликации = ОбъектПубликации;
	КонецЦикла;
	Для каждого ОбъектПубликации Из ПубликуемыеОрганизации Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ОбъектПубликации = ОбъектПубликации;
	КонецЦикла;
	Для каждого ОбъектПубликации Из ПубликуемыеПодразделенияПредприятия Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ОбъектПубликации = ОбъектПубликации;
	КонецЦикла;
	Для каждого ОбъектПубликации Из ПубликуемыеДолжности Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ОбъектПубликации = ОбъектПубликации;
	КонецЦикла;
	Для каждого ОбъектПубликации Из ПубликуемыеПозиции Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ОбъектПубликации = ОбъектПубликации;
	КонецЦикла;
	Таблица = НаборЗаписей.Выгрузить();
	Запрос.УстановитьПараметр("ПубликуемыеОбъекты", Таблица);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПубликуемыеОбъекты.ОбъектПубликации КАК ОбъектПубликации
	|ПОМЕСТИТЬ ВТПубликуемыеОбъекты
	|ИЗ
	|	&ПубликуемыеОбъекты КАК ПубликуемыеОбъекты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеПубликуемыеОбъекты.ОбъектПубликации КАК ОбъектПубликации
	|ИЗ
	|	ВТПубликуемыеОбъекты КАК НовыеПубликуемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО НовыеПубликуемыеОбъекты.ОбъектПубликации = ПубликуемыеОбъекты.ОбъектПубликации
	|ГДЕ
	|	ПубликуемыеОбъекты.ОбъектПубликации ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	// таблица новых публикуемых объектов, для регистрации публикации изменений
	ТаблицаИзменений = РезультатЗапроса.Выгрузить();
	// записываем список публикуемых объектов
	НаборЗаписей.Записать();
	// Регистрация новых объектов
	НаборЗаписей = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	Для каждого СтрокаТЗ Из ТаблицаИзменений Цикл
		Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
		Запись.ПредметПубликации = СтрокаТЗ.ОбъектПубликации;
		Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		Запись.Записать();
	КонецЦикла;
	КабинетСотрудникаВнутренний.ЗарегистрироватьОбновленияПубликацииПравНаОтпуск(ТаблицаИзменений.ВыгрузитьКолонку("ОбъектПубликации"));
	
КонецПроцедуры


#КонецОбласти

#Область РегистрацияПубликуемыхОбъектов

Процедура ОбработатьИзменениеКадровойИстории(ИзменившиесяДанные) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;

	Если ИзменившиесяДанные <> Неопределено И ИзменившиесяДанные.Количество() > 0 Тогда
		
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ИзменившиесяДанные, "Сотрудник", Истина);
		
		УстановитьПривилегированныйРежим(Истина);
		ГоловныеСотрудники = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ГоловнойСотрудник");
		НаборЗаписей = РегистрыСведений.СотрудникиДляОбновленияПубликуемыхОбъектов.СоздатьНаборЗаписей();
		Для каждого Сотрудник Из Сотрудники Цикл
			Если Сотрудник <> ГоловныеСотрудники[Сотрудник] Тогда
				Продолжить;
			КонецЕсли;
			НаборЗаписей.Очистить();
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Сотрудник = Сотрудник;
			НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
			НаборЗаписей.Записать();
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
		
		КабинетСотрудникаВнутренний.ОбработатьИзменениеКадровойИстории(ИзменившиесяДанные);
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбъектПередЗаписью(Объект) Экспорт

	ИменаКонтролируемыхПолей = ИменаКонтролируемыхПолей(Объект);
	Если Объект.ЭтоНовый() Тогда
		ПрежниеДанныеОбъекта = Новый Структура(ИменаКонтролируемыхПолей);
	Иначе
		ПрежниеДанныеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Ссылка, ИменаКонтролируемыхПолей);
	КонецЕсли;
	Объект.ДополнительныеСвойства.Вставить("ПрежниеДанныеПубликуемогоОбъекта", ПрежниеДанныеОбъекта);
	
	КабинетСотрудникаВнутренний.ОбъектПередЗаписью(Объект);
	
КонецПроцедуры

Функция ИменаКонтролируемыхПолей(Объект)
	
	ИменаКонтролируемыхПолей = "";
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.Должности") Тогда
		ИменаКонтролируемыхПолей = "Наименование";
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Организации") Тогда
		ИменаКонтролируемыхПолей = "Наименование,ИНН,ГоловнаяОрганизация";
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ПодразделенияОрганизаций") Тогда
		ИменаКонтролируемыхПолей = "РеквизитДопУпорядочиванияИерархического";
	Иначе
		ИменаКонтролируемыхПолей = КабинетСотрудникаВнутренний.ИменаКонтролируемыхПолей(Объект);
	КонецЕсли;
	
	Возврат ИменаКонтролируемыхПолей;

КонецФункции

Процедура ОбъектПриЗаписи(Объект) Экспорт

	Если ТипЗнч(Объект) = Тип("СправочникОбъект.ФизическиеЛица") Тогда
		ПриЗаписиФизическогоЛица(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ПодразделенияОрганизаций") Тогда
		ПриЗаписиПодразделенияОрганизации(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Организации")
		Или ТипЗнч(Объект) = Тип("СправочникОбъект.Должности") Тогда
		ПриЗаписиПубликуемогоОбъекта(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("БизнесПроцессОбъект.ЗаявкаКабинетСотрудника") Тогда
		ПриЗаписиЗаявки(Объект);
	Иначе
		КабинетСотрудникаВнутренний.ОбъектПриЗаписи(Объект);
	КонецЕсли;
	
КонецПроцедуры
	
Процедура ПриЗаписиФизическогоЛица(ФизическоеЛицо)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛицаКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛицаКабинетСотрудника
	|ГДЕ
	|	ФизическиеЛицаКабинетСотрудника.ФизическоеЛицо = &ФизическоеЛицо";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ПредметПубликации = ФизическоеЛицо.Ссылка;
		МенеджерЗаписи.ВерсияДанных 	 = Строка(Новый УникальныйИдентификатор);
		МенеджерЗаписи.Записать();
		ПроверитьЗаполнениеСвойствФизическихЛиц(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо.Ссылка));
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписиПодразделенияОрганизации(Подразделение)
	
	ЗначенияКонтролируемыхРеквизитов = Неопределено;
	Подразделение.ДополнительныеСвойства.Свойство("ПрежниеДанныеПубликуемогоОбъекта", ЗначенияКонтролируемыхРеквизитов);
	Если Не КонтролируемыеРеквизитыИзменились(ЗначенияКонтролируемыхРеквизитов, Подразделение) Тогда
		Возврат;
	КонецЕсли;
	
	ПодразделениеПредприятия = КабинетСотрудникаВнутренний.ПодразделениеВСтруктуреПредприятия(Подразделение);
	Если ЗначениеЗаполнено(ПодразделениеПредприятия) Тогда
	
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОбъектПубликации", ПодразделениеПредприятия.Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации КАК ОбъектПубликации
		|ИЗ
		|	РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъектыКабинетСотрудника
		|ГДЕ
		|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации = &ОбъектПубликации";
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.ПредметПубликации 	= Выборка.Ссылка;
				МенеджерЗаписи.ВерсияДанных 		= Строка(Новый УникальныйИдентификатор);
				МенеджерЗаписи.Записать();
			КонецЦикла;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Функция КонтролируемыеРеквизитыИзменились(ЗначенияКонтролируемыхРеквизитов, Объект)

	Если ЗначенияКонтролируемыхРеквизитов = Неопределено Тогда
		Возврат Истина;
	Иначе
		Для каждого ЭлементКоллекции Из ЗначенияКонтролируемыхРеквизитов Цикл
			Если Объект[ЭлементКоллекции.Ключ] <> ЭлементКоллекции.Значение Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Процедура ЗарегистрироватьОбновлениеВычетовНаДетей(СписокФизическихЛиц) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ КАК ФизическиеЛицаДляОбновленияПубликации
	|		ПО ФизическиеЛица.ФизическоеЛицо = ФизическиеЛицаДляОбновленияПубликации.ФизическоеЛицо
	|ГДЕ
	|	ФизическиеЛица.ФизическоеЛицо В(&СписокФизическихЛиц)
	|	И ФизическиеЛицаДляОбновленияПубликации.БылаОшибкаПриПубликации ЕСТЬ NULL";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.ФизическоеЛицо 	= Выборка.ФизическоеЛицо;
			МенеджерЗаписи.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ИмущественныеВычетыНДФЛПередЗаписью(НаборЗаписей) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", НаборЗаписей.Отбор.Регистратор.Значение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК ИмущественныеВычетыНДФЛ
	|ГДЕ
	|	ИмущественныеВычетыНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ИмущественныеВычетыНДФЛ.Регистратор = &Регистратор";
	СписокФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	НаборЗаписей.ДополнительныеСвойства.Вставить("ФизическиеЛицаОбновленияПубликацииВычетов", СписокФизическихЛиц);

КонецПроцедуры

Процедура ИмущественныеВычетыНДФЛПриЗаписи(НаборЗаписей) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;
	
	СписокФизическихЛиц = НаборЗаписей.Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	СписокФизическихЛицПередЗаписью = Неопределено;
	НаборЗаписей.ДополнительныеСвойства.Свойство("ФизическиеЛицаОбновленияПубликацииВычетов", СписокФизическихЛицПередЗаписью);
	Если СписокФизическихЛиц <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокФизическихЛиц, СписокФизическихЛицПередЗаписью ,Истина);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ КАК ФизическиеЛицаДляОбновленияПубликации
	|		ПО ФизическиеЛица.ФизическоеЛицо = ФизическиеЛицаДляОбновленияПубликации.ФизическоеЛицо
	|ГДЕ
	|	ФизическиеЛица.ФизическоеЛицо В(&СписокФизическихЛиц)
	|	И ФизическиеЛицаДляОбновленияПубликации.БылаОшибкаПриПубликации ЕСТЬ NULL";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.ФизическоеЛицо 	= Выборка.ФизическоеЛицо;
			МенеджерЗаписи.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ОтложенноеОбновлениеСписковПубликуемыхОбъектов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.СотрудникиДляОбновленияПубликуемыхОбъектов КАК Сотрудники";
	РезультатаЗапроса = Запрос.Выполнить();
	Если РезультатаЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСотрудников = РезультатаЗапроса.Выгрузить();
	СписокСотрудников   = ТаблицаСотрудников.ВыгрузитьКолонку("Сотрудник");
	СписокФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаСотрудников, "ФизическоеЛицо", Истина);
	
	Запрос.УстановитьПараметр("ФизическиеЛица", СписокФизическихЛиц);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛицаКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛицаКабинетСотрудника
	|ГДЕ
	|	ФизическиеЛицаКабинетСотрудника.ФизическоеЛицо В(&ФизическиеЛица)";
	Выборка = Запрос.Выполнить().Выбрать();
	ПубликуемыеФизическиеЛица = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ПубликуемыеФизическиеЛица.Вставить(Выборка.ФизическоеЛицо, Истина);
	КонецЦикла;
	
	ИмяОпции = "ИспользоватьШтатноеРасписание";
	ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
	ВедетсяШтатноеРасписание = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
	
	ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	КадровыеДанныеСтрока = "ВидСобытия";
	Если ВедетсяШтатноеРасписание Тогда
		КадровыеДанныеСтрока = КадровыеДанныеСтрока + ",ДолжностьПоШтатномуРасписанию";
	Иначе
		КадровыеДанныеСтрока = КадровыеДанныеСтрока + ",Подразделение,Должность";
	КонецЕсли;
	ПараметрыПолучения.КадровыеДанные = КадровыеДанныеСтрока;
	ПараметрыПолучения.СписокФизическихЛиц = СписокФизическихЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыеДанные = КадровыйУчет.СотрудникиОрганизации(Ложь, ПараметрыПолучения);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ВедетсяШтатноеРасписание Тогда
		
		Запрос.УстановитьПараметр("КадровыеДанные", КадровыеДанные);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровыеДанные.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанные.Сотрудник КАК Сотрудник,
		|	КадровыеДанные.Подразделение КАК Подразделение,
		|	КадровыеДанные.Должность КАК Должность,
		|	КадровыеДанные.ВидСобытия КАК ВидСобытия
		|ПОМЕСТИТЬ ВТКадровыеДанные
		|ИЗ
		|	&КадровыеДанные КАК КадровыеДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КадровыеДанные.ФизическоеЛицо КАК ФизическоеЛицо,
		|	КадровыеДанные.Сотрудник КАК Сотрудник,
		|	ШтатноеРасписание.Ссылка КАК ДолжностьПоШтатномуРасписанию,
		|	КадровыеДанные.ВидСобытия КАК ВидСобытия
		|ИЗ
		|	ВТКадровыеДанные КАК КадровыеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ПО КадровыеДанные.Подразделение = ШтатноеРасписание.Подразделение
		|			И КадровыеДанные.Должность = ШтатноеРасписание.Должность";
		
		УстановитьПривилегированныйРежим(Истина);
		КадровыеДанные = Запрос.Выполнить().Выгрузить();
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	// получим список публикуемых позиций
	Позиции = ОбщегоНазначения.ВыгрузитьКолонку(КадровыеДанные, "ДолжностьПоШтатномуРасписанию", Истина);
	Запрос.УстановитьПараметр("Позиции", Позиции);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаПубликации.ОбъектПравила КАК Позиция
	|ИЗ
	|	РегистрСведений.ПравилаПубликацииКабинетСотрудника КАК ПравилаПубликации
	|ГДЕ
	|	ПравилаПубликации.ОбъектПравила В(&Позиции)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПубликуемыеОбъекты.ОбъектПубликации КАК Позиция
	|ИЗ
	|	РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|ГДЕ
	|	ПубликуемыеОбъекты.ОбъектПубликации В(&Позиции)";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	ПозицияЕстьВПравилах = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ПозицияЕстьВПравилах.Вставить(Выборка.Позиция, Истина);
	КонецЦикла;
	Выборка = РезультатЗапроса[1].Выбрать();
	ПубликуемыеПозиции = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ПубликуемыеПозиции.Вставить(Выборка.Позиция, Истина);
	КонецЦикла;
	
	ОбрабатываемыеСотрудники = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(СписокСотрудников);
	КадровыеДанныеСотрудников 		= КадровыеДанные.СкопироватьКолонки();
	КадровыеДанныеДругихСотрудников = КадровыеДанные.СкопироватьКолонки();
	Для каждого СтрокаТЗ Из КадровыеДанные Цикл
		Если ОбрабатываемыеСотрудники[СтрокаТЗ.Сотрудник] = Неопределено
					И (СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
					Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.НачальныеДанные
					Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.ВосстановлениеВДолжности
					Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перемещение) Тогда
			// другое рабочее место физического лица
			ЗаполнитьЗначенияСвойств(КадровыеДанныеДругихСотрудников.Добавить(), СтрокаТЗ);
		Иначе	
			ЗаполнитьЗначенияСвойств(КадровыеДанныеСотрудников.Добавить(), СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	
	КадровыеДанныеДругихСотрудников.Индексы.Добавить("ФизическоеЛицо");
	Отбор = Новый Структура("ФизическоеЛицо");
	
	ПозицииКПубликации 			= Новый Массив;
	СотрудникиКУдалению 		= Новый Массив;
	СотрудникиКПубликации 		= Новый Массив;
	ФизическиеЛицаКУдалению 	= Новый Массив;
	ФизическиеЛицаКПубликации 	= Новый Массив;
	СотрудникиОтменаПубликации 		= Новый Массив;
	ФизическиеЛицаОтменаПубликации 	= Новый Массив;
	Для каждого СтрокаТЗ Из КадровыеДанныеСотрудников Цикл
		
		Отбор.ФизическоеЛицо = СтрокаТЗ.ФизическоеЛицо;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ВидСобытия) Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
			
			Если ПубликуемыеФизическиеЛица[СтрокаТЗ.ФизическоеЛицо] <> Неопределено Тогда
				// Физическое лицо публикуется, зарегистрируем удаление сотрудника.
				СотрудникиКУдалению.Добавить(СтрокаТЗ.Сотрудник);
				НайденныеСтроки = КадровыеДанныеДругихСотрудников.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() = 0 Тогда
					// нет других рабочих мест физического лица
					ФизическиеЛицаКУдалению.Добавить(СтрокаТЗ.ФизическоеЛицо);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перемещение Тогда
			
			Если ПозицияЕстьВПравилах[СтрокаТЗ.ДолжностьПоШтатномуРасписанию] = Неопределено Тогда
				// перевод на не публикуемую позицию
				Если ПубликуемыеФизическиеЛица[СтрокаТЗ.ФизическоеЛицо] <> Неопределено Тогда
					// физическое лицо публикуется, проверяем другие рабочие места
					ОтменитьПубликацию = Истина;
					НайденныеСтроки = КадровыеДанныеДругихСотрудников.НайтиСтроки(Отбор);
					Для каждого ДругоеРабочееМесто Из НайденныеСтроки Цикл
						Если ПозицияЕстьВПравилах[ДругоеРабочееМесто.ДолжностьПоШтатномуРасписанию] <> Неопределено Тогда
							ОтменитьПубликацию = Ложь;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					Если ОтменитьПубликацию Тогда
						СотрудникиОтменаПубликации.Добавить(СтрокаТЗ.Сотрудник);
						ФизическиеЛицаОтменаПубликации.Добавить(СтрокаТЗ.ФизическоеЛицо);
						Для каждого ДругоеРабочееМесто Из НайденныеСтроки Цикл
							СотрудникиОтменаПубликации.Добавить(ДругоеРабочееМесто.Сотрудник);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			Иначе
				// перевод на публикуемую позицию
				СотрудникиКПубликации.Добавить(СтрокаТЗ.Сотрудник);
				Если ПубликуемыеФизическиеЛица[СтрокаТЗ.ФизическоеЛицо] = Неопределено Тогда
					ФизическиеЛицаКПубликации.Добавить(СтрокаТЗ.ФизическоеЛицо);
					НайденныеСтроки = КадровыеДанныеДругихСотрудников.НайтиСтроки(Отбор);
					Для каждого ДругоеРабочееМесто Из НайденныеСтроки Цикл
						СотрудникиКПубликации.Добавить(ДругоеРабочееМесто.Сотрудник);
					КонецЦикла;
				КонецЕсли;
				Если ПубликуемыеПозиции[СтрокаТЗ.ДолжностьПоШтатномуРасписанию] = Неопределено Тогда
					ПозицииКПубликации.Добавить(СтрокаТЗ.ДолжностьПоШтатномуРасписанию);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
			Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.НачальныеДанные
			Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.ВосстановлениеВДолжности Тогда
			
			Если ПозицияЕстьВПравилах[СтрокаТЗ.ДолжностьПоШтатномуРасписанию] <> Неопределено Тогда
				СотрудникиКПубликации.Добавить(СтрокаТЗ.Сотрудник);
				Если ПубликуемыеФизическиеЛица[СтрокаТЗ.ФизическоеЛицо] = Неопределено Тогда
					ФизическиеЛицаКПубликации.Добавить(СтрокаТЗ.ФизическоеЛицо);
					// добавляем публикации других рабочих мест
					НайденныеСтроки = КадровыеДанныеДругихСотрудников.НайтиСтроки(Отбор);
					Для каждого ДругоеРабочееМесто Из НайденныеСтроки Цикл
						СотрудникиКПубликации.Добавить(ДругоеРабочееМесто.Сотрудник);
					КонецЦикла;
				КонецЕсли;
				Если ПубликуемыеПозиции[СтрокаТЗ.ДолжностьПоШтатномуРасписанию] = Неопределено Тогда
					ПозицииКПубликации.Добавить(СтрокаТЗ.ДолжностьПоШтатномуРасписанию);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиОтменаПубликации, СотрудникиКУдалению, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛицаОтменаПубликации, ФизическиеЛицаКУдалению, Истина);
	ПозицииКПубликации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПозицииКПубликации);
	
	ДолжностиКПубликации 	 = Новый Массив;
	ПодразделенияКПубликации = Новый Массив;
	Если ПозицииКПубликации.Количество() > 0 Тогда
		ДолжностиКПубликации = НовыеПубликуемыеДолжностиПозиций(ПозицииКПубликации);
		ПодразделенияКПубликации = КабинетСотрудникаВнутренний.НоваяПубликуемаяСтруктураПредприятияПозиций(ПозицииКПубликации);
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Для каждого ОбъектПубликации Из ПозицииКПубликации Цикл
			Запись = РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = ОбъектПубликации;
			Запись.Записать();
			Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ПредметПубликации = ОбъектПубликации;
			Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			Запись.Записать();
		КонецЦикла;
		
		Для каждого ОбъектПубликации Из ДолжностиКПубликации Цикл
			Запись = РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = ОбъектПубликации;
			Запись.Записать();
			Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ПредметПубликации = ОбъектПубликации;
			Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			Запись.Записать();
		КонецЦикла;
		
		Для каждого ОбъектПубликации Из ПодразделенияКПубликации Цикл
			Запись = РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = ОбъектПубликации;
			Запись.Записать();
			Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ПредметПубликации = ОбъектПубликации;
			Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			Запись.Записать();
		КонецЦикла;
		
		Для каждого Сотрудник Из СотрудникиКПубликации Цикл
			Запись = РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = Сотрудник;
			Запись.Записать();
			Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ПредметПубликации = Сотрудник;
			Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			Запись.Записать();
			КабинетСотрудникаВнутренний.ДобавитьСотрудникиДляОбновленияПубликацииПравНаОтпуск(Сотрудник);
		КонецЦикла;
		
		Для каждого Сотрудник Из СотрудникиОтменаПубликации Цикл
			Запись = РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = Сотрудник;
			Запись.Удалить();
		КонецЦикла;
		
		Для каждого Сотрудник Из СотрудникиКУдалению Цикл
			Запись = РегистрыСведений.ОбъектыДляУдаленияИзСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектДляУдаления = Сотрудник;
			Запись.Записать();
		КонецЦикла;
		
		Для каждого ФизическоеЛицо Из ФизическиеЛицаКПубликации Цикл
			Запись = РегистрыСведений.ФизическиеЛицаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ФизическоеЛицо = ФизическоеЛицо;
			Запись.Записать();
			Запись = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ПредметПубликации = ФизическоеЛицо;
			Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			Запись.Записать();
			Запись = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьМенеджерЗаписи();
			Запись.ФизическоеЛицо = ФизическоеЛицо;
			Запись.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			Запись.Записать();
		КонецЦикла;
		
		ПроверитьЗаполнениеСвойствФизическихЛиц(ФизическиеЛицаКПубликации);
		
		Для каждого ФизическоеЛицо Из ФизическиеЛицаОтменаПубликации Цикл
			Запись = РегистрыСведений.ФизическиеЛицаКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ФизическоеЛицо = ФизическоеЛицо;
			Запись.Удалить();
			Запись = РегистрыСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = ФизическоеЛицо;
			Запись.Удалить();
		КонецЦикла;
		
		Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.СотрудникиДляОбновленияПубликуемыхОбъектов КАК Сотрудники
		|ГДЕ
		|	НЕ Сотрудники.Сотрудник В (&СписокСотрудников)";
		НаборЗаписей = РегистрыСведений.СотрудникиДляОбновленияПубликуемыхОбъектов.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
КонецПроцедуры

Функция НовыеПубликуемыеДолжностиПозиций(Позиции)
	
	Должности = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	ЗначенияРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Позиции, "Должность", Ложь);
	Для каждого ЭлементКоллекции Из ЗначенияРеквизита Цикл
		Должности.Добавить(ЭлементКоллекции.Значение);
	КонецЦикла;
	Должности = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Должности);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Должности", Должности);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Должности.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Должности КАК Должности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО (ПубликуемыеОбъекты.ОбъектПубликации = Должности.Ссылка)
	|			И (ПубликуемыеОбъекты.ОбъектПубликации ССЫЛКА Справочник.Должности)
	|ГДЕ
	|	ПубликуемыеОбъекты.ОбъектПубликации ЕСТЬ NULL
	|	И Должности.Ссылка В(&Должности)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

КонецФункции

Процедура ПриЗаписиЗаявки(Объект)

	Если Не Объект.Выполнено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПредметПубликации = Объект.Ссылка;
	МенеджерЗаписи.ВерсияДанных 	 = Строка(Новый УникальныйИдентификатор);
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПубликуемыхОбъектов

Процедура ПроверитьЗаполнениеСвойствФизическихЛиц(МассивФизическихЛиц) Экспорт
	
	Если МассивФизическихЛиц.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизическиеЛица", МассивФизическихЛиц);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОшибкиЗаполнения.ОбъектПубликации КАК ОбъектПубликации
	|ИЗ
	|	РегистрСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника КАК ОшибкиЗаполнения
	|ГДЕ
	|	ОшибкиЗаполнения.ОбъектПубликации В(&ФизическиеЛица)";
	Выборка = Запрос.Выполнить().Выбрать();
	ТекущиеОшибки = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		ТекущиеОшибки.Вставить(Выборка.ОбъектПубликации, Истина);
	КонецЦикла;
	
	ПроверяемыеДанные = "Пол,Фамилия,ТелефонМобильныйПредставление,EMailПредставление";
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаДанных = КадровыйУчет.КадровыеДанныеФизическихЛиц(Ложь, МассивФизическихЛиц, ПроверяемыеДанные);
	УстановитьПривилегированныйРежим(Ложь);
	
	ШаблонПодстроки = "%1" + Символы.ПС + "%2";
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		
		ЕстьОшибки = Ложь;
		ОписаниеОшибки = "";
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Пол) Тогда
			ЕстьОшибки = Истина;
			ОписаниеОшибки = НСтр("ru = 'Не указан пол'");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Фамилия) Тогда
			ЕстьОшибки = Истина;
			Описание = НСтр("ru = 'Не указана фамилия'");
			ОписаниеОшибки = ?(ПустаяСтрока(ОписаниеОшибки),Описание,СтрШаблон(ШаблонПодстроки, ОписаниеОшибки, Описание));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЗ.ТелефонМобильныйПредставление) И Не ЗначениеЗаполнено(СтрокаТЗ.EMailПредставление) Тогда
			ЕстьОшибки = Истина;
			Описание = НСтр("ru = 'Необходимо указать мобильный телефон или адрес электронной почты'");
			ОписаниеОшибки = ?(ПустаяСтрока(ОписаниеОшибки),Описание,СтрШаблон(ШаблонПодстроки, ОписаниеОшибки, Описание));
		Иначе
			Если ЗначениеЗаполнено(СтрокаТЗ.ТелефонМобильныйПредставление) И СтрДлина(СтрокаТЗ.ТелефонМобильныйПредставление) > 20 Тогда
				ЕстьОшибки = Истина;
				Описание = НСтр("ru = 'Мобильный телефон указан некорректно'");
				ОписаниеОшибки = ?(ПустаяСтрока(ОписаниеОшибки),Описание,СтрШаблон(ШаблонПодстроки, ОписаниеОшибки, Описание));
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьОшибки Тогда
			Запись = РегистрыСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = СтрокаТЗ.ФизическоеЛицо;
			Запись.ЕстьОшибки = Истина;
			Запись.ОписаниеОшибки = ОписаниеОшибки;
			Запись.Записать();
		ИначеЕсли ТекущиеОшибки[СтрокаТЗ.ФизическоеЛицо] <> Неопределено Тогда
			Запись = РегистрыСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника.СоздатьМенеджерЗаписи();
			Запись.ОбъектПубликации = СтрокаТЗ.ФизическоеЛицо;
			Запись.Удалить();
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

Функция НовоеHTTPСоединение(СтруктураURI, Таймаут = 100)
	
	ИнтернетПрокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураURI.Схема);
	КонецЕсли;
	ЗащищенноеСоединение = Неопределено;
	Если ВРег(СтруктураURI.Схема) = "HTTPS" Или ВРег(СтруктураURI.Схема) = "FTPS" Тогда
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	КонецЕсли;
	Соединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт,,, ИнтернетПрокси, Таймаут, ЗащищенноеСоединение);
	Возврат Соединение;
	
КонецФункции

// Процедура регламентного задания ОбменССервисомКабинетСотрудника
//
Процедура ОбменССервисомКабинетСотрудника() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника);
	Если ЕстьАктивныеФоновыеЗадания(КлючФоновогоЗаданияПубликации()) Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Если Не Настройки.ВыполнятьРегламентноеЗадание Тогда
		// Реализации паузы первого запуска регламентного задания.
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.ВключитьВыполнениеРегламентногоЗадания();
		Возврат;
	КонецЕсли;
	
	БылиОшибки = Ложь;
	Попытка
		
		ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПередачаИзменений());
		ОпубликоватьИзменения(ПараметрыПодключения, БылиОшибки);
		
		ПараметрыПодключения.ИмяСобытияЖР = ИмяСобытияЖРУдалениеДанных();
		ОпубликоватьУдалениеДанных(ПараметрыПодключения, БылиОшибки);
		
		ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПолучениеИзменений());
		ЗагрузитьИзмененияИзСервиса(ПараметрыПодключения);
		
	Исключение
		БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(ПараметрыПодключения.ИмяСобытияЖР,
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если БылиОшибки Тогда
		ВызватьИсключение НСтр("ru = 'Обмен данными с сервисом 1С:Кабинет сотрудника завершен с ошибками.'");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеПубликацииФоновоеЗадание(Параметры, АдресХранилища) Экспорт

	ОтложенноеОбновлениеСписковПубликуемыхОбъектов();
	
	БылиОшибки = Ложь;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляСервисаКабинетСотрудника");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		КабинетСотрудникаВнутренний.ДобавитьЭлементБлокировкиОбновленияПравНаОтпуск(Блокировка);
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛицаКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.ФизическиеЛицаКабинетСотрудника КАК ФизическиеЛицаКабинетСотрудника";
		ФизическиеЛица = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПубликуемыеОбъекты.ОбъектПубликации КАК ОбъектПубликации
		|ИЗ
		|	РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты";
		ПрочиеПубликуемыеОбъекты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОбъектПубликации");
		
		НаборЗаписейИзменения = РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписейВычеты    = РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьНаборЗаписей();
		Для каждого ОбъектПубликации Из ФизическиеЛица Цикл
			ЗаписьНабора = НаборЗаписейИзменения.Добавить();
			ЗаписьНабора.ПредметПубликации = ОбъектПубликации;
			ЗаписьНабора.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			ЗаписьНабора = НаборЗаписейВычеты.Добавить();
			ЗаписьНабора.ФизическоеЛицо = ОбъектПубликации;
			ЗаписьНабора.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		КонецЦикла;
		
		Для каждого ОбъектПубликации Из ПрочиеПубликуемыеОбъекты Цикл
			ЗаписьНабора = НаборЗаписейИзменения.Добавить();
			ЗаписьНабора.ПредметПубликации = ОбъектПубликации;
			ЗаписьНабора.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
		КонецЦикла;
		
		НаборЗаписейИзменения.Записать();
		НаборЗаписейВычеты.Записать();
		
		КабинетСотрудникаВнутренний.ЗарегистрироватьОбновленияПубликацииПравНаОтпуск(ПрочиеПубликуемыеОбъекты);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(ИмяСобытияЖРРегистрацияИзменений(),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	
	Попытка
		
		ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПередачаИзменений());
		ОпубликоватьИзменения(ПараметрыПодключения, БылиОшибки);
		
		ПараметрыПодключения.ИмяСобытияЖР = ИмяСобытияЖРУдалениеДанных();
		ОпубликоватьУдалениеДанных(ПараметрыПодключения, БылиОшибки);

	Исключение
		БылиОшибки = Истина;
		ЗаписьЖурналаРегистрации(ПараметрыПодключения.ИмяСобытияЖР,
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
	Если БылиОшибки Тогда
		ВызватьИсключение НСтр("ru = 'Обновление публикации в сервисе 1С:Кабинет сотрудника завершено с ошибками.'");
	КонецЕсли;

КонецПроцедуры

// Публикация расчетных листков после подключения к сервису.
Процедура ОпубликоватьВсеРасчетныеЛисткиЗаПериоды(ПериодыРасчетныхЛистков)

	Если ПериодыРасчетныхЛистков.Количество() = 0 Тогда
		Возврат
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТСотрудникиКПубликации
	|ИЗ
	|	РегистрСведений.ПубликуемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъектыКабинетСотрудника
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника КАК ОшибкиЗаполнения
	|			ПО Сотрудники.ФизическоеЛицо = ОшибкиЗаполнения.ОбъектПубликации
	|		ПО ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации = Сотрудники.Ссылка
	|ГДЕ
	|	ПубликуемыеОбъектыКабинетСотрудника.ОбъектПубликации ССЫЛКА Справочник.Сотрудники
	|	И ОшибкиЗаполнения.ЕстьОшибки ЕСТЬ NULL";
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиКПубликации") Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого МесяцПубликации Из ПериодыРасчетныхЛистков Цикл
		
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудников.НачалоПериода = НачалоМесяца(МесяцПубликации);
		ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецМесяца(МесяцПубликации);
		ПараметрыПолученияСотрудников.КадровыеДанные = "Организация";
		КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиОрганизации.Организация КАК Организация,
		|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиКПубликации КАК ПубликуемыеСотрудники
		|		ПО СотрудникиОрганизации.ФизическоеЛицо = ПубликуемыеСотрудники.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	ФизическоеЛицо";
		
		Выборка = Запрос.Выполнить().Выбрать();
			
		ПериодПубликации = НачалоМесяца(МесяцПубликации);
		
		Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
			
			Организация = Выборка.Организация;
			
			СписокФизическихЛиц = Новый Массив;
			Пока Выборка.Следующий() Цикл
				СписокФизическихЛиц.Добавить(Выборка.ФизическоеЛицо);
			КонецЦикла;
			
			Если СписокФизическихЛиц.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПубликацияРасчетныхЛистов());
			
			ВыгрузитьРасчетныеЛисты(ПараметрыПодключения,
				Организация,
				ПериодПубликации,
				СписокФизическихЛиц);
			
			Если ЗначениеЗаполнено(ПараметрыПодключения.Ошибки) Тогда
				ВызватьИсключение НСтр("ru = 'При выгрузке данных в сервис личных кабинетов произошли ошибки.'");
			КонецЕсли;	
			
		КонецЦикла;
		
		Запрос.Текст = "УНИЧТОЖИТЬ ВТСотрудникиОрганизации";
		Запрос.Выполнить();
		
	КонецЦикла;

КонецПроцедуры

Процедура ОчиститьДанныеПриОтключенииСервиса() Экспорт
	
	Константы.РегистрироватьВЖурналеСобытийЗапросы.Установить(Ложь);
	РегистрыСведений.ПравилаПубликацииКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ФизическиеЛицаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ПубликуемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ОшибкиЗаполненияПубликуемыхОбъектовКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ИзмененияДляСервисаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.РасчетныеЛисткиКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.СотрудникиДляОбновленияПубликуемыхОбъектов.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ОбъектыДляУдаленияИзСервисаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ВерсииЗаявокКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	
	КабинетСотрудникаВнутренний.ОчиститьДанныеПриОтключенииСервиса();
	
КонецПроцедуры

Процедура ВключитьНастройкиПрограммыПослеПодключенияСервиса() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не Константы.ИспользоватьБизнесПроцессыИЗадачи.Получить() Тогда
		Константы.ИспользоватьБизнесПроцессыИЗадачи.Установить(Истина);
	КонецЕсли;
	БизнесПроцессы.ЗаявкаКабинетСотрудника.ИнициализироватьРолиИсполнителей();
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если ЗначениеЗаполнено(ТекущийПользователь) Тогда
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаЗаявленияНаОтпуск");
		МенеджерЗаписи.Записать();
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаИзменениеЛичныхДанных");
		МенеджерЗаписи.Записать();
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаНалоговыеВычеты");
		МенеджерЗаписи.Записать();
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаСогласованиеОтсутствий");
		МенеджерЗаписи.Записать();
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаСправкиНДФЛ");
		МенеджерЗаписи.Записать();
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаСправкиОбОстаткахОтпусков");
		МенеджерЗаписи.Записать();
		МенеджерЗаписи = РегистрыСведений.ИсполнителиЗадач.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Исполнитель = ТекущийПользователь;
		МенеджерЗаписи.РольИсполнителя = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.РолиИсполнителей.ОтветственныйЗаСправкиСМестаРаботы");
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция КлючФоновогоЗаданияПубликации() Экспорт

	Возврат "ПубликацияВСервисе";

КонецФункции

Функция ЕстьАктивныеФоновыеЗадания(КлючФоновогоЗадания) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ",      КлючФоновогоЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Возврат (АктивныеФоновыеЗадания.Количество() > 0);
	
КонецФункции

Процедура ЗаписатьСобытиеЗапросВЖурналРегистрации(ПараметрыПодключения, ИмяМетода, HTTPЗапрос)

	ЭтоПередачаФайла = (СтрНайти(HTTPЗапрос.АдресРесурса, РесурсФайлы()) > 0);
	
	АдресСервера = СтрШаблон( "%1://%2", ПараметрыПодключения.СтруктураАдресаПриложения.Схема, ПараметрыПодключения.СтруктураАдресаПриложения.ИмяСервера);
	
	ЗапросТекст = ИмяМетода + " " + АдресСервера + HTTPЗапрос.АдресРесурса+ Символы.ПС;
	Для Каждого Заголовок Из HTTPЗапрос.Заголовки Цикл
		ЗначениеЗаголовка = Заголовок.Значение;
		Если Заголовок.Ключ = "Authorization" Тогда
			ЗначениеЗаголовка = "*";
		КонецЕсли;
		ЗапросТекст = ЗапросТекст + Символы.ПС + Заголовок.Ключ + ": " + ЗначениеЗаголовка;
	КонецЦикла;
	
	ТелоЗапроса = "";
	Если Не ЭтоПередачаФайла Тогда
		ТелоЗапроса = HTTPЗапрос.ПолучитьТелоКакСтроку();
	КонецЕсли;
		
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ТелоЗапроса);
	ИмяСобытияЖР = СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Протокол.Запрос'", ОбщегоНазначения.КодОсновногоЯзыка()));
	ЗаписьЖурналаРегистрации(ИмяСобытияЖР, УровеньЖурналаРегистрации.Информация,,,Комментарий);	

КонецПроцедуры

Процедура ЗаписатьСобытиеОтветВЖурналРегистрации(ПараметрыПодключения, ИмяМетода, HTTPЗапрос, HTTPОтвет)

	ЭтоПередачаФайла = (СтрНайти(HTTPЗапрос.АдресРесурса, РесурсФайлы()) > 0);
	
	АдресСервера = СтрШаблон( "%1://%2", ПараметрыПодключения.СтруктураАдресаПриложения.Схема, ПараметрыПодключения.СтруктураАдресаПриложения.ИмяСервера);
	
	ЗапросТекст = ИмяМетода + " " + АдресСервера + HTTPЗапрос.АдресРесурса+ Символы.ПС;
	Для Каждого Заголовок Из HTTPЗапрос.Заголовки Цикл
		ЗначениеЗаголовка = Заголовок.Значение;
		Если Заголовок.Ключ = "Authorization" Тогда
			ЗначениеЗаголовка = "*";
		КонецЕсли;
		ЗапросТекст = ЗапросТекст + Символы.ПС + Заголовок.Ключ + ": " + ЗначениеЗаголовка;
	КонецЦикла;
	
	Если HTTPОтвет = Неопределено Тогда
		ОтветТекст = НСтр("ru = 'Не удалось получить ответ от сервера.'");
	Иначе
		ОтветТекст = СтрШаблон(НСтр("ru = 'Сервер вернул код состояния: %1'"), HTTPОтвет.КодСостояния);
		Если Не ЭтоПередачаФайла Тогда
			ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Если ЗначениеЗаполнено(ТелоОтвета) Тогда
				ОтветТекст = ОтветТекст + Символы.ПС + Символы.ПС + ТелоОтвета;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Комментарий = НСтр(
	"ru = '%1
	|
	|%2'");
	
	Комментарий = СтрШаблон(Комментарий, ЗапросТекст, ОтветТекст);
	ИмяСобытияЖР = СтрШаблон("%1.%2", НазваниеГруппыСобытийЖР(), НСтр("ru = 'Протокол.Ответ'", ОбщегоНазначения.КодОсновногоЯзыка()));
	ЗаписьЖурналаРегистрации(ИмяСобытияЖР, УровеньЖурналаРегистрации.Информация,,,Комментарий);
	
КонецПроцедуры

Процедура ВыполнитьПубликациюПослеПодключенияКСервисуВФоне(ПериодыРасчетныхЛистков) Экспорт
	
	Попытка
		
		ПараметрыПодключения = ПараметрыПодключения(ИмяСобытияЖРПередачаИзменений());
		ОпубликоватьИзменения(ПараметрыПодключения, Ложь);
		
		Если ПериодыРасчетныхЛистков.Количество() > 0 Тогда
			ОпубликоватьВсеРасчетныеЛисткиЗаПериоды(ПериодыРасчетныхЛистков);
		КонецЕсли;
		
	Исключение
		
		ЗаписьЖурналаРегистрации(ПараметрыПодключения.ИмяСобытияЖР,
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти


