#Область ПрограммныйИнтерфейс

// Выполняет инициализацию и заполнение параметров сканирования по переданному контексту.
// Параметры сканирования необходимы для анализа и обработки штрихкодов маркируемой продукции.
//
// Параметры:
//  Контекст - Неопределено, ДокументСсылка, ФормаКлиентскогоПриложения - Источник данных для формирования параметров сканирования.
//  ФормаВыбора - Неопределено - Описание
//  ВидПродукции - Неопределено - Описание
// Возвращаемое значение:
//  Структура - Описание:
// * ИдентификаторФормы - УникальныйИдентификатор - Уникальный идентификатор формы.
// * КонтрольРасхожденийСДокументомОснованием - Булево - Истина, если необходимо выполнять контроль и сообщать
//                                                       об ошибках в случае несоответствия документу-основанию.
// * КэшированныеЗначения - Структура - Содержит поля кэшируемых значений.
// * ТолькоМаркируемаяПродукция - Булево - Истина, если документ поддерживает работу только с маркируемой продукцией.
// * ДопустимыеВидыПродукции - Массив Из ПеречислениеСсылка.ВидыПродукцииИС - Виды продукции,
//                                                                                                 которые поддерживает контекст.
// * ВозможнаЗагрузкаТСД - Булево - Истина, если контекст поддерживает работу с ТСД.
// * ДокументОснование - Неопределено, ДокументСсылка - Документ основание.
// * СоздаватьШтрихкодУпаковки - Булево - Истина, если необходимо создавать элемент справочника штрихкоды упаковок.
// * АдресСоответствияАкцизныхМарок - Строка - адрес во временном хранилище, где хранятся обработанные данные по штрихкодам.
// * АдресДанныхДокументаОснования - Строка - адрес во временном хранилище, где хранятся данные документа основания.
// * ИспользуетсяСоответствиеШтрихкодовСтрокДерева - Булево - Истина, если реквизит на форме СоответствиеШтрихкодовСтрокДерева.
// * ДанныеВыбораПоМаркируемойПродукции - Структура - последние данные выбора, которые требовалось запомнить для последующего сканирования.
// * ИспользуютсяДанныеВыбораПоМаркируемойПродукции - Булево - Истина, если данные выбора по маркируемой продукции используются.
// * ДополнительныеПараметры - Структура - Параметры необходимые для переопределения обработки прикладных объектов.
// * ОперацияКонтроляАкцизныхМарок - Строка - Доступные типы: "Продажа", "Возврат". "Продажа" - для документов, которые
//                                            отражает расход по складу, "Возврат" - для документов, отражающих приход.
Функция ПараметрыСканирования(Контекст = Неопределено, ФормаВыбора = Неопределено, ВидПродукции = Неопределено) Экспорт
	
	ПараметрыСканирования = ШтрихкодированиеИСКлиентСервер.БазовыеПараметрыСканирования();
	ШтрихкодированиеИСКлиентСервер.ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, Контекст);
	ЗаполнитьПараметрыСканированияДляАлкогольнойПродукции(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора);
	ЗаполнитьПараметрыСканированияДляТабачнойПродукции(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора);
	ЗаполнитьПараметрыСканированияДляПродукцииИСМП(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора);
	ШтрихкодированиеИСКлиентСерверПереопределяемый.ПриЗаполненииПараметровСканирования(ПараметрыСканирования, Контекст, ВидПродукции);
	
	Возврат ПараметрыСканирования;
	
КонецФункции

// Показывает форму ввода штрихкода.
// 
// Параметры:
//  ОписаниеОповещения - ОписаниеОповещения - Оповещение, которое будет выполнено по завершению.
Процедура ПоказатьВводШтрихкода(ОписаниеОповещения) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	ШтрихкодированиеИСКлиентПереопределяемый.ПоказатьВводШтрихкода(ОписаниеОповещения, СтандартнаяОбработка);
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Введите штрихкод'");
	
	ДополнительныеПараметры = Новый Структура("ОповещениеУспешногоВвода, Количество",
		ОписаниеОповещения, Неопределено);
	Оповещение = Новый ОписаниеОповещения("ПоказатьВводШтрихкодаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВводЗначения(Оповещение, "", Заголовок);
	
КонецПроцедуры

// Выполняет обработку штрихкода. Вызывается из формы проверки и подбора маркируемой продукции.
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - оповещение, которое произойдет при завершении обработки.
//  Форма - ФормаКлиентскогоПриложения - форма, в которой отсканирован штрихкод.
//  ДанныеШтрихкода - Структура - структура с ключами:
//   * Штрихкод - Строка - считанный штрихкод,
//   * Количество - Число - количество упаковок.
//  ПараметрыСканирования - Структура, Неопределено - (См. ПараметрыСканирования).
//
Процедура ОбработатьДанныеШтрихкода(ОповещениеПриЗавершении, Форма, ДанныеШтрихкода, ПараметрыСканирования = Неопределено) Экспорт
	
	Если ПараметрыСканирования = Неопределено Тогда
		ПараметрыСканирования = ПараметрыСканирования(Форма);
	КонецЕсли;
	
	ОтразитьИзменениеАдресаДанныхОснованияВФорме(Форма, ПараметрыСканирования);
	
	ШтрихкодированиеИСКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ДанныеШтрихкода);
	
	РезультатОбработкиШтрихкода = ШтрихкодированиеИСВызовСервера.ОбработатьШтрихкод(
		ДанныеШтрихкода, ПараметрыСканирования, Неопределено, Форма.УникальныйИдентификатор);
	
	Если РезультатОбработкиШтрихкода <> Неопределено Тогда
		
		Если ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева
			И РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры Тогда
			
			Если Форма.СоответствиеШтрихкодовСтрокДерева.Получить(ДанныеШтрихкода.Штрихкод) <> Неопределено Тогда
				РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если РезультатОбработкиШтрихкода.ЕстьОшибкиВДеревеУпаковок
			Или Не ПустаяСтрока(РезультатОбработкиШтрихкода.ТекстОшибки)
			Или РезультатОбработкиШтрихкода.ТребуетсяУточнениеДанных
			Или ТребуетсяУточнениеДанныхУПользователя(Форма, РезультатОбработкиШтрихкода, ПараметрыСканирования) Тогда
			
			ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияОбработкиШтрихкода();
			ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода   = РезультатОбработкиШтрихкода;
			ПараметрыЗавершенияВводаШтрихкода.ОповещениеЗавершениеОбработки = ОповещениеПриЗавершении;
			ПараметрыЗавершенияВводаШтрихкода.ПараметрыСканирования         = ПараметрыСканирования;
			ПараметрыЗавершенияВводаШтрихкода.Форма                         = Форма;
			ПараметрыЗавершенияВводаШтрихкода.ДанныеШтрихкода               = ДанныеШтрихкода;
			ПараметрыЗавершенияВводаШтрихкода.ВызовИзФормыДокумента         = Ложь;
			
			ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
			
		Иначе
			
			ДанныеШтрихкода = РезультатОбработкиШтрихкода.ДанныеШтрихкода;
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ДанныеШтрихкода);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует структуру для завершения обработки штрихкода.
//
// Параметры:
// Возвращаемое значение:
//  Структура - Описание:
// * ВызовИзФормыДокумента - Булево - Истина, в случае выполнения вызова из формы документа, Ложь - из формы обработки
// проверки и подбора.
// * ДанныеШтрихкода - (См. ШтрихкодированиеИС.ИнициализироватьДанныеШтрихкода).
// * ПараметрыСканирования - (См. ПараметрыСканирования).
// * ОповещениеПриЗавершении - ОписаниеОповещения - Оповещение, которое необходимо выполнить при завершении обработки.
// * КэшированныеЗначения - Структура - Содержит поля кэшируемых значений.
// * РезультатОбработкиШтрихкода - (См. ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода).
// * Форма - ФормаКлиентскогоПриложения - Форма, для которой выполняется обработка.
Функция ПараметрыЗавершенияОбработкиШтрихкода() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Форма");
	Параметры.Вставить("РезультатОбработкиШтрихкода");
	Параметры.Вставить("КэшированныеЗначения");
	Параметры.Вставить("ПараметрыСканирования");
	Параметры.Вставить("ДанныеШтрихкода");
	Параметры.Вставить("ОповещениеОбработкиШтрихкода");
	Параметры.Вставить("ОповещениеВыполнитьДействие");
	Параметры.Вставить("ОповещениеЗавершениеОбработки");
	Параметры.Вставить("ВызовИзФормыДокумента", Истина);
	
	Возврат Параметры;
	
КонецФункции

// Выполняет завершение обработки штрихкода. На основании ПараметрыЗавершенияОбработкиШтрихкода.РезультатОбработкиШтрихкода
// выполняет необходимые действия.
//
// Параметры:
//  ПараметрыЗавершенияОбработкиШтрихкода - (См. ПараметрыЗавершенияОбработкиШтрихкода).
Процедура ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияОбработкиШтрихкода) Экспорт
	
	Форма                         = ПараметрыЗавершенияОбработкиШтрихкода.Форма;
	ПараметрыСканирования         = ПараметрыЗавершенияОбработкиШтрихкода.ПараметрыСканирования;
	РезультатОбработкиШтрихкода   = ПараметрыЗавершенияОбработкиШтрихкода.РезультатОбработкиШтрихкода;
	ОповещениеЗавершениеОбработки = ПараметрыЗавершенияОбработкиШтрихкода.ОповещениеЗавершениеОбработки;
	
	// ТребуетсяСопоставлениеНоменклатуры - для массива штрихкодов вызывается только 1 раз
	// ОткрытьФормуВводаКодаМаркировки - вызывается только при сканировании 1 штрихкода
	Если РезультатОбработкиШтрихкода.ТребуетсяСопоставлениеНоменклатуры
		Или РезультатОбработкиШтрихкода.ОткрытьФормуВводаКодаМаркировки Тогда
		
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ОткрытьФормуУточненияДанных", 0.1, Истина);
		
		Возврат;
		
	КонецЕсли;
	
	// Далее, общие обработчики для сканера и ТСД
	Если ПрисутствуетПродукцияИСМП(РезультатОбработкиШтрихкода.ВидыПродукции, Истина)
		Или Не ЗначениеЗаполнено(РезультатОбработкиШтрихкода.ВидыПродукции) Тогда
		Если РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры
			Или РезультатОбработкиШтрихкода.ТребуетсяУточнениеДанных Тогда
			
			Форма.КодМаркировкиДляУточнения = ПараметрыЗавершенияОбработкиШтрихкода;
			Форма.ПодключитьОбработчикОжидания("Подключаемый_ОткрытьФормуУточненияДанных", 0.1, Истина);
			
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыСканирования.ВыводитьСообщенияОбОшибках
		И ИнформироватьОНевозможностиДобавления(Форма, РезультатОбработкиШтрихкода) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПрисутствуетАлкогольнаяПродукция(ПараметрыСканирования.ДопустимыеВидыПродукции)
		И ПрисутствуетАлкогольнаяПродукция(РезультатОбработкиШтрихкода.ВидыПродукции) Тогда
		
		МодульОбработки = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеЕГАИСКлиент");
		Если МодульОбработки.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияОбработкиШтрихкода) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПрисутствуетПродукцияИСМП(ПараметрыСканирования.ДопустимыеВидыПродукции, Истина)
		И ПрисутствуетПродукцияИСМП(РезультатОбработкиШтрихкода.ВидыПродукции, Истина) Тогда
		
		МодульОбработки = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеИСМПКлиент");
		Если МодульОбработки.ЗавершитьОбработкуВводаШтрихкода(ПараметрыЗавершенияОбработкиШтрихкода, Ложь) Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОповещениеЗавершениеОбработки <> Неопределено Тогда
		Если ПараметрыЗавершенияОбработкиШтрихкода.ВызовИзФормыДокумента Тогда
			ВыполнитьОбработкуОповещения(ОповещениеЗавершениеОбработки, РезультатОбработкиШтрихкода);
		Иначе
			ВыполнитьОбработкуОповещения(ОповещениеЗавершениеОбработки, РезультатОбработкиШтрихкода.ДанныеШтрихкода);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет завершение после показа формы ввода штрихкода.
// 
// Параметры:
//  Штрихкод - Строка - штрихкод строкой.
//  ДополнительныеПараметры - Структура - Параметры описания оповещения.
Процедура ПоказатьВводШтрихкодаЗавершение(Штрихкод, ДополнительныеПараметры) Экспорт
	
	ОповещениеУспешногоВвода = ДополнительныеПараметры.ОповещениеУспешногоВвода;
	Количество = ДополнительныеПараметры.Количество;
	
	Если Штрихкод = Неопределено Или ПустаяСтрока(Штрихкод) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество = Неопределено Тогда
		Количество = 1;
	КонецЕсли;
	
	Если СтрДлина(Штрихкод) > 200 Тогда
		ТекстСообщения = НСтр("ru='Длина штрихкода не должна быть больше 200 символов.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ОповещениеУспешногоВвода,
		Новый Структура("Штрихкод, Количество", Штрихкод, Количество));
	
КонецПроцедуры

// В случае успешного сопоставления номенклатуры неизвестным штрихкодам выполняет повторную попытку обработки штрихкода.
// 
// Параметры:
//  Результат - Произвольный - результат сопоставления неизветсной номенклатуры штрихкодам формата EAN.
//  ДополнительныеПараметры - Структура - параметры описания оповещения.
Процедура СопоставлениеНоменклатурыШтрихкодамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ШтрихкодированиеИСВызовСервера.ОчиститьОтложенныеКодыМаркировки(
			ДополнительныеПараметры.ПараметрыСканирования.КэшМаркируемойПродукции);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПовторнойОбработки, ДополнительныеПараметры.ИсходныеДанные);
	
КонецПроцедуры

#Область РаботаСФормойКодаМаркировки

//Структура параметров, необходимых для открытия формы считывания кода маркировки.
//
//Возвращаемое значение:
//  Структура - требуемые параметры:
// * Номенклатура - ОпределяемыйТип.Номенклатура - Номенклатура.
// * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры - Характеристика.
// * ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// * МаркируемаяПродукция - Булево - Истина, если продукция маркируемая.
// * ПараметрыСканирования - См. ПараметрыСканирования.
// * ДанныеШтрихкода - См. ШтрихкодированиеИС.ИнициализироватьДанныеШтрихкода.
// * Документ - Произвольный - .
// * РазрешатьДобавлениеБезКодаМарки - Булево - разрешать добавление строки при отказе от ввода кода маркировки.
//
Функция ПараметрыОткрытияФормыВводаКодаМаркировки() Экспорт
	
	Возврат ШтрихкодированиеИСКлиентСервер.ПараметрыОткрытияФормыВводаКодаМаркировки();
	
КонецФункции

//Открывает форму ввода кода маркировки.
//
//Параметры:
//   ФормаВладелец - ФормаКлиентскогоПриложения - форма, из которой необходимо выполнить открытие.
//   ПараметрыОткрытия - См. ПараметрыОткрытияФормыВводаКодаМаркировки.
//   ОповещениеОЗавершении - ОписаниеОповещения, Неопределено - действие после закрытия формы ввода кода маркировки
//
Процедура ОткрытьФормуСчитыванияКодаМаркировки(ФормаВладелец, ПараметрыОткрытия, ОповещениеОЗавершении = Неопределено) Экспорт
	
	Если Не ПараметрыОткрытия.МаркируемаяПродукция Тогда
		
		ПоказатьПредупреждение(
			Неопределено, НСтр("ru = 'Для данной строки не указываются акцизные марки'"));
		
		Возврат;
	КонецЕсли;
	
	Если ОповещениеОЗавершении = Неопределено Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ФормаВладелец);
	КонецЕсли;
	
	Если ПараметрыОткрытия.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")
		И ПрисутствуетАлкогольнаяПродукция(ПараметрыОткрытия.ПараметрыСканирования.ДопустимыеВидыПродукции) Тогда
		
		ОткрытьФорму(
			"Обработка.РаботаСАкцизнымиМаркамиЕГАИС.Форма.ФормаВводаАкцизнойМарки",
			ПараметрыОткрытия, ФормаВладелец,,,,ОповещениеОЗавершении);
		
	ИначеЕсли ПараметрыОткрытия.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак")
		И ПрисутствуетТабачнаяПродукция(ПараметрыОткрытия.ПараметрыСканирования.ДопустимыеВидыПродукции) Тогда
		
		ОткрытьФорму(
			"Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ФормаВводаКодаМаркировки",
			ПараметрыОткрытия, ФормаВладелец,,,,ОповещениеОЗавершении);
		
	ИначеЕсли ИнтеграцияИСКлиентСервер.ЭтоПродукцияИСМП(ПараметрыОткрытия.ВидПродукции)
		И ПрисутствуетПродукцияИСМП(ПараметрыОткрытия.ПараметрыСканирования.ДопустимыеВидыПродукции) Тогда
		
		ОткрытьФорму(
			"Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ФормаВводаКодаМаркировки",
			ПараметрыОткрытия, ФормаВладелец,,,,ОповещениеОЗавершении);
		
	Иначе
		
		ТекстОшибки = ТекстОшибкиНеПоддерживаетсяВидПродукции(
			ПараметрыОткрытия.ПараметрыСканирования, ПараметрыОткрытия.ВидПродукции);
		
		ПараметрыОткрытияФормы = ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		ПараметрыОткрытияФормы.ТекстОшибкиФорматированнаяСтрока = ТекстОшибки;
		ПараметрыОткрытияФормы.Штрихкод                         = ПараметрыОткрытия.ДанныеШтрихкода.Штрихкод;
		
		ОткрытьФорму(
			"ОбщаяФорма.ИнформацияОНевозможностиДобавленияОтсканированного",
			ПараметрыОткрытияФормы, ФормаВладелец);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормойУточненияДанных


// Анализирует результат проверки на необходимость уточнения каких-либо данных у пользователя.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой необходимо выполнить обработку штрихкода.
//  РезультатОбработкиШтрихкода - (См. ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода).
//  ПараметрыСканирования - (см. ПараметрыСканирования).
// Возвращаемое значение:
//  Булево - Истина, если требуется уточнить какие-либо данные у пользователя.
Функция ТребуетсяУточнениеДанныхУПользователя(Форма, РезультатОбработкиШтрихкода, ПараметрыСканирования) Экспорт
	
	Если РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если РезультатОбработкиШтрихкода.ОткрытьФормуВводаКодаМаркировки Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если РезультатОбработкиШтрихкода.ТребуетсяСопоставлениеНоменклатуры Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ПрисутствуетАлкогольнаяПродукция(РезультатОбработкиШтрихкода.ВидыПродукции) Тогда
		
		МодульШтрихкодированиеЕГАИСКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеЕГАИСКлиент");
		Возврат МодульШтрихкодированиеЕГАИСКлиент.ТребуетсяУточнениеДанныхУПользователя(РезультатОбработкиШтрихкода);
		
	КонецЕсли;
	
	Если ПрисутствуетТабачнаяПродукция(РезультатОбработкиШтрихкода.ВидыПродукции) Тогда
		
		МодульШтрихкодированиеМОТПКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеМОТПКлиент");
		Возврат МодульШтрихкодированиеМОТПКлиент.ТребуетсяУточнениеДанныхУПользователя(РезультатОбработкиШтрихкода);
		
	КонецЕсли;

	Если ПрисутствуетПродукцияИСМП(РезультатОбработкиШтрихкода.ВидыПродукции) Тогда

		МодульШтрихкодированиеИСМПКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеИСМПКлиент");
		Возврат МодульШтрихкодированиеИСМПКлиент.ТребуетсяУточнениеДанныхУПользователя(РезультатОбработкиШтрихкода);

	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

// Открывает форму уточнения номенклатуры и серии.
// 
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения - форма владелец.
//  ПараметрыОткрытияФормы - (См. ШтрихкодированиеИСКлиентСервер.ПараметрыОткрытияФормыУточненияДанных)
//  ОповещениеПоЗавершениюУточненияДанных - ОписаниеОповещения - Оповещение после закрытия формы.
Процедура УточнитьДанныеУПользователя(ФормаВладелец, ПараметрыОткрытияФормы, ОповещениеПоЗавершениюУточненияДанных) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОткрытияФормы.КодМаркировкиДляУточнения) Тогда
		ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя = ПреобразоватьВПараметрыОткрытияФормы(ПараметрыОткрытияФормы.КодМаркировкиДляУточнения);
		ВидУпаковки = ПараметрыОткрытияФормы.КодМаркировкиДляУточнения.РезультатОбработкиШтрихкода.ДанныеШтрихкода.ВидУпаковки;
		Если ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Логистическая") Тогда
			ПараметрыОткрытияФормы.Операция = "ОткрытьФормуУточненияУпаковки";
		Иначе
			ПараметрыОткрытияФормы.Операция = "ОткрытьФормуУточненияДанных";
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыОткрытияФормы.Операция = "ОткрытьФормуВводаКодаМаркировки" Тогда
		
		ОткрытьФормуСчитыванияКодаМаркировки(ФормаВладелец, ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя, ОповещениеПоЗавершениюУточненияДанных);
		
	ИначеЕсли ПараметрыОткрытияФормы.Операция = "СопоставлениеНоменклатуры" Тогда
		
		ШтрихкодыКСопоставлению = ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя.ШтрихкодыКСопоставлению;
		ИсходныеДанные          = ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя.ИсходныеДанные;
		ПараметрыСканирования   = ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя.ПараметрыСканирования;
		
		ДополнительныеПараметры = Новый Структура("ОповещениеПовторнойОбработки, ИсходныеДанные, ПараметрыСканирования",
			ОповещениеПоЗавершениюУточненияДанных, ИсходныеДанные, ПараметрыСканирования);
		ОповещениеОЗавершенииСопоставления = Новый ОписаниеОповещения("СопоставлениеНоменклатурыШтрихкодамЗавершение", ШтрихкодированиеИСКлиент, ДополнительныеПараметры);
		ШтрихкодированиеИСКлиентПереопределяемый.ОткрытьФормуПодбораНоменклатурыПоШтрихкодам(
			ШтрихкодыКСопоставлению, ФормаВладелец, ОповещениеОЗавершенииСопоставления);
		
	ИначеЕсли ПараметрыОткрытияФормы.Операция = "ОткрытьФормуУточненияДанных" Тогда
		
		ДополнитьПараметрыУточнения(ФормаВладелец, ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя);
		РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму(
			"ОбщаяФорма.ФормаУточненияДанныхИС", ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя,
			ФормаВладелец,,,, ОповещениеПоЗавершениюУточненияДанных, РежимОткрытия);
		
	ИначеЕсли ПараметрыОткрытияФормы.Операция = "ОткрытьФормуУточненияУпаковки" Тогда
		
		ДополнитьПараметрыУточнения(ФормаВладелец, ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя, Истина);
		РежимОткрытия = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ОткрытьФорму(
			"ОбщаяФорма.УточнениеСоставаУпаковкиИС", ПараметрыОткрытияФормы.ДанныеДляУточненияСведенийПользователя,
			ФормаВладелец,,,, ОповещениеПоЗавершениюУточненияДанных, РежимОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует структуру данных, собранных после уточнения сведений у пользователя.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * ПараметрыСканирования - См. ПараметрыСканирования.
// * ИсходныеДанные - Структура
//    * Штрихкод - Строка - Штрихкод.
//    * Количество - Число - Количество.
// * КэшированныеЗначения - Произвольный - кэш обработанных ранее штрихкодов.
// * РезультатВыбора - Произвольный - результат уточнения данных пользователем.
// * РезультатОбработкиШтрихкода - См. ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода.
// * Действие - Строка - Действие, которое необходимо выполнить с данными.
Функция ИнициализацияРезультатаУточненияДанныхДляВыполненияДальнейшихДействий() Экспорт
	
	РезультатУточненияДанных = Новый Структура;
	
	РезультатУточненияДанных.Вставить("Действие");
	РезультатУточненияДанных.Вставить("РезультатОбработкиШтрихкода");
	РезультатУточненияДанных.Вставить("РезультатВыбора");
	РезультатУточненияДанных.Вставить("КэшированныеЗначения");
	РезультатУточненияДанных.Вставить("ИсходныеДанные");
	РезультатУточненияДанных.Вставить("ПараметрыСканирования");
	РезультатУточненияДанных.Вставить("ДанныеШтрихкода");
	
	Возврат РезультатУточненияДанных;
	
КонецФункции

// Выполняет обработку оповещения "ОповещениеВыполнитьДействие" и передает результат уточнения данных у пользователя.
// 
// Параметры:
//  РезультатВыбора - Произвольный - результат уточнения данных у пользователя.
//  ДополнительныеПараметры - Структура - дополнительные параметры описания оповещения.
Процедура УточненияДанныхЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОповещениеВыполнитьДействие <> Неопределено Тогда
		
		РезультатУточненияДанных = ИнициализацияРезультатаУточненияДанныхДляВыполненияДальнейшихДействий();
		РезультатУточненияДанных.Действие                    = "ОбработатьУточнениеДанных";
		РезультатУточненияДанных.РезультатОбработкиШтрихкода = ДополнительныеПараметры.РезультатОбработкиШтрихкода;
		РезультатУточненияДанных.РезультатВыбора             = РезультатВыбора;
		РезультатУточненияДанных.КэшированныеЗначения        = ДополнительныеПараметры.КэшированныеЗначения;
		РезультатУточненияДанных.ИсходныеДанные              = ДополнительныеПараметры.ДанныеШтрихкода;
		РезультатУточненияДанных.ПараметрыСканирования       = ДополнительныеПараметры.ПараметрыСканирования;
		РезультатУточненияДанных.ДанныеШтрихкода             = ДополнительныеПараметры.ДанныеШтрихкода;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеВыполнитьДействие, РезультатУточненияДанных);
		
	ИначеЕсли ДополнительныеПараметры.ВызовИзФормыДокумента Тогда // Ветка для старого механизма.
		
		Форма = ДополнительныеПараметры.Форма;
		
		Действие = "ОбработатьУточнениеДанных";
		РезультатОбработкиШтрихкода = Форма.Подключаемый_ВыполнитьДействие(
			Действие,
			РезультатВыбора,
			ДополнительныеПараметры.РезультатОбработкиШтрихкода,
			ДополнительныеПараметры.КэшированныеЗначения);
		
		ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияОбработкиШтрихкода();
		ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода = РезультатОбработкиШтрихкода;
		ПараметрыЗавершенияВводаШтрихкода.КэшированныеЗначения        = ДополнительныеПараметры.КэшированныеЗначения;
		ПараметрыЗавершенияВводаШтрихкода.Форма                       = Форма;
		ДополнительныеПараметры.Свойство("ОповещениеПриЗавершении", ПараметрыЗавершенияВводаШтрихкода.ОповещениеПриЗавершении);
		
		ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияВводаШтрихкода);
		
	Иначе
		
		ШтрихкодированиеИСВызовСервера.ОбработатьУточнениеДанныхДляФормыПроверкиИПодбора(
			РезультатВыбора,
			ДополнительныеПараметры.РезультатОбработкиШтрихкода,
			ДополнительныеПараметры.ПараметрыСканирования,
			ДополнительныеПараметры.КэшированныеЗначения);
		
		ЗавершитьОбработкуШтрихкода(ДополнительныеПараметры);
		
		Если ДополнительныеПараметры.ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции
			И ТипЗнч(ДополнительныеПараметры.РезультатОбработкиШтрихкода.ДанныеШтрихкода) = Тип("Структура") Тогда
			ДанныеШтрихкода = ДополнительныеПараметры.РезультатОбработкиШтрихкода.ДанныеШтрихкода;
			ШтрихкодированиеИСМПКлиентСервер.ОбработатьСохраненныйВыборДанныхПоМаркируемойПродукции(ДополнительныеПараметры.Форма, ДанныеШтрихкода);
		КонецЕсли;
		
		ГрупповаяОбработкаШтрихкодовИСКлиент.ПрименитьСохраненныйВыбор(
			ДополнительныеПараметры.Форма, 
			ДополнительныеПараметры.ПараметрыСканирования);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормойНевозможностиДобавленияОтсканированного

// Инициализирует параметры открытия формы невозможности добавления отсканированного.
//
// Параметры:
// 	ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид маркируемой продукции.
// Возвращаемое значение:
// 	Структура - Описание:
// * АдресДереваУпаковок - Строка - адрес хранилища, где находится дерево упаковок.
// * АлкогольнаяПродукция - СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС - алкогольная продукция.
// * ТипШтрихкода - ПеречислениеСсылка.ТипыШтрихкодов - Тип штрихкода.
// * ТекстОшибки - Строка - Описание причины невозможности обработки отсканированного штрихкода.
// * Штрихкод - Строка - Штрихкод строкой.
// * ПредставлениеНоменклатуры - Строка - Представление маркируемой продукции.
// * ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид маркируемой продукции.
Функция ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного(ВидПродукции = Неопределено) Экспорт
	
	ИнформацияПроблемы = Новый Структура;
	
	ИнформацияПроблемы.Вставить("ВидПродукции", ВидПродукции);
	
	// Информация по маркированному товару.
	ИнформацияПроблемы.Вставить("ПредставлениеНоменклатуры",        Неопределено);
	ИнформацияПроблемы.Вставить("Штрихкод",                         Неопределено);
	ИнформацияПроблемы.Вставить("ТекстОшибки",                      Неопределено);
	ИнформацияПроблемы.Вставить("ТекстОшибкиФорматированнаяСтрока", Неопределено);
	ИнформацияПроблемы.Вставить("ТипШтрихкода",                     Неопределено);
	ИнформацияПроблемы.Вставить("ВидУпаковки",                      Неопределено);
	ИнформацияПроблемы.Вставить("АлкогольнаяПродукция",             Неопределено);
	
	// Информация по дереву упаковок.
	ИнформацияПроблемы.Вставить("АдресДереваУпаковок", Неопределено);
	
	Возврат ИнформацияПроблемы;
	
КонецФункции

// Открывает форму с описанием ошибки о невозможности обработать отсканированный штрихкод.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой необходимо выполнить обработку штрихкода.
//  ПараметрыОткрытияФормы - (См. ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного).
//
Процедура ОткрытьФормуНевозможностиДобавленияОтсканированного(Форма, ПараметрыОткрытияФормы) Экспорт
	
	Если ПараметрыОткрытияФормы.ВидПродукции = Неопределено Тогда
		
		ОткрытьФорму("ОбщаяФорма.ИнформацияОНевозможностиДобавленияОтсканированного", ПараметрыОткрытияФормы, Форма);
		
	ИначеЕсли ПараметрыОткрытияФормы.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		
		МодульШтрихкодированиеЕГАИСКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеЕГАИСКлиент");
		МодульШтрихкодированиеЕГАИСКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(Форма, ПараметрыОткрытияФормы);
		
	ИначеЕсли ПараметрыОткрытияФормы.ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак") Тогда
		
		МодульШтрихкодированиеМОТПКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеМОТПКлиент");
		МодульШтрихкодированиеМОТПКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(Форма, ПараметрыОткрытияФормы);
		
	Иначе
		
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
			
			Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияИСМП(ПараметрыОткрытияФормы.ВидПродукции) Тогда
				
				МодульШтрихкодированиеИСМПКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеИСМПКлиент");
				МодульШтрихкодированиеИСМПКлиент.ОткрытьФормуНевозможностиДобавленияОтсканированного(Форма, ПараметрыОткрытияФормы);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Получает данные из кэша, необходимые для уточнения сведений у пользователя. Открывает форму уточнения сведений.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа.
//  ОписаниеОповещенияОЗавершении - ОписаниеОповещения - оповещение, которое будет выполнено после получения сведений.
Процедура Подключаемый_ОткрытьФормуУточненияДанных(Форма, ОписаниеОповещенияОЗавершении) Экспорт
	
	// Сценарий: есть текущий результат обработки кода маркировки
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "КодМаркировкиДляУточнения")
		И ЗначениеЗаполнено(Форма.КодМаркировкиДляУточнения) Тогда
		КодМаркировкиДляУточнения = Форма.КодМаркировкиДляУточнения;
		Форма.КодМаркировкиДляУточнения = Неопределено;
		ПараметрыОткрытияФормыУточненияДанных = ШтрихкодированиеИСКлиентСервер.ПараметрыОткрытияФормыУточненияДанных();
		ПараметрыОткрытияФормыУточненияДанных.КодМаркировкиДляУточнения = КодМаркировкиДляУточнения;
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"УточненияДанныхЗавершение", ЭтотОбъект, КодМаркировкиДляУточнения);
		УточнитьДанныеУПользователя(Форма, ПараметрыОткрытияФормыУточненияДанных, ОписаниеОповещения);
		Возврат;
	КонецЕсли;
	
	ДанныеДляУточненияСведений = ШтрихкодированиеИСВызовСервера.ДанныеДляУточненияСведенийПользователя(Форма.КэшМаркируемойПродукции);
	
	Если ПустаяСтрока(ДанныеДляУточненияСведений.Операция) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляУточненияСведенийПользователя = ДанныеДляУточненияСведений.Данные;
	Если ТипЗнч(ДанныеДляУточненияСведений.Данные) = Тип("Соответствие") Тогда
		
		Для Каждого КлючЗначение Из ДанныеДляУточненияСведений.Данные Цикл
			ДанныеДляУточненияСведенийПользователя = КлючЗначение.Значение;
		КонецЦикла;
		
	КонецЕсли;
	
	ПараметрыОткрытияФормыУточненияДанных = ШтрихкодированиеИСКлиентСервер.ПараметрыОткрытияФормыУточненияДанных();
	ПараметрыОткрытияФормыУточненияДанных.Операция = ДанныеДляУточненияСведений.Операция;
	ПараметрыОткрытияФормыУточненияДанных.ДанныеДляУточненияСведенийПользователя = ДанныеДляУточненияСведенийПользователя;
	
	УточнитьДанныеУПользователя(Форма, ПараметрыОткрытияФормыУточненияДанных, ОписаниеОповещенияОЗавершении);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаВидовПродукции

// Выполняет проверку в переданном массиве "ВидыПродукции" наличие элемента алкогольной продукции.
//
// Параметры:
//  ВидыПродукции - Массив из ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//  Булево - Истина, если в переданном массиве найден элемент перечисления алкогольная продукция.
Функция ПрисутствуетАлкогольнаяПродукция(ВидыПродукции) Экспорт
	
	Возврат ВидыПродукции.Найти(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")) <> Неопределено;
	
КонецФункции

// Выполняет проверку в переданном массиве "ВидыПродукции" наличие элемента табачной продукции.
//
// Параметры:
//  ВидыПродукции - Массив из ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
// Возвращаемое значение:
//  Булево - Истина, если в переданном массиве найден элемент перечисления табачная продукция.
Функция ПрисутствуетТабачнаяПродукция(ВидыПродукции) Экспорт
	
	Возврат ВидыПродукции.Найти(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак")) <> Неопределено;
	
КонецФункции

// Выполняет проверку в переданном массиве "ВидыПродукции" наличие элемента продукции ИСМП.
//
// Параметры:
//  ВидыПродукции - Массив из ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции ИС.
//  ВключатьТабачнуюПродукцию - Булево - Признак включения табачной продукции
//  ВключатьМолочнуюПродукцию - Булево - Признак вкючения молочной продукции
// Возвращаемое значение:
//  Булево - Истина, если в переданном массиве найден элемент перечисления обувная продукция.
Функция ПрисутствуетПродукцияИСМП(ВидыПродукции, ВключатьТабачнуюПродукцию = Ложь, ВключатьМолочнуюПродукцию = Истина) Экспорт
	
	Для Каждого ВидПродукции Из ВидыПродукции Цикл
		Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияИСМП(ВидПродукции, ВключатьТабачнуюПродукцию, ВключатьМолочнуюПродукцию) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

// Показывает окно оповещения об окончании обработки данных ТСД.
//
// Параметры:
//   ПараметрыУведомления - Структура, Неопределено - параметры уведомления.
Процедура ОповеститьОбОкончанииОбработкиДанныхТСД(ПараметрыУведомления = Неопределено) Экспорт
	
	Если ПараметрыУведомления = Неопределено Тогда
		ТекстЗаголовка   = ЗаголовокОповещенияТСД();
		ТекстУведомления = НСтр("ru = 'Закончена обработка полученных из ТСД данных.'");
	Иначе
		ТекстЗаголовка   = ПараметрыУведомления.Заголовок;
		ТекстУведомления = ПараметрыУведомления.Текст;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		ТекстЗаголовка,,
		ТекстУведомления,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры


// Показывает окно оповещения о начале обработки данных ТСД.
//
// Параметры:
//	ПараметрыУведомления - Структура, Неопределено - Параметры уведомления.
Процедура ОповеститьОНачалеОбработкиДанныхТСД(ПараметрыУведомления = Неопределено) Экспорт
	
	Если ПараметрыУведомления = Неопределено Тогда
		ТекстЗаголовка   = ЗаголовокОповещенияТСД();
		ТекстУведомления = НСтр("ru = 'Начата обработка полученных из ТСД данных.'");
	Иначе
		ТекстЗаголовка   = ПараметрыУведомления.Заголовок;
		ТекстУведомления = ПараметрыУведомления.Текст;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		ТекстЗаголовка,,
		ТекстУведомления,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Преобразует массив штрихкодов в формат Base64.
// 
// Параметры:
//  ШтрихкодыТСД - Массив из Строка - список штрихкодов.
Процедура ПреобразоватьШтрихкодыТСДВBase64(ШтрихкодыТСД) Экспорт
	
	Для Каждого ЭлементМассива Из ШтрихкодыТСД Цикл
		Если ЭлементМассива.Свойство("ШтрихкодыПреобразованы") Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ЭлементМассива.Штрихкод) Тогда
			ЭлементМассива.Штрихкод = ШтрихкодированиеИСКлиентСервер.ШтрихкодВBase64(ЭлементМассива.Штрихкод);
		КонецЕсли;
		Если ЭлементМассива.Свойство("ШтрихкодУпаковки") И ЗначениеЗаполнено(ЭлементМассива.ШтрихкодУпаковки) Тогда
			ЭлементМассива.ШтрихкодУпаковки = ШтрихкодированиеИСКлиентСервер.ШтрихкодВBase64(ЭлементМассива.ШтрихкодУпаковки);
		КонецЕсли;
		ЭлементМассива.Вставить("ШтрихкодыПреобразованы");
	КонецЦикла;
	
КонецПроцедуры

// Инициализирует структуру сохраненных данных в документе
// 
// Возвращаемое значение:
//  Структура - Описание:
// * ДанныеВыбора - Структура - данные сохраненного выбора.
// * ЗапомнитьВыбор - Булево - признак сохранения выбора пользователем.
//
Функция ИнициализацияСтруктурыДанныхСохраненногоВыбора() Экспорт
	
	ДанныеВыбора = Новый Структура;
	ДанныеВыбора.Вставить("Номенклатура",                    Неопределено);
	ДанныеВыбора.Вставить("ПредставлениеНоменклатуры",       "");
	ДанныеВыбора.Вставить("Характеристика",                  Неопределено);
	ДанныеВыбора.Вставить("Серия",                           Неопределено);
	ДанныеВыбора.Вставить("КодМаркировки",                   "");
	
	//Коды остатков
	ДанныеВыбора.Вставить("GTIN",                            "");
	
	//Специфика для молочной продукции
	ДанныеВыбора.Вставить("ИдентификаторПроисхожденияВЕТИС", Неопределено);
	ДанныеВыбора.Вставить("ГоденДо",                         '00010101');
	ДанныеВыбора.Вставить("Скоропортящаяся",                 Ложь);
	
	//Специфика для табачной и молочной продукции
	ДанныеВыбора.Вставить("СоставКодаМаркировки",            Неопределено);
	
	//Для уточнения коэффициента групповой упаковки
	ДанныеВыбора.Вставить("Коэффициент",            0);
	
	// Для документов, печатающих коды маркировки
	ДанныеВыбора.Вставить("СразуНаПринтер",         Ложь);
	ДанныеВыбора.Вставить("ШаблонЭтикетки",         Неопределено);
	ДанныеВыбора.Вставить("ШаблонМаркировки",       Неопределено);
	
	// Индивидуально перемаркирова
	ДанныеВыбора.Вставить("ПричинаПеремаркировки",  Неопределено);
	ДанныеВыбора.Вставить("НоваяНоменклатура",      Неопределено);
	ДанныеВыбора.Вставить("НоваяХарактеристика",    Неопределено);
	
	// Для формы проверки и подбора
	ДанныеВыбора.Вставить("ДобавлятьНовуюУпаковку", Ложь);
	ДанныеВыбора.Вставить("ЭтоКодВводаОстатков",    Ложь);
	
	СтруктурыДанных = Новый Структура;
	СтруктурыДанных.Вставить("ЗапомнитьВыбор", Ложь);
	СтруктурыДанных.Вставить("ДанныеВыбора",   ДанныеВыбора);
	
	Возврат СтруктурыДанных;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСФормойУточненияДанных

//Сценарий: на вход уточнения данных передан частично обработанный код маркировки
//
Функция ПреобразоватьВПараметрыОткрытияФормы(КодМаркировкиДляУточнения)
	ДанныеДляУточнения = ШтрихкодированиеИСКлиентСервер.ПараметрыОткрытияФормыУточненияДанных();
	ДанныеДляУточнения.ПараметрыСканирования = КодМаркировкиДляУточнения.ПараметрыСканирования;
	ДанныеШтрихкода = КодМаркировкиДляУточнения.РезультатОбработкиШтрихкода.ДанныеШтрихкода;
	ЗаполнитьЗначенияСвойств(ДанныеДляУточнения, ДанныеШтрихкода);
	ДанныеДляУточнения.КодМаркировки = ДанныеШтрихкода.Штрихкод;
	ДанныеДляУточнения.УказатьИдентификаторВЕТИС = ДанныеШтрихкода.Свойство("ТребуетсяВыборВСД")
		И ДанныеШтрихкода.ТребуетсяВыборВСД
		И Не (ЗначениеЗаполнено(ДанныеШтрихкода.ИдентификаторПроисхожденияВЕТИС)
			И ЗначениеЗаполнено(ДанныеШтрихкода.ГоденДо));
	ДанныеДляУточнения.ШтрихкодEAN = ДанныеШтрихкода.EAN;
	Если ДанныеШтрихкода.СписокНоменклатуры.Количество() > 1 Тогда
		Номенклатура = Новый Соответствие;
		Характеристика = Новый Соответствие;
		Для Каждого ЭлементСписка Из ДанныеШтрихкода.СписокНоменклатуры Цикл
			Если ЗначениеЗаполнено(ЭлементСписка.Номенклатура) Тогда
				Номенклатура.Вставить(ЭлементСписка.Номенклатура);
			КонецЕсли;
			Если ЗначениеЗаполнено(ЭлементСписка.Характеристика) Тогда
				Характеристика.Вставить(ЭлементСписка.Характеристика);
			КонецЕсли;
			Если Номенклатура.Количество() > 1 Тогда
				ДанныеДляУточнения.Номенклатура = Новый Массив;
				Для Каждого КлючИЗначение Из Номенклатура Цикл
					ДанныеДляУточнения.Номенклатура.Добавить(КлючИЗначение.Ключ);
				КонецЦикла;
			КонецЕсли;
			Если Характеристика.Количество() > 1 Тогда
				ДанныеДляУточнения.Характеристика = Новый Массив;
				Для Каждого КлючИЗначение Из Характеристика Цикл
					ДанныеДляУточнения.Характеристика.Добавить(КлючИЗначение.Ключ);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ДанныеШтрихкода.ВидУпаковки = ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Групповая")
		И ДанныеШтрихкода.КоличествоБлоков = 0 Тогда
		ДанныеДляУточнения.УточнитьКоэффициент = Истина;
	КонецЕсли;
	Возврат ДанныеДляУточнения;
КонецФункции

//Сценарий: получаем варианты уточнения данных по коду маркировки (или упаковки)
//  из формы документа (или формы проверки и подбора).
//
Процедура ДополнитьПараметрыУточнения(ФормаВладелец, ПараметрыОткрытияФормы, УточнитьУпаковку = Ложь)
	
	// Обработка кодов маркировки остатков
	РаботаСПустойНоменклатурой = ПараметрыОткрытияФормы.ПараметрыСканирования.РазрешенаОбработкаКодовСПустойНоменклатурой;
		
	// Форма проверки и подбора ИС МП, МОТП
	Если ФормаВладелец.ИмяФормы = "Обработка.ПроверкаИПодборПродукцииИСМП.Форма.ПроверкаИПодбор"
		Или ФормаВладелец.ИмяФормы = "Обработка.ПроверкаИПодборТабачнойПродукцииМОТП.Форма.ПроверкаИПодбор" Тогда
		
		Если Не ФормаВладелец.ПодобраннаяМаркируемаяПродукция.Количество() Тогда
			Возврат;
		КонецЕсли;
		
		ПоляДокумента = Новый Структура;
		ПоляПоиска = Новый Структура;
		КолонкиТовары = ФормаВладелец.ПодобраннаяМаркируемаяПродукция[0];
		Если УточнитьУпаковку Или Не ЗначениеЗаполнено(ПараметрыОткрытияФормы.Номенклатура) Тогда
			ПоляДокумента.Вставить("Номенклатура");
			ПоляДокумента.Вставить("Характеристика");
		Иначе
			ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "Номенклатура", ПараметрыОткрытияФормы.Номенклатура);
			ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "Характеристика", ПараметрыОткрытияФормы.Характеристика);
		КонецЕсли;
		Если УточнитьУпаковку Или Не ЗначениеЗаполнено(ПараметрыОткрытияФормы.Серия) Тогда
			ПоляДокумента.Вставить("Серия");
		Иначе
			ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "Серия", ПараметрыОткрытияФормы.Серия);
		КонецЕсли;
		Если УточнитьУпаковку Или ПараметрыОткрытияФормы.УказатьИдентификаторВЕТИС Тогда
			ПоляДокумента.Вставить("ИдентификаторПроисхожденияВЕТИС");
		ИначеЕсли ЗначениеЗаполнено(ПараметрыОткрытияФормы.ИдентификаторПроисхожденияВЕТИС) Тогда
			ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "ИдентификаторПроисхожденияВЕТИС", ПараметрыОткрытияФормы.ИдентификаторПроисхожденияВЕТИС);
		КонецЕсли;
		Если УточнитьУпаковку Или ЗначениеЗаполнено(ПараметрыОткрытияФормы.ГоденДо) Тогда
			ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "СрокГодности", ПараметрыОткрытияФормы.ГоденДо);
		Иначе
			ПоляДокумента.Вставить("СрокГодности");
		КонецЕсли;
		
		// * Включаем режим выбора
		Если ПоляПоиска.Количество() Тогда
			НеЗавершенПодбор = ФормаВладелец.ПодобраннаяМаркируемаяПродукция.НайтиСтроки(ПоляПоиска);
		Иначе
			НеЗавершенПодбор = ФормаВладелец.ПодобраннаяМаркируемаяПродукция;
		КонецЕсли;
		
		Если НеЗавершенПодбор.Количество() Тогда
			Для Каждого СтрокаПодбор Из НеЗавершенПодбор Цикл
				Если (СтрокаПодбор.КоличествоПодобрано < СтрокаПодбор.Количество) И
				(ЗначениеЗаполнено(СтрокаПодбор.Номенклатура) Или РаботаСПустойНоменклатурой) Тогда
					ПараметрыОткрытияФормы.РежимПодбораИзДокумента = Истина;
					НовыйЭлементВыбора = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПоляДокумента);
					ЗаполнитьЗначенияСвойств(НовыйЭлементВыбора, СтрокаПодбор);
					Если НовыйЭлементВыбора.Свойство("СрокГодности") И СтрокаПодбор.Свойство("ГоденДо") Тогда
						НовыйЭлементВыбора.СрокГодности = СтрокаПодбор.ГоденДо;
					КонецЕсли;
					ПараметрыОткрытияФормы.ДанныеДокумента.Добавить(НовыйЭлементВыбора);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Поведение по-умолчанию (сканирование из формы документа):
	// * Ищем кеш штрихкодов упаковок
	// * Ищем заполненную ТЧ Товары документа
	// * Если флаг подбора уже взведен - ничего не меняем
	Если ПараметрыОткрытияФормы.РежимПодбораИзДокумента Тогда
		Возврат;
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаВладелец, "ДанныеШтрихкодовУпаковокГосИС") Тогда
		Возврат;
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаВладелец, "Объект") Тогда
		Возврат;
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаВладелец.Объект, "Товары") Тогда
		Возврат;
	ИначеЕсли Не ФормаВладелец.Объект.Товары.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиТовары = ФормаВладелец.Объект.Товары[0];
	// * Формируем таблицу выбора
	ПоляДокумента = Новый Структура;
	ПоляПоиска = Новый Структура("СтатусПроверкиГосИС", 2);
	Если УточнитьУпаковку Или Не ЗначениеЗаполнено(ПараметрыОткрытияФормы.Номенклатура) Тогда
		ПоляДокумента.Вставить("Номенклатура");
		ПоляДокумента.Вставить("Характеристика");
	Иначе
		ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "Номенклатура", ПараметрыОткрытияФормы.Номенклатура);
		ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "Характеристика", ПараметрыОткрытияФормы.Характеристика);
	КонецЕсли;
	Если УточнитьУпаковку Или Не ЗначениеЗаполнено(ПараметрыОткрытияФормы.Серия) Тогда
		ПоляДокумента.Вставить("Серия");
	Иначе
		ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "Серия", ПараметрыОткрытияФормы.Серия);
	КонецЕсли;
	Если УточнитьУпаковку Или ПараметрыОткрытияФормы.УказатьИдентификаторВЕТИС Тогда
		ПоляДокумента.Вставить("ИдентификаторПроисхожденияВЕТИС");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыОткрытияФормы.ИдентификаторПроисхожденияВЕТИС) Тогда
		ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "ИдентификаторПроисхожденияВЕТИС", ПараметрыОткрытияФормы.ИдентификаторПроисхожденияВЕТИС);
	КонецЕсли;
	
	Если УточнитьУпаковку Или ЗначениеЗаполнено(ПараметрыОткрытияФормы.ГоденДо) Тогда
		ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, "СрокГодности", ПараметрыОткрытияФормы.ГоденДо);
	Иначе
		ПоляДокумента.Вставить("СрокГодности");
	КонецЕсли;
	
	// * Включаем режим выбора
	НеЗавершенПодбор = ФормаВладелец.Объект.Товары.НайтиСтроки(ПоляПоиска);
	
	Если НеЗавершенПодбор.Количество() Тогда
		Для Каждого СтрокаПодбор Из НеЗавершенПодбор Цикл
			Если Не(ЗначениеЗаполнено(СтрокаПодбор.Номенклатура) Или РаботаСПустойНоменклатурой) Тогда
				Продолжить;
			КонецЕсли;
			ПараметрыОткрытияФормы.РежимПодбораИзДокумента = Истина;
			НовыйЭлементВыбора = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПоляДокумента);
			ЗаполнитьЗначенияСвойств(НовыйЭлементВыбора, СтрокаПодбор);
			Если НовыйЭлементВыбора.Свойство("СрокГодности") И СтрокаПодбор.Свойство("ГоденДо") Тогда
				НовыйЭлементВыбора.СрокГодности = СтрокаПодбор.ГоденДо;
			КонецЕсли;
			ПараметрыОткрытияФормы.ДанныеДокумента.Добавить(НовыйЭлементВыбора);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьПоляПоиска(КолонкиТовары, ПоляПоиска, Имя, Значение)
	Если КолонкиТовары.Свойство(Имя) Тогда 
		ПоляПоиска.Вставить(Имя, Значение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработкаРезультатаНекорректногоСканирования

Функция ТекстОшибкиНеПоддерживаетсяВидПродукции(ПараметрыСканирования, ВидПродукции)
	
	Если ПараметрыСканирования.ДопустимыеВидыПродукции.Количество() = 0 Тогда
		ПоддерживаемыеВидыПродукцииСтрокой = НСтр("ru = 'Не определены'");
	Иначе
		ПоддерживаемыеВидыПродукцииСтрокой = СтрСоединить(ПараметрыСканирования.ДопустимыеВидыПродукции, " ,");
	КонецЕсли;
	
	Фрагмент1 = СтрШаблон(НСтр("ru='Вид продукции штрихкода: %1.'"), ВидПродукции);
	Фрагмент2 = СтрШаблон(НСтр("ru='Ожидаемые виды продукции: %1.'"), ПоддерживаемыеВидыПродукцииСтрокой);
	
	ТекстОшибки = Новый ФорматированнаяСтрока(Фрагмент1, Символы.ПС, Фрагмент2);
	
	Возврат ТекстОшибки;
	
КонецФункции

Функция ИнформироватьОНевозможностиДобавления(Форма, РезультатОбработкиШтрихкода)
	
	Если РезультатОбработкиШтрихкода.ОбщаяОшибка = Истина Тогда
		
		ПараметрыОткрытия = ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		ПараметрыОткрытия.Штрихкод            = РезультатОбработкиШтрихкода.Штрихкод;
		ПараметрыОткрытия.АдресДереваУпаковок = РезультатОбработкиШтрихкода.АдресДереваУпаковок;
		
		Если ТипЗнч(РезультатОбработкиШтрихкода.ТекстОшибки) = Тип("ФорматированнаяСтрока") Тогда
			ПараметрыОткрытия.ТекстОшибкиФорматированнаяСтрока = РезультатОбработкиШтрихкода.ТекстОшибки;
		Иначе
			ПараметрыОткрытия.ТекстОшибки = РезультатОбработкиШтрихкода.ТекстОшибки;
		КонецЕсли;
		
		ОткрытьФорму("ОбщаяФорма.ИнформацияОНевозможностиДобавленияОтсканированного", ПараметрыОткрытия, Форма);
		
		Возврат Истина;
		
	ИначеЕсли РезультатОбработкиШтрихкода.ОшибкаДопустимостиВидовПродукции Тогда
		
		ПараметрыОткрытия = ПараметрыОткрытияФормыНевозможностиДобавленияОтсканированного();
		ПараметрыОткрытия.АдресДереваУпаковок = РезультатОбработкиШтрихкода.АдресДереваУпаковок;
		ОткрытьФорму("ОбщаяФорма.ИнформацияОНевозможностиДобавленияОтсканированного", ПараметрыОткрытия, Форма);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

// Обновляет отображение адреса данных документа основания на форме.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, для которой выполняется обработка штрихкода(ов).
//  ПараметрыСканирования - (см. ПараметрыСканирования).
Процедура ОтразитьИзменениеАдресаДанныхОснованияВФорме(Форма, ПараметрыСканирования)
	
	Если Не ЗначениеЗаполнено(ПараметрыСканирования.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;

	ФормаСоЗначением = Неопределено;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "АдресДанныхДокументаОснования") Тогда
		ФормаСоЗначением = Форма;
	ИначеЕсли Форма.ВладелецФормы <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.ВладелецФормы, "АдресДанныхДокументаОснования") Тогда
		ФормаСоЗначением = Форма.ВладелецФормы;
	КонецЕсли;
	
	Если ФормаСоЗначением = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФормаСоЗначением.АдресДанныхДокументаОснования <> ПараметрыСканирования.АдресДанныхДокументаОснования Тогда
		ФормаСоЗначением.АдресДанныхДокументаОснования = ПараметрыСканирования.АдресДанныхДокументаОснования
	КонецЕсли;
	
КонецПроцедуры

// Возвращает текст заголовка оповещения для ТСД.
//
// Параметры:
// Возвращаемое значение:
//  Строка - Текст заголовка.
Функция ЗаголовокОповещенияТСД()
	
	Возврат НСтр("ru = 'Загрузка из ТСД'");
	
КонецФункции

#Область ЗаполнениеПараметровСканирования

// Включает поддержку алкогольной продукции и заполняет параметры сканирования с учетом ее специфики.
//
// Параметры:
//  Контекст - Неопределено, ФормаКлиентскогоПриложения, ДокументСсылка - Описание
//  ФормаВыбора - ФормаКлиентскогоПриложения - форма выбора.
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции, по которому будут инициализированы параметры сканирования.
//  ПараметрыСканирования - (См. ПараметрыСканирования).
Процедура ЗаполнитьПараметрыСканированияДляАлкогольнойПродукции(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ГосИС.ЕГАИС")
		И (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная")
		   Или ВидПродукции = Неопределено) Тогда
		
		МодульШтрихкодированиеЕГАИСКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеЕГАИСКлиентСервер");
		МодульШтрихкодированиеЕГАИСКлиентСервер.ЗаполнитьПараметрыСканирования(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

// Включает поддержку табачной продукции и заполняет параметры сканирования с учетом ее специфики.
//
// Параметры:
//  Контекст - Неопределено, ФормаКлиентскогоПриложения, ДокументСсылка - Описание
//  ФормаВыбора - ФормаКлиентскогоПриложения - форма выбора.
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции, по которому будут инициализированы параметры сканирования.
//  ПараметрыСканирования - (См. ПараметрыСканирования).
Процедура ЗаполнитьПараметрыСканированияДляТабачнойПродукции(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ГосИС.ИСМП")
		И (ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак")
		   Или ВидПродукции = Неопределено) Тогда
		
		МодульШтрихкодированиеМОТПКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеМОТПКлиентСервер");
		МодульШтрихкодированиеМОТПКлиентСервер.ЗаполнитьПараметрыСканирования(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

// Включает поддержку обувной продукции и заполняет параметры сканирования с учетом ее специфики.
//
// Параметры:
//  Контекст - Неопределено, ФормаКлиентскогоПриложения, ДокументСсылка - Описание
//  ФормаВыбора - ФормаКлиентскогоПриложения - форма выбора.
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции, по которому будут инициализированы параметры сканирования.
//  ПараметрыСканирования - (См. ПараметрыСканирования).
Процедура ЗаполнитьПараметрыСканированияДляПродукцииИСМП(ПараметрыСканирования, Контекст, ВидПродукции, ФормаВыбора)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ГосИС.ИСМП") Тогда
		
		Если ИнтеграцияИСКлиентСервер.ЭтоПродукцияИСМП(ВидПродукции)
			Или Не ЗначениеЗаполнено(ВидПродукции) Тогда
			
			МодульШтрихкодированиеИСМПКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ШтрихкодированиеИСМПКлиентСервер");
			МодульШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканирования(Контекст, ФормаВыбора, ВидПродукции, ПараметрыСканирования);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
