///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается перед открытием формы нового письма.
// Открытие формы может быть отменено изменением параметра СтандартнаяОбработка.
//
// Параметры:
//  ПараметрыОтправки    - см. РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма
//  ОбработчикЗавершения - ОписаниеОповещения - описание процедуры, которая будет вызвана после завершения
//                                              отправки письма.
//  СтандартнаяОбработка - Булево - признак продолжения открытия формы нового письма после выхода из этой
//                                  процедуры. Если установить Ложь, форма письма открыта не будет.
Процедура ПередОткрытиемФормыОтправкиПисьма(ПараметрыОтправки, ОбработчикЗавершения, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПисьмаПоУмолчанию = ОтправкаПочтовыхСообщенийВызовСервера.ПараметрыПисьмаПоУмолчанию();
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Тема");
	ПараметрыПисьма.Вставить("Вложения");
	ПараметрыПисьма.Вставить("ШаблоныПисьма");
	ПараметрыПисьма.Вставить("АдресПолучателяСкрытойКопии");
	ПараметрыПисьма.Вставить("УдалятьФайлыПослеОтправки");
	ПараметрыПисьма.Вставить("ВыбиратьПолучателей");
	ПараметрыПисьма.Вставить("АдресИсходногоВложения");
	//++ЭЛИАС 20171201 
	ПараметрыПисьма.Вставить("АР_БыстраяОтправка");
	ПараметрыПисьма.Вставить("АР_УчетнаяЗапись");
	ПараметрыПисьма.Вставить("АР_ПодписиИПечати");
	Если ПараметрыОтправки.Свойство("АР_УчетнаяЗапись") И ЗначениеЗаполнено(ПараметрыОтправки.АР_УчетнаяЗапись) Тогда
		ПараметрыОтправки.Вставить("Отправитель", ПараметрыОтправки.АР_УчетнаяЗапись);
	КонецЕсли;
	//--ЭЛИАС 20171201
	ЗаполнитьЗначенияСвойств(ПараметрыПисьма, ПараметрыОтправки);
	
	Если ПараметрыОтправки.Свойство("Отправитель") Тогда
		Если ЗначениеЗаполнено(ПараметрыОтправки.Отправитель) Тогда
			Отправитель = ПараметрыОтправки.Отправитель;
		Иначе
			Отправитель = ПараметрыПисьмаПоУмолчанию.Отправитель;
		КонецЕсли;
		
		ПараметрыПисьма.Вставить("Отправитель", Отправитель);
	КонецЕсли;
	
	Если ПараметрыОтправки.Свойство("Получатель") Тогда
		ПараметрыПисьма.Вставить("Получатель", ПараметрыОтправки.Получатель);
	КонецЕсли;
	
	Если ПараметрыОтправки.Свойство("Текст") Тогда
		Если ЗначениеЗаполнено(ПараметрыОтправки.Текст) Тогда
			Текст = ПараметрыОтправки.Текст;
		Иначе
			Текст = ПараметрыПисьмаПоУмолчанию.Текст;
		КонецЕсли;
		
		ПараметрыПисьма.Вставить("Текст", Текст);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПисьма.АдресПолучателяСкрытойКопии) Тогда
		ПараметрыПисьма.АдресПолучателяСкрытойКопии = ПараметрыПисьмаПоУмолчанию.АдресПолучателяСкрытойКопии;
	КонецЕсли;
	
	//++ЭЛИАС 20171129 
	Если ПараметрыПисьма.Свойство("АР_БыстраяОтправка") И ПараметрыПисьма.АР_БыстраяОтправка = Истина Тогда
		Если ПараметрыПисьма.Свойство("АР_ПодписиИПечати") И НЕ ПараметрыПисьма.АР_ПодписиИПечати Тогда
			Если ПараметрыОтправки.Свойство("Получатель") Тогда
				Если ТипЗнч(ПараметрыОтправки.Получатель) = Тип("Массив") Тогда
					Получатель = Новый Массив;
					Для Каждого Элемент Из ПараметрыОтправки.Получатель Цикл
						Если ЗначениеЗаполнено(Элемент.Адрес) Тогда
							Получатель.Добавить(Элемент);
						КонецЕсли;
					КонецЦикла;
				Иначе
					Получатель = ПараметрыОтправки.Получатель;
				КонецЕсли;
				ПараметрыПисьма.Вставить("Кому", Получатель);
			КонецЕсли;
			АР_ОбщиеПроцедуры.ОтправитьПочтовоеСообщение(ПараметрыПисьма);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//--ЭЛИАС 20171129
	ОткрытьФорму("ОбщаяФорма.ОтправкаСообщенияБП", ПараметрыПисьма, , , , , ОбработчикЗавершения);
	
КонецПроцедуры

#КонецОбласти

