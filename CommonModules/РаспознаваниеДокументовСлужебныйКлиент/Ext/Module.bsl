#Область СлужебныеПроцедурыИФункции

#Область ОбщегоНазначения

Функция ЭтоПеречисление(Тип)
	
	ОписаниеТипов = РаспознаваниеДокументовСлужебныйКлиентПовтИсп.ПеречисленияТипВсеСсылки();
	Возврат ОписаниеТипов.СодержитТип(Тип);
	
КонецФункции

Функция ИспользуетсяВстроенныйIE() Экспорт
	
#Если ВебКлиент Тогда
	Возврат Ложь;
#Иначе
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СистемнаяИнформация.ТипПлатформы <> ТипПлатформы.Windows_x86 
		И СистемнаяИнформация.ТипПлатформы <> ТипПлатформы.Windows_x86_64 Тогда
		
		Возврат Ложь;
	КонецЕсли;
	
	ВерсияПлатформы = СтрРазделить(СистемнаяИнформация.ВерсияПриложения, ".");
	
	Возврат НЕ ((Число(ВерсияПлатформы[0]) >= 8)
		И (Число(ВерсияПлатформы[1]) >= 3)
		И (Число(ВерсияПлатформы[2]) >= 14));
	
#КонецЕсли
	
КонецФункции

#КонецОбласти

#Область ПроверкаАвторизации

Процедура ПослеАвторизации(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Тогда
		РезультатАвторизации = Ложь;
	ИначеЕсли Контекст.ТипАвторизации = "ПоТикетуИТС" Тогда
		ПринудительноИТС = Истина;
		РезультатАвторизации = РаспознаваниеДокументовСлужебныйВызовСервера.ПодключеноКСервисуРаспознавания(ПринудительноИТС);
	ИначеЕсли Контекст.ТипАвторизации = "ПоЛогинуПаролю" Тогда
		РезультатАвторизации = (Результат = "АвторизацияВыполнена");
	Иначе
		РезультатАвторизации = Ложь;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеОЗавершении, РезультатАвторизации);
	
КонецПроцедуры

#КонецОбласти

#Область СохранитьФайл

Процедура СохранитьФайл(РаспознанныйДокументСсылка, ИмяФайла) Экспорт
	
	Если Не ДоступенНеоплаченныйРаспознанныйДокумент(РаспознанныйДокументСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Местоположение = ПолучитьНавигационнуюСсылку(РаспознанныйДокументСсылка, "ИсходноеИзображение");
	ИмяФайла = ИмяФайла + ".jpeg";
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФайла", ЭтотОбъект);
	
	ФайловаяСистемаКлиент.СохранитьФайл(Оповещение, Местоположение, ИмяФайла);
	
КонецПроцедуры

Процедура СохранитьФайлыДокумента(РаспознанныйДокумент, ИдентификаторФормы) Экспорт
	
	Если Не ДоступенНеоплаченныйРаспознанныйДокумент(РаспознанныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлыДокумента = РаспознаваниеДокументовСлужебныйВызовСервера.ФайлыРаспознанногоДокумента(РаспознанныйДокумент, ИдентификаторФормы);
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияФайла", ЭтотОбъект);
	
	Если Не ФайлыДокумента.Количество() Тогда
		Текст =
			НСтр("ru = 'Не удалось сохранить файлы. Возможно они были удалены.
			           |Обратитесь к администратору приложения или техническому специалисту'");
		ВызватьИсключение Текст;
	ИначеЕсли ФайлыДокумента.Количество() = 1 Тогда
		ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	Иначе
		ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайлов();
	КонецЕсли;
	
	// Сохранение параметров БСП для последующего восстановления и установка параметров не вызывающих вопрос об использовании
	// расширения для работы с файлами в веб-клиенте. См. ПослеСохраненияФайла.
	//
	ИмяПараметра = "СтандартныеПодсистемы.ПредлагатьУстановкуРасширенияРаботыСФайлами";
	ИсходноеЗначение = ПараметрыПриложения[ИмяПараметра];
	ПараметрыПриложения.Вставить(ИмяПараметра, Ложь);
	ПараметрыПриложения.Вставить("РаспознаваниеДокументов.ПредлагатьУстановкуРасширенияРаботыСФайлами", ИсходноеЗначение);
	
	ФайловаяСистемаКлиент.СохранитьФайлы(Оповещение, ФайлыДокумента, ПараметрыСохранения);
	
КонецПроцедуры

Процедура ПослеСохраненияФайла(ПолученныеФайлы, Контекст) Экспорт
	
	// Восстановление параметров БСП использования расширения работы с файлами.
	//
	ИсходноеЗначение = ПараметрыПриложения["РаспознаваниеДокументов.ПредлагатьУстановкуРасширенияРаботыСФайлами"];
	ПараметрыПриложения.Вставить("СтандартныеПодсистемы.ПредлагатьУстановкуРасширенияРаботыСФайлами", ИсходноеЗначение);
	
	Если ПолученныеФайлы <> Неопределено Тогда
		
		Если ПолученныеФайлы.Количество() = 1 Тогда
			ТекстОповещения = НСтр("ru = 'Распознаваемый документ успешно сохранен в файл'");
		Иначе
			ТекстОповещения = НСтр("ru = 'Файлы сохранены в выбранную папку'");
		КонецЕсли;
		
		Заголовок = НСтр("ru = 'Сохранение файлов'");
		ПоказатьОповещениеПользователя(Заголовок, , ТекстОповещения, БиблиотекаКартинок.Успешно32);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаспознанныйДокумент

Процедура ПоказатьРаспознанныйДокумент(Ссылка, ТипДокумента, ИдентификаторРезультата, Отбор = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФормыОбработчика = РаспознаваниеДокументовСлужебныйКлиентПовтИсп.ПолучитьИмяОткрываемойФормыПоТипу(ТипДокумента);
	Если Не ПустаяСтрока(ИмяФормыОбработчика) Тогда
		КлючеваяОперация = "РаспознаваниеДокументов.ОткрытиеФормы." + ИмяФормыОбработчика;
		Замер = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
		
		Комментарий = Новый Структура;
		Комментарий.Вставить("ИдентификаторРезультата", ИдентификаторРезультата);
		
		ОценкаПроизводительностиКлиент.УстановитьКомментарийЗамера(Замер, Комментарий);
		
		ПараметрыФормы = Новый Структура("Ключ, ОтборИзСписка", Ссылка, Отбор);
		ОткрытьФорму(ИмяФормыОбработчика, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьТипДокумента(Форма) Экспорт 
	
	Обработчик = Новый ОписаниеОповещения("ПослеПодтвержденияИзмененияТипаДокумента", ЭтотОбъект, Форма);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Записать", НСтр("ru = 'Записать'"));
	Кнопки.Добавить("Отмена", НСтр("ru = 'Отмена'"));
	
	Параметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	Параметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	Параметры.Заголовок = НСтр("ru = 'Записать документ'");
	Параметры.КнопкаПоУмолчанию = "Записать";
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
		Обработчик,
		НСтр("ru = 'Для изменения типа документа требуется его записать.
		           |Записать?'"),
		Кнопки,
		Параметры
	);
	
КонецПроцедуры

Процедура ПослеПодтвержденияИзмененияТипаДокумента(РезультатВопроса, Форма) Экспорт
	
	Если ТипЗнч(РезультатВопроса) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВопроса.Значение = "Записать" Тогда 
		
		// Тип будет пересчитан ПередЗаписью документа.
		
		Ссылка = Форма.Объект.Ссылка;
		ТипДокумента = Форма.Объект.ТипДокумента;
		ИдентификаторРезультата = Форма.Объект.ИдентификаторРезультата;
		
		Форма.Записать();
		Форма.Закрыть();
		
		РаспознаваниеДокументовСлужебныйКлиент.ПоказатьРаспознанныйДокумент(
			Ссылка,
			ТипДокумента,
			ИдентификаторРезультата
		);
	Иначе
		
		Форма.Объект.ТипДокумента = РаспознаваниеДокументовСлужебныйВызовСервера.ТипРаспознанногоДокумента(
			Форма.Объект.Ссылка
		);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуСозданияДокумента(
		ИмяЭлемента,
		НаборДанных,
		ДополнительныеДанные,
		Объект,
		ТаблицаДокумента,
		Элемент,
		ЗначениеПодходящейСтроки
	) Экспорт
	
	СвязанныеКолонки = РаспознаваниеДокументовСлужебныйВызовСервера.ПараметрыСозданияНовогоЭлемента(ИмяЭлемента, Объект.ТипДокумента, Объект.Направление);
	
	СтрокаНаименование = "";
	ИндексЮридическоеФизическоеЛицоВМассивеЗначенияОткрытия = Неопределено;
	
	ЗначенияОткрытия = Новый Массив;
	Для Каждого РеквизитЭлемента Из СвязанныеКолонки Цикл
		
		ПараметрыРеквизита = Новый Структура();
		ПараметрыРеквизита.Вставить("Имя", РеквизитЭлемента.Реквизит);
		Если РеквизитЭлемента.Свойство("Синоним") Тогда
			ПараметрыРеквизита.Вставить("Синоним", РеквизитЭлемента.Синоним);
		Иначе
			ПараметрыРеквизита.Вставить("Синоним", РеквизитЭлемента.Реквизит);
		КонецЕсли;
		Если РеквизитЭлемента.Свойство("РеквизитыЗаполнения") Тогда
			ПараметрыРеквизита.Вставить("РеквизитыЗаполнения", РеквизитЭлемента.РеквизитыЗаполнения);
		Иначе
			ПараметрыРеквизита.Вставить("РеквизитыЗаполнения", "");
		КонецЕсли;
		
		ПараметрыРеквизита.Вставить("Значение", "");
		ПараметрыРеквизита.Вставить("РаспознанныйТекст", "");
		ПараметрыРеквизита.Вставить("Картинка", "");
		ПараметрыРеквизита.Вставить("ВысотаКартинки", 0);
		ПараметрыРеквизита.Вставить("Координаты", ПолучитьНаборКоординат());
		
		ПараметрыСвязаннойКолонки = Неопределено;
		Если РеквизитЭлемента.Свойство("ИмяРеквизита") Тогда
			Если ТипЗнч(НаборДанных) = Тип("Массив") Тогда
				Для Каждого СвойствоНабора Из НаборДанных Цикл
					Если СвойствоНабора.ИмяРеквизита = РеквизитЭлемента.ИмяРеквизита Тогда
						ПараметрыСвязаннойКолонки = СвойствоНабора;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Отбор = Новый Структура("ИмяРеквизита", РеквизитЭлемента.ИмяРеквизита);
				НайденныеСтроки = НаборДанных.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() Тогда
					ПараметрыСвязаннойКолонки = НайденныеСтроки[0];
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ПараметрыСвязаннойКолонки = Неопределено Тогда
				Если СтрНачинаетсяС(РеквизитЭлемента.ИмяРеквизита, "ИННКПП") Тогда
					ПараметрыРеквизита.Значение = ПолучитьИННКПП(
						ПараметрыСвязаннойКолонки.РаспознанныйТекст,
						РеквизитЭлемента.Реквизит
					);
				Иначе
					ПараметрыРеквизита.Значение = ПараметрыСвязаннойКолонки.РаспознанныйТекст;
				КонецЕсли;
				ПараметрыРеквизита.РаспознанныйТекст = ПараметрыСвязаннойКолонки.РаспознанныйТекст;
				ПараметрыРеквизита.Картинка = ПараметрыСвязаннойКолонки.АдресКартинки;
				ПараметрыРеквизита.ВысотаКартинки = ПараметрыСвязаннойКолонки.СтрокВИзображении;
				ПараметрыРеквизита.Координаты = ПолучитьНаборКоординат(ПараметрыСвязаннойКолонки);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыРеквизита.Имя = "Наименование" Тогда
			СтрокаНаименование = ПараметрыРеквизита.Значение;
		ИначеЕсли ПараметрыРеквизита.Имя = "ЮридическоеФизическоеЛицо" Тогда
			ИндексЮридическоеФизическоеЛицоВМассивеЗначенияОткрытия = ЗначенияОткрытия.Количество();
		ИначеЕсли ПараметрыРеквизита.Имя = "ВидДоговора" Тогда
			ПараметрыРеквизита.Значение = ДополнительныеДанные.ВидДоговора;
		КонецЕсли;
		
		Если РеквизитЭлемента.Свойство("ПустаяСсылка") Тогда
			Если НЕ ПараметрыСвязаннойКолонки = Неопределено Тогда
				Если ТипЗнч(НаборДанных) = Тип("Массив") Тогда
					Отбор = Новый Структура("НомерСтроки", ПараметрыСвязаннойКолонки.НомерСтрокиТЧ);
					НайденныеСтроки = ТаблицаДокумента.НайтиСтроки(Отбор);
					Если НайденныеСтроки.Количество() Тогда
						СтрокаТаблицыДокумента = НайденныеСтроки[0];
						Значение = СтрокаТаблицыДокумента[РеквизитЭлемента.ИмяРеквизита];
						ПараметрыРеквизита.Вставить("Значение", Значение);
					КонецЕсли;
				Иначе
					ПараметрыРеквизита.Вставить("Значение", ПараметрыСвязаннойКолонки.Значение);
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ПараметрыРеквизита.Значение) Тогда
				ПараметрыРеквизита.Вставить("Значение", РеквизитЭлемента.ПустаяСсылка);
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыРеквизита.Вставить("ИскатьПодходящие", РеквизитЭлемента.ИскатьПодходящие);
		ПараметрыРеквизита.Вставить("Обязательный", РеквизитЭлемента.Обязательный);
		Если РеквизитЭлемента.Свойство("ПодходящиеОбязательноеРавенство") Тогда
			ПараметрыРеквизита.Вставить("ПодходящиеОбязательноеРавенство", РеквизитЭлемента.ПодходящиеОбязательноеРавенство);
		Иначе
			ПараметрыРеквизита.Вставить("ПодходящиеОбязательноеРавенство", "");
		КонецЕсли;
		
		Если РеквизитЭлемента.Свойство("ВыборГруппИЭлементов") Тогда 
			ПараметрыРеквизита.Вставить("ВыборГруппИЭлементов", РеквизитЭлемента.ВыборГруппИЭлементов);
		КонецЕсли;
		
		ЗначенияОткрытия.Добавить(ПараметрыРеквизита);
	КонецЦикла;
	
	Если ИндексЮридическоеФизическоеЛицоВМассивеЗначенияОткрытия <> Неопределено Тогда
		ЮридическоеФизическоеЛицо = ОпределитьЮридическоеФизическоеЛицо(СтрокаНаименование, ДополнительныеДанные.ЮрФизЛицоПоОрганизационнойФорме);
		
		Если ЗначениеЗаполнено(ЮридическоеФизическоеЛицо) Тогда
			ЗначенияОткрытия[ИндексЮридическоеФизическоеЛицоВМассивеЗначенияОткрытия].Значение = ЮридическоеФизическоеЛицо;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначенияОткрытия.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	КлючеваяОперация = "РаспознаваниеДокументов.ОткрытиеФормы.СозданиеЭлементаСОтображениемКартинки";
	Замер = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	Комментарий = Новый Структура;
	Комментарий.Вставить("ИдентификаторРезультата", Объект.ИдентификаторРезультата);
	Комментарий.Вставить("ИмяЭлемента", ИмяЭлемента);
	
	ОценкаПроизводительностиКлиент.УстановитьКомментарийЗамера(Замер, Комментарий);
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("РеквизитыСоздаваемогоОбъекта", ЗначенияОткрытия);
	ПараметрыОткрытия.Вставить("СоздаваемыйОбъект", ЗначениеПодходящейСтроки);
	
	ОткрытьФорму("Документ.РаспознанныйДокумент.Форма.СозданиеЭлементаСОтображениемКартинки", ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

Функция ПолучитьНаборКоординат(Данные = Неопределено) Экспорт
	
	КоординатыКартинки = Новый Массив;
	
	Если НЕ Данные = Неопределено Тогда
		КоординатыКартинки.Добавить(Данные.КоординатаX0);
		КоординатыКартинки.Добавить(Данные.КоординатаY0);
		КоординатыКартинки.Добавить(Данные.КоординатаX1);
		КоординатыКартинки.Добавить(Данные.КоординатаY1);
	Иначе
		КоординатыКартинки.Добавить(0);
		КоординатыКартинки.Добавить(0);
		КоординатыКартинки.Добавить(0);
		КоординатыКартинки.Добавить(0);
	КонецЕсли;
	
	Возврат КоординатыКартинки;
	
КонецФункции

Функция ДанныеЗаполненияДляНовогоЭлемента(ИсточникДанных)Экспорт
	
	ДанныеЗаполнения = Новый Структура();
	Для Каждого СвойствоИсточника Из ИсточникДанных Цикл
		Если СвойствоИсточника.ИмяРеквизита = "Номенклатура" Тогда
			ДанныеЗаполнения.Вставить("Наименование", СвойствоИсточника.РаспознанныйТекст);
		Иначе
			ДанныеЗаполнения.Вставить(СвойствоИсточника.ИмяРеквизита, СвойствоИсточника.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция ПолучитьИННКПП(ИННКПП, ИмяРеквизита)
	
	НаборЗначений = СтрРазделить(ИННКПП, "/", Ложь);
	
	Если НаборЗначений.Количество() И ИмяРеквизита = "ИНН" Тогда
		Возврат НаборЗначений[0];
	ИначеЕсли НаборЗначений.Количество() > 1 И ИмяРеквизита = "КПП" Тогда
		Возврат НаборЗначений[1];
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция ОпределитьЮридическоеФизическоеЛицо(Знач Наименование, ЮрФизЛицоПоОрганизационнойФорме)
	
	Если НЕ ПустаяСтрока(Наименование) Тогда
		Наименование = НРег(Наименование);
		Для Каждого Элемент Из ЮрФизЛицоПоОрганизационнойФорме Цикл
			Если СтрНачинаетсяС(Наименование, Элемент.Ключ + " ") Тогда
				Возврат Элемент.Значение;
			КонецЕсли;
			
			Если СтрНайти(Наименование, " " + Элемент.Ключ + " ") <> 0 Тогда
				Возврат Элемент.Значение;
			КонецЕсли;
			
			ДлинаОкончания = СтрДлина(Элемент.Ключ) + 1; // +1, т.к. добавляем пробел слева
			Если Прав(Наименование, ДлинаОкончания) = " " + Элемент.Ключ Тогда
				Возврат Элемент.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ОткрытьФормуОбратнойСвязи(Форма) Экспорт
	
	ПараметрыФормы = Новый Структура("Комментарий", Форма.Объект.Комментарий);
	ОткрытьФорму("Документ.РаспознанныйДокумент.Форма.ФормаОбратнойСвязи", ПараметрыФормы, Форма);
	
КонецПроцедуры

Процедура ОбработкаВыбораОбратнойСвязи(Форма, ВыбранноеЗначение) Экспорт
	
	Форма.Объект.Комментарий = ВыбранноеЗначение.Комментарий;
	Форма.Записать();
	
КонецПроцедуры

Процедура ЗагрузитьКартинкуПоАдресу(ЭлементФормы, АдресКартинки) Экспорт
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресКартинки);
	
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементКартинкиПоляHTML = ЭлементФормы.Документ.getElementById("image_setter");
	ЭлементКартинкиПоляHTML.setAttribute("data-img", Base64Строка(ДвоичныеДанные));
	ЭлементКартинкиПоляHTML.click();
	
КонецПроцедуры

#КонецОбласти

#Область НечеткийПоиск

Процедура ЗапросСохраненияСопоставленийТекстаСОбъектом(ПараметрыСохранения) Экспорт
	
	Если СтрДлина(ПараметрыСохранения.РаспознанныйТекст) > 3
		И ЗначениеЗаполнено(ПараметрыСохранения.ВыбранныйОбъект)
		И НЕ ПараметрыСохранения.РаспознанныйТекст = Строка(ПараметрыСохранения.ВыбранныйОбъект) Тогда
		
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Всегда использовать ""%1""
								|для текста ""%2""?'"), ПараметрыСохранения.ВыбранныйОбъект, ПараметрыСохранения.РаспознанныйТекст);
		Если ПараметрыСохранения.ВариантСохраненияСоответствий = ПредопределенноеЗначение("Перечисление.ВариантыСохраненияСоответствийБРД.ЗадаватьВопрос") Тогда
			ПоказатьВопрос(ПараметрыСохранения.Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		ИначеЕсли ПараметрыСохранения.ВариантСохраненияСоответствий = ПредопределенноеЗначение("Перечисление.ВариантыСохраненияСоответствийБРД.НеСохранятьИНеЗадаватьВопрос") Тогда
			ВыполнитьОбработкуОповещения(ПараметрыСохранения.Оповещение, КодВозвратаДиалога.Нет);
		Иначе
			ВыполнитьОбработкуОповещения(ПараметрыСохранения.Оповещение, КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначенияСпискаВыбора(Текст, ТипЗначения, ВозможныеВарианты) Экспорт
	
	ЭтоПримитивныйТип = РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗначения);
	Если ЭтоПримитивныйТип Тогда
		КартинкаСоздание = 0;
	Иначе
		Если ЭтоПеречисление(ТипЗначения) Тогда
			КартинкаСоздание = 0;
		Иначе
			КартинкаСоздание = 1;
		КонецЕсли;
	КонецЕсли;
	
	Варианты = Неопределено;
	ВозможныеВариантыОпределен = ТипЗнч(ВозможныеВарианты) = Тип("ДанныеФормыЭлементКоллекции");
	Если ВозможныеВариантыОпределен Тогда 
		Варианты = ВозможныеВарианты.ЗначенияВыбора;
	КонецЕсли;
	
	Возврат РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьСписокДляВыбораПользователем(Текст, Варианты, КартинкаСоздание);
	
КонецФункции

Процедура ДобавитьСоответствиеРаспознаваемыхСтрок(
		Объект,
		ВариантСохраненияСоответствий,
		НаборСоответствийРаспознанныхСтрок,
		ИмяЭлемента,
		НомерСтроки = Неопределено,
		ПереданноеЗначение = Неопределено
	) Экспорт
	
	Контекст = СоответствиеПереданногоРаспознанногоЗначения(
		Объект,
		ИмяЭлемента,
		НомерСтроки,
		ПереданноеЗначение
	);
	Контекст.Вставить("НаборСоответствийРаспознанныхСтрок", НаборСоответствийРаспознанныхСтрок);
	
	Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(Контекст.ПереданноеЗначение)) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьСоответствиеРаспознаваемыхСтрокЗавершение", ЭтотОбъект, Контекст);
	
	ПараметрыСохранения = Новый Структура();
	ПараметрыСохранения.Вставить("РаспознанныйТекст", Контекст.РаспознанныйТекст);
	ПараметрыСохранения.Вставить("ВыбранныйОбъект", Контекст.ПереданноеЗначение);
	ПараметрыСохранения.Вставить("Оповещение", Оповещение);
	ПараметрыСохранения.Вставить("ВариантСохраненияСоответствий", ВариантСохраненияСоответствий);
	
	ЗапросСохраненияСопоставленийТекстаСОбъектом(ПараметрыСохранения);
	
КонецПроцедуры

Функция СоответствиеПереданногоРаспознанногоЗначения(
		Объект,
		ИмяЭлемента,
		НомерСтроки = Неопределено,
		ПереданноеЗначение = Неопределено
	)
	
	РаспознанныйТекст = Неопределено;
	
	Если НомерСтроки = Неопределено Тогда
		Отбор = Новый Структура("ИмяЭлемента", ИмяЭлемента);
		НайденныеСтроки = Объект.РеквизитыДокумента.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			ПереданноеЗначение = НайденныеСтроки[0].Значение;
			РаспознанныйТекст = НайденныеСтроки[0].РаспознанныйТекст;
		КонецЕсли;
	Иначе
		Отбор = Новый Структура("ИмяРеквизита, НомерСтрокиТЧ", ИмяЭлемента, НомерСтроки);
		НайденныеСтроки = Объект.РеквизитыТабличныхЧастей.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			РаспознанныйТекст = НайденныеСтроки[0].РаспознанныйТекст;
		КонецЕсли;
	КонецЕсли;
	
	КлючеваяОперация = 
		?(НомерСтроки = Неопределено, 
			"РаспознаваниеДокументов.ИзменениеЗначенияПоля",
			"РаспознаваниеДокументов.ИзменениеЗначенияКолонки"
		);
	
	Замер = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	Комментарий = Новый Структура;
	Комментарий.Вставить("ИдентификаторРезультата", Объект.ИдентификаторРезультата);
	Комментарий.Вставить("ИмяЭлемента", ИмяЭлемента);
	Комментарий.Вставить("НомерСтроки", НомерСтроки);
	Комментарий.Вставить("ЗначениеПредставление", Строка(ПереданноеЗначение));
	Комментарий.Вставить("РаспознанныйТекст", Строка(РаспознанныйТекст));
	
	ОценкаПроизводительностиКлиент.УстановитьКомментарийЗамера(Замер, Комментарий);
	
	Возврат Новый Структура("РаспознанныйТекст, ПереданноеЗначение", РаспознанныйТекст, ПереданноеЗначение);
	
КонецФункции

Процедура ДобавитьСоответствиеРаспознаваемыхСтрокЗавершение(Результат, Контекст) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ТипЗначенияЗаписи = РаспознаваниеДокументовСлужебныйВызовСервера.ПолучитьТипЗначенияСтрокой(Контекст.ПереданноеЗначение);
		
		Отбор = Новый Структура("ТипЗначения, РаспознаннаяСтрока", ТипЗначенияЗаписи, Контекст.РаспознанныйТекст);
		
		Записи = Контекст.НаборСоответствийРаспознанныхСтрок.НайтиСтроки(Отбор);
		Если Записи.Количество() = 0 Тогда
			Запись = Контекст.НаборСоответствийРаспознанныхСтрок.Добавить();
		Иначе
			Запись = Записи[0];
		КонецЕсли;
		
		Если Контекст.ПереданноеЗначение.Пустая() Тогда
			Контекст.НаборСоответствийРаспознанныхСтрок.Удалить(Запись);
		Иначе
			Запись.ТипЗначения = ТипЗначенияЗаписи;
			Запись.РаспознаннаяСтрока = Контекст.РаспознанныйТекст;
			Запись.СоответствующееЗначение = Контекст.ПереданноеЗначение;
			Запись.КоличествоПовторений = 1;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Биллинг

Процедура ПоказатьТребованиеВключитьИТС(Обработчик) Экспорт
	
	ИмяПараметра = "РаспознаваниеДокументов.ПриВыполненииСтандартныхПериодическихПроверок";
	Если ПараметрыПриложения[ИмяПараметра] <> Неопределено Тогда
		ДатаСохраненная = ПараметрыПриложения[ИмяПараметра];
		ДатаСеанса      = ОбщегоНазначенияКлиент.ДатаСеанса();
		Если ТипЗнч(ДатаСохраненная) = Тип("Дата") И ДеньГода(ДатаСохраненная) = ДеньГода(ДатаСеанса) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекстВопроса =
		НСтр("ru = 'Пилотная программа 1С:Распознавание Документов завершена.
		           |
		           |Выполните переход на авторизацию через интернет-поддержку для дальнейшей работы.'");
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Подключить", НСтр("ru = 'Перейти'"));
	КнопкиВопроса.Добавить("НапомнитьПозже", НСтр("ru = 'Напомнить позже'"));
	КнопкиВопроса.Добавить("НеСегодня", НСтр("ru = 'Не напоминать сегодня'"));
	
	Контекст = Новый Структура;
	Контекст.Вставить("Обработчик", Обработчик);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеОтветаНаВопрос", ЭтотОбъект, Контекст);
	ПоказатьВопрос(
		ОписаниеОповещения,
		ТекстВопроса,
		КнопкиВопроса,
		,
		"Подключить",
		КлиентскоеПриложение.ПолучитьЗаголовок()
	);
	
КонецПроцедуры

Процедура ПоказатьПредупреждениеНулевойБаланс(Обработчик, ДатаОтключения) Экспорт
	
	ИмяПараметра = "РаспознаваниеДокументов.ПриВыполненииСтандартныхПериодическихПроверок";
	Если ПараметрыПриложения[ИмяПараметра] <> Неопределено Тогда
		ДатаСохраненная = ПараметрыПриложения[ИмяПараметра];
		ДатаСеанса      = ОбщегоНазначенияКлиент.ДатаСеанса();
		Если ТипЗнч(ДатаСохраненная) = Тип("Дата") И ДеньГода(ДатаСохраненная) = ДеньГода(ДатаСеанса) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон(
		НСтр("ru = 'У Вас закончились оплаченные страницы распознавания в сервисе ""1С:Распознавание документов"".
		           |
		           |Необходимо пополнить счет до %1, иначе работа в сервисе будет заблокирована'"),
		Формат(ДатаОтключения, "ДЛФ=DD")
	);
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Пополнить", НСтр("ru = 'Пополнить'"));
	КнопкиВопроса.Добавить("НапомнитьПозже", НСтр("ru = 'Напомнить позже'"));
	КнопкиВопроса.Добавить("НеСегодня", НСтр("ru = 'Не напоминать сегодня'"));
	
	Контекст = Новый Структура;
	Контекст.Вставить("Обработчик", Обработчик);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеОтветаНаВопрос", ЭтотОбъект, Контекст);
	ПоказатьВопрос(
		ОписаниеОповещения,
		ТекстВопроса,
		КнопкиВопроса,
		,
		"Пополнить",
		КлиентскоеПриложение.ПолучитьЗаголовок()
	);
	
КонецПроцедуры

Процедура ПослеОтветаНаВопрос(Результат, Контекст) Экспорт
	
	Если Результат = "Подключить" Тогда
		Обработчик = Новый ОписаниеОповещения();
		РаспознаваниеДокументовКлиент.ПоказатьАвторизациюИТС(Обработчик);
	ИначеЕсли Результат = "НеСегодня" Тогда
		ИмяПараметра = "РаспознаваниеДокументов.ПриВыполненииСтандартныхПериодическихПроверок";
		ПараметрыПриложения.Вставить(ИмяПараметра, ОбщегоНазначенияКлиент.ДатаСеанса());
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.Обработчик, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

Функция ДоступенНеоплаченныйРаспознанныйДокумент(РаспознанныйДокумент) Экспорт
	
	Если РаспознаваниеДокументовСлужебныйВызовСервера.ДоступенНеоплаченныйРаспознанныйДокумент(РаспознанныйДокумент) Тогда
		Возврат Истина;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Данные может просматривать и скачивать только автор загруженного файла.'"));
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти