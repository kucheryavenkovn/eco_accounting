#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодключеноКСервисуРаспознавания = РаспознаваниеДокументов.ПодключеноКСервисуРаспознавания();
	АккаунтАктивирован = РаспознаваниеДокументов.АккаунтАктивирован();
	ТекущиеНастройки = РегистрыСведений.ОбщиеНастройкиРаспознаваниеДокументов.ТекущиеНастройки();
	
	АдресЭлПочты = ТекущиеНастройки.АдресЭлПочты;
	
	ОбновитьПодключенныеМобильныеПриложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПерерисоватьПоСостоянию();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодключенныеМобильныеПриложения

&НаКлиенте
Процедура ПодключенныеМобильныеПриложенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", Элемент.ТекущиеДанные.Ссылка);
	
	Обработчик = Новый ОписаниеОповещения("ПослеДобавленияИзмененияМобильногоПриложения", ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.МобильныеПриложенияРаспознаванияДокументов.Форма.ФормаЭлемента",
		ПараметрыОткрытия,
		ЭтотОбъект,
		Элемент.ТекущиеДанные.Идентификатор,
		,
		,
		Обработчик
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключенныеМобильныеПриложенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Обработчик = Новый ОписаниеОповещения("ПослеДобавленияИзмененияМобильногоПриложения", ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.МобильныеПриложенияРаспознаванияДокументов.Форма.ФормаЭлемента",
		,
		ЭтотОбъект,
		Новый УникальныйИдентификатор,
		,
		,
		Обработчик
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключенныеМобильныеПриложенияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Авторизация(Команда)
	
	ВыполнитьАвторизацию();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьАдресЭлПочты(Команда)
	
	СохранитьАдресЭлПочтыНаСервере();
	ПоказатьОповещениеПользователя(НСтр("ru = 'Адрес электронной почты изменен.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьПодключенныеМобильныеПриложения();
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОткрытьФорму("Справочник.МобильныеПриложенияРаспознаванияДокументов.Форма.ФормаЗагрузки");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КомандыДействий

&НаКлиенте
Процедура ВыполнитьАвторизацию()
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроверкиПодключенияКСервисуРаспознавания", ЭтотОбъект);
	РаспознаваниеДокументовКлиент.ПоказатьАвторизациюИТС(Обработчик, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерерисоватьПоСостоянию()
	
	Если ПодключеноКСервисуРаспознавания Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.МобильныеПриложения;
	КонецЕсли;
	
	Если ПодключеноКСервисуРаспознавания И Не АккаунтАктивирован Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.ОжиданиеАктивации;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПодключенныеМобильныеПриложения()
	
	ПодключенныеМобильныеПриложения.Очистить();
	
	Справочники.МобильныеПриложенияРаспознаванияДокументов.АктуализироватьДанные();
	
	Для Каждого МобильноеПриложение Из Справочники.МобильныеПриложенияРаспознаванияДокументов.ПолучитьСписок() Цикл
		ЗаполнитьЗначенияСвойств(ПодключенныеМобильныеПриложения.Добавить(), МобильноеПриложение);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОбработчикиСобытий

&НаКлиенте
Процедура ПослеПроверкиПодключенияКСервисуРаспознавания(Результат, Контекст) Экспорт
	
	Если Результат Тогда 
		ПодключеноКСервисуРаспознавания = РаспознаваниеДокументовСлужебныйВызовСервера.ПодключеноКСервисуРаспознавания();
		ПерерисоватьПоСостоянию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеДобавленияИзмененияМобильногоПриложения(Результат, Контекст) Экспорт
	
	ОбновитьПодключенныеМобильныеПриложения();
	ПерерисоватьПоСостоянию();
	
КонецПроцедуры

#КонецОбласти

#Область БизнесЛогика

&НаСервере
Процедура СохранитьАдресЭлПочтыНаСервере()
	
	РаспознаваниеДокументовКоннекторСлужебный.УстановитьАдресЭлектроннойПочты(АдресЭлПочты);
	
	МенеджерЗаписи = РегистрыСведений.ОбщиеНастройкиРаспознаваниеДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.АдресЭлПочты = АдресЭлПочты;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти