
&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна",  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыФормыВыбора.Вставить("РежимВыбора",        Истина);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Истина);
	
	ОткрытьФорму("Справочник.АР_ЗданияИЭтажи.ФормаВыбора", ПараметрыФормыВыбора, ЭтаФорма);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьСписок(Команда)
	
	ЗаполнитьСписокНаСервере();
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСписокНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АР_ЗданияИЭтажи.Ссылка КАК ЗданиеЭтаж
	|ИЗ
	|	Справочник.АР_ЗданияИЭтажи КАК АР_ЗданияИЭтажи
	|ГДЕ
	|	НЕ АР_ЗданияИЭтажи.ПометкаУдаления";
	
	ЗданияИЭтажи = Запрос.Выполнить().Выгрузить();
	
	ТаблицаЗданий.Загрузить(ЗданияИЭтажи);
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьТипОбъекта(Команда)
	
	ФормаВыбора = ПолучитьФорму("Справочник.АР_ТипыОбъектовАренды.ФормаВыбора", , ЭтаФорма);
	ТипОбъекта = ФормаВыбора.ОткрытьМодально();
	
	Если ЗначениеЗаполнено(ТипОбъекта) Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаЗданий Цикл
			СтрокаТаблицы.ТипОбъекта = ТипОбъекта;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеТиповОбъектов()
	
	Отказ = Ложь;
	НомерСтроки = 1;
	Для Каждого Стр Из ТаблицаЗданий Цикл
		Если НЕ ЗначениеЗаполнено(Стр.ТипОбъекта) Тогда
			ТекстСообщения = "В строке " + НомерСтроки + " не заполнен тип объекта";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ТаблицаЗданий[" + Строка(НомерСтроки - 1) + "].ТипОбъекта", "Форма", Отказ);
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура СоздатьОбъектыНедвижимости(Команда)
	
	Если НЕ ПроверитьЗаполнениеТиповОбъектов() Тогда
		СоздатьОбъектыНедвижимостиНаСервере();
		Оповестить("СозданыНовыеОбъектыАренды");
	КонецЕсли;	
		
КонецПроцедуры


&НаСервере
Процедура СоздатьОбъектыНедвижимостиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТабличнаяЧасть", ТаблицаЗданий.Выгрузить());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.ЗданиеЭтаж,
	|	ТабличнаяЧасть.ТипОбъекта
	|ПОМЕСТИТЬ ТабличнаяЧасть
	|ИЗ
	|	&ТабличнаяЧасть КАК ТабличнаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АР_ОбъектыАренды.Родитель КАК Родитель,
	|	АР_ОбъектыАренды.ЗданиеИЭтаж КАК ЗданиеИЭтаж,
	|	АР_ОбъектыАренды.Ссылка КАК ОбъектАренды,
	|	АР_ОбъектыАренды.ЗданиеИЭтаж.Наименование КАК Наименование,
	|	АР_ОбъектыАренды.ЗданиеИЭтаж.ОбщаяПлощадь КАК ОбщаяПлощадь,
	|	АР_ОбъектыАренды.ЗданиеИЭтаж.РасположениеНаПлане КАК РасположениеНаПлане,
	|	АР_ОбъектыАренды.ЕдиницаИзмерения,
	|	АР_ОбъектыАренды.Наименование КАК НаименованиеОбъекта,
	|	ТабличнаяЧасть.ТипОбъекта
	|ИЗ
	|	ТабличнаяЧасть КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АР_ОбъектыАренды КАК АР_ОбъектыАренды
	|		ПО ТабличнаяЧасть.ЗданиеЭтаж = АР_ОбъектыАренды.ЗданиеИЭтаж
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование,
	|	НаименованиеОбъекта
	|ИТОГИ ПО
	|	Родитель,
	|	ЗданиеИЭтаж";
	
	ВыборкаРодитель = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаРодитель.Следующий() Цикл
		ВыборкаЗдание = ВыборкаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗдание.Следующий() Цикл
			Выборка = ВыборкаЗдание.Выбрать();
			
			ЗданиеЭтаж = Справочники.АР_ОбъектыАренды.НайтиПоНаименованию(Строка(ВыборкаЗдание.ЗданиеИЭтаж), Истина, ВыборкаЗдание.Родитель); // если объект создается впервые
			Если НЕ ЗначениеЗаполнено(ЗданиеЭтаж) И ЗначениеЗаполнено(ВыборкаЗдание.Родитель) Тогда
				ЗданиеЭтаж = Справочники.АР_ОбъектыАренды.НайтиПоНаименованию(Строка(ВыборкаЗдание.ЗданиеИЭтаж), Истина, ВыборкаЗдание.Родитель.Родитель); // в случае повторного запуска
			КонецЕсли;
			Если ЗначениеЗаполнено(ЗданиеЭтаж) Тогда
				ОбъектЗданиеЭтаж = ЗданиеЭтаж.ПолучитьОбъект();
			Иначе
				ОбъектЗданиеЭтаж = Справочники.АР_ОбъектыАренды.СоздатьЭлемент();
				ОбъектЗданиеЭтаж.Родитель = ВыборкаРодитель.Родитель;
				ТекстСообщения = "Создан объект недвижимости " + Строка(ВыборкаЗдание.ЗданиеИЭтаж);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
			
			ЭтоПервый = Истина;
			
			Пока Выборка.Следующий() Цикл
				
				Если ЭтоПервый Тогда
					ОбъектЗданиеЭтаж.Наименование = Выборка.Наименование;
					ОбъектЗданиеЭтаж.ЗначениеХарактеристики = Выборка.ОбщаяПлощадь;
					ОбъектЗданиеЭтаж.РасположениеНаПлане = Выборка.РасположениеНаПлане;
					ОбъектЗданиеЭтаж.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
					ОбъектЗданиеЭтаж.ТипОбъекта = Выборка.ТипОбъекта;
					ОбъектЗданиеЭтаж.Записать();
				КонецЕсли;
				
				ОбъектАренды = Выборка.ОбъектАренды.ПолучитьОбъект();
				ОбъектАренды.Родитель = ОбъектЗданиеЭтаж.Ссылка;
				ОбъектАренды.Записать();

				Если ЭтоПервый Тогда
					Попытка
						УстановитьКИ(Выборка.ОбъектАренды);
					Исключение
					КонецПопытки;
					ЭтоПервый = Ложь;
				КонецЕсли;	
					
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьОписание(Команда)
	
	ПоказыватьВстроеннуюСправку = Не ПоказыватьВстроеннуюСправку;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВстроеннаяСправка = Справочники.АР_ЗданияИЭтажи.ПолучитьТекстВстроеннойСправки();
	ПоказыватьВстроеннуюСправку = Истина;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ГруппаВстроеннаяСправка.Видимость = Форма.ПоказыватьВстроеннуюСправку;
	Элементы.ФормаПоказатьОписание.Заголовок = ?(Форма.ПоказыватьВстроеннуюСправку, "Скрыть справку", "Показать справку");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКИ(ОбъектАренды)
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
			Для Каждого ЗданиеЭтаж Из ВыбранноеЗначение Цикл
				Если ТаблицаЗданий.НайтиСтроки(Новый Структура("ЗданиеЭтаж", ЗданиеЭтаж)).Количество() = 0 Тогда
					СтрокаТаблицы = ТаблицаЗданий.Добавить();
					СтрокаТаблицы.ЗданиеЭтаж = ЗданиеЭтаж;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если ТаблицаЗданий.НайтиСтроки(Новый Структура("ЗданиеЭтаж", ВыбранноеЗначение)).Количество() = 0 Тогда
				СтрокаТаблицы = ТаблицаЗданий.Добавить();
				СтрокаТаблицы.ЗданиеЭтаж = ВыбранноеЗначение;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
