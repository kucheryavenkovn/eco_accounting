
&НаКлиенте
Процедура ОткрытьФормуПереносаЗданийИЭтажей(Команда)
	
	ФормаПереносаДанных = ПолучитьФорму("Справочник.АР_ЗданияИЭтажи.Форма.ФормаПереносаДанных", , ЭтаФорма);
	ФормаПереносаДанных.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИспользованиеСправочникаЗданийИЭтажей(Команда)
	
	ИзменитьИспользованиеСправочникаЗданийИЭтажейНаСервере();
	УправлениеФормой(ЭтаФорма);
	Оповестить("ИзменениеНастроекИспользованияЗданийИЭтажей");	
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьИспользованиеСправочникаЗданийИЭтажейНаСервере()
	
	ИспользованиеСправочникаЗданийИЭтажей = НЕ ИспользованиеСправочникаЗданийИЭтажей;
	
	Константы.АР_ИспользоватьСправочникЗданияИЭтажи.Установить(ИспользованиеСправочникаЗданийИЭтажей);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЗаголовокКнопки = ?(Форма.ИспользованиеСправочникаЗданийИЭтажей, 
						"Отключить использование справочника ""Здания и этажи""",
						"Включить использование справочника ""Здания и этажи""");
	ЗаголовокФормы = ?(Форма.ИспользованиеСправочникаЗданийИЭтажей, 
						"(рекомендуется отключить) Здания и этажи",
						"(не используется) Здания и этажи");
	Элементы.ФормаИзменитьИспользованиеСправочникаЗданийИЭтажей.Заголовок = ЗаголовокКнопки;	
	Элементы.ФормаОткрытьФормуПереносаЗданийИЭтажей.Видимость = Форма.ИспользованиеСправочникаЗданийИЭтажей;
	Элементы.ФормаИзменитьИспользованиеСправочникаЗданийИЭтажей.Видимость = Форма.ПоказыватьНастройкуИспользованияСправочника;
	
	Форма.Автозаголовок = Ложь;
	Форма.Заголовок = ЗаголовокФормы;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользованиеСправочникаЗданийИЭтажей = Константы.АР_ИспользоватьСправочникЗданияИЭтажи.Получить();
	ПоказыватьНастройкуИспользованияСправочника = Константы.АР_ПоказыватьНастройкуИспользованияСправочникаЗданияИЭтажи.Получить();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры
