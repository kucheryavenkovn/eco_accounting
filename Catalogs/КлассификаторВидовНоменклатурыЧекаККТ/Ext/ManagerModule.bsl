#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПроверитьЗначениеОпцииИспользоватьПодконтрольныеТоварыККТ() Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПодконтрольныеТоварыККТ")
		И ЕстьПодконтрольныеТоварыККТ() Тогда
	
		УстановитьПривилегированныйРежим(Истина);
		Константы.ИспользоватьПодконтрольныеТоварыККТ.Установить(Истина);
		ОбновитьПовторноИспользуемыеЗначения();
	
	КонецЕсли; 

КонецПроцедуры

Функция ЭлементКлассификатораПоКоду(КодВидаНоменклатурнойКлассификации) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Код", КодВидаНоменклатурнойКлассификации);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторВидовНоменклатурыЧекаККТ.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторВидовНоменклатурыЧекаККТ КАК КлассификаторВидовНоменклатурыЧекаККТ
	|ГДЕ
	|	КлассификаторВидовНоменклатурыЧекаККТ.Код = &Код
	|	И НЕ КлассификаторВидовНоменклатурыЧекаККТ.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
	
		КодыВидаНоменклатурнойКлассификации = МенеджерОборудованияВызовСервера.КодыВидаНоменклатурнойКлассификации();
		
		НаименованиеКодаВидаНоменклатурнойКлассификации = "";
	
		ОтборСтрок = Новый Структура("КодВидаНоменклатурнойКлассификации", КодВидаНоменклатурнойКлассификации);
		Строки = КодыВидаНоменклатурнойКлассификации.НайтиСтроки(ОтборСтрок);
		Если Строки.Количество() > 0 Тогда
			НаименованиеКодаВидаНоменклатурнойКлассификации = Строки[0].НаименованиеВидаНоменклатурнойКлассификации;
		КонецЕсли;
		
		НовыйЭлементКлассификатора = Справочники.КлассификаторВидовНоменклатурыЧекаККТ.СоздатьЭлемент();
		НовыйЭлементКлассификатора.Код = КодВидаНоменклатурнойКлассификации;
		НовыйЭлементКлассификатора.Наименование = НаименованиеКодаВидаНоменклатурнойКлассификации;
		НовыйЭлементКлассификатора.НаименованиеПолное = НаименованиеКодаВидаНоменклатурнойКлассификации;
		НовыйЭлементКлассификатора.Записать();
		Возврат НовыйЭлементКлассификатора.Ссылка;
		
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьПодконтрольныеТоварыККТ()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.СредствоИндивидуальнойЗащиты";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти


#КонецЕсли