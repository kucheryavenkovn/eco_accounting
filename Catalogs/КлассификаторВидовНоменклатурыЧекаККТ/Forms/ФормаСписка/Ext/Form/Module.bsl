#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	
	Если Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.КлассификаторВидовНоменклатурыЧекаККТ) Тогда
		Элементы.ФормаПодборИзКлассификатора.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборИзКлассификатора(Команда)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПодборИзКлассификатораЗавершение", ЭтотОбъект);
	
	МенеджерОборудованияКлиент.НачатьВыборКодаВидаНоменклатурнойКлассификации(ОповещениеОЗакрытии);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодборИзКлассификатораЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовыйЭлемент = ДобавитьЭлементКлассификатора(РезультатВыбора.КодВидаНоменклатурнойКлассификации, РезультатВыбора.НаименованиеВидаНоменклатурнойКлассификации);
	
	ОповеститьОбИзменении(НовыйЭлемент);
	
	Элементы.Список.ТекущаяСтрока = НовыйЭлемент;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьЭлементКлассификатора(Код, Наименование)
	
	Если НЕ ЗначениеЗаполнено(Код) Тогда
		Возврат Справочники.КлассификаторВидовНоменклатурыЧекаККТ.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Код", Код);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторВидовНоменклатурыЧекаККТ.Ссылка
	|ИЗ
	|	Справочник.КлассификаторВидовНоменклатурыЧекаККТ КАК КлассификаторВидовНоменклатурыЧекаККТ
	|ГДЕ
	|	КлассификаторВидовНоменклатурыЧекаККТ.Код = &Код";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		НовыйЭлемент = Справочники.КлассификаторВидовНоменклатурыЧекаККТ.СоздатьЭлемент();
		НовыйЭлемент.Код = Код;
		НовыйЭлемент.Наименование = Наименование;
		НовыйЭлемент.НаименованиеПолное = Наименование;
		НовыйЭлемент.Записать();
		Возврат НовыйЭлемент.Ссылка;
	КонецЕсли;
	
КонецФункции

#КонецОбласти