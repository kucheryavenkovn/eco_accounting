
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Билет = Параметры.Билет;

	Операции.Параметры.УстановитьЗначениеПараметра("Билет", Билет);
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОперацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	Если Поле = Элементы.ОперацииБилетЗамена Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,Элементы.Операции.ТекущиеДанные.БилетЗамена);
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПокупка(Команда)
	ОткрытьФормуНовойОперации(ПредопределенноеЗначение("Перечисление.ВидыОперацийСБилетами.Покупка"));
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВозврат(Команда)
	ОткрытьФормуНовойОперации(ПредопределенноеЗначение("Перечисление.ВидыОперацийСБилетами.Возврат"));
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОбмен(Команда)
	ОткрытьФормуНовойОперации(ПредопределенноеЗначение("Перечисление.ВидыОперацийСБилетами.Обмен"));
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДоплата(Команда)
	ОткрытьФормуНовойОперации(ПредопределенноеЗначение("Перечисление.ВидыОперацийСБилетами.Доплата"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ШрифтПоясняющихНадписей = Новый Шрифт(Элементы.Операции.Шрифт,,,, Истина);
		
	// НАСТРОЙКА ТАБЛИЦЫ ОПЕРАЦИЙ
	
	// Вид операции = ЗаменаВозврат ИЛИ Вид операции = ЗаменаПокупка
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииНадписьЗамена");	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииБилетЗамена");	
	
	ВидыОперацийЗамена = Новый СписокЗначений;
	ВидыОперацийЗамена.Добавить(Перечисления.ВидыОперацийСБилетами.ЗаменаВозврат);
	ВидыОперацийЗамена.Добавить(Перечисления.ВидыОперацийСБилетами.ЗаменаПокупка);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Операции.ВидОперации", ВидСравненияКомпоновкиДанных.НеВСписке, ВидыОперацийЗамена);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Вид операции = ЗаменаВозврат	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииНадписьЗамена");	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Операции.ВидОперации", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыОперацийСБилетами.ЗаменаВозврат);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Новый билет:'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтПоясняющихНадписей);
	
	// Вид операции = ЗаменаПокупка	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииНадписьЗамена");	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Операции.ВидОперации", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыОперацийСБилетами.ЗаменаПокупка);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='Прежний билет:'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтПоясняющихНадписей);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНовойОперации(ВидОперации)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияОперации(ВидОперации));
	
	ОткрытьФорму("Документ.ОперацияСБилетом.ФормаОбъекта", ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ЗначенияЗаполненияОперации(ВидОперации)

	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВидОперации", ВидОперации);
	ЗначенияЗаполнения.Вставить("Билет", Билет);
	ЗначенияЗаполнения.Вставить("Организация", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Билет, "Организация"));

	Возврат ЗначенияЗаполнения;
	
КонецФункции
 
#КонецОбласти