
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Функция ПолучитьРасписаниеНаСервере()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	Возврат СправочникОбъект.ПолучитьРасписание();
	
КонецФункции

&НаСервере
Процедура УстановитьРасписаниеНаСервере(Знач РасписаниеОткрытое)
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	АдресВременногоХранилища = СправочникОбъект.УстановитьРасписание(РасписаниеОткрытое, УникальныйИдентификатор);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(АдресВременногоХранилища) Тогда
		ТекущийОбъект.ЗаписатьРасписание(АдресВременногоХранилища);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура Расписание(Команда)
	
	РасписаниеОткрытое = ПолучитьРасписаниеНаСервере();
	
	Если ТипЗнч(РасписаниеОткрытое) <> Тип("РасписаниеРегламентногоЗадания") Тогда
		РасписаниеОткрытое = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеОткрытое);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытиеРасписанияЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеРасписанияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		УстановитьРасписаниеНаСервере(Результат);
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

