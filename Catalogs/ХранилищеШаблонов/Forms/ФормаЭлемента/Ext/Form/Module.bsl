
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ФормаВосстановитьПредопределенныйШаблон.Видимость = Объект.Предопределенный;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьШаблон(Команда)
	Оповещение = Новый ОписаниеОповещения("РедактированиеМакетаЗавершение", ЭтотОбъект);
	
	СтандартнаяОбработка = ЛОжь;
	
	ПараметрыШаблонаЭтикеток = ПараметрыШаблонаЭтикеток();
	
	МенеджерОборудованияКлиент.НачатьРедактированиеМакета(Оповещение, ПараметрыШаблонаЭтикеток.XMLОписаниеМакета, ПараметрыШаблонаЭтикеток.АдресХранилищаСКД);
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПредопределенныйШаблон(Команда)
	ВосстановитьПредопределенныйШаблонНаСервере();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыШаблонаЭтикеток()
	Возврат Справочники.ХранилищеШаблонов.ПараметрыШаблонаЭтикеток(РеквизитФормыВЗначение("Объект"));
КонецФункции

&НаКлиенте
Процедура РедактированиеМакетаЗавершение(Значение, Параметры) Экспорт
	Если Значение = КодВозвратаДиалога.ОК Тогда
		
		ЗаполнитьЗначенияСвойств(Объект, Параметры, "XMLОписаниеМакета, РазмерМакета");
		
		Объект.Поля.Очистить();
		Для каждого ПолеДанных Из Параметры.Поля Цикл
			ЗаполнитьЗначенияСвойств(Объект.Поля.Добавить(), ПолеДанных);
		КонецЦикла; 
		
		Модифицированность = Истина;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ВосстановитьПредопределенныйШаблонНаСервере()
	Макет = Справочники.ХранилищеШаблонов.ПолучитьМакет("ШаблоныЭтикеток");
	
	НомерСтроки = 1;
	
	ИмяПредопределенного = Макет.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
	
	Пока ИмяПредопределенного <> "" Цикл
		Если ИмяПредопределенного = Объект.ИмяПредопределенныхДанных Тогда
			Объект.XMLОписаниеМакета      = Макет.Область(НомерСтроки, 2, НомерСтроки, 2).Текст;
			Объект.РазмерМакета           = Макет.Область(НомерСтроки, 3, НомерСтроки, 3).Текст;
			Объект.МаркированнаяПродукция = (Макет.Область(НомерСтроки, 4, НомерСтроки, 4).Текст = "1");
			Объект.ПоляШаблона            = "ПоляШаблонаНоменклатура";
			
			Модифицированность = Истина;
			Прервать;
		КонецЕсли; 
		
		НомерСтроки = НомерСтроки + 1;
		ИмяПредопределенного = Макет.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 
 
