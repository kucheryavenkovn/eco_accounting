#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьПредопределенныеЭлементы() Экспорт
	Макет = Справочники.ХранилищеШаблонов.ПолучитьМакет("ШаблоныЭтикеток");
	НомерСтроки = 1;
	
	ИмяПредопределенного = Макет.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
	
	Пока  ИмяПредопределенного <> "" Цикл
		
		ШаблонСсылка = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ХранилищеШаблонов."+ИмяПредопределенного);
		Если ШаблонСсылка <> Неопределено 
			И НЕ ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ШаблонСсылка, "XMLОписаниеМакета")) Тогда
			
			ШаблонОбъект = ШаблонСсылка.ПолучитьОбъект();
			
			ШаблонОбъект.XMLОписаниеМакета      = Макет.Область(НомерСтроки, 2, НомерСтроки, 2).Текст;
			ШаблонОбъект.РазмерМакета           = Макет.Область(НомерСтроки, 3, НомерСтроки, 3).Текст;
			ШаблонОбъект.МаркированнаяПродукция = (Макет.Область(НомерСтроки, 4, НомерСтроки, 4).Текст = "1");
			ШаблонОбъект.ПоляШаблона            = "ПоляШаблонаНоменклатура";
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ШаблонОбъект);
		КонецЕсли; 
		
		НомерСтроки = НомерСтроки + 1;
		ИмяПредопределенного = Макет.Область(НомерСтроки, 1, НомерСтроки, 1).Текст;
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыШаблонаЭтикеток(Знач ШаблонЭтикетки) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("АдресХранилищаСКД");
	Результат.Вставить("XMLОписаниеМакета");
	
	РеквизитыОбъекта = Новый Структура("ПоляШаблона, XMLОписаниеМакета");
	
	Если ТипЗнч(ШаблонЭтикетки) = Тип("СправочникОбъект.ХранилищеШаблонов") Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ШаблонЭтикетки);
	Иначе
		Если ТипЗнч(ШаблонЭтикетки) = Тип("Строка") Тогда
			ШаблонЭтикетки = "Справочник.ХранилищеШаблонов."+ШаблонЭтикетки;
		КонецЕсли; 
		
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ШаблонЭтикетки, РеквизитыОбъекта);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОбъекта.ПоляШаблона) Тогда
		Возврат Результат;
	КонецЕсли; 
	
	МакетПоляШаблона = Справочники.ХранилищеШаблонов.ПолучитьМакет(РеквизитыОбъекта.ПоляШаблона);
	
	Результат.Вставить("АдресХранилищаСКД", ПоместитьВоВременноеХранилище(МакетПоляШаблона, Новый УникальныйИдентификатор));
	Результат.Вставить("XMLОписаниеМакета", РеквизитыОбъекта.XMLОписаниеМакета);
	
	Возврат Результат;
КонецФункции 


#КонецОбласти 


#КонецЕсли
 
