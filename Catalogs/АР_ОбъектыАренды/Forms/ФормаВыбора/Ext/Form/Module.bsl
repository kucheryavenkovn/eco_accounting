
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Список.Параметры.УстановитьЗначениеПараметра("ПериодСреза", НачалоДня(ТекущаяДата()));
	
	Если Параметры.Свойство("РежимПодбора") Тогда
		РежимПодбора = Параметры.РежимПодбора;
	КонецЕсли;
	
	РежимОтображенияСписка = Элементы.Список.Отображение;
	
	Если Параметры.Свойство("ВыборРодительскихОбъектов") Тогда
		Заголовок = "Выберите родительский объект недвижимости";
		АвтоЗаголовок = Ложь;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АР_ОбъектыАренды.Родитель
		|ИЗ
		|	Справочник.АР_ОбъектыАренды КАК АР_ОбъектыАренды
		|ГДЕ
		|	НЕ АР_ОбъектыАренды.Родитель = ЗНАЧЕНИЕ(Справочник.АР_ОбъектыАренды.ПустаяСсылка)";
		Выборка = Запрос.Выполнить().Выбрать();
		
		МассивРодителей = Новый Массив;
		Пока Выборка.Следующий() Цикл
			МассивРодителей.Добавить(Выборка.Родитель);
		КонецЦикла;
		Параметры.Отбор.Вставить("Ссылка", Новый ФиксированныйМассив(МассивРодителей));
	КонецЕсли;
	
	Если Параметры.Свойство("ВыборЗдания") Тогда
		
		Заголовок = "Выберите здание";
		АвтоЗаголовок = Ложь;
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АР_ОбъектыАренды.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.АР_ОбъектыАренды КАК АР_ОбъектыАренды
		|ГДЕ
		|	АР_ОбъектыАренды.ТипОбъекта.КатегорияОбъекта = ЗНАЧЕНИЕ(Перечисление.АР_КатегорииОбъектовНедвижимости.Здание)";
		Выборка = Запрос.Выполнить().Выбрать();
		
		МассивЗданий = Новый Массив;
		Пока Выборка.Следующий() Цикл
			МассивЗданий.Добавить(Выборка.Ссылка);
		КонецЦикла;
		Параметры.Отбор.Вставить("Ссылка", Новый ФиксированныйМассив(МассивЗданий));
		
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Элементы.Список.Отображение = РежимОтображенияСписка;
	
КонецПроцедуры

#КонецОбласти

