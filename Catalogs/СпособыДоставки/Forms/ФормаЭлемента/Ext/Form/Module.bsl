
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТолькоПросмотр = (Объект.ИмяПредопределенныхДанных = "Самовывоз");
	
	НаименованиеПоУмолчанию = НаименованиеПоУмолчанию();
	
	КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	ПроверитьУстановитьНаименование();
	
	Если Объект.ОплатаПриПолучении Тогда
		УстановитьДоговорКонтрагента(Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	КонецЕсли; 
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПриПолученииПриИзменении(Элемент)
	
	КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна(ЭтотОбъект);
	
	ПроверитьУстановитьНаименование();
	
	Если Объект.ОплатаПриПолучении Тогда
		УстановитьДоговорКонтрагента(Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
	КонецЕсли; 
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьДоговорКонтрагента(Организация, Контрагент, ДоговорКонтрагента)
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента,
		Контрагент,
		Организация,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СТранспортнойКомпанией));
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУстановитьНаименование()
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) 
		ИЛИ Объект.Наименование = НаименованиеПоУмолчанию Тогда
		
		Объект.Наименование = НаименованиеПоУмолчанию();
		НаименованиеПоУмолчанию = Объект.Наименование;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Функция НаименованиеПоУмолчанию()
	
	СтруктураНаименование = Новый Массив;
	
	СтруктураНаименование.Добавить(НСтр("ru = 'Доставка'"));
	
	СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Объект.Контрагент);
	СтруктураНаименование.Добавить(СведенияОКонтрагенте.Представление);
	
	Если Объект.ОплатаПриПолучении Тогда
		СтруктураНаименование.Добавить(НСтр("ru = '(оплата при получении)'"));
	КонецЕсли; 
	
	Возврат СтрСоединить(СтруктураНаименование, " ");
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// Контрагент
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОплатаПриПолучении", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// ДоговорКонтрагента
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДоговорКонтрагента");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОплатаПриПолучении", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ДоговорКонтрагента", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// СчетУчетаРасчетов
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетУчетаРасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОплатаПриПолучении", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.СчетУчетаРасчетов", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;

	Элементы.Наименование.Доступность           = НЕ Объект.Предопределенный;
	Элементы.Организация.Видимость              = НЕ Объект.Предопределенный;
	Элементы.УслугаДоставки.Видимость           = (Объект.ИмяПредопределенныхДанных <> "Самовывоз");
	Элементы.ГруппаРеквизиты.Видимость          = НЕ Объект.Предопределенный;
	Элементы.ГруппаОплатаПриПолучении.Видимость = Объект.ОплатаПриПолучении;
	Элементы.ДоговорКонтрагента.Доступность     = ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючСохраненияПоложенияОкна(Форма)
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.ИмяПредопределенныхДанных) Тогда
		КлючСохраненияПоложенияОкна = Объект.ИмяПредопределенныхДанных;
	ИначеЕсли Объект.ОплатаПриПолучении Тогда
		КлючСохраненияПоложенияОкна = "ТранспортнаяКомпанияОплатаПриПолучении";
	Иначе
		КлючСохраненияПоложенияОкна = "ТранспортнаяКомпания";
	КонецЕсли;

	Возврат КлючСохраненияПоложенияОкна;
КонецФункции

#КонецОбласти 