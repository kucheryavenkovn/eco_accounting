#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Отбор") 
		И Параметры.Отбор.Свойство("Организация") Тогда
		
		Если ТипЗнч(Параметры.Отбор.Организация) = Тип("ФиксированныйМассив") Тогда
			Для каждого ЭлементОтбора Из Параметры.Отбор.Организация Цикл
				Если НЕ ЗначениеЗаполнено(ЭлементОтбора) Тогда
					Продолжить;
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(ОсновнаяОрганизация) 
					И ОсновнаяОрганизация <> ЭлементОтбора Тогда
				
					ОсновнаяОрганизация = Неопределено;
					Прервать;
				Иначе
					ОсновнаяОрганизация = ЭлементОтбора;
				КонецЕсли; 
			КонецЦикла; 
		Иначе
			ОсновнаяОрганизация = Параметры.Отбор.Организация;
		КонецЕсли; 
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийБухгалтерскийУчет") Тогда
		
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли; 
	
	КомандаСоздать = Элементы.Найти("ФормаСоздать");
	
	Если КомандаСоздать <> Неопределено Тогда
		Элементы.ФормаСоздать.Доступность = ЗначениеЗаполнено(ОсновнаяОрганизация);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если ЗначениеЗаполнено(ОсновнаяОрганизация) Тогда
		ДанныеЗаполнения = Новый структура;
		ДанныеЗаполнения.Вставить("Организация", ОсновнаяОрганизация);
		ОткрытьФорму("Справочник.СпособыДоставки.Форма.ФормаЭлемента", Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения));
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти 
