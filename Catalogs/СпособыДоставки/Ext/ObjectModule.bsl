#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Перем ОтборОрганизация;
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("Организация", ОтборОрганизация) Тогда
		
		Если ТипЗнч(ОтборОрганизация) = Тип("СправочникСсылка.Организации") Тогда
		
			Организация = ОтборОрганизация;
			
		ИначеЕсли ТипЗнч(ОтборОрганизация) = Тип("ФиксированныйМассив") Тогда
			
			Организация = Неопределено;
			Для каждого ЭлементОтбора Из ОтборОрганизация Цикл
				Если НЕ ЗначениеЗаполнено(ЭлементОтбора) Тогда
					Продолжить;
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(Организация) 
					И Организация <> ЭлементОтбора Тогда
					
					Организация = Неопределено;
					Прервать;
				Иначе
					Организация = ЭлементОтбора;
				КонецЕсли; 
			КонецЦикла; 
			
		КонецЕсли; 
			
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ОплатаПриПолучении Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетов");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяПредопределенныхДанных) Тогда
	
		МассивНепроверяемыхРеквизитов.Добавить("Организация");
	
	КонецЕсли; 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

#КонецОбласти 

#КонецЕсли
