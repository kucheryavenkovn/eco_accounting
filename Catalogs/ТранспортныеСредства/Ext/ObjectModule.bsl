#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьУникальностьVIN(Отказ);
	ВыполнитьПроверкуКлючевыхРеквизитов(Отказ);
	
	Если ПустаяСтрока(Наименование) Тогда
		
		Наименование = СтрШаблон(НСтр("ru = '%1 (%2)'"), Марка, РегистрационныйЗнак);
		
	Иначе
		
		ПредыдущиеЗначения = Неопределено;
		Если ДополнительныеСвойства.Свойство("ПредыдущиеЗначения", ПредыдущиеЗначения)
			И ТипЗнч(ПредыдущиеЗначения) = Тип("Структура") Тогда
			
			Наименование = СтрЗаменить(Наименование, ПредыдущиеЗначения.Марка, Марка);
			Наименование = СтрЗаменить(Наименование, ПредыдущиеЗначения.РегистрационныйЗнак, РегистрационныйЗнак);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Наименование = "";
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Наименование");
	
	Если Не ОформляютсяПутевыеЛисты Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Код");
		МассивНепроверяемыхРеквизитов.Добавить("Собственник");
		МассивНепроверяемыхРеквизитов.Добавить("Топливо");
		МассивНепроверяемыхРеквизитов.Добавить("НормаРасхода");
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПроверятьТопливо") И Не ДополнительныеСвойства.ПроверятьТопливо Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Топливо");
		МассивНепроверяемыхРеквизитов.Добавить("НормаРасхода");
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// Создание элемента на основании введенных данных из поле ввода
	Если ДанныеЗаполнения = Неопределено И Не ПустаяСтрока(ТекстЗаполнения) Тогда
		Марка = ТекстЗаполнения;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Собственник) Тогда
		Собственник = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуАвтотранспортом") Тогда
		ОформляютсяПутевыеЛисты = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьПроверкуКлючевыхРеквизитов(Отказ)
	
	Если ЭтоНовый() Тогда
		// В случае нового транспортного средства не производить проверку
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Получим значения реквизитов транспортного средства из информационной базы
	РеквизитыТранспортногоСредстваИзИБ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, 
		"ПометкаУдаления, ОформляютсяПутевыеЛисты");
	
	Если ПометкаУдаления <> РеквизитыТранспортногоСредстваИзИБ.ПометкаУдаления Тогда
		// В случае установки или снятия пометки удаления не производить проверку
		Возврат;
	КонецЕсли;
	
	Если ОформляютсяПутевыеЛисты Тогда
		// В случае если включили возможность оформлять путевые листы, то не проверяем.
		// Так как теперь это транспортное средство может указываться в путевом листе.
		Возврат;
	КонецЕсли;
	
	// Проверим, можно ли изменять реквизиты транспортного средства.
	
	// Для элемента нужно получить документы, в которых использовано транспортное средство:
	Если ЕстьПроведенныеДокументыПоТранспортномуСредству(РеквизитыТранспортногоСредстваИзИБ) Тогда
		СписокРеквизитов = Новый Структура("ОформляютсяПутевыеЛисты", 
			НСтр("ru = 'Оформляются путевые листы по транспортному средству'"));
		ТекстСообщения = НСтр("ru = 'Существуют путевые листы, оформленные на транспортное средство %1.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Наименование);
		ТекстСообщения = ТекстСообщения + НСтр("ru = '
			|Реквизит %2 не может быть изменен.'");
		Для Каждого КлючИЗначение Из СписокРеквизитов Цикл
			СообщитьОНекорректномРеквизите(РеквизитыТранспортногоСредстваИзИБ, КлючИЗначение.Ключ, КлючИЗначение.Значение, ТекстСообщения, Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьПроведенныеДокументыПоТранспортномуСредству(РеквизитыТранспортногоСредстваИзИБ)
	
	Если ОформляютсяПутевыеЛисты = РеквизитыТранспортногоСредстваИзИБ.ОформляютсяПутевыеЛисты Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТранспортноеСредство", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПутевойЛист.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПутевойЛист КАК ПутевойЛист
	|ГДЕ
	|	ПутевойЛист.ТранспортноеСредство = &ТранспортноеСредство
	|	И ПутевойЛист.Проведен";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

Процедура СообщитьОНекорректномРеквизите(РеквизитыОбъекта, ИмяРеквизита, СинонимРеквизита, ШаблонСообщения, Отказ)
	
	Если РеквизитыОбъекта[ИмяРеквизита] <> ЭтотОбъект[ИмяРеквизита] Тогда
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%2", СинонимРеквизита);
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", 
			СинонимРеквизита, , , ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ИмяРеквизита, "Объект", Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьУникальностьVIN(Отказ)

	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОформляютсяПутевыеЛисты Тогда
		// Проверка нужна только для транспортных средств, по которым оформляются путевые листы
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТранспортныеСредства.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|ГДЕ
	|	ТранспортныеСредства.ОформляютсяПутевыеЛисты
	|	И ТранспортныеСредства.Код = &Код
	|	И ТранспортныеСредства.Ссылка <> &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = НСтр("ru='Указан не уникальный VIN для транспортного средства'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Код", "Объект", Отказ);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
