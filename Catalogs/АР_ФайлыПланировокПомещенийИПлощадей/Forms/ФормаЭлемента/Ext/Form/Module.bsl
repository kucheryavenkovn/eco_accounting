
&НаКлиенте
Процедура ФайлШаблонаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогФыбораФайла								=	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогФыбораФайла.Фильтр						=	"Файл планировок MS EXCEL(*.xls; *.xlsx)|*.xls; *.xlsx";
	ДиалогФыбораФайла.Расширение					=	"xls";
	ДиалогФыбораФайла.Заголовок						=	"Выберите файл";
	ДиалогФыбораФайла.ПредварительныйПросмотр		=	Ложь;
	ДиалогФыбораФайла.ИндексФильтра					=	0;
	ДиалогФыбораФайла.ПолноеИмяФайла				=	Объект.ИмяФайла;
	ДиалогФыбораФайла.ПроверятьСуществованиеФайла	=	Истина;
	Если ДиалогФыбораФайла.Выбрать() Тогда
		Объект.ИмяФайла = ДиалогФыбораФайла.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_Планировка");

КонецПроцедуры

&НаКлиенте
Процедура ФайлШаблонаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не АР_ОбщиеПроцедурыКлиент.ДоступнаРаботаMSOffice(2) Тогда
		Возврат;		
	КонецЕсли;
	
	Если ЭтаФорма.Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("СпроситьОЗаписи", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, "Открытие файла возможно только после записи. Продолжить?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Нет);	
	Иначе	
		ОткрытьФайлНаКлиентеЗавершение();
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура СпроситьОЗаписи(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	ОткрытьФайлНаКлиентеЗавершение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлНаКлиентеЗавершение()
	
	ДанныеФайла = ПолучитьДанныеФайлаНаСервере();
	Если Не ДанныеФайла = Неопределено Тогда
		
		Файл = Новый Файл(ДанныеФайла.ИмяФайла);
		ИмяФайла = Файл.Имя;
		
		Если ПодключитьРасширениеРаботыСФайлами()Тогда
			ПолноеИмяФайлаНаКлиенте = "";
			ФайлМожноОткрывать = РаботаСФайламиСлужебныйКлиент.ПолучитьФайлВРабочийКаталог(
				ДанныеФайла.СсылкаНаДвоичныеДанныеФайла,
				"",
				ТекущаяДата(),
				ИмяФайла,
				КаталогВременныхФайлов(),
				ПолноеИмяФайлаНаКлиенте
			);
			Если ФайлМожноОткрывать Тогда
				ЗапуститьПриложение(ПолноеИмяФайлаНаКлиенте);
			КонецЕсли;
		Иначе
			ПолучитьФайл(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ИмяФайла, Истина);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеФайлаНаСервере()
	
	ПолноеИмяФайла = АР_ОбщиеПроцедуры.ПолучитьПутьКФайлуПланировки(Объект.Ссылка);
	
	Файл = Новый Файл(ПолноеИмяФайла);
	Если Файл.Существует() Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
		СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		
		НоваяСтруктураДанных = Новый Структура;
		НоваяСтруктураДанных.Вставить("СсылкаНаДвоичныеДанныеФайла", СсылкаНаДвоичныеДанныеФайла);
		НоваяСтруктураДанных.Вставить("ИмяФайла", АР_ОбщиеПроцедурыКлиентСервер.ПолучитьИмяФайлаПланировки(Объект.Ссылка, "Файл планировки"));
		
		Возврат НоваяСтруктураДанных; 
	Иначе 
		Возврат Неопределено;		
	КонецЕсли;
	
КонецФункции

