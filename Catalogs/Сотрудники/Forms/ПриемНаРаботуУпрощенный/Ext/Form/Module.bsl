#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		Параметры,
			"ТекущаяОрганизация, ТекущееПодразделение,
			|ТекущийВидЗанятости, ТекущийСпособРасчетаАванса,
			|ТекущийАванс");
	
	
	ДополнительныеПараметрыОткрытия = ПолучитьИзВременногоХранилища(Параметры.АдресДополнительныхПараметров);
	ЗначениеВРеквизитФормы(ДополнительныеПараметрыОткрытия.Сотрудник, "Сотрудник");
	ЗначениеВРеквизитФормы(ДополнительныеПараметрыОткрытия.ФизическоеЛицо, "ФизическоеЛицо");
	
	Заголовок = СтрШаблон(НСтр("ru = 'Прием на работу (%1)'"), ФизическоеЛицо.ФИО);
	
	ДатаПриема = ТекущаяДатаСеанса();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПроцентСевернойНадбавки",
		"Видимость",
		Параметры.ПрименятьСевернуюНадбавку
		И Параметры.УчетЗарплатыИКадровСредствамиБухгалтерии);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаАвансСотрудника",
		"Видимость",
		Параметры.АвансРедактируется);
		
	РасчетЗарплатыКлиентСервер.УстановитьПоказРазмераАванса(ЭтотОбъект, "ТекущийСпособРасчетаАванса");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		СохранитьИЗакрытьНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриемаПриИзменении(Элемент)
	
	ПроверитьОрганизациюИПодразделениеСотрудника();
	ПроверитьВидЗанятостиСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗанятостиПриИзменении(Элемент)
	
	СотрудникиКлиент.ПроверитьКонфликтыВидаЗанятостиССуществующимиСотрудниками(
		Сотрудник.Ссылка,
		Сотрудник.ФизическоеЛицо,
		ТекущаяОрганизация,
		ТекущийВидЗанятости,
		ДатаПриема);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособРасчетаАвансаПриИзменении(Элемент)
	
	Если ТекущийСпособРасчетаАванса = ПредопределенноеЗначение("Перечисление.СпособыРасчетаАванса.ПроцентомОтТарифа") Тогда
		ТекущийАванс = Параметры.РазмерАвансаВПроцентахПоУмолчанию;
	Иначе
		ТекущийАванс = 0;
	КонецЕсли; 
	
	РасчетЗарплатыКлиентСервер.УстановитьПоказРазмераАванса(ЭтотОбъект, "ТекущийСпособРасчетаАванса");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СохранитьИЗакрытьНаКлиенте()
	
	ПараметрыВозврата = Новый Структура("ДатаПриема, ТекущаяОрганизация, 
		|ТекущееПодразделение, ТекущаяДолжность, ОформленПоТрудовомуДоговору,
		|ТекущийВидЗанятости, ТекущаяТарифнаяСтавка, ТекущийПроцентСевернойНадбавки,
		|ТекущийСпособРасчетаАванса,ТекущийАванс");
	
	ЗаполнитьЗначенияСвойств(ПараметрыВозврата, ЭтотОбъект);
	
	Модифицированность = Ложь;
	
	Закрыть(ПараметрыВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат, ДополнительныеПараметры) Экспорт 
	
	СохранитьИЗакрытьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОрганизациюИПодразделениеСотрудника()
	
	Если Параметры.ИспользоватьНесколькоОрганизаций Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Сотрудник.ГоловнаяОрганизация)
		ИЛИ НЕ ЗначениеЗаполнено(ТекущаяОрганизация)
		ИЛИ НЕ ЗначениеЗаполнено(ТекущееПодразделение) Тогда
		ЗаполнитьОрганизациюПодразделение();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОрганизациюПодразделение()
	
	ЗначенияПоУмолчанию = Новый Структура("Организация, Подразделение");
	ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
	
	Если НЕ ЗначениеЗаполнено(Сотрудник.ГоловнаяОрганизация)
		ИЛИ НЕ ЗначениеЗаполнено(ТекущаяОрганизация) Тогда
		ТекущаяОрганизация = ЗначенияПоУмолчанию.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущееПодразделение) Тогда
		ТекущееПодразделение = ЗначенияПоУмолчанию.Подразделение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВидЗанятостиСотрудника()
	
	Если НЕ Параметры.ИспользоватьНесколькоОрганизаций Тогда
		Если НЕ ЗначениеЗаполнено(ТекущийВидЗанятости) Тогда
			ТекущийВидЗанятости = ПредопределенноеЗначение("Перечисление.ВидыЗанятости.ОсновноеМестоРаботы");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
