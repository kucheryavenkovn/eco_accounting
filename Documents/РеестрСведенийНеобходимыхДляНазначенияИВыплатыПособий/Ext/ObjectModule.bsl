#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Удаление полей с условной проверкой заполнения.
	Если Не (ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ПособияПоНетрудоспособности) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НомерЛисткаНетрудоспособности");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ДатаВыдачиЛисткаНетрудоспособности");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.КодПричиныНетрудоспособности");
	КонецЕсли;
	
	// Удаление полей, проверка которых выполняется в функции ПроверитьЗаполнениеПоРегламентуФСС.
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "КодПодчиненностиФСС");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ОГРН");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "КПП");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ИНН");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Руководитель");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ГлавныйБухгалтер");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДолжностьРуководителя");
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НаименованиеМедицинскойОрганизации");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ОГРНМедицинскойОрганизации");
	
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ФамилияРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ИмяРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ДатаРожденияРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ОчередностьРожденияРебенка");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ВидПодтверждающегоДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НаименованиеПодтверждающегоДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.ДатаДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.СерияДокумента");
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СведенияНеобходимыеДляНазначенияПособий.НомерДокумента");
	
	// Проверка значений.
	ПроверитьПовторноВыбранныеИНепроведенныеЗаявления(Отказ);
	ПроверитьТребованияФормата(Отказ);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И ОбъектЗафиксирован() Тогда
		ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьВторичныеДанныеДокумента(ЭтотОбъект);
	КонецЕсли;
	
	ЗаполнитьФизическихЛицИзЗаявлений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПередЗаписью

Процедура ЗаполнитьФизическихЛицИзЗаявлений()
	ФизическиеЛица.Очистить();
	
	Если СведенияНеобходимыеДляНазначенияПособий.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаявлениеСотрудникаНаВыплатуПособия.ФизическоеЛицо
		|ИЗ
		|	Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК ЗаявлениеСотрудникаНаВыплатуПособия
		|ГДЕ
		|	ЗаявлениеСотрудникаНаВыплатуПособия.Ссылка В(&МассивСсылок)";
		Запрос.УстановитьПараметр("МассивСсылок", СведенияНеобходимыеДляНазначенияПособий.ВыгрузитьКолонку("Заявление"));
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ФизическиеЛица, Результат.Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо"), "ФизическоеЛицо");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработкаПроверкиЗаполнения

Процедура ПроверитьПовторноВыбранныеИНепроведенныеЗаявления(Отказ)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Заявления", СведенияНеобходимыеДляНазначенияПособий.Выгрузить(,"НомерСтроки, Заявление, ПервичныйДокумент, ИзвещениеИзФССИспользование"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заявления.НомерСтроки КАК НомерСтроки,
	|	Заявления.Заявление КАК Заявление,
	|	Заявления.ПервичныйДокумент КАК ПервичныйДокумент,
	|	Заявления.ИзвещениеИзФССИспользование КАК ИзвещениеИзФССИспользование
	|ПОМЕСТИТЬ ВТЗаявления
	|ИЗ
	|	&Заявления КАК Заявления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВТЗаявления.НомерСтроки) КАК НомерСтроки,
	|	ЗаявленияПовторы.НомерСтроки КАК НомерСтрокиПовтора,
	|	ВТЗаявления.Заявление КАК Заявление
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗаявления КАК ЗаявленияПовторы
	|		ПО ВТЗаявления.Заявление = ЗаявленияПовторы.Заявление
	|			И ВТЗаявления.ПервичныйДокумент = ЗаявленияПовторы.ПервичныйДокумент
	|			И ВТЗаявления.НомерСтроки < ЗаявленияПовторы.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТЗаявления.Заявление,
	|	ВТЗаявления.ПервичныйДокумент,
	|	ЗаявленияПовторы.НомерСтроки
	|ИТОГИ ПО
	|	Заявление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаявления.НомерСтроки КАК НомерСтроки,
	|	ВТЗаявления.Заявление КАК Заявление
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК ЗаявлениеСотрудникаНаВыплатуПособия
	|		ПО ВТЗаявления.Заявление = ЗаявлениеСотрудникаНаВыплатуПособия.Ссылка
	|ГДЕ
	|	НЕ ЗаявлениеСотрудникаНаВыплатуПособия.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаявления.Заявление КАК Заявление,
	|	ВТЗаявления.НомерСтроки КАК НомерСтроки,
	|	ДругиеРеестрыСЭтимиЗаявлениями.Ссылка КАК Ссылка
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.СведенияНеобходимыеДляНазначенияПособий КАК ДругиеРеестрыСЭтимиЗаявлениями
	|		ПО ВТЗаявления.Заявление = ДругиеРеестрыСЭтимиЗаявлениями.Заявление
	|			И ВТЗаявления.ПервичныйДокумент = ДругиеРеестрыСЭтимиЗаявлениями.ПервичныйДокумент
	|ГДЕ
	|	ДругиеРеестрыСЭтимиЗаявлениями.Ссылка <> &Ссылка
	|	И НЕ ДругиеРеестрыСЭтимиЗаявлениями.Ссылка.ПометкаУдаления
	|	И НЕ ВТЗаявления.ИзвещениеИзФССИспользование";
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПовторяющиесяЗаявления = МассивРезультатов[1];
	НепроведенныеЗаявления = МассивРезультатов[2];
	УжеИспользованныеЗаявления = МассивРезультатов[3];
	
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	Если Не ПовторяющиесяЗаявления.Пустой() Тогда
		Выборка = ПовторяющиесяЗаявления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			ВыборкаПоЗаявлению = Выборка.Выбрать();
			Если ВыборкаПоЗаявлению.Следующий() Тогда
				ТекстПовторныеСтроки = "" + ВыборкаПоЗаявлению.НомерСтроки + ", " + ВыборкаПоЗаявлению.НомерСтрокиПовтора;
				Пока ВыборкаПоЗаявлению.Следующий() Цикл
					ТекстПовторныеСтроки = ТекстПовторныеСтроки + ", " + ВыборкаПоЗаявлению.НомерСтрокиПовтора;
				КонецЦикла;
				Текст = СтрШаблон(НСтр("ru = '%1 использовано в нескольких строках: %2.'"), Выборка.Заявление, ТекстПовторныеСтроки);
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Выборка, "Заявление", Текст);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не НепроведенныеЗаявления.Пустой() Тогда
		Выборка = НепроведенныеЗаявления.Выбрать();
		Пока Выборка.Следующий() Цикл
			Текст = СтрШаблон(НСтр("ru = '%1 не проведено.'"), Выборка.Заявление);
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Выборка, "Заявление", Текст);
		КонецЦикла;
	КонецЕсли;
	
	Если Не УжеИспользованныеЗаявления.Пустой() Тогда
		Выборка = УжеИспользованныеЗаявления.Выбрать();
		Пока Выборка.Следующий() Цикл
			Текст = СтрШаблон(НСтр("ru = '%1 уже использовано в документе %2.'"), Выборка.Заявление, Выборка.Ссылка);
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Выборка, "Заявление", Текст);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьТребованияФормата(Отказ)
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	РеквизитыЗаявлений = РеквизитыЗаявлений();
	
	ПредставлениеАдресаФакт = Метаданные.Документы.ЗаявлениеСотрудникаНаВыплатуПособия.Реквизиты.АдресПолучателяФактический.Представление();
	
	ПредставлениеАдресаРегистрации = Метаданные.Документы.ЗаявлениеСотрудникаНаВыплатуПособия.Реквизиты.АдресПолучателяРегистрации.Представление();
	
	СтатусРезидент = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент");
	
	Для Каждого Строка Из СведенияНеобходимыеДляНазначенияПособий Цикл
		
		Если Строка.ИзвещениеИзФССИспользование Тогда
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ИзвещениеИзФССНомер");
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ИзвещениеИзФССДата");
		КонецЕсли;
		
		РеквизитыЗаявления = РеквизитыЗаявлений.Получить(Строка.Заявление);
		Если РеквизитыЗаявления = Неопределено Тогда
			Продолжить; // Заполненность заявление проверяется стандартным образом.
		КонецЕсли;
		
		Если РеквизитыЗаявления.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
			ТекстОшибки = ПроверитьАдрес(
				РеквизитыЗаявления.АдресПолучателяФактический,
				РеквизитыЗаявления.АдресПолучателяФактическийИдентификаторФИАС,
				РеквизитыЗаявления.АдресПолучателяФактическийКодКЛАДР,
				ПредставлениеАдресаФакт);
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "АдресПолучателяФактический", ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
		
		Если РеквизитыЗаявления.СтатусНалогоплательщикаПолучателя = СтатусРезидент Тогда
			ТекстОшибки = ПроверитьАдрес(
				РеквизитыЗаявления.АдресПолучателяРегистрации,
				РеквизитыЗаявления.АдресПолучателяРегистрацииИдентификаторФИАС,
				РеквизитыЗаявления.АдресПолучателяРегистрацииКодКЛАДР,
				ПредставлениеАдресаРегистрации);
			Если ЗначениеЗаполнено(ТекстОшибки) Тогда
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "АдресПолучателяРегистрации", ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьАдрес(ЗначениеАдреса, ИдентификаторФИАС, КодКЛАДР, ЗаголовокПоля)
	Если Не ЗначениеЗаполнено(ЗначениеАдреса) Тогда
		Возврат СтрШаблон(НСтр("ru = 'Не заполнен %1'"), НРег(ЗаголовокПоля));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИдентификаторФИАС) И Не ЗначениеЗаполнено(КодКЛАДР) Тогда
		Если Дата > ДатаПереходаАдресовНаGUID() Тогда
			Возврат СтрШаблон(НСтр("ru = '%1: не заполнен идентификатор ФИАС. Возможно, адрес введен некорректно, либо не обновлен классификатор адресов.'"), ЗаголовокПоля);
		Иначе
			Возврат СтрШаблон(НСтр("ru = '%1: не заполнен код КЛАДР. Возможно, адрес введен некорректно, либо не обновлен классификатор адресов.'"), ЗаголовокПоля);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ДатаПереходаАдресовНаGUID()
	Возврат '20180101'; // 1 января - дата из регламента, '20180219' - фактическая дата начала приема.
КонецФункции

#КонецОбласти

#Область ПроверкиЗаполненияПоРегламентуФСС

Функция ПроверитьЗаполнениеПоРегламентуФСС() Экспорт
	
	Отказ = Ложь;
	
	СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КодПодчиненностиФСС");
	СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ОГРН");
	СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ИНН");
	СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "Руководитель");
	
	Если ЗарплатаКадры.ЭтоЮридическоеЛицо(Организация) Тогда
		СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КПП");
		СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ГлавныйБухгалтер");
		СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ДолжностьРуководителя");
	КонецЕсли;
	
	Если ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ПособияПоНетрудоспособности Тогда
		ПроверитьЗаполнениеСтрокСведенийПоВременнойНетрудоспособности(Отказ);
	ИначеЕсли ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ЕжемесячныеПособияПоУходуЗаРебенком Тогда
		ПроверитьЗаполнениеСтрокСведенийПоУходуЗаРебенком(Отказ);
	КонецЕсли;
	
	ПроверитьДатуПредставленияПакетаДокументов(Отказ);
	
	Возврат Отказ = Ложь;
	
КонецФункции

Процедура ПроверитьЗаполнениеСтрокСведенийПоВременнойНетрудоспособности(Отказ)
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	РеквизитыЗаявлений = РеквизитыЗаявлений();
	
	МассивБольничных = СведенияНеобходимыеДляНазначенияПособий.ВыгрузитьКолонку("ПервичныйДокумент");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БольничныйЛист.Ссылка КАК Ссылка,
	|	БольничныйЛист.ЭЛНКарантинПоКоронавирусу КАК ЭЛНКарантинПоКоронавирусу
	|ИЗ
	|	Документ.БольничныйЛист КАК БольничныйЛист
	|ГДЕ
	|	БольничныйЛист.Ссылка В(&МассивБольничных)";
	Запрос.УстановитьПараметр("МассивБольничных", МассивБольничных);
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаБольничных = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого Строка Из СведенияНеобходимыеДляНазначенияПособий Цикл
		
		СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "НаименованиеМедицинскойОрганизации");
		СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОГРНМедицинскойОрганизации");
		
		РеквизитыБольничного = ТаблицаБольничных.Найти(Строка.ПервичныйДокумент, "Ссылка");
		Если РеквизитыБольничного = Неопределено Тогда
			ЭЛНКарантинПоКоронавирусу = Ложь;
		Иначе
			ЭЛНКарантинПоКоронавирусу = РеквизитыБольничного.ЭЛНКарантинПоКоронавирусу;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.Заявление) Тогда
			РеквизитыЗаявления = РеквизитыЗаявлений.Получить(Строка.Заявление);
			
			Если РеквизитыЗаявления.ТипЗанятостиРаботника = Перечисления.ВидыЗанятости.Совместительство
				И Не ЗначениеЗаполнено(Строка.НомерЛисткаПоОсновномуМестуРаботы) Тогда
				Текст = НСтр("ru = 'Для совместителя должен быть указан номер листка нетрудоспособности, предъявляемого на основном месте работы.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "НомерЛисткаПоОсновномуМестуРаботы", Текст);
			КонецЕсли;
			
			Если (РеквизитыЗаявления.ВидПособия = Перечисления.ПособияНазначаемыеФСС.ПособиеПоБеременностиИРодам
				Или РеквизитыЗаявления.ВидПособия = Перечисления.ПособияНазначаемыеФСС.ПособиеПоБеременностиИРодамВставшимНаУчетВРанниеСроки) Тогда
				
				Если Строка.КодПричиныНетрудоспособности <> "05" Тогда
					Текст = НСтр("ru = 'Для пособия по беременности и родам должен быть указан код причины нетрудоспособности 05.'");
					СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "КодПричиныНетрудоспособности", Текст);
				КонецЕсли;
				
				Если РеквизитыЗаявления.ПорядокРасчетаСреднегоЗаработка <> Перечисления.СпособыРасчетаСреднегоЗаработкаДляОплатыПособия.СреднийЗаработок Тогда
					ДатаПроверки = УчетПособийСоциальногоСтрахованияКлиентСервер.ДатаОтдельногоРасчетаПособийПоМатеринству();
					Если Строка.ОсвобождениеДатаНачала1 >= ДатаПроверки Тогда
						Текст = НСтр("ru = 'Для отпуска по беременности и родам, начинающегося 1 января 2013 года или позже, указан недопустимый порядок расчета среднего заработка: из оклада или МРОТ.'");
						СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПорядокРасчетаСреднегоЗаработка", Текст);
					КонецЕсли;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Строка.ДатаИзмененияКодаПричиныНетрудоспособности)
					И Строка.ЯвляетсяПервичнымЛисткомНетрудоспособности
					И Не ЗначениеЗаполнено(Строка.ДополнительныйКодПричиныНетрудоспособности) Тогда
					Текст = НСтр("ru = 'Для пособия по беременности и родам в поле ""Дата 1"" должна быть указана предполагаемая дата родов.'");
					СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаИзмененияКодаПричиныНетрудоспособности", Текст);
				КонецЕсли;
				
			КонецЕсли;
			
			Если (Строка.УсловияИсчисленияКод1 = "46" Или Строка.УсловияИсчисленияКод2 = "46" Или Строка.УсловияИсчисленияКод3 = "46")
				И (Не ЗначениеЗаполнено(РеквизитыЗаявления.ДатаНачалаТрудовогоДоговораПолучателя)
					Или Не ЗначениеЗаполнено(РеквизитыЗаявления.ДатаОкончанияТрудовогоДоговораПолучателя)) Тогда
				Текст = НСтр("ru = 'Для принятого по срочному трудовому договору сроком менее 6 месяцев должен быть указан период этого договора.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаНачалаТрудовогоДоговораПолучателя", Текст);
			КонецЕсли;
			
			Если РеквизитыЗаявления.ВидПособия <> Перечисления.ПособияНазначаемыеФСС.ПособиеВСвязиСНесчастнымСлучаемНаПроизводстве
				И Не ЗначениеЗаполнено(Строка.СтажЛет)
				И Не ЗначениеЗаполнено(Строка.СтажМесяцев) Тогда
				Текст = НСтр("ru = 'Не заполнен стаж.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "СтажЛет", Текст);
			КонецЕсли;
		
		КонецЕсли;
		
		Если (Строка.УсловияИсчисленияКод1 = "43" Или Строка.УсловияИсчисленияКод2 = "43" Или Строка.УсловияИсчисленияКод3 = "43")
			И Не ЗначениеЗаполнено(Строка.ФинансированиеФедеральнымБюджетом) Тогда
			Текст = НСтр("ru = 'Для лица, подвергшегося воздействию радиации, не указана причина этого воздействия'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ФинансированиеФедеральнымБюджетом", Текст);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ВторойКодПричиныНетрудоспособности)
			И Не ЗначениеЗаполнено(Строка.ДатаИзмененияКодаПричиныНетрудоспособности) Тогда
			Текст = НСтр("ru = 'В поле ""Дата 1"" не указана дата изменения кода причины нетрудоспособности.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаИзмененияКодаПричиныНетрудоспособности", Текст);
		КонецЕсли;
		
		Если ЭтоСанаторнаяПутевка(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.ДатаИзмененияКодаПричиныНетрудоспособности) Тогда
				Текст = НСтр("ru = 'В поле ""Дата 1"" не указана дата начала санаторной путевки.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаИзмененияКодаПричиныНетрудоспособности", Текст);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ДатаОкончанияПутевки) Тогда
				Текст = НСтр("ru = 'В поле ""Дата 2"" не указана дата окончания санаторной путевки.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаОкончанияПутевки", Текст);
			КонецЕсли;
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОГРН_Санатория");
		КонецЕсли;
		
		Если ЭтоУходЗаРодственниками(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.ПоУходуФИО1) Тогда
				Текст = НСтр("ru = 'Не заполнено ФИО первого родственника, за которым осуществлялся уход.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПоУходуФИО1", Текст);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ПоУходуВозрастЛет1 + Строка.ПоУходуВозрастМесяцев1) Тогда
				Текст = НСтр("ru = 'Не заполнен возраст первого родственника, за которым осуществлялся уход.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПоУходуВозрастЛет1", Текст);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.ПоУходуРодственнаяСвязь1) Тогда
				Текст = НСтр("ru = 'Не заполнена родственная связь первого родственника, за которым осуществлялся уход.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПоУходуРодственнаяСвязь1", Текст);
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ПоУходуФИО2) Тогда
				Если Не ЗначениеЗаполнено(Строка.ПоУходуВозрастЛет2 + Строка.ПоУходуВозрастМесяцев2) Тогда
					Текст = НСтр("ru = 'Не заполнен возраст второго родственника, за которым осуществлялся уход.'");
					СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПоУходуВозрастЛет2", Текст);
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Строка.ПоУходуРодственнаяСвязь2) Тогда
					Текст = НСтр("ru = 'Не заполнена родственная связь второго родственника, за которым осуществлялся уход.'");
					СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПоУходуРодственнаяСвязь2", Текст);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоНарушениеРежима(Строка) Тогда
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаНарушенияРежима");
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "КодНарушенияРежима");
		КонецЕсли;
		
		ПериодыНетрудоспособностиЗаполнены = ПроверитьПериодНетрудоспособности(
			Отказ,
			Строка,
			1,
			ИмяТаблицы,
			НСтр("ru = 'первого периода освобождения от работы'"));
		
		Если ЗначениеЗаполнено(Строка.ОсвобождениеДатаНачала2) Тогда
			ПериодЗаполнен = ПроверитьПериодНетрудоспособности(
				Отказ,
				Строка,
				2,
				ИмяТаблицы,
				НСтр("ru = 'второго периода освобождения от работы'"));
			Если ПериодЗаполнен И Строка.ОсвобождениеДатаОкончания1 + 86400 <> Строка.ОсвобождениеДатаНачала2 Тогда
				Текст = НСтр("ru = 'Начало второго периода освобождения от работы должно непосредственно следовать за окончанием первого периода.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОсвобождениеДатаНачала2", Текст);
			КонецЕсли;
			ПериодыНетрудоспособностиЗаполнены = ПериодыНетрудоспособностиЗаполнены И ПериодЗаполнен;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ОсвобождениеДатаНачала3) Тогда
			ПериодЗаполнен = ПроверитьПериодНетрудоспособности(
				Отказ,
				Строка,
				3,
				ИмяТаблицы,
				НСтр("ru = 'третьего периода освобождения от работы'"));
			Если ПериодЗаполнен И Строка.ОсвобождениеДатаОкончания2 + 86400 <> Строка.ОсвобождениеДатаНачала3 Тогда
				Текст = НСтр("ru = 'Начало третьего периода освобождения от работы должно непосредственно следовать за окончанием второго периода.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОсвобождениеДатаНачала3", Текст);
			КонецЕсли;
			ПериодыНетрудоспособностиЗаполнены = ПериодыНетрудоспособностиЗаполнены И ПериодЗаполнен;
		КонецЕсли;
		
		Если Строка.ДатаВыдачиЛисткаНетрудоспособности > Строка.ОсвобождениеДатаНачала1
			И Не ЭтоНахождениеВСтационареСРебенком(Строка)
			И Не ЭтоПродолжение(Строка) Тогда
			Текст = НСтр("ru = 'Листок нетрудоспособности выдан после начала освобождения от работы.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаВыдачиЛисткаНетрудоспособности", Текст);
		КонецЕсли;
		
		Если ЕстьОплатаЗаСчетФСС(Строка) Тогда
			ПериодОплатыЗаполнен = СообщенияПользователюБЗК.ПроверитьПериодВСтрокеТаблицы(
				Отказ,
				ЭтотОбъект,
				ИмяТаблицы,
				Строка,
				"ДатаНачалаОплаты",
				"ДатаОкончанияОплаты",
				НСтр("ru = 'выплаты пособия за счет ФСС'"));
			Если ПериодОплатыЗаполнен И ПериодыНетрудоспособностиЗаполнены Тогда
				Если Строка.ДатаНачалаОплаты < Строка.ОсвобождениеДатаНачала1 Тогда
					Текст = НСтр("ru = 'Период оплаты за счет ФСС начинается до начала периода освобождения от работы.'");
					СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаОкончанияОплаты", Текст);
				КонецЕсли;
				Если Строка.ДатаОкончанияОплаты > Макс(Строка.ОсвобождениеДатаОкончания1, Строка.ОсвобождениеДатаОкончания2, Строка.ОсвобождениеДатаОкончания3) Тогда
					Текст = НСтр("ru = 'Период оплаты за счет ФСС заканчивается после окончания периода освобождения от работы.'");
					СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаОкончанияОплаты", Текст);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоНахождениеВСтационареСРебенком(Строка) Тогда
			СообщенияПользователюБЗК.ПроверитьПериодВСтрокеТаблицы(
				Отказ,
				ЭтотОбъект,
				ИмяТаблицы,
				Строка,
				"ПериодНахожденияВСтационареСРебенкомС",
				"ПериодНахожденияВСтационареСРебенкомПо",
				НСтр("ru = 'нахождения в стационаре'"));
		КонецЕсли;
		
		Если ЕстьИзменениеСостоянияНетрудоспособного(Строка) Тогда
			Если Не ЗначениеЗаполнено(Строка.НовыйСтатусНетрудоспособного) Тогда
				Текст = НСтр("ru = 'В поле ""Иное"" не заполнен код изменения состояния нетрудоспособного.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "НовыйСтатусНетрудоспособного", Текст);
			ИначеЕсли Не ЗначениеЗаполнено(Строка.ДатаНовыйСтатусНетрудоспособного)
				И (Строка.НовыйСтатусНетрудоспособного = "32"
					Или Строка.НовыйСтатусНетрудоспособного = "33"
					Или Строка.НовыйСтатусНетрудоспособного = "34"
					Или Строка.НовыйСтатусНетрудоспособного = "36") Тогда
				Текст = НСтр("ru = 'В поле ""Иное"" не заполнена дата изменения состояния нетрудоспособного.'");
				СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаНовыйСтатусНетрудоспособного", Текст);
			КонецЕсли;
		ИначеЕсли Не ЭЛНКарантинПоКоронавирусу Тогда
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ПриступитьКРаботеС");
		КонецЕсли;
		
		Если ЭтоПродолжение(Строка) Тогда
			СообщенияПользователюБЗК.ПроверитьЗаполнениеРеквизитаСтрокиТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "НомерЛисткаПродолжения");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеСтрокСведенийПоУходуЗаРебенком(Отказ)
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	Для Каждого Строка Из СведенияНеобходимыеДляНазначенияПособий Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ФамилияРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнена фамилия ребенка.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ФамилияРебенка", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ИмяРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнено имя ребенка.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ИмяРебенка", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ДатаРожденияРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнена дата рождения ребенка.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаРожденияРебенка", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ОчередностьРожденияРебенка) Тогда
			Текст = НСтр("ru = 'Не заполнена очередность рождения ребенка.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОчередностьРожденияРебенка", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ВидПодтверждающегоДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнен вид подтверждающего документа.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ВидПодтверждающегоДокумента", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.НаименованиеПодтверждающегоДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнено наименование подтверждающего документа.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "НаименованиеПодтверждающегоДокумента", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.ДатаДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнена дата подтверждающего документа.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаДокумента", Текст);
		КонецЕсли;
		
		Если Строка.ВидПодтверждающегоДокумента = Перечисления.ВидыПодтверждающихДокументовОтпускаПоУходу.СвидетельствоОРождении
			И Не ЗначениеЗаполнено(Строка.СерияДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнена серия подтверждающего документа.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "СерияДокумента", Текст);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.НомерДокумента) Тогда
			Текст = НСтр("ru = 'Не заполнен номер подтверждающего документа.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "НомерДокумента", Текст);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьДатуПредставленияПакетаДокументов(Отказ)
	ИмяТаблицы = "СведенияНеобходимыеДляНазначенияПособий";
	
	МинимальнаяДатаПолучения = НачалоДня(Дата - 5*86400);
	
	РеквизитыЗаявлений = РеквизитыЗаявлений();
	
	Для Каждого Строка Из СведенияНеобходимыеДляНазначенияПособий Цикл
		
		Если ЗначениеЗаполнено(Строка.ДатаПредставленияПакетаДокументов) Тогда
			ДатаПолучения = Строка.ДатаПредставленияПакетаДокументов;
		Иначе
			РеквизитыЗаявления = РеквизитыЗаявлений.Получить(Строка.Заявление);
			Если ЗначениеЗаполнено(РеквизитыЗаявления.ДатаПредставленияПакетаДокументов) Тогда
				ДатаПолучения = РеквизитыЗаявления.ДатаПредставленияПакетаДокументов;
			Иначе
				ДатаПолучения = РеквизитыЗаявления.Дата;
			КонецЕсли;
		КонецЕсли;
		
		Если НачалоДня(ДатаПолучения) < МинимальнаяДатаПолучения Тогда
			Текст = НСтр("ru = 'Прошло более 5 календарных дней с даты представления застрахованным лицом заявления и документов.'");
			СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ДатаПредставленияПакетаДокументов", Текст);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьПериодНетрудоспособности(Отказ, Строка, НомерПериода, ИмяТаблицы, Представление)
	ПериодЗаполнен = СообщенияПользователюБЗК.ПроверитьПериодВСтрокеТаблицы(
		Отказ,
		ЭтотОбъект,
		ИмяТаблицы,
		Строка,
		"ОсвобождениеДатаНачала" + НомерПериода,
		"ОсвобождениеДатаОкончания" + НомерПериода,
		Представление);
	Если Не ЗначениеЗаполнено(Строка["ОсвобождениеФИОВрача" + НомерПериода]) Тогда
		Текст = НСтр("ru = 'Не заполнено ФИО врача %1.'");
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, Представление);
		СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОсвобождениеФИОВрача1", Текст);
		ПериодЗаполнен = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Строка["ОсвобождениеДолжностьВрача" + НомерПериода]) Тогда
		Текст = НСтр("ru = 'Не заполнена должность врача %1.'");
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, Представление);
		СообщенияПользователюБЗК.СообщитьОбОшибкеВСтрокеТаблицы(Отказ, ЭтотОбъект, ИмяТаблицы, Строка, "ОсвобождениеДолжностьВрача1", Текст);
		ПериодЗаполнен = Ложь;
	КонецЕсли;
	
	Возврат ПериодЗаполнен;
КонецФункции

Функция ЭтоСанаторнаяПутевка(Строка)
	Возврат Строка.КодПричиныНетрудоспособности = "08"
		Или Строка.ВторойКодПричиныНетрудоспособности = "08"
		Или Строка.ДополнительныйКодПричиныНетрудоспособности = "017"
		Или Строка.ДополнительныйКодПричиныНетрудоспособности = "018"
		Или Строка.ДополнительныйКодПричиныНетрудоспособности = "019"
		Или ЗначениеЗаполнено(Строка.ДатаОкончанияПутевки);
КонецФункции

Функция ЭтоУходЗаРодственниками(Строка)
	Возврат Строка.КодПричиныНетрудоспособности = "09"
		Или Строка.КодПричиныНетрудоспособности = "12"
		Или Строка.КодПричиныНетрудоспособности = "13"
		Или Строка.КодПричиныНетрудоспособности = "14"
		Или Строка.КодПричиныНетрудоспособности = "15"
		Или Строка.ВторойКодПричиныНетрудоспособности = "09"
		Или Строка.ВторойКодПричиныНетрудоспособности = "12"
		Или Строка.ВторойКодПричиныНетрудоспособности = "13"
		Или Строка.ВторойКодПричиныНетрудоспособности = "14"
		Или Строка.ВторойКодПричиныНетрудоспособности = "15";
КонецФункции

Функция ЭтоНарушениеРежима(Строка)
	Возврат ЗначениеЗаполнено(Строка.ДатаНарушенияРежима)
		Или ЗначениеЗаполнено(Строка.КодНарушенияРежима);
КонецФункции

Функция ЭтоНахождениеВСтационареСРебенком(Строка)
	Возврат ЗначениеЗаполнено(Строка.ПериодНахожденияВСтационареСРебенкомС)
		Или ЗначениеЗаполнено(Строка.ПериодНахожденияВСтационареСРебенкомПо);
КонецФункции

Функция ЭтоПродолжение(Строка)
	Возврат Строка.НовыйСтатусНетрудоспособного = "31"
		Или ЗначениеЗаполнено(Строка.НомерЛисткаПродолжения);
КонецФункции

Функция ЕстьОплатаЗаСчетФСС(Строка)
	Возврат ЗначениеЗаполнено(Строка.ДатаНачалаОплаты)
		Или ЗначениеЗаполнено(Строка.ДатаОкончанияОплаты);
КонецФункции

Функция ЕстьИзменениеСостоянияНетрудоспособного(Строка)
	Возврат ЗначениеЗаполнено(Строка.НовыйСтатусНетрудоспособного)
		Или ЗначениеЗаполнено(Строка.ДатаНовыйСтатусНетрудоспособного);
КонецФункции

Функция РеквизитыЗаявлений()
	РеквизитыЗаявлений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "РеквизитыЗаявлений");
	Если РеквизитыЗаявлений <> Неопределено Тогда
		Возврат РеквизитыЗаявлений;
	КонецЕсли;
	МассивЗаявлений = СведенияНеобходимыеДляНазначенияПособий.ВыгрузитьКолонку("Заявление");
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("ВидПособия");
	ИменаРеквизитов.Добавить("СпособВыплатыПособия");
	ИменаРеквизитов.Добавить("АдресПолучателяФактический");
	ИменаРеквизитов.Добавить("АдресПолучателяФактическийИдентификаторФИАС");
	ИменаРеквизитов.Добавить("АдресПолучателяФактическийКодКЛАДР");
	ИменаРеквизитов.Добавить("АдресПолучателяРегистрации");
	ИменаРеквизитов.Добавить("АдресПолучателяРегистрацииИдентификаторФИАС");
	ИменаРеквизитов.Добавить("АдресПолучателяРегистрацииКодКЛАДР");
	ИменаРеквизитов.Добавить("СтатусНалогоплательщикаПолучателя");
	ИменаРеквизитов.Добавить("ТипЗанятостиРаботника");
	ИменаРеквизитов.Добавить("ДатаНачалаТрудовогоДоговораПолучателя");
	ИменаРеквизитов.Добавить("ДатаОкончанияТрудовогоДоговораПолучателя");
	ИменаРеквизитов.Добавить("ПорядокРасчетаСреднегоЗаработка");
	ИменаРеквизитов.Добавить("Дата");
	ИменаРеквизитов.Добавить("ДатаПредставленияПакетаДокументов");
	УстановитьПривилегированныйРежим(Истина);
	РеквизитыЗаявлений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивЗаявлений, ИменаРеквизитов);
	УстановитьПривилегированныйРежим(Ложь);
	ДополнительныеСвойства.Вставить("РеквизитыЗаявлений", РеквизитыЗаявлений);
	Возврат РеквизитыЗаявлений;
КонецФункции

#КонецОбласти

#Область МеханизмФиксацииИзменений

Функция ОбъектЗафиксирован() Экспорт
	Возврат Не ПрямыеВыплатыПособийСоциальногоСтрахования.СтатусПозволяетРедактироватьДокумент(СтатусДокумента);
КонецФункции

Функция ОбновитьВторичныеДанныеДокумента() Экспорт
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	ПараметрыФиксации = Документы.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий.ПараметрыФиксацииВторичныхДанных();
	
	Если ОбновитьДанныеОрганизации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьСведенияОПособиях(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ОбновитьСведенияОМедицинскихОрганизациях(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ОбновитьДанныеОрганизации(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыДокумента = Новый Структура;
	Для Каждого ОписаниеРеквизита Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		Если Не ОписаниеРеквизита.Значение.РеквизитСтроки Тогда
			РеквизитыДокумента.Вставить(ОписаниеРеквизита.Значение.ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	ИменаРеквизитовОрганизации = "РегистрационныйНомерФСС, КодПодчиненностиФСС, ДополнительныйКодФСС, ОГРН";
	ЗначенияРеквизитовОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, ИменаРеквизитовОрганизации);
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗначенияРеквизитовОрганизации, ИменаРеквизитовОрганизации);
	
	СведенияОПодписях = ПодписиДокументов.СведенияОПодписяхПоУмолчаниюДляОбъектаМетаданных(Метаданные(), Организация);
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, СведенияОПодписях);
	
	ДатаПолученияСведений = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	
	ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, ДатаПолученияСведений, "ТелОрганизации,АдресЭлектроннойПочтыОрганизации,ИННЮЛ,КППЮЛ");
	ОргСведения.Свойство("ТелОрганизации",                   РеквизитыДокумента.ТелефонСоставителя);
	ОргСведения.Свойство("АдресЭлектроннойПочтыОрганизации", РеквизитыДокумента.АдресЭлектроннойПочтыСоставителя);
	ОргСведения.Свойство("ИННЮЛ",                            РеквизитыДокумента.ИНН);
	ОргСведения.Свойство("КППЮЛ",                            РеквизитыДокумента.КПП);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыДокумента, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ОбновитьСведенияОПособиях(ПараметрыФиксации)
	Если СведенияНеобходимыеДляНазначенияПособий.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	ТаблицаОснований = СведенияНеобходимыеДляНазначенияПособий.Выгрузить(, "Заявление, ПервичныйДокумент");
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросРеестраПрямыхВыплатПоПервичнымДокументам(ЭтотОбъект, ТаблицаОснований, "ВТВторичныеДанные");
	Если Запрос = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Запрос.Выполнить();
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект, "СведенияНеобходимыеДляНазначенияПособий", "ВТВторичныеДанные", ПараметрыФиксации);
КонецФункции

Функция ОбновитьСведенияОМедицинскихОрганизациях(ПараметрыФиксации)
	Если СведенияНеобходимыеДляНазначенияПособий.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	ТаблицаОснований = СведенияНеобходимыеДляНазначенияПособий.Выгрузить(, "Заявление, ПервичныйДокумент, МедицинскаяОрганизация");
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОснований.Заявление КАК Заявление,
	|	ТаблицаОснований.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ТаблицаОснований.МедицинскаяОрганизация КАК МедицинскаяОрганизация
	|ПОМЕСТИТЬ ТаблицаОснований
	|ИЗ
	|	&ТаблицаОснований КАК ТаблицаОснований
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОснований.Заявление КАК Заявление,
	|	ТаблицаОснований.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ТаблицаОснований.МедицинскаяОрганизация КАК МедицинскаяОрганизация,
	|	МедицинскиеОрганизации.Наименование КАК НаименованиеМедицинскойОрганизации,
	|	МедицинскиеОрганизации.ОГРН КАК ОГРНМедицинскойОрганизации,
	|	МедицинскиеОрганизации.Адрес КАК АдресМедицинскойОрганизации
	|ПОМЕСТИТЬ ВТВторичныеДанные
	|ИЗ
	|	ТаблицаОснований КАК ТаблицаОснований
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МедицинскиеОрганизации КАК МедицинскиеОрганизации
	|		ПО ТаблицаОснований.МедицинскаяОрганизация = МедицинскиеОрганизации.Ссылка";
	Запрос.УстановитьПараметр("ТаблицаОснований", ТаблицаОснований);
	Запрос.Выполнить();
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект, "СведенияНеобходимыеДляНазначенияПособий", "ВТВторичныеДанные", ПараметрыФиксации);
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли