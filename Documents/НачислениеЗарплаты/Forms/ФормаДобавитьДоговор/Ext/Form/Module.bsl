
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// "Распаковываем" параметры
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчета, "Ссылка, Организация, Подразделение, МесяцНачисления, Сотрудник, ФизическоеЛицо");
	
	КадровыеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическоеЛицо, "ФамилияИО", МесяцНачисления);
	
	ПредставлениеСотрудника = КадровыеДанныеФизЛиц[0].ФамилияИО;
	
	Заголовок = СтрШаблон(НСтр("ru='Договор подряда (%1)'"), ПредставлениеСотрудника);
	
	Если НЕ ЗначениеЗаполнено(Подразделение) Тогда
		ЗначенияПоУмолчанию = Новый Структура("Организация, Подразделение");
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
		Подразделение = ЗначенияПоУмолчанию.Подразделение;
	КонецЕсли;
	
	СоответствиеКодовВычетовКодамДоходов = Новый ФиксированноеСоответствие(УчетНДФЛ.ВычетыКДоходам(Год(МесяцНачисления)));
	
	КодыДохода = УчетНДФЛБазовый.ДоходыНДФЛПоВидуОсобыхНачислений(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги);
	Если КодыДохода.Количество() <> 0 Тогда
		КодДохода = КодыДохода[0];
	КонецЕсли;
	
	ВычетПримененныйКДоходам = Ложь;
	Если ЗначениеЗаполнено(КодДохода) Тогда
		ВычетПримененныйКДоходам = ЭтотОбъект.СоответствиеКодовВычетовКодамДоходов.Получить(КодДохода) <> Неопределено
	КонецЕсли;
	
	Если ВычетПримененныйКДоходам Тогда
		
		КодВычета = УчетНДФЛ.КодВычетаПоКодуДоходаНДФЛ(КодДохода);
		
		Если ЗначениеЗаполнено(КодВычета) Тогда
			СуммаВычета = УчетНДФЛ.ВычетКДоходуСотрудника(
			Ссылка,
			Организация,
			МесяцНачисления,
			Сотрудник,
			КодДохода,
			КодВычета,
			Результат,
			1);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ОпределитьКодДоходаСтраховыеВзносы();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	ОбработатьИзменениеНачислений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиИзмененияВОбъектФормыВладельца();
	Закрыть(Сотрудник);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПеренестиИзмененияВОбъектФормыВладельца()
	
	Если Модифицированность Тогда
		Оповестить("ДобавленДоговор", ПоместитьИзмененныеДанныеВоВременноеХранилище(), ЭтотОбъект);
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеНачислений()
	
	Если ЗначениеЗаполнено(КодДохода) Тогда
		ВычетПримененныйКДоходам = ЭтотОбъект.СоответствиеКодовВычетовКодамДоходов.Получить(КодДохода) <> Неопределено
	КонецЕсли;
	
	Если ВычетПримененныйКДоходам Тогда
		КодВычета = УчетНДФЛ.КодВычетаПоКодуДоходаНДФЛ(КодДохода);
		Если Не ЗначениеЗаполнено(КодВычета) Тогда
			КодВычета = УчетНДФЛ.КодВычетаПоКодуДоходаНДФЛ(КодДохода);
		КонецЕсли;
	Иначе
		КодВычета = Справочники.ВидыВычетовНДФЛ.ПустаяСсылка();
		СуммаВычета = 0;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодВычета) И СуммаВычета = 0 Тогда
		СуммаВычета = УчетНДФЛ.ВычетКДоходуСотрудника(
									Ссылка,
									Организация,
									МесяцНачисления,
									Сотрудник,
									КодДохода,
									КодВычета,
									Результат,
									1);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьКодДоходаСтраховыеВзносы()
	
	ОблагаетсяФСС_НС = Ложь;
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ОблагаетсяФСС_НС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ОблагаетсяФСС_НС");
	КонецЕсли;
	
	КодДоходаСтраховыеВзносы = УчетСтраховыхВзносов.ВидДоходаДляДоговораНаВыполнениеРабот(ОблагаетсяФСС_НС);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	ПланируемаяДатаВыплаты = КонецМесяца(МесяцНачисления);
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ДатаУвольнения");
	Если КадровыеДанные.Количество()>0 Тогда
		ДатаУвольнения = КадровыеДанные[0].ДатаУвольнения;
		Если ЗначениеЗаполнено(ДатаУвольнения)
			И НачалоМесяца(ДатаУвольнения) = МесяцНачисления Тогда
			ПланируемаяДатаВыплаты = ДатаУвольнения;
		КонецЕсли;
	КонецЕсли;
	
	ВозвращаемыеСведения = Новый Структура;
	
	ДанныеНачисления = Новый Структура();
	ДанныеНачисления.Вставить("Подразделение",            Подразделение);
	ДанныеНачисления.Вставить("Сотрудник",                Сотрудник);
	ДанныеНачисления.Вставить("ДокументОснование",        ДокументОснование);
	ДанныеНачисления.Вставить("Результат",                Результат);
	ДанныеНачисления.Вставить("КодВычета",                КодВычета);
	ДанныеНачисления.Вставить("СуммаВычета",              СуммаВычета);
	ДанныеНачисления.Вставить("КодДохода",                КодДохода);
	ДанныеНачисления.Вставить("КодДоходаСтраховыеВзносы", КодДоходаСтраховыеВзносы);
	ДанныеНачисления.Вставить("ПланируемаяДатаВыплаты",   ПланируемаяДатаВыплаты);
	
	ВозвращаемыеСведения.Вставить("НачисленияПоДоговорам", ДанныеНачисления);
	ВозвращаемыеСведения.Вставить("ФизическоеЛицо",        ФизическоеЛицо);
	ВозвращаемыеСведения.Вставить("Сотрудник",             Сотрудник);
	
	Возврат ПоместитьВоВременноеХранилище(ВозвращаемыеСведения, Новый УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти
