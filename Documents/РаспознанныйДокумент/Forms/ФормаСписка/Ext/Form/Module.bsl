#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.НавигационнаяСсылка = "e1cib/list/Документ.РаспознанныйДокумент";
	
	ПоказыватьМиниатюры = Ложь;
	ПоказыватьМиниатюрыНаСервере();
	
	ОбщиеНастройки = ПолучитьОбщиеНастройки();
	АдресРезультатаРаспознавания = ПоместитьВоВременноеХранилище(Новый Структура("ФайловВОбработке", Неопределено), УникальныйИдентификатор);
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = НачалоМесяца(ТекущаяДатаСеанса());
	ЭлементОтбора.Использование = (ВариантОтбора = 0);
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Дата");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = КонецМесяца(ТекущаяДатаСеанса());
	ЭлементОтбора.Использование = (ВариантОтбора = 0);
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Контрагент");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = "";
	ЭлементОтбора.Использование = (ВариантОтбора = 1);
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = "";
	ЭлементОтбора.Использование = (ВариантОтбора = 2);
	
	ФильтрПоСтатусу = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ФильтрПоСтатусу.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Статус");
	ФильтрПоСтатусу.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	
	СписокВсеСтатусы = Новый СписокЗначений();
	СписокВсеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.ПустаяСсылка"));
	СписокВсеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый"));
	СписокВсеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Ошибка"));
	
	ФильтрПоСтатусу.ПравоеЗначение = СписокВсеСтатусы;
	ФильтрПоСтатусу.Использование = Истина;
	
	ИдентификаторФильтраПоСтатусам = Список.Отбор.ПолучитьИдентификаторПоОбъекту(ФильтрПоСтатусу);
	
	Если УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей()
		И Не Пользователи.ЭтоПолноправныйПользователь( , , Ложь) Тогда

		ДоступныеОрганизации = Новый СписокЗначений;
		ДоступныеОрганизации.ЗагрузитьЗначения(РаспознаваниеДокументовСлужебный.ДоступныеОрганизации());
		ДоступныеОрганизации.Добавить(Справочники.Организации.ПустаяСсылка());
		
		ФильтрПоОрганизации = ОтборОрганизация.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ФильтрПоОрганизации.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		ФильтрПоОрганизации.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ФильтрПоОрганизации.ПравоеЗначение = ДоступныеОрганизации;
		
	КонецЕсли;
	
	ОбновитьКоличествоФайловПоСтатусам();
	
	ЗаполнитьПериодыДляОтборов();
	
	ДатаСеанса = ТекущаяДатаСеанса();
	ДатаСеансаУниверсальная = УниверсальноеВремя(ДатаСеанса, ЧасовойПоясСеанса());
	ПоправкаКУниверсальномуВремени = ДатаСеанса - ДатаСеансаУниверсальная;
	Список.Параметры.УстановитьЗначениеПараметра("ПоправкаВремени", ПоправкаКУниверсальномуВремени);
	
	ОграниченныеПоля = Новый Массив;
	ОграниченныеПоля.Добавить("ТипДокумента");
	Список.УстановитьОграниченияИспользованияВГруппировке(ОграниченныеПоля);
	Список.УстановитьОграниченияИспользованияВПорядке(ОграниченныеПоля);
	Список.УстановитьОграниченияИспользованияВОтборе(ОграниченныеПоля);
	
	УстановитьУсловноеОформление();
	СброситьРазмерыИПоложениеОкна();
	
	ОбновитьПредставлениеБаланса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВсеСтроки1 = ОтборПериодДерево.ПолучитьЭлементы();
	Если ВсеСтроки1.Количество() <> 0 Тогда
		Элементы.ОтборПериодДерево.Развернуть(ВсеСтроки1[0].ПолучитьИдентификатор());
	КонецЕсли;
	
	ОбновитьОбработчикиОжидания(Ложь);
	
	Если РаспознаваниеДокументовСлужебныйКлиент.ИспользуетсяВстроенныйIE() Тогда // Версия меньше 8.3.14
		Элементы.ОтображениеМиниатюр.Видимость = Ложь;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеПоказаПредупреждания", ЭтотОбъект);
	
	Если ПоказатьТребованиеВключитьИТС Тогда
		РаспознаваниеДокументовСлужебныйКлиент.ПоказатьТребованиеВключитьИТС(Обработчик);
	КонецЕсли;
	
	РаспознаваниеДокументовЗамерыВремениСлужебныйКлиент.ПодключитьОбработчикОтправкиЗамеровВремени();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "РаспознанныйДокумент_СтатусОбработан" Тогда
		ПараметрыОтбораСозданныхДокументов1С = ПолучитьПараметрыОтбораСозданныхДокументов1С();
		УстановитьСтатусОбработанНаСервере(Источник, ПараметрыОтбораСозданныхДокументов1С);
		РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
		Элементы.Список.Обновить();
		ОбновитьКоличествоФайловПоСтатусам();
	ИначеЕсли ИмяСобытия = "РаспознанныйДокумент_ОбновитьОтборФормыСписка" Тогда
		// FIXME: После того, как у центрального списка появится основная таблица, в формах ТОРГ12 и СчетНаОплату нужно будет
		// заменить вызовы Оповестить("РаспознанныйДокумент_ОбновитьОтборФормыСписка") на ОповеститьОбИзменении(Объект.Ссылка)
		Элементы.Список.Обновить();
		Если ВариантОтбора = 1 Тогда
			Элементы.ОтборКонтрагент.Обновить();
			ОтборКонтрагентПриАктивизацииСтроки(Элементы.ОтборКонтрагент);
		ИначеЕсли ВариантОтбора = 2 Тогда
			Элементы.ОтборОрганизация.Обновить();
			ОтборОрганизацияПриАктивизацииСтроки(Элементы.ОтборОрганизация);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "РаспознанныйДокумент_ОбновитьОбработчикиОжидания" Тогда
		ОбновитьОбработчикиОжидания(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантОтбораПриИзменении(Элемент)
	
	ВариантОтбораПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьМиниатюрыПриИзменении(Элемент)
	
	Если ДанныеДеревьев = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДанныеДеревьев.ТекущаяСтрока = Неопределено;
	
	ПриАктивизацииСтрокиДеревоСписок(Элементы.Список.ТекущиеДанные, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСозданныеДокументы1СПриИзменении(Элемент)
	
	ПараметрыОтбораСозданныхДокументов1С = ПолучитьПараметрыОтбораСозданныхДокументов1С();
	ОбновитьДеревоДокументовИФайловВсеНаСервере(ПараметрыОтбораСозданныхДокументов1С);
	РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	Элементы.ГруппаДеревоПраво.Видимость = ПоказыватьСозданныеДокументы1С;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборПериодДерево

&НаКлиенте
Процедура ОтборПериодДеревоПриАктивизацииСтроки(Элемент)
	
	ТекущаяДата = Неопределено;
	ТекущаяДатаПериод = Неопределено;
	ТекущийКонтрагент = Неопределено;
	ТекущаяОрганизация = Неопределено;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущаяДата = Элемент.ТекущиеДанные.День;
	ПериодДень = (Элемент.ТекущиеДанные.Вложенность = 1);
	
	ТекущаяДатаПериод = ?(ПериодДень, "День", "Месяц");
	
	ИзменитьОтборПоПериоду(ТекущаяДата, ПериодДень);
	Если ПоказыватьСозданныеДокументы1С Тогда 
		ОбновитьДеревоДокументовИФайловВсеНаСервере(Новый Структура("НачалоПериода, ПериодДень", ТекущаяДата, ПериодДень));
		РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборКонтрагент

&НаКлиенте
Процедура ОтборКонтрагентПриАктивизацииСтроки(Элемент)
	
	ТекущаяДата = Неопределено;
	ТекущаяДатаПериод = Неопределено;
	ТекущийКонтрагент = Неопределено;
	ТекущаяОрганизация = Неопределено;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущийКонтрагент = Элемент.ТекущиеДанные.Контрагент;
	Список.Отбор.Элементы[2].ПравоеЗначение = ТекущийКонтрагент;
	
	Если ПоказыватьСозданныеДокументы1С Тогда 
		ОбновитьДеревоДокументовИФайловВсеНаСервере(ТекущийКонтрагент);
		РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборОрганизация

&НаКлиенте
Процедура ОтборОрганизацияПриАктивизацииСтроки(Элемент)
	
	ТекущаяДата = Неопределено;
	ТекущаяДатаПериод = Неопределено;
	ТекущийКонтрагент = Неопределено;
	ТекущаяОрганизация = Неопределено;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущаяОрганизация = Элемент.ТекущиеДанные.Организация;
	ЭлементОтбора = Список.Отбор.Элементы[3];
	Если ЗначениеЗаполнено(ТекущаяОрганизация) Тогда
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = ТекущаяОрганизация;
	Иначе
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация.Наименование");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		ЭлементОтбора.ПравоеЗначение = Неопределено;
	КонецЕсли;
	Если ПоказыватьСозданныеДокументы1С Тогда
		ОбновитьДеревоДокументовИФайловВсеНаСервере(ТекущаяОрганизация);
		РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// FIXME:
	// Надо тексты рассчитывать не в зависимости от номера иконки, который может измениться
	// при изменении коллекции картинки, а от реального состояния текущих данных строки.
	// При этом для всех требуемых данных надо установить флажок "использовать всегда".
	
	СтандартнаяОбработка = Ложь;
	Если Поле.Имя = "СтатусРаспознавания" Тогда
		
		СтатусРаспознавания = Элемент.ТекущиеДанные.СтатусРаспознавания;
		Если СтатусРаспознавания = 0 Тогда
			Если Элемент.ТекущиеДанные.ЭтоЗаданиеРаспознавания Тогда 
				Статус = НСтр("ru = 'Задание распознавания отменено.'");
			Иначе 
				Статус = НСтр("ru = 'Установлена пометка на удаление.'");
			КонецЕсли;
		ИначеЕсли СтатусРаспознавания = 1 Тогда
			Статус = НСтр("ru = 'Ошибка распознавания.'");
		ИначеЕсли СтатусРаспознавания = 4 Тогда
			Статус = НСтр("ru = 'Документ обработан.'");
		ИначеЕсли СтатусРаспознавания = 2 Тогда
			Статус = НСтр("ru = 'Документ распознан и ожидает проверки.'");
		ИначеЕсли СтатусРаспознавания = 3 Тогда
			Статус = НСтр("ru = 'Документ в процессе распознавания. Пожалуйста, подождите...'");
		КонецЕсли;
		ПоказатьПредупреждение(, Статус);
		
	ИначеЕсли Поле.Имя = "СтатусСвязанногоДокумента" Тогда
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("РаспознанныйДокумент", ТекущиеДанные.Ссылка);
		
		ОткрытьФорму(
			"Документ.РаспознанныйДокумент.Форма.ПрикрепитьИзображениеКНайденомуДокументу",
			ПараметрыОткрытия,
			ЭтотОбъект);
		
	Иначе
		
		Отбор = Новый Структура;
		Отбор.Вставить("ТекущаяДата", ТекущаяДата);
		Отбор.Вставить("ТекущаяДатаПериод", ТекущаяДатаПериод);
		Отбор.Вставить("ТекущаяОрганизация", ТекущаяОрганизация);
		Отбор.Вставить("ТекущийКонтрагент", ТекущийКонтрагент);
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		РаспознаваниеДокументовСлужебныйКлиент.ПоказатьРаспознанныйДокумент(
			ТекущиеДанные.Ссылка,
			ТекущиеДанные.ТипДокумента,
			ТекущиеДанные.Идентификатор,
			Отбор
		);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПриАктивизацииСтрокиДеревоСписок(Элементы.Список.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьФормуДобавления();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоДокументыИФайлы

&НаКлиенте
Процедура ДеревоДокументыИФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	Иначе
		ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ТекущиеДанные.Ссылка, УникальныйИдентификатор);
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДокументыИФайлыПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура БалансПредствлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Перейти" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПроверкиПодключенияКСервисуРаспознавания", ЭтотОбъект);
		РаспознаваниеДокументовКлиент.ПоказатьАвторизациюИТС(Обработчик, ЭтотОбъект);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "Подробнее" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение( ,
			НСтр("ru = 'Распознавание документов предоставляется в пилотном режиме.
			           |Условия использования сервиса могут быть изменены.'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлыДляРаспознавания(Команда)
	
	ОткрытьФормуДобавления();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеМиниатюр(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Кнопка = Элементы.ОтображениеМиниатюр;
	ВключитьФильтр = НЕ Кнопка.Пометка;
	Кнопка.Пометка = ВключитьФильтр;
	ПоказыватьМиниатюры = ВключитьФильтр;
	
	ИзменитьТекстПодсказки(ВключитьФильтр, "ОтображениеМиниатюр");
	
	ДанныеДеревьев.ТекущаяСтрока = Неопределено;
	ПриАктивизацииСтрокиДеревоСписок(Элементы.Список.ТекущиеДанные, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеСозданныхДокументов(Команда)
	
	Кнопка = Элементы.ОтображениеСозданныхДокументов;
	ВключитьФильтр = НЕ Кнопка.Пометка;
	Кнопка.Пометка = ВключитьФильтр;
	
	ИзменитьТекстПодсказки(ВключитьФильтр, "ОтображениеСозданныхДокументов");
	
	ПоказыватьСозданныеДокументы1С = Кнопка.Пометка;
	
	ПараметрыОтбораСозданныхДокументов1С = ПолучитьПараметрыОтбораСозданныхДокументов1С();
	ОбновитьДеревоДокументовИФайловВсеНаСервере(ПараметрыОтбораСозданныхДокументов1С);
	РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	Элементы.ГруппаДеревоПраво.Видимость = ВключитьФильтр;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаданияИЗагрузить(Команда)
	
	ЗагрузкаРаспознанныхДокументовНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоЗаданиеРаспознавания Тогда
		Если ТекущиеДанные.ПометкаУдаления Тогда
			ПоказатьПредупреждение(,
				НСтр("ru = 'Нельзя сохранить файл отмененного задания распознавания.'"));
		Иначе
			ПоказатьПредупреждение(,
				НСтр("ru = 'Нельзя сохранить в процессе обработки.
				           |Дождитесь окончания распознавания и повторите.'"));
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебныйКлиент.СохранитьФайлыДокумента(ТекущиеДанные.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайловВОбработке(Команда)
	
	Фильтр = ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.ПустаяСсылка");
	Кнопка = Элементы.СписокФильтрСтатусВОбработке;
	ОбработатьНажатиеКнопкиСтатистики(Фильтр, Кнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайловНовых(Команда)
	
	Фильтр = ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый");
	Кнопка = Элементы.СписокФильтрСтатусНовые;
	ОбработатьНажатиеКнопкиСтатистики(Фильтр, Кнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайловСОшибкой(Команда)
	
	Фильтр = ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Ошибка");
	Кнопка = Элементы.СписокФильтрСтатусСОшибкой;
	ОбработатьНажатиеКнопкиСтатистики(Фильтр, Кнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрСтатусОбработан(Команда)
	
	Фильтр = ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан");
	Кнопка = Элементы.СписокФильтрСтатусОбработан;
	ОбработатьНажатиеКнопкиСтатистики(Фильтр, Кнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрСтатусУдаленные(Команда)
	
	Фильтр = ПредопределенноеЗначение("Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Удален");
	Кнопка = Элементы.СписокФильтрСтатусУдаленные;
	ОбработатьНажатиеКнопкиСтатистики(Фильтр, Кнопка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИдентификаторДокумента(Команда)
	
	Если ТипЗнч(Элементы.Список.ТекущиеДанные) = Тип("ДанныеФормыСтруктура") Тогда
		ПараметрыСообщения = Новый Структура;
		ПараметрыСообщения.Вставить("Заголовок", НСтр("ru = 'Идентификатор документа'"));
		ПараметрыСообщения.Вставить("Сообщение", Элементы.Список.ТекущиеДанные.Идентификатор);
		ПараметрыСообщения.Вставить("МногострочныйРежим", Ложь);
		
		ОткрытьФорму("ОбщаяФорма.ФормаСообщениеБРД", ПараметрыСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепитьИзображениеКНайденомуДокументу(Команда)
	
	Для Каждого СтрокаСписка Из Элементы.Список.ВыделенныеСтроки Цикл
		ТекущиеДанные = Элементы.Список.ДанныеСтроки(СтрокаСписка);
		Если ТипЗнч(ТекущиеДанные) = Тип("ДанныеФормыСтруктура") Тогда
			
			Если Не ТекущиеДанные.ЭтоЗаданиеРаспознавания Тогда 
				ПрикрепитьИзображение(ТекущиеДанные.Ссылка);
				ОповеститьОбИзменении(ТекущиеДанные.Ссылка);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Список.Обновить();
	ОбновитьКоличествоФайловПоСтатусам();
	
КонецПроцедуры

&НаСервере
Процедура ПрикрепитьИзображение(Ссылка)
	
	ДокументОбъект = Ссылка.ПолучитьОбъект();
	АдресКартинки = ПоместитьВоВременноеХранилище(ДокументОбъект.ИсходноеИзображение.Получить());
	
	УстановитьПривилегированныйРежим(Истина);
	
	КандидатыВСвязанныеДокументы = РаспознаваниеДокументовСлужебный.НайтиПотенциальныеСвязаныеДокументы(ДокументОбъект);
	
	Для Каждого Кандидат Из КандидатыВСвязанныеДокументы Цикл
		
		РаспознаваниеДокументовСлужебный.ДобавитьПрисоединенныйФайл(ДокументОбъект, Кандидат.Ссылка, АдресКартинки);
		РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ЗаписатьЗначения(Кандидат.Ссылка, ДокументОбъект.Ссылка, Ложь);
		
		РезультатОбратнойСвязи = РаспознаваниеДокументов.ОписаниеОбратнойСвязи("ПрикрепилСкан");
		Пакет = Новый Структура("created", РезультатОбратнойСвязи);
		РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(ДокументОбъект.ИдентификаторРезультата, Пакет);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	УдалитьИзВременногоХранилища(АдресКартинки);
	
	Если КандидатыВСвязанныеДокументы.Количество() > 0 Тогда 
		
		ТекущиеНастройки = РегистрыСведений.ОбщиеНастройкиРаспознаваниеДокументов.ТекущиеНастройки();
		
		Если ТекущиеНастройки.ПомечатьДокументОбработаннымПриПрикреаленииИзображения Тогда
			ДокументОбъект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан;
			ДокументОбъект.Записать();
			
			РезультатОбратнойСвязи = РаспознаваниеДокументов.ОписаниеОбратнойСвязи("Проведен");
			Пакет = Новый Структура("created", РезультатОбратнойСвязи);
			РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(ДокументОбъект.ИдентификаторРезультата, Пакет);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьОбработано(Команда)
	
	Для Каждого СтрокаСписка Из Элементы.Список.ВыделенныеСтроки Цикл
		ТекущиеДанные = Элементы.Список.ДанныеСтроки(СтрокаСписка);
		Если ТипЗнч(ТекущиеДанные) = Тип("ДанныеФормыСтруктура") Тогда
			ОтметитьОбработаноНаСервере(ТекущиеДанные.Ссылка);
			ОповеститьОбИзменении(ТекущиеДанные.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Список.Обновить();
	ОбновитьКоличествоФайловПоСтатусам();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	
	ПометитьНаУдалениеНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеОбработчикиСобытий

&НаКлиенте
Процедура ПослеПроверкиПодключенияКСервисуРаспознавания(Результат, Контекст) Экспорт
	
	ОбновитьПредставлениеБаланса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииСтрокиДеревоСписок(ТекДанные, ПереключитьМиниатюры = Ложь)
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекДанные.Ссылка) Тогда
		ИдСтроки = ТекДанные.Ссылка;
		ДокументСсылка = ТекДанные.Ссылка;
	Иначе
		ИдСтроки = Неопределено;
		ДокументСсылка = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДеревьев) Тогда
		ДанныеДеревьев = Новый Структура("ТекущаяСтрока, ИдентификаторДереваДокументов, СсылкаДереваДокументов");
	ИначеЕсли ДанныеДеревьев.ТекущаяСтрока = ИдСтроки Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДеревьев.ТекущаяСтрока = ИдСтроки;
	ДанныеСтроки = Неопределено;
	
	Если ПоказыватьМиниатюры Или ПереключитьМиниатюры Тогда
		ДанныеДеревьев.Вставить("ПараметрыПриАктивизации", Новый Структура("ДанныеСтроки, ДокументСсылка, ПереключитьМиниатюры", ДанныеСтроки, ДокументСсылка, ПереключитьМиниатюры));
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииСтроки", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриАктивизацииСтрокиНаСервере(ДанныеСтроки, ДокументСсылка, ПереключитьМиниатюры)
	
	Если ДокументСсылка = Неопределено Тогда
		Если ПоказыватьМиниатюры Тогда
			Элементы.ГруппаМиниатюры.Видимость = Ложь;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ПереключитьМиниатюры Тогда
		ПоказыватьМиниатюрыНаСервере();
	КонецЕсли;
	
	Если ДанныеСтроки = Неопределено Тогда
		ДанныеСтроки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ИдентификаторЗадания,ТипДокумента");
	КонецЕсли;
	
	Если ПоказыватьМиниатюры Тогда
		ОбновитьКартинкиВПодвалеНаСервере(ДанныеСтроки.ИдентификаторЗадания, ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииСтроки()
	
	ПриАктивизацииСтрокиНаСервере(ДанныеДеревьев.ПараметрыПриАктивизации.ДанныеСтроки,
		ДанныеДеревьев.ПараметрыПриАктивизации.ДокументСсылка,
		ДанныеДеревьев.ПараметрыПриАктивизации.ПереключитьМиниатюры);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АдресИзображенияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РеквизитМиниатюры = РеквизитыМиниатюр[Элемент.Имя];
	Если Не РаспознаваниеДокументовСлужебныйКлиент.ДоступенНеоплаченныйРаспознанныйДокумент(РеквизитМиниатюры.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = РеквизитМиниатюры.ИмяФайла + ".jpeg";
	ФайловаяСистемаКлиент.ОткрытьФайл(РеквизитМиниатюры.Местоположение,, ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеДобавленияФайлов(Результат, ДополнительныеПараметры) Экспорт
	
	РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	Элементы.Список.Обновить();
	ОбновитьКоличествоФайловПоСтатусам();
	ЗаполнитьПериодыДляОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПоказаПредупреждания(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьПредставлениеБаланса();
	
КонецПроцедуры

#КонецОбласти

#Область Представления

&НаСервере
Процедура ОбновитьКартинкиВПодвалеНаСервере(ИдентификаторЗадания, ДокументСсылка)
	
	Элементы.ГруппаМиниатюры.Видимость = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЗадания", ИдентификаторЗадания);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокумент.Ссылка КАК Ссылка,
	|	РаспознанныйДокумент.Миниатюра КАК Миниатюра,
	|	РаспознанныйДокумент.ИсходноеИзображение КАК ИсходноеИзображение,
	|	РаспознанныйДокумент.ИмяФайла КАК ИмяФайла
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|ГДЕ
	|	РаспознанныйДокумент.ИдентификаторЗадания = &ИдентификаторЗадания
	|	И РаспознанныйДокумент.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	РаспознанныйДокумент.Дата
	|";
	
	Если ДокументСсылка = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РаспознанныйДокумент.Ссылка = &Ссылка", "");
	Иначе
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	КоличествоИзображений = Выборка.Количество();
	
	мДобавляемыхРеквизитов = Новый Массив;
	ИдИзображения = ДобавленоРеквизитов;
	Пока ИдИзображения < КоличествоИзображений Цикл
		ИмяРеквизита = "АдресИзображения" + ИдИзображения;
		НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("Строка"));
		мДобавляемыхРеквизитов.Добавить(НовыйРеквизит);
		ИдИзображения = ИдИзображения + 1;
	КонецЦикла;
	ИзменитьРеквизиты(мДобавляемыхРеквизитов);
	
	ШиринаФормы = 300;
	ШиринаПоляКартинки = 20;
	КоличествоПолейВГруппе = Цел(ШиринаФормы/(ШиринаПоляКартинки*10 + 10));
	Если КоличествоПолейВГруппе = 0 Тогда 
		КоличествоПолейВГруппе = 1;
	КонецЕсли;
	Пока ДобавленоРеквизитов < КоличествоИзображений Цикл
		НомерГруппыДобавления = Цел((ДобавленоРеквизитов)/КоличествоПолейВГруппе);
		
		ИмяГруппыСтроки = "ГруппаМиниатюры" + НомерГруппыДобавления;
		Если Элементы.Найти(ИмяГруппыСтроки) = Неопределено Тогда
			НовыйЭлемент = Элементы.Вставить(ИмяГруппыСтроки, Тип("ГруппаФормы"), Элементы.ГруппаМиниатюры);
			НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
			НовыйЭлемент.ОтображатьЗаголовок = Ложь;
			НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			НовыйЭлемент.Высота = 10;
			НовыйЭлемент.РастягиватьПоВертикали = Ложь;
		КонецЕсли;
		
		ИмяГруппы = "ГруппаАдресИзображения"+ДобавленоРеквизитов;
		НовыйЭлемент = Элементы.Вставить(ИмяГруппы, Тип("ГруппаФормы"), Элементы[ИмяГруппыСтроки]);
		НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
		НовыйЭлемент.ОтображатьЗаголовок = Ложь;
		НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		
		ИмяРеквизита = "АдресИзображения"+ДобавленоРеквизитов;
		НовыйЭлемент = Элементы.Вставить(ИмяРеквизита, Тип("ПолеФормы"), Элементы[ИмяГруппы]);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеКартинки;
		НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовыйЭлемент.РазмерКартинки = РазмерКартинки.Пропорционально;
		НовыйЭлемент.Ширина = ШиринаПоляКартинки;
		НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_АдресИзображенияНажатие");
		
		НовыйЭлемент = Элементы.Вставить("ДекорацияИмяФайла"+ДобавленоРеквизитов, Тип("ДекорацияФормы"), Элементы[ИмяГруппы]);
		НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
		НовыйЭлемент.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
		НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
		НовыйЭлемент.МаксимальнаяШирина = ШиринаПоляКартинки;
		НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
		
		ДобавленоРеквизитов = ДобавленоРеквизитов + 1;
	КонецЦикла;
	
	ИдИзображения = 0;
	РеквизитыМиниатюр = Новый Структура;
	мДобавляемыхРеквизитов = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		Если РаспознаваниеДокументовСлужебныйВызовСервера.ДоступенНеоплаченныйРаспознанныйДокумент(Выборка.Ссылка) Тогда
			Миниатюра = Выборка.Миниатюра.Получить();
		Иначе
			Миниатюра = БиблиотекаКартинок.ОтсутствуетИзображениеРаспознанногоДокумента.ПолучитьДвоичныеДанные();
		КонецЕсли;
		
		Если Миниатюра = Неопределено Тогда
			Миниатюра = Выборка.ИсходноеИзображение.Получить();
			Если Миниатюра = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ИмяРеквизита = "АдресИзображения" + ИдИзображения;
		
		ЭтотОбъект[ИмяРеквизита] = ПоместитьВоВременноеХранилище(Миниатюра, УникальныйИдентификатор);
		Элементы["Группа" + ИмяРеквизита].Видимость = Истина;
		Элементы["ДекорацияИмяФайла" + ИдИзображения].Заголовок = Выборка.ИмяФайла;
		
		ИдИзображения = ИдИзображения + 1;
		
		ИсходноеИзображение = Выборка.ИсходноеИзображение.Получить();
		Местоположение = ПоместитьВоВременноеХранилище(ИсходноеИзображение, УникальныйИдентификатор);
		
		РеквизитМиниатюры = Новый Структура;
		РеквизитМиниатюры.Вставить("Ссылка", Выборка.Ссылка);
		РеквизитМиниатюры.Вставить("ИмяФайла", Выборка.ИмяФайла);
		РеквизитМиниатюры.Вставить("Местоположение", Местоположение);
		
		РеквизитыМиниатюр.Вставить(ИмяРеквизита, РеквизитМиниатюры);
	КонецЦикла;
	
	Пока ИдИзображения < ДобавленоРеквизитов Цикл
		ИмяРеквизита = "ГруппаАдресИзображения" + ИдИзображения;
		Элементы[ИмяРеквизита].Видимость = Ложь;
		ИдИзображения = ИдИзображения + 1;
	КонецЦикла;
	
	Элементы.ДекорацияНетИзображений.Видимость = (КоличествоИзображений = 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеКнопкиСтатистики(Фильтр, Кнопка)
	
	ВключитьФильтр = НЕ Кнопка.Пометка;
	Кнопка.Пометка = ВключитьФильтр;
	ИзменитьОтборПоСтатусу(Фильтр, ВключитьФильтр);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОтборПоСтатусу(Фильтр, ВключитьФильтр)
	
	ЭлементОтбора = Список.Отбор.ПолучитьОбъектПоИдентификатору(ИдентификаторФильтраПоСтатусам);
	
	СписокОтбора = ЭлементОтбора.ПравоеЗначение;
	Если ВключитьФильтр Тогда
		СписокОтбора.Добавить(Фильтр);
	Иначе
		СписокОтбора.Удалить(СписокОтбора.НайтиПоЗначению(Фильтр));
	КонецЕсли;
	
	ЭлементОтбора.Использование = СписокОтбора.Количество();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТекстПодсказки(ВключитьФильтр, ИмяКоманды)
	
	Команда = Команды.Найти(ИмяКоманды);
	ТекстПодсказки = Команда.Подсказка;
	
	Если ВключитьФильтр Тогда
		ТекстПодсказки = СтрЗаменить(ТекстПодсказки, НСтр("ru = 'Показать'"), НСтр("ru = 'Скрыть'"));
	Иначе
		ТекстПодсказки = СтрЗаменить(ТекстПодсказки, НСтр("ru = 'Скрыть'"), НСтр("ru = 'Показать'"));
	КонецЕсли;
	
	Команда.Подсказка = ТекстПодсказки;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоФайловПоСтатусам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(1) КАК ФайловКОбработке
	|ИЗ
	|	РегистрСведений.ИсходныеДанныеЗаданийРаспознаваниеДокументов КАК ИсходныеДанныеЗаданийРаспознаваниеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыОбработкиЗаданийРаспознаваниеДокументов КАК РезультатыОбработкиЗаданийРаспознаваниеДокументов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|			ПО РезультатыОбработкиЗаданийРаспознаваниеДокументов.ИдентификаторРезультата = РаспознанныйДокумент.ИдентификаторРезультата
	|		ПО ИсходныеДанныеЗаданийРаспознаваниеДокументов.ИдентификаторФайла = РезультатыОбработкиЗаданийРаспознаваниеДокументов.ИдентификаторФайла
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаданийРаспознаваниеДокументов КАК СостоянияЗаданийРаспознаваниеДокументов
	|		ПО ИсходныеДанныеЗаданийРаспознаваниеДокументов.ИдентификаторЗадания = СостоянияЗаданийРаспознаваниеДокументов.ИдентификаторЗадания
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка ЕСТЬ NULL
	|	И СостоянияЗаданийРаспознаваниеДокументов.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыНаСервисеРаспознаваниеДокументов.Отменено)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ВЫБОР
	|			КОГДА РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Ошибка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФайловСОшибкой,
	|	СУММА(ВЫБОР
	|			КОГДА РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
	|					ИЛИ РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Изменен)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФайловНовых,
	|	СУММА(ВЫБОР
	|			КОГДА РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ДокументовОбработано
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|ГДЕ
	|	НЕ РаспознанныйДокумент.ПометкаУдаления";
	
	ФайловКОбработке = 0;
	ФайловСОшибкой = 0;
	ФайловНовых = 0;
	ДокументовОбработано = 0;
	
	Результаты = Запрос.ВыполнитьПакет();
	Выборка = Результаты[0].Выбрать();
	Если Выборка.Следующий() Тогда
		ФайловКОбработке = Выборка.ФайловКОбработке;
	КонецЕсли;
	
	Выборка = Результаты[1].Выбрать();
	Если Выборка.Следующий() Тогда
		ФайловСОшибкой = Выборка.ФайловСОшибкой;
		ФайловНовых = Выборка.ФайловНовых;
		ДокументовОбработано = Выборка.ДокументовОбработано;
	КонецЕсли;
	
	Элементы.СписокФильтрСтатусВОбработке.Заголовок = СтрШаблон(НСтр("ru = 'В обработке: %1'"), ФайловКОбработке);
	Элементы.СписокФильтрСтатусНовые.Заголовок = СтрШаблон(НСтр("ru = 'Распознанные: %1'"), ФайловНовых);
	Элементы.СписокФильтрСтатусСОшибкой.Заголовок = СтрШаблон(НСтр("ru = 'С ошибкой: %1'"), ФайловСОшибкой);
	Элементы.СписокФильтрСтатусОбработан.Заголовок = СтрШаблон(НСтр("ru = 'Обработанные: %1'"), ДокументовОбработано);
	
КонецПроцедуры

&НаСервере
Процедура ПоказыватьМиниатюрыНаСервере()
	
	Элементы.ГруппаМиниатюры.Видимость = ПоказыватьМиниатюры;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавления()
	
	Обработчик = Новый ОписаниеОповещения("ПослеДобавленияФайлов", ЭтотОбъект);
	РаспознаваниеДокументовКлиент.ПоказатьДобавлениеФайлов(УникальныйИдентификатор, Обработчик);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеБаланса()
	
	ПараметрыАвторизацииПереопределяемые = Новый Структура("Логин, Пароль");
	РаспознаваниеДокументовПереопределяемый.ПриВыполненииАвторизации(ПараметрыАвторизацииПереопределяемые);
	АвторизацияПереопределена = ЗначениеЗаполнено(ПараметрыАвторизацииПереопределяемые.Логин);
	
	Если АвторизацияПереопределена Тогда
		Элементы.ГруппаБиллинг.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ЭтоУчастникПилотнойПрограммы = РаспознаваниеДокументовКоннекторСлужебный.ЭтоУчастникПилотнойПрограммы();
	УчастникПилотнойПрограммы = ЭтоУчастникПилотнойПрограммы.УчастникПилотнойПрограммы;
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизации = РаспознаваниеДокументов.ТекущиеПараметрыАвторизации();
	АвторизацияВыполнена = (ПараметрыАвторизации.ТипАутентификации <> "НеВыполнена");
	АккаунтАктивирован = (ПараметрыАвторизации.ТипАутентификации = "ПоЛогинуПаролю" Или ПараметрыАвторизации.Состояние = "Активирован");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не АккаунтАктивирован Тогда
		АккаунтАктивирован = РаспознаваниеДокументов.АккаунтАктивирован();
	КонецЕсли;
	
	Если Не АвторизацияВыполнена Тогда
		
		БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='<a href = ""Перейти"">Начать использовать</a> сервис распознавания документов.'")
		);
		Элементы.БалансПредствление.Высота = 2;
		Элементы.Биллинг.Картинка = БиблиотекаКартинок.Информация;
		
	ИначеЕсли Не АккаунтАктивирован Тогда
		
		БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='Заявка на подключение сервиса распознавания документов отправлена и будет обработана в ближайшие дни.'")
		);
		Элементы.БалансПредствление.Высота = 3;
		Элементы.Биллинг.Картинка = БиблиотекаКартинок.ВыполненоНужноПодождать;
		
	ИначеЕсли УчастникПилотнойПрограммы Тогда
		
		ДатаОкончания = ЭтоУчастникПилотнойПрограммы.ДатаОкончания;
		ДнейДоОтключения = Цел((ДатаОкончания - НачалоДня(ТекущаяДатаСеанса())) / 86400);
		
		Если Не ЗначениеЗаполнено(ДатаОкончания) Тогда
			
			БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru='Предоставляется в пилотном режиме. <a href = ""Подробнее"">Подробнее...</a>'")
			);
			Элементы.БалансПредствление.Высота = 1;
			Элементы.Биллинг.Картинка = БиблиотекаКартинок.Информация;
			
		ИначеЕсли ДнейДоОтключения > 0 Тогда
			
			БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru='Предоставляется в пилотном режиме до <b>%1</b> <a href = ""Подробнее"">Подробнее...</a>'"), 
				Формат(ДатаОкончания, "ДЛФ=DD")
			);
			Элементы.БалансПредствление.Высота = 1;
			Элементы.Биллинг.Картинка = БиблиотекаКартинок.Информация;
			
		Иначе
			
			ПоказатьТребованиеВключитьИТС = Истина;
			
			БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru='Предоставляется в пилотном режиме до <b><span style=""color: ЦветШрифтаОшибки"">%1</span></b> <a href = ""Подробнее"">Подробнее...</a>
				         |<a href = ""Перейти"">Перейти на авторизацию с помощью интернет-поддержки</a>.'"), 
				Формат(ДатаОкончания, "ДЛФ=DD")
			);
			Элементы.БалансПредствление.Высота = 2;
			Элементы.Биллинг.Картинка = БиблиотекаКартинок.Предупреждение32;
			
		КонецЕсли;
	Иначе
		
		ТекущийБаланс = РаспознаваниеДокументовКоннекторСлужебный.ТекущийБаланс();
		
		Баланс = ТекущийБаланс.Баланс;
		БалансНадпись = СтрокаСЧислом(
			НСтр("ru=';
			         |%1 страница;
			         |;
			         |%1 страницы;
			         |%1 страниц;
			         |%1 страницы'"),
			?(Баланс > 0, Баланс, -Баланс),
			ВидЧисловогоЗначения.Количественное,
			"L=ru"
		);
		
		Если Баланс >= 0 Тогда
			БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru='Доступно по тарифу: <b>%1</b>.'"), 
				БалансНадпись
			);
			Элементы.БалансПредствление.Высота = 1;
			Элементы.Биллинг.Картинка = БиблиотекаКартинок.Информация;
		Иначе
			ДатаОтключения = ТекущийБаланс.ДатаОтключения;
			ДнейДоОтключения = Цел((ДатаОтключения - ТекущаяДатаСеанса()) / 86400);
			
			Если ДнейДоОтключения > 0 Тогда
				
				Лимит = ТекущийБаланс.Лимит;
				ЛимитПредставление = СтрокаСЧислом(
					НСтр("ru=';
					         |%1 страница;
					         |;
					         |%1 страницы;
					         |%1 страниц;
					         |%1 страницы'"),
					?(Лимит > 0, Лимит, -Лимит) + ?(Баланс > 0, Баланс, -Баланс),
					ВидЧисловогоЗначения.Количественное,
					"L=ru"
				);
				
				БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru='<b><span style=""color: ЦветШрифтаОшибки"">Оплаченные страницы закончились.</span></b>
					         |Вам предоставлен кредит: <b>%1</b> до <b>%2</b>
					         |Из них использовано: <b>%3</b>.
					         |Пожалуйста, пополните баланс.'"),
					ЛимитПредставление,
					Формат(ДатаОтключения, "ДЛФ=DD"),
					БалансНадпись
				);
				Элементы.БалансПредствление.Высота = 5;
			Иначе
				БалансПредствление = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru='Баланс: <b><span style=""color: ЦветШрифтаОшибки"">-%1</span></b>.
					         |Сервис отключен.'"), 
					БалансНадпись
				);
				Элементы.БалансПредствление.Высота = 2;
			КонецЕсли;
			
			Элементы.Биллинг.Картинка = БиблиотекаКартинок.Предупреждение32;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БизнесЛогика

&НаСервереБезКонтекста
Функция ПолучитьОбщиеНастройки()
	Возврат РегистрыСведений.ОбщиеНастройкиРаспознаваниеДокументов.ТекущиеНастройки();
КонецФункции

&НаСервереБезКонтекста
Процедура ОтметитьОбработаноНаСервере(ДокументСсылка)
	
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.РаспознанныйДокумент");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументСсылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		
		Если ДокументОбъект.ПометкаУдаления Тогда 
			ВызватьИсключение НСтр("ru = 'Невозможно отправить в обработанные помеченный на удаление документ.'");
		КонецЕсли;
		
		ДокументОбъект.Заблокировать();
		ДокументОбъект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан;
		ДокументОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	// Замер производительности
	РезультатОбратнойСвязи = РаспознаваниеДокументов.ОписаниеОбратнойСвязи("Проигнорировано");
	Пакет = Новый Структура("created", РезультатОбратнойСвязи);
	РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(ДокументСсылка.ИдентификаторРезультата, Пакет);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	// FIXME: Только для расширений до тех пор, пока не исправят ошибку работы кнопки "Установить стандартные настройки"
	Если ОбщегоНазначения.ЭтоВебКлиент() И РаспознаваниеДокументовСлужебный.ЭтоРасширениеКонфигурации() Тогда
		ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
		Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
			ПолноеИмяФормы = Метаданные.Документы.РаспознанныйДокумент.Формы.ФормаСписка.ПолноеИмя();
			ХранилищеСистемныхНастроек.Удалить(ПолноеИмяФормы, "", ИмяПользователя);
		КонецЕсли;
		КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОбработчикиОжидания(ОбновитьОбщиеНастройки)
	
	Если ОбновитьОбщиеНастройки Тогда
		ОбщиеНастройки = ПолучитьОбщиеНастройки();
		ОтключитьОбработчикОжидания("ЗагрузкаРаспознанныхДокументов");
		ОтключитьОбработчикОжидания("ОтправкаВложенийИзПочты");
		ОтключитьОбработчикОжидания("ОтправкаФайловИзКаталога");
	КонецЕсли;
	
	Если ОбщиеНастройки.ЧастотаЗагрузкиДокументовИзСервиса <> 0 Тогда
		ПодключитьОбработчикОжидания("ЗагрузкаРаспознанныхДокументов", ОбщиеНастройки.ЧастотаЗагрузкиДокументовИзСервиса);
	КонецЕсли;
	
	Если ОбщиеНастройки.РаспознаватьВложенияИзПочты И ОбщиеНастройки.ЧастотаОтправкиВложенийИзПочтыНаРаспознавание <> 0 Тогда
		ПодключитьОбработчикОжидания("ОтправкаВложенийИзПочты", ОбщиеНастройки.ЧастотаОтправкиВложенийИзПочтыНаРаспознавание);
	КонецЕсли;
	
	Если ОбщиеНастройки.РаспознаватьФайлыИзКаталога И ОбщиеНастройки.ЧастотаОтправкиФайловИзКаталогаНаРаспознавание <> 0 Тогда
		ПодключитьОбработчикОжидания("ОтправкаФайловИзКаталога", ОбщиеНастройки.ЧастотаОтправкиФайловИзКаталогаНаРаспознавание);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаРаспознанныхДокументов()
	
	ЗагрузкаРаспознанныхДокументовНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаРаспознанныхДокументовНаСервере()
	
	РаспознаваниеДокументовСлужебный.ЗагрузкаРаспознанныхДокументов(АдресРезультатаРаспознавания);
	ОбновитьКоличествоФайловПоСтатусам();
	
	НовоеКоличествоРаспознанных = НовоеКоличествоРаспознанных();
	Если КоличествоРаспознанных < НовоеКоличествоРаспознанных Тогда
		КоличествоРаспознанных = НовоеКоличествоРаспознанных;
		ОбновитьПредставлениеБаланса();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовоеКоличествоРаспознанных()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РаспознанныйДокумент.Ссылка) КАК Количество
		|ИЗ
		|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Количество;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

&НаКлиенте
Процедура ОтправкаВложенийИзПочты()
	
	Если Не ЗначениеЗаполнено(ОбщиеНастройки.ЭлектроннаяПочтаДляОбработкиВложений) Тогда
		Возврат;
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебныйВызовСервера.ОтправкаВложенийИзПочты(ОбщиеНастройки.ЭлектроннаяПочтаДляОбработкиВложений);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаФайловИзКаталога()
	
	Если ПустаяСтрока(ОбщиеНастройки.КаталогДляОбработкиФайлов) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРаспознаванияИзКаталога = Новый Структура;
	ПараметрыРаспознаванияИзКаталога.Вставить("Каталог", ОбщиеНастройки.КаталогДляОбработкиФайлов);
	ПараметрыРаспознаванияИзКаталога.Вставить("ИскатьВПодкаталогах", ОбщиеНастройки.ВключаяВложенныеКаталоги);
	
	РаспознаваниеДокументовСлужебныйВызовСервера.ОтправкаФайловИзКаталога(ПараметрыРаспознаванияИзКаталога);
	
КонецПроцедуры

#Область Отборы

&НаКлиенте
Функция ПолучитьПараметрыОтбораСозданныхДокументов1С()
	
	Если НЕ ПоказыватьСозданныеДокументы1С Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВариантОтбора = 0 Тогда
		ДанныеСтроки = Элементы.ОтборПериодДерево.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Результат = Неопределено;
		Иначе
			Результат = Новый Структура("НачалоПериода, ПериодДень", ДанныеСтроки.День, (ДанныеСтроки.Вложенность = 1));
		КонецЕсли;
	ИначеЕсли ВариантОтбора = 1 Тогда
		ДанныеСтроки = Элементы.ОтборКонтрагент.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Возврат Неопределено;
		Иначе
			Результат = ДанныеСтроки.Контрагент;
		КонецЕсли;
	ИначеЕсли ВариантОтбора = 2 Тогда
		ДанныеСтроки = Элементы.ОтборОрганизация.ТекущиеДанные;
		Если ДанныеСтроки = Неопределено Тогда
			Возврат Неопределено;
		Иначе
			Результат = ДанныеСтроки.Организация;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ВариантОтбораПриИзмененииНаСервере()
	
	Элементы.ГруппаОтборПериод.Видимость = (ВариантОтбора = 0);
	Элементы.ГруппаОтборКонтрагент.Видимость = (ВариантОтбора = 1);
	Элементы.ГруппаОтборОрганизация.Видимость = (ВариантОтбора = 2);
	
	ЭлементыОтбора = Список.Отбор.Элементы;
	ЭлементыОтбора[0].Использование = (ВариантОтбора = 0);
	ЭлементыОтбора[1].Использование = (ВариантОтбора = 0);
	ЭлементыОтбора[2].Использование = (ВариантОтбора = 1);
	ЭлементыОтбора[3].Использование = (ВариантОтбора = 2);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПериодыДляОтборов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(РаспознанныйДокумент.Дата, ИсходныеДанныеЗаданийРаспознаваниеДокументов.ДатаЗагрузки), ДЕНЬ) КАК День,
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(РаспознанныйДокумент.Дата, ИсходныеДанныеЗаданийРаспознаваниеДокументов.ДатаЗагрузки), МЕСЯЦ) КАК Месяц,
	|	1 КАК Вложенность
	|ИЗ
	|	РегистрСведений.ИсходныеДанныеЗаданийРаспознаваниеДокументов КАК ИсходныеДанныеЗаданийРаспознаваниеДокументов
	|		ПОЛНОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|		ПО (НАЧАЛОПЕРИОДА(ИсходныеДанныеЗаданийРаспознаваниеДокументов.ДатаЗагрузки, ДЕНЬ) = НАЧАЛОПЕРИОДА(РаспознанныйДокумент.Дата, ДЕНЬ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	День УБЫВ
	|ИТОГИ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(День, МЕСЯЦ)) КАК День
	|ПО
	|	Месяц";
	
	ДеревоПериодов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗначениеВРеквизитФормы(ДеревоПериодов, "ОтборПериодДерево");
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтборПоПериоду(НачалоПериода, ПериодДень)
	
	Если ПериодДень Тогда
		КонецПериода = КонецДня(НачалоПериода);
	Иначе
		КонецПериода = КонецМесяца(НачалоПериода);
	КонецЕсли;
	
	ЭлементыОтбора = Список.Отбор.Элементы;
	ЭлементыОтбора[0].ПравоеЗначение = НачалоПериода;
	ЭлементыОтбора[1].ПравоеЗначение = КонецПериода;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДеревом

#Область ДеревоДокументыИФайлы

&НаСервере
Процедура ОбновитьДеревоДокументовИФайловВсеНаСервере(ПараметрыОтбора)
	
	Если НЕ ПоказыватьСозданныеДокументы1С ИЛИ ПараметрыОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СвязанныеОбъектыРаспознаниеДокументов.СсылкаНаОбъект КАК Ссылка,
	|	СвязанныеОбъектыРаспознаниеДокументов.ПотенциальныйКандидат КАК ПотенциальныйКандидат,
	|	СвязанныеОбъектыРаспознаниеДокументов.СсылкаНаОбъект.Представление КАК Наименование
	|ИЗ
	|	РегистрСведений.СвязанныеОбъектыРаспознаниеДокументов КАК СвязанныеОбъектыРаспознаниеДокументов
	|ГДЕ
	|	СвязанныеОбъектыРаспознаниеДокументов.РаспознанныйДокумент.Дата >= &НачалоПериода
	|	И СвязанныеОбъектыРаспознаниеДокументов.РаспознанныйДокумент.Дата <= &КонецПериода
	|	И СвязанныеОбъектыРаспознаниеДокументов.РаспознанныйДокумент.Контрагент = &Контрагент
	|	И СвязанныеОбъектыРаспознаниеДокументов.РаспознанныйДокумент.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвязанныеОбъектыРаспознаниеДокументов.РаспознанныйДокумент.Дата,
	|	СвязанныеОбъектыРаспознаниеДокументов.СсылкаНаОбъект.Представление";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ВсеОтборы = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор;
	
	Запрос = Новый Запрос;
	Если ВариантОтбора = 0 Тогда
		ВсеОтборы.Удалить(3);
		ВсеОтборы.Удалить(2);
		
		НачалоПериода = ПараметрыОтбора.НачалоПериода;
		Если ПараметрыОтбора.ПериодДень Тогда
			КонецПериода = КонецДня(НачалоПериода);
		Иначе
			КонецПериода = КонецМесяца(НачалоПериода);
		КонецЕсли;
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	ИначеЕсли ВариантОтбора = 1 Тогда
		ВсеОтборы.Удалить(3);
		ВсеОтборы.Удалить(1);
		ВсеОтборы.Удалить(0);
		
		Запрос.УстановитьПараметр("Контрагент", ПараметрыОтбора);
	ИначеЕсли ВариантОтбора = 2 Тогда
		ВсеОтборы.Удалить(2);
		ВсеОтборы.Удалить(1);
		ВсеОтборы.Удалить(0);
		
		Запрос.УстановитьПараметр("Организация", ПараметрыОтбора);
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
	Иначе
		МодульРаботаСФайлами = Неопределено;
	КонецЕсли;
	
	ТекДерево = РеквизитФормыВЗначение("ДеревоДокументыИФайлы");
	ТекДерево.Строки.Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрДерева = ТекДерево.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрДерева, Выборка);
		Если МодульРаботаСФайлами <> Неопределено Тогда
			ВложенныеФайлы = Новый Массив;
			МодульРаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Выборка.Ссылка, ВложенныеФайлы);
			Для Каждого ВложенныйФайл Из ВложенныеФайлы Цикл
				РеквизитыФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВложенныйФайл, "Наименование,Расширение");
				СтрДерева2 = СтрДерева.Строки.Добавить();
				СтрДерева2.Ссылка = ВложенныйФайл;
				СтрДерева2.Наименование = РеквизитыФайла.Наименование+"."+РеквизитыФайла.Расширение;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТекДерево, "ДеревоДокументыИФайлы");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДокументыИФайлыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка <> Неопределено И ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И
		ПараметрыПеретаскивания.Значение.Количество() > 0 Тогда
		
		СтрокаДерева = ДеревоДокументыИФайлы.НайтиПоИдентификатору(Строка);
		Если СтрокаДерева.ПолучитьРодителя() = Неопределено Тогда
			МассивСсылок = Новый Массив;
			Для Каждого ТекущееЗначение Из ПараметрыПеретаскивания.Значение Цикл
				Если ТипЗнч(ТекущееЗначение) = Тип("ДокументСсылка.РаспознанныйДокумент") Тогда
					МассивСсылок.Добавить(ТекущееЗначение);
				ИначеЕсли ТипЗнч(ТекущееЗначение) = Тип("Число") Тогда
					ТекущиеДанныеСтроки = Элементы.Список.ДанныеСтроки(ТекущееЗначение);
					Если ЗначениеЗаполнено(ТекущиеДанныеСтроки.Ссылка) Тогда
						МассивСсылок.Добавить(ТекущиеДанныеСтроки.Ссылка);
					Иначе
						Возврат;
					КонецЕсли;
				Иначе
					Возврат;
				КонецЕсли;
			КонецЦикла;
			
			СтандартнаяОбработка = Не РазрешеноПеретаскивание(МассивСсылок);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазрешеноПеретаскивание(МассивСсылок)
	
	Для Каждого ДокументСсылка Из МассивСсылок Цикл
		Если ЗначениеЗаполнено(ДокументСсылка)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Статус")
				= Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Ошибка Тогда
			
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ДеревоДокументыИФайлыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	МассивСсылок = Новый Массив;
	Для Каждого ТекущееЗначение Из ПараметрыПеретаскивания.Значение Цикл
		Если ТипЗнч(ТекущееЗначение) = Тип("ДокументСсылка.РаспознанныйДокумент") Тогда
			МассивСсылок.Добавить(ТекущееЗначение);
		ИначеЕсли ТипЗнч(ТекущееЗначение) = Тип("Число") Тогда
			ТекущиеДанныеСтроки = Элементы.Список.ДанныеСтроки(ТекущееЗначение);
			Если ЗначениеЗаполнено(ТекущиеДанныеСтроки.Ссылка) Тогда
				МассивСсылок.Добавить(ТекущиеДанныеСтроки.Ссылка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	СтрокаДерева = ДеревоДокументыИФайлы.НайтиПоИдентификатору(Строка);
	
	ИменаФайлов = Новый Массив;
	СтрокиВложений = СтрокаДерева.ПолучитьЭлементы();
	Для Каждого СтрокаВложений Из СтрокиВложений Цикл
		ИменаФайлов.Добавить(СтрокаВложений.Наименование);
	КонецЦикла;
	
	ПараметрыОтбораСозданныхДокументов1С = ПолучитьПараметрыОтбораСозданныхДокументов1С();
	ДобавитьИсходноеИзображениеНаСервере(МассивСсылок, ИменаФайлов, СтрокаДерева.Ссылка, ПараметрыОтбораСозданныхДокументов1С);
	РазвернутьСтрокиДерева("ДеревоДокументыИФайлы");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИсходноеИзображениеНаСервере(МассивСсылок, ИменаФайлов, КудаДобавляемСсылка, ПараметрыОтбораСозданныхДокументов1С)
	
	ОставитьТолькоДопустимыеСсылки(МассивСсылок);
	Для Каждого РаспознанныйДокумент Из МассивСсылок Цикл
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РаспознанныйДокумент, "ИмяФайла,ИсходноеИзображение");
		СтруктураИмениФайла = РаспознаваниеДокументовСлужебный.РазложитьИмяФайла(РеквизитыДокумента.ИмяФайла);
		РасширениеБезТочки = НРег(СтруктураИмениФайла.Расширение);
		Если ИменаФайлов.Найти(СтруктураИмениФайла.Имя + "." + РасширениеБезТочки) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыФайла = Новый Структура;
		ПараметрыФайла.Вставить("ВладелецФайлов",     КудаДобавляемСсылка);
		ПараметрыФайла.Вставить("Автор",              Неопределено);
		ПараметрыФайла.Вставить("ИмяБезРасширения",   СтруктураИмениФайла.Имя);
		// 
		ПараметрыФайла.Вставить("РасширениеБезТочки", РасширениеБезТочки);
		ПараметрыФайла.Вставить("ГруппаФайлов",       Неопределено);
		ПараметрыФайла.Вставить("ВремяИзменения");
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное");
		
		ИсходноеИзображение = РеквизитыДокумента.ИсходноеИзображение.Получить();
		ИсходноеИзображение = ПоместитьВоВременноеХранилище(ИсходноеИзображение);
		РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, ИсходноеИзображение);
	КонецЦикла;
	ОбновитьДеревоДокументовИФайловВсеНаСервере(ПараметрыОтбораСозданныхДокументов1С);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОставитьТолькоДопустимыеСсылки(МассивСсылок)
	
	Ид = МассивСсылок.Количество();
	Пока Ид > 0 Цикл
		Ид = Ид - 1;
		Если Не ЗначениеЗаполнено(МассивСсылок[Ид])
			Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МассивСсылок[Ид], "Статус")
			<> Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Ошибка Тогда
 			
			МассивСсылок.Удалить(Ид);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура РазвернутьСтрокиДерева(ИмяЭлемента)
	
	Если (НЕ ПоказыватьСозданныеДокументы1С И ИмяЭлемента = "ДеревоДокументыИФайлы") Тогда
		Возврат;
	КонецЕсли;
	
	ВсеСтроки1 = ЭтотОбъект[ИмяЭлемента].ПолучитьЭлементы();
	Для Каждого СтрДерева Из ВсеСтроки1 Цикл
		Элементы[ИмяЭлемента].Развернуть(СтрДерева.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусОбработанНаСервере(РаспознанныйДок, ПараметрыОтбораСозданныхДокументов1С)
	
	ОбновитьДеревоДокументовИФайловВсеНаСервере(ПараметрыОтбораСозданныхДокументов1С);
	
КонецПроцедуры

#КонецОбласти

#Область ПометитьНаУдаление

&НаКлиенте
Процедура ПометитьНаУдалениеНаКлиенте()
	
	Для Каждого СтрокаСписка Из Элементы.Список.ВыделенныеСтроки Цикл
		ТекущиеДанные = Элементы.Список.ДанныеСтроки(СтрокаСписка);
		Если ТипЗнч(ТекущиеДанные) = Тип("ДанныеФормыСтруктура") Тогда
			
			Если ТекущиеДанные.ЭтоЗаданиеРаспознавания Тогда 
				Если ТекущиеДанные.ПометкаУдаления Тогда 
					ПоказатьПредупреждение(, НСтр("ru = 'Задание распознавания уже отменено.'"));
				Иначе
					ОтменитьЗаданиеРаспознавания(ТекущиеДанные.Идентификатор);
				КонецЕсли;
			Иначе 
				ПометитьНаУдалениеНаСервере(ТекущиеДанные.Ссылка);
				ОповеститьОбИзменении(ТекущиеДанные.Ссылка);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Список.Обновить();
	ОбновитьКоличествоФайловПоСтатусам();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеНаСервере(ДокументСсылка)
	
	Если Не ЗначениеЗаполнено(ДокументСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.РаспознанныйДокумент");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДокументСсылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(Не ДокументОбъект.ПометкаУдаления);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьЗаданиеРаспознавания(ИдентификаторЗадания)
	
	РегистрыСведений.СостоянияЗаданийРаспознаваниеДокументов.ИзменитьСтатусОбработкиЗадания(
		ИдентификаторЗадания,
		Перечисления.СтатусыНаСервисеРаспознаваниеДокументов.Отменено
	);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Оформление колонки день дерева отбора по периоду.
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборПериодДерево.Вложенность");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 1;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтборПериодДеревоДень");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ='MMMM yyyy'");
	ЭлементОформления.Использование = Истина;
	
	// Оформление помеченных на удаление.
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Дата");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТипДокумента");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Источник");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Организация");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Контрагент");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Номер");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(192, 192, 192));
	ЭлементОформления.Использование = Истина;
	
	// Оформление недоступных организаций
	
	Если УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей()
		И Не Пользователи.ЭтоПолноправныйПользователь( , , Ложь) Тогда
		
		ДоступныеОрганизации = Новый СписокЗначений;
		ДоступныеОрганизации.ЗагрузитьЗначения(РаспознаваниеДокументовСлужебный.ДоступныеОрганизации());
		ДоступныеОрганизации.Добавить(Справочники.Организации.ПустаяСсылка());
		
		ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
		
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
		ОтборЭлемента.ПравоеЗначение = ДоступныеОрганизации;
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Организация");
		
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = ''"));
		ЭлементОформления.Использование = Истина;
		
	КонецЕсли;
	
	// Оформление поля дата списка распознанных документов.
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект);
	
	// Оформление статуса связанных документов.
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтатусСвязанногоДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("СтатусСвязанногоДокумента");
	
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	ЭлементОформления.Использование = Истина;
	
	// Оформление отбора по Контрагенту.
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборКонтрагент.Контрагент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтборКонтрагентКонтрагент");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не определено'"));
	ЭлементОформления.Использование = Истина;
	
	// Оформление отбора по Организации.
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборОрганизация.Организация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтборОрганизацияОрганизация");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не определено'"));
	ЭлементОформления.Использование = Истина;
	
КонецПроцедуры

#КонецОбласти
