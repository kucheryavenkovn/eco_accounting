#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
//

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	// Заполним табличную часть СчетаРасчетов
	СчетаРасчетов.Загрузить(Документы.ИнвентаризацияРасчетовСКонтрагентами.СчетаДетализацииЗадолженности());
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата 		  = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	ДетальныеЗаписи.Очистить();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Документы.ИнвентаризацияРасчетовСКонтрагентами.ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты, Неопределено, Ложь);

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПереченьСчетов = Документы.ИнвентаризацияРасчетовСКонтрагентами.ПереченьСчетовДокумента(СчетаРасчетов);
	
	Если ДетализироватьЗадолженность Тогда
		// Удалим строки из табличной части ДетальныеЗаписи, для которых не осталось ведущих строк в табличной части Контрагенты.
		НеиспользуемыеКлючиСтрок = ДетальныеЗаписи.Выгрузить(, "КлючСтроки");
		НеиспользуемыеКлючиСтрок.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(10));
		
		Для каждого СтрокаТаблицы Из Контрагенты Цикл
			НоваяСтрока = НеиспользуемыеКлючиСтрок.Добавить();
			НоваяСтрока.КлючСтроки = СтрокаТаблицы.КлючСтроки;
			НоваяСтрока.Количество = 1;
		КонецЦикла;		

		НеиспользуемыеКлючиСтрок.Свернуть("КлючСтроки", "Количество");
		
		ОтборПоКлючу = Новый Структура("КлючСтроки");
		
		Для каждого СтрокаТаблицы Из НеиспользуемыеКлючиСтрок Цикл
			Если СтрокаТаблицы.Количество = 0 Тогда
				// Ключ строки есть только в ДетальныхЗаписях, удаляем его.
				ОтборПоКлючу.КлючСтроки = СтрокаТаблицы.КлючСтроки;
				НайденныеСтроки = ДетальныеЗаписи.НайтиСтроки(ОтборПоКлючу);
				Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					ДетальныеЗаписи.Удалить(НайденнаяСтрока);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		ДетальныеЗаписи.Очистить();
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
