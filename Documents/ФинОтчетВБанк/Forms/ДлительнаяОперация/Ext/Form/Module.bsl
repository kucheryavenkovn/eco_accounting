
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.ТекстПояснения) Тогда
		ТекстПояснения = Параметры.ТекстПояснения;
	Иначе
		ТекстПояснения = НСтр("ru = 'Подписание и отправка отчетности в банк'");
	КонецЕсли;
	
	ИдентификаторЗадания = Параметры.ИдентификаторЗадания;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтменитьОтправку(Команда)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ЗакрытиеМесяцаВызовСервера.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ФинОтчетВБанк_Прогресс" И Источник = ВладелецФормы Тогда
		// Отобразим прогресс выполнения, который прислала форма владелец.
		ОтобразитьПрогрессВыполнения(Параметр);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПроцедурыИФункции

&НаКлиенте
Процедура ОтобразитьПрогрессВыполнения(Прогресс)

	Если ТипЗнч(Прогресс) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Прогресс.Текст) Тогда
		ТекстПояснения = Прогресс.Текст;
	КонецЕсли;
	
	Если Прогресс.Процент <> Неопределено Тогда
		// Назад не откатываемся в любом случае.
		Если Прогресс.Процент > ПроцентВыполнения Тогда
			ПроцентВыполнения = Прогресс.Процент;
		КонецЕсли;
	КонецЕсли;
	
	// Могло смениться фоновое задание, поэтому обновляем идентификатор.
	ИдентфиикаторЗадания = Прогресс.ИдентификаторЗадания;
	
	ОбновитьОтображениеДанных();

КонецПроцедуры

#КонецОбласти
