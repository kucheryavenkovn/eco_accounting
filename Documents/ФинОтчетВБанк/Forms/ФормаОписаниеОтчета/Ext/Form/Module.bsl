
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок     = Параметры.Заголовок;
	КонецЕсли;
	
	ПодробноеОписание.УстановитьФорматированнуюСтроку(СтроковыеФункции.ФорматированнаяСтрока(Параметры.ПодробноеОписание));
	
	Элементы.ГруппаШаблонФайла.Видимость    = ЗначениеЗаполнено(Параметры.ИмяФайлаШаблона);
	Элементы.ДекорацияШаблонФайла.Заголовок = Параметры.ИмяФайлаШаблона;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбразецФайлаНажатие(Элемент)
	
	АдресФайлаШаблона = АдресФайлаШаблона(Параметры.Банк, Параметры.ИмяФайлаШаблона, УникальныйИдентификатор);
	
	Если ПустаяСтрока(АдресФайлаШаблона) Тогда
		
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Для %1 отсутствует шаблон с именем ""%2""'"),
			Строка(Параметры.Банк), Параметры.ИмяФайлаШаблона);
		ПоказатьПредупреждение( , ТекстПредупреждения, , НСтр("ru = 'Шаблон отсутствует'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохранения.Диалог.Заголовок = НСтр("ru = 'Сохранить шаблон файла отчетности'");
	ПараметрыСохранения.Диалог.Расширение = НРег(ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(Параметры.ИмяФайлаШаблона));
	Если ПараметрыСохранения.Диалог.Расширение = "jpg"
	 Или ПараметрыСохранения.Диалог.Расширение = "jpeg"
	 Или ПараметрыСохранения.Диалог.Расширение = "png" Тогда
		Фильтр = НСтр("ru = 'Изображение|*.%1'");
	ИначеЕсли ПараметрыСохранения.Диалог.Расширение = "doc" Или ПараметрыСохранения.Диалог.Расширение = "docx"
	 Или ПараметрыСохранения.Диалог.Расширение = "xls" Или ПараметрыСохранения.Диалог.Расширение = "xlsx"
	 Или ПараметрыСохранения.Диалог.Расширение = "ods" Или ПараметрыСохранения.Диалог.Расширение = "pdf" Тогда
		Фильтр = НСтр("ru = 'Офисный документ|*.%1'");
	Иначе
		Фильтр = НСтр("ru = 'Файл|*.%1'");
	КонецЕсли;
	ПараметрыСохранения.Диалог.Фильтр = СтрШаблон(Фильтр, ПараметрыСохранения.Диалог.Расширение);
	
	ИнтерактивноеОповещение = Новый ОписаниеОповещения("ДекорацияОбразецФайлаОповещение", ЭтотОбъект, АдресФайлаШаблона);
	ФайловаяСистемаКлиент.СохранитьФайл(ИнтерактивноеОповещение, АдресФайлаШаблона, Параметры.ИмяФайлаШаблона, ПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбразецФайлаОповещение(ПолученныеФайлы, АдресФайлаШаблона) Экспорт
	
	УдалитьИзВременногоХранилища(АдресФайлаШаблона);
	
	Если ПолученныеФайлы = Неопределено Или ПолученныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПутьКФайлу = ПолученныеФайлы[0].ПолноеИмя;
	ОткрытиеКаталога = Новый ОписаниеОповещения("ДекорацияОбразецФайлаОткрытьКаталог", ЭтотОбъект, ПутьКФайлу);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Шаблон успешно сохранен'"), ОткрытиеКаталога, ПутьКФайлу);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбразецФайлаОткрытьКаталог(ПутьКФайлу) Экспорт
	
	ПутьКФайлу = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПутьКФайлу).Путь;
	ФайловаяСистемаКлиент.ОткрытьПроводник(ПутьКФайлу);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресФайлаШаблона(Знач Банк, Знач ИмяФайлаШаблона, Знач УникальныйИдентификатор)
	
	ДанныеФайла = ЗаполнениеФинОтчетностиВБанки.ДанныеШаблона(Банк, ИмяФайлаШаблона);
	
	Возврат ?(ДанныеФайла = Неопределено, "", ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор));
	
КонецФункции

#КонецОбласти