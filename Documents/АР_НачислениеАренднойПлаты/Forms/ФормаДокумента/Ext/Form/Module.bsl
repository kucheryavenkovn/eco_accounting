#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.Основание) Тогда
		Если ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.АР_СчетНаАренду")
			И Не Параметры.Основание.ВидОперации = Перечисления.АР_ВидыОперацийСчетНаАренду.ОплатаАренды Тогда
			ТекстСообщения = "Ввод документа ""Начисление арендной платы"" возможен только на основании счета на аренду с видом операции ""Оплата аренды""";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Параметры.Основание,
				"ВидОперации", "Объект", Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;

	ТекущийПериодАренды = Объект.НачалоПериода;
	ТекущийВидПериода = Объект.ВидПериода;
	
	ОтключитьВалютныеКомандыПечати();
	
	ДоступныПолныеПрава = Пользователи.РолиДоступны("ПолныеПрава");
	УправлениеФормой(ЭтаФорма);
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_СчетФактураВыданный"
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ДокументыОснования") И Параметр.ДокументыОснования.Найти(Объект.Ссылка) <> Неопределено Тогда
		ЗаполнитьТекстПроСчетФактуру(ЭтаФорма, Параметр.РеквизитыСФ);
		УправлениеФормой(ЭтаФорма);
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		// Статус документа изменен в форме списка
		Если Не Объект.Ссылка.Пустая() И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
			СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;

	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Документ.АР_НачислениеАренднойПлаты.Форма.ФормаРеквизитыОрганизацииКонтрагентаУслуги" Тогда
		ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
		Модифицированность = Истина;
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.Патенты.Форма.ФормаЭлемента" Тогда
		ЗаполнитьПатентОбработкаВыбора(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПечатьФискальныхДокументовКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ЗаполнитьДобавленныеКолонкиТаблиц();

	ОтветственныеЛицаБП.УстановитьПодписиПоУмолчанию(Объект.Ссылка, ПараметрыЗаписи);
	
	Если ПараметрыЗаписи.Свойство("ВыписатьСчетФактуру") 
		И ПараметрыЗаписи.ВыписатьСчетФактуру Тогда 
		
		Результат = УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОсновании(Объект.Ссылка, Неопределено, ЭтаФорма.УникальныйИдентификатор);
		
		Если Результат <> Неопределено
			И Результат.Свойство("ФоновоеВыполнение") Тогда // обработать выполнение в фоновом задании
			
			Результат.ДлительнаяОперация.Вставить("АдресХранилищаСОшибками", Результат.АдресХранилищаСОшибками);
			ПараметрыЗаписи.Вставить("ДлительнаяОперация", Результат.ДлительнаяОперация);
			
		Иначе
			
			УчетНДСКлиентСервер.ЗаполнитьРеквизитыФормыПроСчетФактуруВыданный(ЭтаФорма, Результат);
			
			УправлениеФормой(ЭтаФорма);
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьСостояниеДокумента();
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.ДекорацияСостояниеЭДО = Элементы.ДекорацияСостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СтруктураРеквизитовПодписи = Новый Структура;
	СтруктураРеквизитовПодписи.вставить("Руководитель",		"ЗаРуководителяНаОсновании");
	СтруктураРеквизитовПодписи.вставить("ГлавныйБухгалтер", "ЗаГлавногоБухгалтераНаОсновании");
	
	ОтветственныеЛицаБП.ПроверитьИзменениеПодписейДокумента(ТекущийОбъект,СтруктураРеквизитовПодписи,ПараметрыЗаписи);
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// Дополнительные свойства для установки статусов по документу
	ЕстьСчетФактура = ЗначениеЗаполнено(СчетФактура) 
		ИЛИ ПараметрыЗаписи.Свойство("ВыписатьСчетФактуру") 
		И ПараметрыЗаписи.ВыписатьСчетФактуру;
	ТекущийОбъект.ДополнительныеСвойства.Вставить("СтатусДокумента",      СтатусДокумента);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТребуетсяСчетФактура", ТребуетсяСчетФактура И НЕ Объект.ДокументБезНДС);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЕстьСчетФактура",      ЕстьСчетФактура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ВыписатьСчетФактуру") 
		И ПараметрыЗаписи.ВыписатьСчетФактуру
		И ПараметрыЗаписи.Свойство("ДлительнаяОперация") Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Основание", Объект.Ссылка);
		ДополнительныеПараметры.Вставить("АдресХранилищаСОшибками", ПараметрыЗаписи.ДлительнаяОперация.АдресХранилищаСОшибками);
		ДополнительныеПараметры.Вставить("ВыписыватьСчетаФактурыСпецРежимы",
			ПолучитьФункциональнуюОпциюИнтерфейса("ВыписыватьСчетаФактурыСпецРежимы"));
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ОбработатьВыпискуСчетаФактуры", ЭтаФорма, ДополнительныеПараметры);
			
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Актуализация расчетов с контрагентами'");
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ПараметрыЗаписи.ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	КонецЕсли;
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение  Тогда
		
		СчетаНаОплатуПокупателю = Новый Массив;
		Для каждого СтрокаУслуги Из Объект.Состав Цикл
			Если ЗначениеЗаполнено(СтрокаУслуги.СчетНаАренду) 
				И СчетаНаОплатуПокупателю.Найти(СтрокаУслуги.СчетНаАренду) = Неопределено Тогда
			
				СчетаНаОплатуПокупателю.Добавить(СтрокаУслуги.СчетНаАренду);
			КонецЕсли; 
		КонецЦикла; 
		Если СчетаНаОплатуПокупателю.Количество() = 0 
			И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
			
			СчетаНаОплатуПокупателю.Добавить(Объект.ДокументОснование);
		КонецЕсли;
		
		Для каждого СчетНаОплатуПокупателю Из СчетаНаОплатуПокупателю Цикл
			ОповеститьОбИзменении(СчетНаОплатуПокупателю);
		КонецЦикла; 
		
	КонецЕсли;
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтотОбъект, ПараметрыЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Объект.ДатаКурса = Объект.Дата;
	ДатаПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
	ПечатьФискальныхДокументовФормыКлиентСервер.ПоказатьПредупреждениеПечатьЧека(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ДоговорКонтрагентаПриИзмененииНаСервере();
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗачетаАвансовПриИзменении(Элемент)
	
	СпособЗачетаАвансовПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСчетФактураНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьСчетФактуру(ЭтаФорма, СчетФактура, "СчетФактураВыданный");

КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьИзмененияПоКнопкеЦеныИВалюты();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");

КонецПроцедуры

&НаКлиенте
Процедура ДатаКурсаПриИзменении(Элемент)
	
	ДатаКурсаПриИзмененииНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыОрганизацииКонтрагентаНажатие(Элемент)
	
	Если НЕ ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр", 				ТолькоПросмотр);
	ПараметрыФормы.Вставить("Организация", 					Объект.Организация);
	ПараметрыФормы.Вставить("Контрагент",					Объект.Контрагент);
	ПараметрыФормы.Вставить("СчетОрганизации", 				Объект.СчетОрганизации);
	ПараметрыФормы.Вставить("ДатаДокумента",				Объект.Дата);
	ПараметрыФормы.Вставить("Руководитель",					Объект.Руководитель);
	ПараметрыФормы.Вставить("ЗаРуководителяНаОсновании", 	Объект.ЗаРуководителяНаОсновании);
	ПараметрыФормы.Вставить("ГлавныйБухгалтер",				Объект.ГлавныйБухгалтер);
	ПараметрыФормы.Вставить("ЗаГлавногоБухгалтераНаОсновании",Объект.ЗаГлавногоБухгалтераНаОсновании);
	ПараметрыФормы.Вставить("ДоверенностьЧерезКого",		Объект.ДоверенностьЧерезКого);
	ПараметрыФормы.Вставить("ЗаЗаказчикаНаОсновании",		Объект.ЗаЗаказчикаНаОсновании);
	ПараметрыФормы.Вставить("ОтветственныйЗаОформление",	Объект.ОтветственныйЗаОформление);
	
	ОткрытьФорму("Документ.АР_НачислениеАренднойПлаты.Форма.ФормаРеквизитыОрганизацииКонтрагентаУслуги", ПараметрыФормы, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеЭДОНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	ОбменСКонтрагентамиКлиент.ОткрытьДеревоЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьДепозитПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовДокументАвансаПриИзменении(Элемент)

	Строка = Элементы.ЗачетАвансов.ТекущиеДанные;
	Строка.СуммаЗачета = 0;

КонецПроцедуры

&НаКлиенте
Процедура ЗачетАвансовДокументАвансаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ЗачетАвансов.ТекущиеДанные;

	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("Дата"                 , Объект.Дата);
	ПараметрыОбъекта.Вставить("ДоговорКонтрагента"   , Объект.ДоговорКонтрагента);
	ПараметрыОбъекта.Вставить("Контрагент"           , Объект.Контрагент);
	ПараметрыОбъекта.Вставить("СчетУчета"            , Объект.СчетУчетаРасчетовПоАвансам);
	ПараметрыОбъекта.Вставить("Организация"          , Объект.Организация);
	ПараметрыОбъекта.Вставить("ОстаткиОбороты"       , "Кт");
	ПараметрыОбъекта.Вставить("ТипыДокументов"       , "Метаданные.Документы.РеализацияТоваровУслуг.ТабличныеЧасти.ЗачетАвансов.Реквизиты.ДокументАванса.Тип");
	ПараметрыОбъекта.Вставить("РежимОтбораДокументов", ПредопределенноеЗначение("Перечисление.РежимОтбораДокументов.ПоОстаткам"));

	ПараметрыФормы = Новый Структура("ПараметрыОбъекта", ПараметрыОбъекта);
	ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)

	ВыбранноеЗначение = Объект.ДокументОснование;
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	КонтрагентДоИзменения = Объект.Контрагент;
	ДоговорКонтрагентаДоИзменения = Объект.ДоговорКонтрагента;

	РеквизитыСчетаНаОплату = АР_ОбщиеПроцедуры.ЗначенияРеквизитовОбъекта(
		ВыбранноеЗначение, "Контрагент, ДоговорКонтрагента");

	Объект.Контрагент 				= РеквизитыСчетаНаОплату.Контрагент;
	Объект.ДоговорКонтрагента 		= РеквизитыСчетаНаОплату.ДоговорКонтрагента;
	
	Если КонтрагентДоИзменения <> Объект.Контрагент Тогда
		КонтрагентОбработатьИзменение();
	КонецЕсли;	
	
	Если ДоговорКонтрагентаДоИзменения <> Объект.ДоговорКонтрагента Тогда
		ДоговорКонтрагентаОбработатьИзменение();
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#Область РаботаСПериодом

&НаКлиенте
Процедура ПериодНачалоВыбораЗавершение(СтруктураПериода, ДополнительныеПараметры) Экспорт
	
	// Установим полученный период
	Если СтруктураПериода <> Неопределено Тогда
		Объект.ВидПериода = СтруктураПериода.ВидПериода;
		Период = СтруктураПериода.Период;
		Объект.НачалоПериода = СтруктураПериода.НачалоПериода;
		Объект.ОкончаниеПериода = СтруктураПериода.КонецПериода;
		
		ОбновитьСодержаниеУслуг();
		ТекущийПериодАренды = Объект.НачалоПериода;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВидПериодаПриИзменении(Элемент)
	
	ВыборПериодаКлиент.ВидПериодаПриИзменении(Элемент, Объект.ВидПериода, Объект.НачалоПериода, Объект.ОкончаниеПериода, Период);
	ВыборПериодаКлиентСервер.ПереключитьТекущуюСтраницуВыбораПериода(Объект.ВидПериода, Элементы.ГруппаПоляВводаПериода);
	
	ОбновитьСодержаниеУслуг();
	ТекущийВидПериода = Объект.ВидПериода;
	ТекущийПериодАренды = Объект.НачалоПериода;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ВыборПериодаКлиент.ПериодПриИзменении(Элемент, Период, Объект.НачалоПериода, Объект.ОкончаниеПериода);
	
	ОбновитьСодержаниеУслуг();
	ТекущийПериодАренды = Объект.НачалоПериода;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодНачалоВыбораЗавершение", ЭтотОбъект);
	ВыборПериодаКлиент.ПериодНачалоВыбора(ЭтаФорма, Элемент, СтандартнаяОбработка, 
		Объект.ВидПериода, Объект.НачалоПериода, ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка,
		Объект.ВидПериода, Период, Объект.НачалоПериода, Объект.ОкончаниеПериода);
	
	ОбновитьСодержаниеУслуг();
	ТекущийПериодАренды = Объект.НачалоПериода;
		
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка,
		Объект.ВидПериода, Период, Объект.НачалоПериода, Объект.ОкончаниеПериода);
	
	ОбновитьСодержаниеУслуг();
	ТекущийПериодАренды = Объект.НачалоПериода;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка,
		Объект.ВидПериода, Период, Объект.НачалоПериода, Объект.ОкончаниеПериода);
	
	ОбновитьСодержаниеУслуг();
	ТекущийПериодАренды = Объект.НачалоПериода;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

&НаКлиенте
Процедура СоставПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтаФорма);
	
	ПечатьФискальныхДокументовФормыКлиентСервер.ПоказатьПредупреждениеПечатьЧека(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, СтавкаНДС, СчетДоходов, Субконто, СчетРасходов, СчетУчетаНДСПоРеализации,
			| ОбъектАренды, Контрагент, ДоговорКонтрагента, СчетРасчетов");
		
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Ссылка, ОкончаниеПериода, СуммаВключаетНДС, Дата, НачалоПериода, ВидПериода");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Дата = Объект.НачалоПериода;
	
	СоставНоменклатураПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);	
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокаТаблицы);
	
	ПриИзмененииСумма(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСодержаниеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	РаботаСНоменклатуройКлиент.СодержаниеПриИзменении(ТекущиеДанные, Объект.НачалоПериода)
	
КонецПроцедуры

&НаКлиенте
Процедура СоставОбъектАрендыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Состав.ТекущиеДанные;
	ТекущаяСтрока.Коэффициент = 1;
	ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, СчетДоходов, СчетРасходов, СчетРасчетов, СчетУчетаНДСПоРеализации, Субконто, ОбъектАренды, Контрагент, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, ТекущаяСтрока);
	
	ПараметрыОбъекта = Новый Структура("Организация, Ссылка, ОкончаниеПериода, СуммаВключаетНДС, Дата");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	
	АР_ПроцедурыНачисления.ЗаполнитьДанныеПоАгентскимУслугам(ДанныеСтрокаТаблицы, Объект.Организация, Объект.ОкончаниеПериода);
	
	СоставОбъектАрендыПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(Элементы.Состав.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставПериодНачисленияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Состав.ТекущиеДанные;
	ПривестиКНачалуПериода(ТекущаяСтрока.ПериодАренды, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСпособНачисленияПриИзменении(Элемент)

	РассчитатьСумму(Элементы.Состав.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКоличествоПриИзменении(Элемент)
	
	РассчитатьСумму(Элементы.Состав.ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура СоставКоличествоДнейПриИзменении(Элемент)
	
	РассчитатьСумму(Элементы.Состав.ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура СоставСтавкаСпособаНачисленияПриИзменении(Элемент)
	
	РассчитатьСуммуБезпересчетаЦены(Элементы.Состав.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСуммаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	ТекущиеДанные.Цена = ТекущиеДанные.Сумма / ?(ТекущиеДанные.Количество = 0, 1, ТекущиеДанные.Количество);
	ПриИзмененииСумма(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСтавкаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	ПриИзмененииСумма(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСуммаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	ПриИзмененииСуммаНДС(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставКонтрагентПриИзменении(Элемент)
	
	ДанныеСтрокаТаблицы = Новый Структура("ДоговорКонтрагента, Контрагент, СчетРасчетов");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элементы.Состав.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	
	СоставКонтрагентПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(Элементы.Состав.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставДоговорКонтрагентаПриИзменении(Элемент)
	
	ДанныеСтрокаТаблицы = Новый Структура("ДоговорКонтрагента, Контрагент, СчетРасчетов");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элементы.Состав.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	
	СоставДоговорКонтрагентаПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(Элементы.Состав.ТекущиеДанные, ДанныеСтрокаТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПостояннуюЧастьОплаты(Команда)
	
	ДобавитьПоСчетуНаСервере(Истина);
	УстановитьСпециальныйКурс();
	
	Если Объект.Состав.Количество()=0 Тогда  // на самом деле эта проверка не правильная
		Сообщить("Данные для заполнения не обнаружены!");
	КонецЕсли;
	
	ОбновитьИтоги(ЭтаФорма);
	СформироватьНадписьЦеныИВалюта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПеременнуюЧастьОплаты(Команда)
	
	ДобавитьПоСчетуНаСервере(Ложь);
	УстановитьСпециальныйКурс();
	
	Если Объект.Состав.Количество()=0 Тогда  // на самом деле эта проверка не правильная
		Сообщить("Данные для заполнения не обнаружены!");
	КонецЕсли;
	
	ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПостояннуюЧастьОплаты(Команда)
	
	РассчитатьПостояннуюЧастьОплатыНаСервере();
	УстановитьСпециальныйКурс();
	
	Если Объект.Состав.Количество()=0 Тогда 
		Сообщить("Данные для заполнения не обнаружены!");
	КонецЕсли;
	
	ОбновитьИтоги(ЭтаФорма);
	СформироватьНадписьЦеныИВалюта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьПеременнуюЧастьОплаты(Команда)
	
	РассчитатьПеременнуюЧастьОплатыНаСервере();
	УстановитьСпециальныйКурс();
	
	Если Объект.Состав.Количество()=0 Тогда 
		Сообщить("Данные для заполнения не обнаружены!");
	КонецЕсли;
	
	ОбновитьИтоги(ЭтаФорма);
	СформироватьНадписьЦеныИВалюта(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСостав(Команда)
	
	Если Объект.Состав.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("СпроситьОбОчисткеТабЧасти", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, "Очистить табличную часть?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);

	Иначе	
		ОбновитьИтоги(ЭтаФорма);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыписатьСчетФактуру(Команда)
	
	РеквизитыСФ = УчетНДСКлиент.СоздатьСчетФактуруВыданный(ЭтаФорма);
	ЗаполнитьТекстПроСчетФактуру(ЭтаФорма, РеквизитыСФ);
	УправлениеФормой(ЭтаФорма);
	
	Оповестить("Запись_СчетФактураВыданный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьВыпискуСчетаФактуры(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Результат.Вставить("ФоновоеВыполнение", Истина);
	КонецЕсли;
	РеквизитыСФ = УчетНДСКлиент.ОбработатьВыпискуСчетаФактуры(ЭтаФорма, Результат, ДополнительныеПараметры);
	
	Если РеквизитыСФ <> Неопределено Тогда 
		УчетНДСКлиентСервер.ЗаполнитьРеквизитыФормыПроСчетФактуруВыданный(ЭтаФорма, РеквизитыСФ);
	КонецЕсли;
	
	Если СчетФактура.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаРеквизитовНаСервере

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	Если (Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета) Тогда
		СтруктураКурсаДокумента        = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.ДатаКурса, Объект.ДоговорКонтрагента);
		Объект.КурсВзаиморасчетов      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьВзаиморасчетов = СтруктураКурсаДокумента.Кратность;
	КонецЕсли;
	
	УстановитьФункциональныеОпцииФормы();
	
	Если ПлательщикНДС Тогда
		Объект.ДокументБезНДС = Ложь;
	КонецЕсли;
	
	УчетПСН.ДатаПриИзменении(Объект);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДатаКурсаПриИзмененииНаСервере()
	
	Если (Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета) Тогда
		СтруктураКурсаДокумента        = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.ДатаКурса, Объект.ДоговорКонтрагента);
		Объект.КурсВзаиморасчетов      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьВзаиморасчетов = СтруктураКурсаДокумента.Кратность;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ОрганизацияОбработатьИзменение();
	УстановитьВидимостьАгентскойСхемы();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияОбработатьИзменение()

	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
		
	УстановитьФункциональныеОпцииФормы();
	
	Если ПлательщикНДС Тогда
		Объект.ДокументБезНДС = Ложь;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, НалоговыйУчетУСН.ДеятельностьНаПатентеПоУмолчанию(Объект.Организация, Объект.Дата));
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);

	КонтрагентОбработатьИзменение();
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	КонтрагентОбработатьИзменение();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентОбработатьИзменение()

	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Организация); 

	ДоговорКонтрагентаОбработатьИзменение();

КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	ДоговорКонтрагентаОбработатьИзменение();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаОбработатьИзменение()

	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаДоИзменения = Объект.ВалютаДокумента;
	КурсДоИзменения   = Объект.КурсВзаиморасчетов;
	СуммаВключаетНДСДоИзменения = Объект.СуммаВключаетНДС;

	РеквизитыДоговора = АР_ОбщиеПроцедуры.ЗначенияРеквизитовОбъекта(
		Объект.ДоговорКонтрагента, "ВалютаВзаиморасчетов,Владелец, ТипЦен, РасчетыВУсловныхЕдиницах");

	Объект.ВалютаДокумента         = РеквизитыДоговора.ВалютаВзаиморасчетов;
	ВалютаВзаиморасчетов 		   = РеквизитыДоговора.ВалютаВзаиморасчетов;
	
	СтруктураКурсаДокумента        = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.ДатаКурса, Объект.ДоговорКонтрагента);
	Объект.КурсВзаиморасчетов      = СтруктураКурсаДокумента.Курс;
	Объект.КратностьВзаиморасчетов = СтруктураКурсаДокумента.Кратность;

	Если ПолучитьФункциональнуюОпцию("АР_ИспользоватьСхемуОпределенияСтавокНДСПоАрендеНаОсновеДоговора") Тогда
		ЗаполнитьПризнакиНДСПоДоговору();
	ИначеЕсли ЗначениеЗаполнено(РеквизитыДоговора.ТипЦен) Тогда
		 Объект.СуммаВключаетНДС = РеквизитыДоговора.ТипЦен.ЦенаВключаетНДС;
	КонецЕсли;
	
	ПересчитатьНДС = Объект.СуммаВключаетНДС <> СуммаВключаетНДСДоИзменения;
	
	ЗаполнитьСчетаУчетаВТабличнойЧасти();
	
	Документы.АР_НачислениеАренднойПлаты.ЗаполнитьСчетаУчетаРасчетов(Объект);
	
	УстановитьСпособЗачетаАвансов();
	
	ПараметрыОбъекта = Новый Структура("ВалютаДокумента,ДоговорКонтрагента, КурсВзаиморасчетов, КратностьВзаиморасчетов, ДатаКурса, СуммаВключаетНДС, НачалоПериода, ОкончаниеПериода");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	ПараметрыОбъекта.Вставить("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	Если РеквизитыДоговора.РасчетыВУсловныхЕдиницах Тогда
		ВалютаОплаты = ВалютаРегламентированногоУчета;
	Иначе
		ВалютаОплаты = РеквизитыДоговора.ВалютаВзаиморасчетов;
	КонецЕсли;
	
	УчетДенежныхСредствБП.УстановитьБанковскийСчет(
		Объект.СчетОрганизации, Объект.Организация, ВалютаОплаты, Истина);
		
	ПересчитатьЦены = ВалютаДоИзменения <> Объект.ВалютаДокумента;
	ЗаполнитьРассчитатьСуммы(ВалютаДоИзменения, Ложь, ПересчитатьЦены, Ложь, Объект.ДокументБезНДС);	
		
	Если ПересчитатьНДС Тогда
		Для Каждого СтрокаТаблицы Из Объект.Состав Цикл
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
			СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
			ПостояннаяЧастьОплаты = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Номенклатура, "АР_ПостояннаяЧастьОплаты");
			СтрокаТаблицы.ВсегоКомУслуг = ?(ПостояннаяЧастьОплаты = Ложь, СтрокаТаблицы.Сумма, 0);
		КонецЦикла;
	КонецЕсли;
	
	ТребуетсяСчетФактура = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СпособЗачетаАвансовПриИзмененииНаСервере()
	
	СпособЗачетаАвансовОбработатьИзменение();
	УправлениеФормой(ЭтаФорма);

	Если Объект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗачетАвансов;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СпособЗачетаАвансовОбработатьИзменение()

	Объект.СчетУчетаРасчетовПоАвансам = Неопределено;
	Объект.ЗачетАвансов.Очистить();

	Если Объект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.Автоматически
		ИЛИ Объект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		
		СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(
			Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
		Объект.СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетАвансовПокупателя;
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоставНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ПараметрыОбъекта)

	НаименованиеПолное = СтрокаТабличнойЧасти.Номенклатура.НаименованиеПолное;
	Если ПустаяСтрока(НаименованиеПолное) Тогда
		СтрокаТабличнойЧасти.Вставить("Содержание", СтрокаТабличнойЧасти.Номенклатура.Наименование);
	Иначе
		СтрокаТабличнойЧасти.Вставить("Содержание", НаименованиеПолное);
	КонецЕсли;
	СтрокаТабличнойЧасти.Содержание = РаботаСНоменклатуройКлиентСервер.СодержаниеУслуги(СтрокаТабличнойЧасти.Содержание, СтрокаТабличнойЧасти.Номенклатура.ПериодичностьУслуги, 
		ПараметрыОбъекта.НачалоПериода, Истина, ПараметрыОбъекта.ВидПериода);
	
	МетаданныеДокумента = ПараметрыОбъекта.Ссылка.Метаданные();
	ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ПараметрыОбъекта, "Состав", МетаданныеДокумента);

	АР_ПроцедурыНачисления.ЗаполнитьДанныеПоАгентскимУслугам(СтрокаТабличнойЧасти, ПараметрыОбъекта.Организация, ПараметрыОбъекта.ОкончаниеПериода);
	
	Документы.АР_НачислениеАренднойПлаты.ЗаполнитьСчетаУчетаВСтрокеТабЧасти(ПараметрыОбъекта, СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоставОбъектАрендыПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ПараметрыОбъекта)

	Документы.АР_НачислениеАренднойПлаты.ЗаполнитьСчетаУчетаВСтрокеТабЧасти(ПараметрыОбъекта, СтрокаТабличнойЧасти);

КонецПроцедуры

&НаСервере
Процедура СоставКонтрагентПриИзмененииНаСервере(СтрокаТабличнойЧасти, ПараметрыОбъекта)
	
	Если РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(СтрокаТабличнойЧасти.ДоговорКонтрагента, СтрокаТабличнойЧасти.Контрагент, ПараметрыОбъекта.Организация, Перечисления.ВидыДоговоровКонтрагентов.СКомитентом) Тогда
		СоставДоговорКонтрагентаПриИзмененииНаСервере(СтрокаТабличнойЧасти, ПараметрыОбъекта);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СоставДоговорКонтрагентаПриИзмененииНаСервере(СтрокаТабличнойЧасти, ПараметрыОбъекта)
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДоговорКонтрагента) И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Контрагент) Тогда
		СтрокаТабличнойЧасти.Контрагент = СтрокаТабличнойЧасти.ДоговорКонтрагента.Владелец;
	КонецЕсли;
	
	СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(ПараметрыОбъекта.Организация, СтрокаТабличнойЧасти.Контрагент, СтрокаТабличнойЧасти.ДоговорКонтрагента);
    СтрокаТабличнойЧасти.СчетРасчетов = СчетаУчета.СчетРасчетовСКомитентом;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьНаПатентеПриИзменении(Элемент)
	
	ДеятельностьНаПатентеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПатентПриИзменении(Элемент)
	
	РеализацияТоваровУслугФормыКлиент.ПатентПриИзменении(ЭтотОбъект, Элемент);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеобходимоЗаполнитьПатентОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	РеализацияТоваровУслугФормыКлиент.ТекстНеобходимоЗаполнитьПатентОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаКомандНаСервере

&НаСервере
Процедура ДобавитьПоСчетуНаСервере(ПостояннаяЧастьОплаты)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ДобавитьПоСчету(ПостояннаяЧастьОплаты);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьПостояннуюЧастьОплатыНаСервере()

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.РассчитатьПостояннуюЧастьОплаты();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры // КомандаРассчитатьПостояннуюЧастьОплаты()

&НаСервере
Процедура РассчитатьПеременнуюЧастьОплатыНаСервере()

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.РассчитатьПеременнуюЧастьОплаты();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры

#КонецОбласти

// Обслуживание счета-фактуры:

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьТекстПроСчетФактуру(Форма, РеквизитыСФ = Неопределено)

	УчетНДСКлиентСервер.ЗаполнитьРеквизитыФормыПроСчетФактуруВыданный(Форма, РеквизитыСФ);
	
КонецПроцедуры

// Обслуживание типа цен - валюты - НДС:

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты(ПересчитатьЦены = Ложь, ПерезаполнитьЦены = Ложь)

	// 1. Формируем структуру параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВалютаДокумента",      Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",                 Объект.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Кратность",            Объект.КратностьВзаиморасчетов);
	СтруктураПараметров.Вставить("СуммаВключаетНДС",     Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("ДокументБезНДС",       Объект.ДокументБезНДС);
	СтруктураПараметров.Вставить("Контрагент",           Объект.Контрагент);
	СтруктураПараметров.Вставить("Договор",              Объект.ДоговорКонтрагента);
	СтруктураПараметров.Вставить("Организация",          Объект.Организация);
	СтруктураПараметров.Вставить("ДатаДокумента",        Объект.Дата);
	СтруктураПараметров.Вставить("ПерезаполнитьЦены",    ПерезаполнитьЦены);
	СтруктураПараметров.Вставить("ПересчитатьЦены",      ПересчитатьЦены);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения", Ложь);

	// 2. Открываем форму "Цены и Валюта".
	ДополнительныеПараметры = Новый Структура;
	
	Если ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета Тогда 
		ОткрыватьИзМеню = Ложь;
	Иначе
		ОткрыватьИзМеню = Истина;
		ДополнительныеПараметры.Вставить("СтруктураПараметровКоманды", СтруктураПараметров);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ОткрыватьИзМеню Тогда
		
		СписокКоманд = Новый СписокЗначений;
		
		Если Не ПлательщикНДС Тогда
			СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.ДокументБезНДС"));
		КонецЕсли;
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДССверху"));
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме"));
		
		ПоказатьВыборИзМеню(ОповещениеОЗакрытии, СписокКоманд, Элементы.ЦеныИВалюта);		
	Иначе
		ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров,,,,, ОповещениеОЗакрытии);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("СтруктураПараметровКоманды") Тогда
		
		СтруктураЦеныИВалюта = ДополнительныеПараметры.СтруктураПараметровКоманды;
		
		СуммаВключаетНДСДоИзменения = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Если РезультатЗакрытия = Неопределено Тогда 
			Возврат;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.ДокументБезНДС") Тогда
			СтруктураЦеныИВалюта.ДокументБезНДС 	= Истина;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме") Тогда
			СтруктураЦеныИВалюта.ДокументБезНДС 	= Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Истина;
		Иначе
			СтруктураЦеныИВалюта.ДокументБезНДС 	= Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Ложь;
		КонецЕсли;
		
		СтруктураЦеныИВалюта.Вставить("ПерезаполнитьЦены",    Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьЦены",      Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьНДС",       СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.ДокументБезНДС <> СтруктураЦеныИВалюта.ДокументБезНДС);
		СтруктураЦеныИВалюта.Вставить("БылиВнесеныИзменения", СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.ДокументБезНДС <> СтруктураЦеныИВалюта.ДокументБезНДС);
			
	Иначе
		СтруктураЦеныИВалюта = РезультатЗакрытия;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураЦеныИВалюта) = Тип("Структура") Тогда

		ВалютаДоИзменения    = Объект.ВалютаДокумента;
		КурсДоИзменения      = Объект.КурсВзаиморасчетов;
		КратностьДоИзменения = Объект.КратностьВзаиморасчетов;
		
		Объект.ВалютаДокумента         = СтруктураЦеныИВалюта.ВалютаДокумента;
		Объект.КурсВзаиморасчетов      = СтруктураЦеныИВалюта.Курс;
		Объект.КратностьВзаиморасчетов = СтруктураЦеныИВалюта.Кратность;
		Объект.СуммаВключаетНДС        = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Объект.ДокументБезНДС		   = СтруктураЦеныИВалюта.ДокументБезНДС;
		
		Модифицированность = Истина;

		ПересчитатьНДС = СтруктураЦеныИВалюта.ПересчитатьНДС;
		ПересчитатьЦены = ВалютаДоИзменения <> СтруктураЦеныИВалюта.ВалютаДокумента;
		
		ЗаполнитьРассчитатьСуммы(ВалютаДоИзменения, 
			Ложь, // в аренде нет заполнения цен по ценам номенклатуры 
			ПересчитатьЦены, 
			Ложь, // не используем типовой пересчет НДС
			Объект.ДокументБезНДС);
			
		Если ПересчитатьНДС Тогда
			Для Каждого Стр Из Объект.Состав Цикл
				ПриИзмененииСумма(Стр);	
			КонецЦикла;
		КонецЕсли;
		
		ОбновитьИтоги(ЭтаФорма);
		СформироватьНадписьЦеныИВалюта(ЭтаФорма);
				
	КонецЕсли;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРассчитатьСуммы(Знач ВалютаДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь, ДокументБезНДС = Ложь)

	Если НЕ (ПерезаполнитьЦены ИЛИ ПересчитатьЦены ИЛИ ПересчитатьНДС ИЛИ ДокументБезНДС) Тогда
		Возврат;
	КонецЕсли;

	Если ПерезаполнитьЦены Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Дата",            Объект.Дата);
		Запрос.УстановитьПараметр("Состав",          Объект.Состав.Выгрузить());

		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаДокумента.Номенклатура
		|ПОМЕСТИТЬ ТаблицаСостав
		|ИЗ
		|	&Состав КАК ТаблицаДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаСостав.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	ТаблицаСостав КАК ТаблицаТовары
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Курс КАК Курс,
		|	КурсыВалютСрезПоследних.Кратность КАК Кратность,
		|	КурсыВалютСрезПоследних.Валюта КАК Валюта
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Цена,
		|	ЦеныНоменклатурыСрезПоследних.Валюта КАК Валюта,
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ТипЦен = &ТипЦен) КАК ЦеныНоменклатурыСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Номенклатура,
		|	КурсыВалют.Курс,
		|	КурсыВалют.Кратность,
		|	ЦеныНоменклатуры.Цена,
		|	ЦеныНоменклатуры.Валюта
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ТаблицаНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО (ЦеныНоменклатуры.Валюта = КурсыВалют.Валюта)";

		Результат = Запрос.Выполнить().Выбрать();
	ИначеЕсли ПересчитатьЦены Тогда
		СтруктураКурса = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(ВалютаДоИзменения, Объект.ДатаКурса, Объект.ДоговорКонтрагента);
	КонецЕсли;

	Для Каждого Строка Из Объект.Состав Цикл
		ЗаполнитьРассчитатьСуммыВСтроке(Строка, Результат, ВалютаДоИзменения, СтруктураКурса, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС, Истина, 1, Объект.ДокументБезНДС);
	КонецЦикла;

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРассчитатьСуммыВСтроке(Строка, Результат, ВалютаПередИзменением, СтруктураКурса,
										ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС, ЕстьНДС,
										ЗначениеПустогоКоличества, ДокументБезНДС)

	Если ПерезаполнитьЦены Тогда // в нашем случае не используется, нет типа цен
		Результат.Сбросить();
		Если Результат.НайтиСледующий(Строка.Номенклатура, "Номенклатура") Тогда

			Сумма = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				Результат.Сумма, Результат.Валюта, Объект.ВалютаДокумента, Результат.Курс,
				Результат.Кратность, Объект.КурсВзаиморасчетов, Объект.КратностьВзаиморасчетов);

		Иначе
			Сумма = 0;
		КонецЕсли;
		// Признак того, что цена включает НДС, хранится в реквизите ЦенаВключаетНДС типа цен
		ЦенаВключаетНДС = Ложь;
	Иначе
		Если ПересчитатьЦены Тогда

			Сумма = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				Строка.Сумма, ВалютаПередИзменением, Объект.ВалютаДокумента, СтруктураКурса.Курс,
				СтруктураКурса.Кратность, Объект.КурсВзаиморасчетов, Объект.КратностьВзаиморасчетов);

		Иначе
			Сумма = Строка.Сумма;
		КонецЕсли;
		// Признак того, что цена включает НДС, хранится в реквизите СуммаВключаетНДС документа
		ЦенаВключаетНДС = ?(ПересчитатьНДС, НЕ Объект.СуммаВключаетНДС, Объект.СуммаВключаетНДС);
	КонецЕсли;

	Если ДокументБезНДС Тогда
		Строка.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Если ЕстьНДС Тогда

		Строка.Сумма = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
			Сумма, ЦенаВключаетНДС, Объект.СуммаВключаетНДС, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(Строка.СтавкаНДС));

		Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, ЗначениеПустогоКоличества, Строка.Количество);
		Строка.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(Строка.Сумма, Объект.СуммаВключаетНДС, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(Строка.СтавкаНДС));
		Строка.Всего 	= Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС);

	Иначе
		Строка.Сумма = Сумма;
		Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, ЗначениеПустогоКоличества, Строка.Количество);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПересчитатьЦеныВалНаСервере(ТекущиеДанные, ВалютаСпособа)

	СтруктураКурса = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(ВалютаСпособа, Объект.ДатаКурса, Объект.ДоговорКонтрагента);
	ЗаполнитьРассчитатьСуммыВСтроке(ТекущиеДанные, Неопределено, ВалютаСпособа, СтруктураКурса, Ложь, Истина, Ложь, Истина, 1, Объект.ДокументБезНДС);

КонецПроцедуры

// Серверная обработка изменения реквизитов:

&НаСервере
Процедура УстановитьСпособЗачетаАвансов()

	Объект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.Автоматически;
	
	СпособЗачетаАвансовОбработатьИзменение();

КонецПроцедуры

// Пересчеты реквизитов в строках табличных частей

&НаКлиенте
Процедура РассчитатьСумму(ТекущиеДанные)
	
	ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, СтавкаСпособаНачисления, Цена, СпособНачисления, Сумма, Количество, КоличествоДней, СтавкаНДС, СуммаНДС, Всего");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("НачалоПериода", Объект.НачалоПериода);
	
	АР_ОбщиеПроцедурыКлиентСервер.РассчитатьСумму(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ВалютаСпособа = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(ДанныеСтрокаТаблицы.СпособНачисления, "Валюта");
	Если ЗначениеЗаполнено(ВалютаСпособа) И НЕ ВалютаСпособа = Объект.ВалютаДокумента Тогда
		ПересчитатьЦеныВалНаСервере(ДанныеСтрокаТаблицы, ВалютаСпособа);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокаТаблицы);
	
	ПриИзмененииСумма(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСумма(СтрокаТаблицы)

	СтрокаТаблицы.Цена = СтрокаТаблицы.Сумма / ?(СтрокаТаблицы.Количество = 0, 1, СтрокаТаблицы.Количество);
	
	Если СтрокаТаблицы.Свойство("СуммаНДС") Тогда
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("ВсегоКомУслуг") Тогда
		ПостояннаяЧастьОплаты = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Номенклатура, "АР_ПостояннаяЧастьОплаты");
		СтрокаТаблицы.ВсегоКомУслуг = ?(ПостояннаяЧастьОплаты = Ложь, СтрокаТаблицы.Сумма, 0);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммаНДС(СтрокаТаблицы)

	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЕсли;

	Если СтрокаТаблицы.Свойство("ВсегоКомУслуг") Тогда
		ПостояннаяЧастьОплаты = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Номенклатура, "АР_ПостояннаяЧастьОплаты");
		СтрокаТаблицы.ВсегоКомУслуг = ?(ПостояннаяЧастьОплаты = Ложь, СтрокаТаблицы.Сумма, 0);
	КонецЕсли;
	
КонецПроцедуры

// Внешний вид, содержание надписей и т.п.

&НаСервере
Процедура УстановитьУсловноеОформление()

	// СоставСтавкаНДС, УслугиСуммаНДС, УслугиВсего

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставВсего");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ДокументБезНДС", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);
	
	ПлательщикЕНВД			= УчетнаяПолитика.ПлательщикЕНВД(Объект.Организация, Объект.Дата);
	ПлательщикНДС			= УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата);	
	ПлательщикНДФЛ			= УчетнаяПолитика.ПлательщикНДФЛ(Объект.Организация, Объект.Дата);
	ПрименяетсяУСНПатент	= УчетнаяПолитика.ПрименяетсяУСНПатент(Объект.Организация, Объект.Дата);
	
	Если ПрименяетсяУСНПатент Тогда
		ПрименяетсяНесколькоПатентов = Справочники.Патенты.ИспользуетсяНесколькоПатентов(Объект.Организация, Объект.Дата);
	КонецЕсли;
	
	ПрименяетсяТолькоУСНПатент       = УчетнаяПолитика.ПрименяетсяТолькоУСНПатент(Объект.Организация, Объект.Дата);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	// Управление видимостью патента.
	Элементы.ТекстНеобходимоЗаполнитьПатент.Видимость =
		УчетПСНКлиентСервер.ПоказатьПредупреждениеНеобходимоЗаполнитьПатент(
			Объект.Организация,
			Объект.Патент,
			Форма.ПрименяетсяТолькоУСНПатент,
			Форма.ПрименяетсяНесколькоПатентов);
	
	Элементы.Патент.Доступность = Объект.ДеятельностьНаПатенте ИЛИ Форма.ПрименяетсяТолькоУСНПатент;
	Элементы.Патент.Видимость   = Форма.ПрименяетсяНесколькоПатентов ИЛИ Не Форма.ПрименяетсяТолькоУСНПатент;
	
	// Если только патент, то флаг избыточен - патент должен быть доступен для выбора всегда.
	Элементы.ДеятельностьНаПатенте.Видимость = НЕ Форма.ПрименяетсяТолькоУСНПатент;
	
	Элементы.ГруппаДеятельностьНаПатенте.Видимость = Форма.ПрименяетсяУСНПатент;
	
	Элементы.ГруппаСчетаУчетаРасчетов.Видимость   = Форма.ПоказыватьВДокументахСчетаУчета;
	Элементы.ГруппаЗачетАвансов.Видимость    = Объект.СпособЗачетаАвансов = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.ПоДокументу");

	Элементы.ДатаКурса.Видимость = НЕ Форма.ВалютаРегламентированногоУчета = Объект.ВалютаДокумента;
	
	Элементы.ДоговорКонтрагента.Доступность       = ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	Элементы.СпособЗачетаАвансов.Доступность      = ЗначениеЗаполнено(Объект.ДоговорКонтрагента);

	Элементы.СоставКонтрагент.Видимость = Форма.ВидимостьАгентскойСхемы;
	Элементы.СоставДоговорКонтрагента.Видимость = Форма.ВидимостьАгентскойСхемы;
	Элементы.СоставСчетРасчетов.Видимость = Форма.ВидимостьАгентскойСхемы И Форма.ПоказыватьВДокументахСчетаУчета;
	
	Элементы.СчетУчетаДепозита.Видимость = Объект.УчитыватьДепозит;
	
	//Элементы.НеКонтролироватьСоответствиеСодержаниюДоговора.Доступность = Форма.ДоступныПолныеПрава;
	
	Элементы.НадписьСчетФактура.Гиперссылка = Форма.ТребуетсяСчетФактура;
	
	// Счет-фактура
	Если Форма.ТребуетсяСчетФактура И НЕ ЗначениеЗаполнено(Форма.СчетФактура) Тогда
		Элементы.ГруппаСчетФактураСтраницы.ТекущаяСтраница = Элементы.ГруппаВыписатьСчетФактуру;
	Иначе
		Элементы.ГруппаСчетФактураСтраницы.ТекущаяСтраница = Элементы.ГруппаСчетФактураСсылка;
	КонецЕсли;
	Элементы.НадписьСчетФактура.Гиперссылка = Форма.ТребуетсяСчетФактура;
	
	Элементы.ГруппаСчетФактура.Видимость	= Не Объект.ДокументБезНДС;
	Элементы.ИтогиВсегоНДС.Видимость 		= Не Объект.ДокументБезНДС;
	
	ОбновитьИтоги(Форма);
	СформироватьНадписьЦеныИВалюта(Форма);
	
	Форма.Период = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Объект.ВидПериода, Объект.НачалоПериода, Объект.ОкончаниеПериода);
		
	ВыборПериодаКлиентСервер.ПереключитьТекущуюСтраницуВыбораПериода(Объект.ВидПериода, Элементы.ГруппаПоляВводаПериода);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Форма.ИтогиВсего    		= Форма.Объект.Состав.Итог("Всего");
	Форма.ИтогиВсегоКомУслуги 	= Форма.Объект.Состав.Итог("ВсегоКомУслуг");
	Форма.ИтогиВсегоНДС 		= Форма.Объект.Состав.Итог("СуммаНДС");

КонецПроцедуры

&НаСервере
Процедура ДеятельностьНаПатентеПриИзмененииНаСервере()
	
	РеализацияТоваровУслугФормы.ОбработатьИзменениеДеятельностьНаПатенте(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПатентОбработкаВыбора(ВыбранныйПатент)
	
	Если ТипЗнч(ВыбранныйПатент) <> Тип("СправочникСсылка.Патенты") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПатентОбработкаВыбораНаСервере(ВыбранныйПатент);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПатентОбработкаВыбораНаСервере(ВыбранныйПатент)
	
	Если Не УчетПСН.ПатентДоступенДляВыбора(ВыбранныйПатент, Объект.Организация, Объект.Дата) Тогда
		ВыбранныйПатент = Справочники.Патенты.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранныйПатент) Тогда
		Объект.ДеятельностьНаПатенте       = Истина;
		Объект.Патент                      = ВыбранныйПатент;
		
		ДеятельностьНаПатентеПриИзмененииНаСервере();
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьЦеныИВалюта(Форма)
	
	Объект = Форма.Объект;
	СтруктураНадписи = Новый Структура(
		"ВалютаДокумента, Курс, Кратность, СуммаВключаетНДС, ДокументБезНДС",
		Объект.ВалютаДокумента,
		Объект.КурсВзаиморасчетов,
		Объект.КратностьВзаиморасчетов,
		Объект.СуммаВключаетНДС,
		Объект.ДокументБезНДС);
	Форма.ЦеныИВалюта = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);

КонецПроцедуры 

&НаКлиенте
Процедура ПривестиКНачалуПериода(Дата, Договор) 
	
	Если ТипЗнч(Дата) = Тип("Дата") Тогда
		Если ЗначениеЗаполнено(Договор) Тогда 
			Дата = АР_ОбщиеПроцедуры.ОпределитьНачалоПериодаОплатыПоДоговору(Дата, Договор);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Прочий функционал:

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()

 	Для Каждого СтрокаТаблицы Из Объект.Состав Цикл
		
		СтрокаТаблицы.Всего 		= СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
		СтрокаТаблицы.ВсегоКомУслуг = ?(СтрокаТаблицы.Номенклатура.АР_ПостояннаяЧастьОплаты, 0, СтрокаТаблицы.Сумма);
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.СчетНаАренду) Тогда
			Если СтрокаТаблицы.Номенклатура.АР_ПостояннаяЧастьОплаты И Объект.ДоговорКонтрагента.АР_ВестиВзаиморасчетыПоСчетамПост ИЛИ
				НЕ СтрокаТаблицы.Номенклатура.АР_ПостояннаяЧастьОплаты И Объект.ДоговорКонтрагента.АР_ВестиВзаиморасчетыПоСчетамПерем Тогда
				Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
					СтрокаТаблицы.СчетНаАренду = Объект.ДокументОснование;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ТекстНеобходимоЗаполнитьПатент = УчетПСН.ТекстНеобходимоЗаполнитьПатент();
	
	УстановитьФункциональныеОпцииФормы();
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);
	
	УстановитьСостояниеДокумента();
	
	СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ДоговорКонтрагента, "ВидДоговора, РасчетыВУсловныхЕдиницах, ВалютаВзаиморасчетов");
		ВалютаВзаиморасчетов = РеквизитыДоговора.ВалютаВзаиморасчетов;
	Иначе	
		ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
	КонецЕсли;
	
	ВыписыватьСчетФактуру = Истина;
	СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(Объект.Ссылка);
		
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ПоказыватьВДокументахСчетаУчета	= Истина;
	
	УстановитьПараметрыВыбора(ЭтаФорма);
	
	УстановитьВидимостьАгентскойСхемы();
	
	ЭтаФорма.ТребуетсяСчетФактура = ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
	ЗаполнитьТекстПроСчетФактуру(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
	
	ПечатьФискальныхДокументовФормыКлиентСервер.ПодготовитьФормуНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьАгентскойСхемы()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(АР_СобственникиОбъектовАренды.Собственник, АР_СобственникиОбъектовАрендыВсе.Собственник) КАК Собственник,
		|	АР_ОбъектыАренды.Ссылка КАК ОбъектАренды
		|ПОМЕСТИТЬ ВТДействующиеСобственники
		|ИЗ
		|	Справочник.АР_ОбъектыАренды КАК АР_ОбъектыАренды
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АР_СобственникиОбъектовАренды.СрезПоследних(&Период, ) КАК АР_СобственникиОбъектовАренды
		|		ПО АР_ОбъектыАренды.Ссылка = АР_СобственникиОбъектовАренды.ОбъектАренды
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АР_СобственникиОбъектовАренды.СрезПоследних(&Период, ОбъектАренды = ЗНАЧЕНИЕ(Справочник.АР_ОбъектыАренды.ПустаяСсылка)) КАК АР_СобственникиОбъектовАрендыВсе
		|		ПО (ИСТИНА)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(АР_СобственникиОбъектовАренды.Собственник, АР_СобственникиОбъектовАрендыВсе.Собственник),
		|	АР_ОбъектыАренды.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТДействующиеСобственники.ОбъектАренды КАК ОбъектАренды,
		|	ВТДействующиеСобственники.Собственник КАК Собственник,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Доля КАК Доля,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.НомерЗаписиВЕГРП КАК НомерЗаписиВЕГРП,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.ДатаЗаписиВЕГРП КАК ДатаЗаписиВЕГРП,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскаяСхема КАК АгентскаяСхема,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскийДоговор КАК ДоговорПринципала,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Собственник КАК Контрагент
		|ИЗ
		|	ВТДействующиеСобственники КАК ВТДействующиеСобственники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АР_СобственникиОбъектовНедвижимости.Собственники КАК АР_СобственникиОбъектовНедвижимостиСобственники
		|		ПО ВТДействующиеСобственники.Собственник = АР_СобственникиОбъектовНедвижимостиСобственники.Ссылка
		|ГДЕ
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскийДоговор.Организация = &Организация
		|	И АР_СобственникиОбъектовНедвижимостиСобственники.АгентскаяСхема = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТДействующиеСобственники.ОбъектАренды,
		|	ВТДействующиеСобственники.Собственник,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Доля,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.НомерЗаписиВЕГРП,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.ДатаЗаписиВЕГРП,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскаяСхема,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.АгентскийДоговор,
		|	АР_СобственникиОбъектовНедвижимостиСобственники.Собственник";
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Период",      КонецДня(Объект.Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	ВидимостьАгентскойСхемы = Выборка.Следующий();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти()

	Документы.АР_НачислениеАренднойПлаты.ЗаполнитьСчетаУчетаВТабЧасти(Объект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбора(Форма)

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСпециальныйКурс()
	
	Если Объект.Состав.Количество() > 0 Тогда
		Если Объект.ВалютаДокумента = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(Объект.Состав[0].СпособНачисления, "Валюта") Тогда
			СтруктураКурса = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.ДатаКурса, Объект.ДоговорКонтрагента);
			Если СтруктураКурса.СпециальныйКурс И Объект.КурсВзаиморасчетов <> СтруктураКурса.Курс Тогда
				Объект.КурсВзаиморасчетов = СтруктураКурса.Курс;
				Объект.КратностьВзаиморасчетов = СтруктураКурса.Кратность;
				СформироватьНадписьЦеныИВалюта(ЭтаФорма);
			КонецЕсли;
		КонецЕсли;  
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакиНДСПоДоговору()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АР_СтавкиСрезПоследних.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.АР_Ставки.СрезПоследних(
	|			&Период,
	|			Договор = &Договор
	|				И (СрокДействия >= &Период
	|					ИЛИ СрокДействия = ДАТАВРЕМЯ(1, 1, 1))) КАК АР_СтавкиСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	АР_СтавкиСрезПоследних.Период УБЫВ";
	
	Запрос.УстановитьПараметр("Договор", Объект.ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.СуммаВключаетНДС = Выборка.Регистратор.СуммаВключаетНДС;
		Объект.ДокументБезНДС = Выборка.Регистратор.ДокументБезНДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпроситьОбОчисткеТабЧасти(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Состав.Очистить();	
		ОбновитьИтоги(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьВалютныеКомандыПечати()
	
	Если НЕ Объект.ВалютаДокумента = ЭтаФорма.ВалютаРегламентированногоУчета Тогда
		Возврат;
	КонецЕсли;
	
	СписокОтключаемыхКоманд = Новый Массив;
	СписокОтключаемыхКоманд.Добавить("АктРуб");
	СписокОтключаемыхКоманд.Добавить("АктСводныйРуб");
	
	ПодменюПечать = ЭтаФорма.Элементы.Найти("ПодменюПечать");
	Если НЕ ПодменюПечать = Неопределено Тогда
		Для Каждого Элемент Из СписокОтключаемыхКоманд Цикл
			АР_МодификацияКонфигурации.ОтключитьКомандыПечати(ПодменюПечать, Элемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

&НаСервере
Процедура ОбновитьСодержаниеУслуг()
	РаботаСНоменклатурой.ОбновитьСодержаниеУслуг(Объект.Состав, Объект.НачалоПериода, ТекущийПериодАренды, Объект.ВидПериода, ТекущийВидПериода);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуБезПересчетаЦены(ТекущиеДанные)
	
	ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, СтавкаСпособаНачисления, Цена, СпособНачисления, Сумма, Количество, КоличествоДней, СтавкаНДС, СуммаНДС, Всего");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("НачалоПериода", Объект.НачалоПериода);
	
	АР_ОбщиеПроцедурыКлиентСервер.РассчитатьСумму(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ВалютаСпособа = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(ДанныеСтрокаТаблицы.СпособНачисления, "Валюта");
	Если ЗначениеЗаполнено(ВалютаСпособа) И НЕ ВалютаСпособа = Объект.ВалютаДокумента Тогда
		ПересчитатьЦеныВалНаСервере(ДанныеСтрокаТаблицы, ВалютаСпособа);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокаТаблицы);
	
	ТекущиеДанные.Цена = ТекущиеДанные.СтавкаСпособаНачисления;
	
	Если ТекущиеДанные.Свойство("СуммаНДС") Тогда
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(ТекущиеДанные, Объект.СуммаВключаетНДС);
	КонецЕсли;

	Если ТекущиеДанные.Свойство("Всего") Тогда
		ТекущиеДанные.Всего = ТекущиеДанные.Сумма + ?(Объект.СуммаВключаетНДС, 0, ТекущиеДанные.СуммаНДС);
	КонецЕсли;

	Если ТекущиеДанные.Свойство("ВсегоКомУслуг") Тогда
		ПостояннаяЧастьОплаты = АР_ОбщиеПроцедуры.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Номенклатура, "АР_ПостояннаяЧастьОплаты");
		ТекущиеДанные.ВсегоКомУслуг = ?(ПостояннаяЧастьОплаты = Ложь, ТекущиеДанные.Сумма, 0);
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область СтатусыДокументов

&НаСервереБезКонтекста
Функция ПолучитьСтатусДокумента(Знач Организация, Знач Ссылка)
	
	Возврат (РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокумента(Ссылка, Организация).Статус =
		Перечисления.СтатусыДокументовРеализации.Подписан);
	
КонецФункции

&НаСервере
Процедура СоставПлощадкаПриИзмененииНаСервере(Номенклатура, Площадка, Содержание)
	Содержание = "" + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "НаименованиеПолное") + " ("+Площадка+") ";
КонецПроцедуры

&НаКлиенте
Процедура СоставПлощадкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Состав.ТекущиеДанные;
	Если НЕ ТекущиеДанные = Неопределено Тогда
		СоставПлощадкаПриИзмененииНаСервере(ТекущиеДанные.Номенклатура, ТекущиеДанные.Площадка, ТекущиеДанные.Содержание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

