#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ПроверитьСтатусыОбъектов(Отказ)

	// Проверим, что для объектов не установлен никакой статус
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВТТабличнаяЧасть
	|ИЗ
	|	&ТабличнаяЧасть КАК ТабличнаяЧасть";
	Запрос.УстановитьПараметр("ТабличнаяЧасть", ЭтотОбъект.ОбъектыАренды.Выгрузить());
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("Период", Новый МоментВремени(Дата, Ссылка));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыАренды.НомерСтроки,
	|	ОбъектыАренды.ОбъектАренды,
	|	АР_СтатусыОбъектовАрендыСрезПоследних.ЗаключениеДоговора,
	|	АР_СтатусыОбъектовАрендыСрезПоследних.Статус,
	|	АР_СтатусыОбъектовАрендыСрезПоследних.ДатаНачалаАренды,
	|	АР_СтатусыОбъектовАрендыСрезПоследних.ДатаОкончанияАренды
	|ИЗ
	|	ВТТабличнаяЧасть КАК ОбъектыАренды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АР_СтатусыОбъектовАренды.СрезПоследних(&Период, ) КАК АР_СтатусыОбъектовАрендыСрезПоследних
	|		ПО ОбъектыАренды.ОбъектАренды = АР_СтатусыОбъектовАрендыСрезПоследних.ОбъектАренды
	|			И (НЕ(АР_СтатусыОбъектовАрендыСрезПоследних.ДатаОкончанияАренды < ОбъектыАренды.ДатаНачала
	|						И АР_СтатусыОбъектовАрендыСрезПоследних.ДатаОкончанияАренды <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ИЛИ ОбъектыАренды.ДатаОкончания < АР_СтатусыОбъектовАрендыСрезПоследних.ДатаНачалаАренды
	|						И ОбъектыАренды.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)))
	|			И &Ссылка <> АР_СтатусыОбъектовАрендыСрезПоследних.Регистратор";
	Выборка = Запрос.Выполнить().Выбрать();
	ИмяСписка = "Объекты аренды";
	Пока Выборка.Следующий() Цикл
		Префикс = ИмяСписка + "[" + Формат(Выборка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
		ТекстСообщения = НСтр("ru = 'Объект аренды """ + Выборка.ОбъектАренды + """ в период с " 
		+ Формат(Выборка.ДатаНачалаАренды, "ДФ=dd.MM.yyyy") + " по " + Формат(Выборка.ДатаОкончанияАренды, "ДФ=dd.MM.yyyy") + " имеет статус """ +
		Выборка.Статус + """.'");				
		Если ТипЗнч(Выборка.ЗаключениеДоговора) = Тип("ДокументСсылка.АР_ИзменениеСтатусовОбъектов") Тогда
			ТекстСообщения = ТекстСообщения + НСтр("ru = ' Для изменения статуса необходимо установить соответстующую дату окончания в документе """ + Выборка.ЗаключениеДоговора + """'");
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения("Колонка", "Корректность", НСтр("ru = 'Дата начала'"),
			Выборка.НомерСтроки, ИмяСписка, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Выборка.ЗаключениеДоговора,,, Отказ);
		Иначе
			ТекстСообщения = ТекстСообщения + НСтр("ru = ' Изменение статуса невозможно'");
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения("Колонка", "Корректность", НСтр("ru = 'Дата начала'"),
			Выборка.НомерСтроки, ИмяСписка, ТекстСообщения);
			Поле = Префикс + "ДатаНачала";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьПризнакСдачиВАренду(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ИмяСписка = "Объекты аренды";
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВТТабличнаяЧасть
	|ИЗ
	|	&ТабличнаяЧасть КАК ТабличнаяЧасть";
	Запрос.УстановитьПараметр("ТабличнаяЧасть", ОбъектыАренды.Выгрузить());
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.НомерСтроки,
	|	ТабличнаяЧасть.ОбъектАренды,
	|	ЕСТЬNULL(ОбъектыПодлежащиеСдачеВАренду.ПодлежитСдачеВАренду, ЛОЖЬ) КАК ПодлежитСдачеВАренду
	|ИЗ
	|	ВТТабличнаяЧасть КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АР_ОбъектыПодлежащиеСдачеВАренду.СрезПоследних(&Период, ) КАК ОбъектыПодлежащиеСдачеВАренду
	|		ПО ТабличнаяЧасть.ОбъектАренды = ОбъектыПодлежащиеСдачеВАренду.ОбъектАренды
	|ГДЕ
	|	НЕ ЕСТЬNULL(ОбъектыПодлежащиеСдачеВАренду.ПодлежитСдачеВАренду, ЛОЖЬ)
	|	И (ТабличнаяЧасть.Статус = ЗНАЧЕНИЕ(Перечисление.АР_СтатусыОбъектовАренды.ВАренде)
	|			ИЛИ ТабличнаяЧасть.Статус = ЗНАЧЕНИЕ(Перечисление.АР_СтатусыОбъектовАренды.Свободен))";
                    
	Запрос.УстановитьПараметр("Период", Дата);
	                                
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Префикс = "ОбъектыАренды[" + Формат(Выборка.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";
		ТекстСообщения = НСтр("ru = 'Для объекта недвижимости """ + Выборка.ОбъектАренды.Наименование + """ на дату " + Формат(Дата, "ДФ=dd.MM.yyyy") + 
		" не установлен признак ""Объект подлежит сдаче в аренду""
		|Объекту нельзя назначить статус ""Свободен"" или ""В аренде""'");
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения("Колонка", "Корректность", НСтр("ru = 'Объект аренды'"),
		Выборка.НомерСтроки, ИмяСписка, ТекстСообщения);
		Поле = Префикс + "ОбъектАренды";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
	КонецЦикла;    
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Исключаем из проверки реквизиты, заполнение которых стало необязательным:
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	ПроверитьПризнакСдачиВАренду(Отказ);
	ПроверитьСтатусыОбъектов(Отказ);
	
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.АР_ИзменениеСтатусовОбъектов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	АР_ОбщиеПроцедуры.СформироватьДвиженияПоРегистру(ПараметрыПроведения.ОбъектыАренды, Движения, "АР_СтатусыОбъектовАренды", Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

#КонецЕсли
