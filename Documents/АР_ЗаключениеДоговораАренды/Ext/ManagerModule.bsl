#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицыДокумента(НомераТаблиц)
		+ ТекстЗапросаСтатусыОбъектовАренды(НомераТаблиц)
		+ ТекстЗапросаСтавки(НомераТаблиц)
		+ ТекстЗапросаПараметрыНачисленияПени(НомераТаблиц)
		+ ТекстЗапросаКурсыВалют(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаПостояннаяЧасть"     , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаПеременнаяЧасть"     , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПостояннаяЧасть.Ссылка,
	|	ТаблицаПостояннаяЧасть.НомерСтроки,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.Услуга,
	|	ТаблицаПостояннаяЧасть.Количество,
	|	ТаблицаПостояннаяЧасть.СпособНачисления,
	|	ТаблицаПостояннаяЧасть.Ставка,
	|	ТаблицаПостояннаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПостояннаяЧасть.ДатаОкончанияСрокаАренды,
	|	ТаблицаПостояннаяЧасть.СтавкаНДС
	|ПОМЕСТИТЬ ТаблицаПостояннаяЧасть
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды.ПостояннаяЧастьАренднойПлаты КАК ТаблицаПостояннаяЧасть
	|ГДЕ
	|	ТаблицаПостояннаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПеременнаяЧасть.Ссылка,
	|	ТаблицаПеременнаяЧасть.НомерСтроки,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.Услуга,
	|	ТаблицаПеременнаяЧасть.Количество,
	|	ТаблицаПеременнаяЧасть.ЕдиницаИзмерения,
	|	ТаблицаПеременнаяЧасть.СпособНачисления,
	|	ТаблицаПеременнаяЧасть.Ставка,
	|	ТаблицаПеременнаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПеременнаяЧасть.ДатаОкончанияСрокаАренды,
	|	ТаблицаПеременнаяЧасть.Счетчик,
	|	ТаблицаПеременнаяЧасть.КоэффициентОтнесенияСчетчика,
	|	ТаблицаПеременнаяЧасть.СтавкаНДС
	|ПОМЕСТИТЬ ТаблицаПеременнаяЧасть
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды.ПеременнаяЧастьАренднойПлаты КАК ТаблицаПеременнаяЧасть
	|ГДЕ
	|	ТаблицаПеременнаяЧасть.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаПостояннаяЧасть", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПеременнаяЧасть", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПостояннаяЧасть.Ссылка,
	|	ТаблицаПостояннаяЧасть.НомерСтроки,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.Услуга,
	|	ТаблицаПостояннаяЧасть.Количество,
	|	ТаблицаПостояннаяЧасть.СпособНачисления,
	|	ТаблицаПостояннаяЧасть.Ставка,
	|	ТаблицаПостояннаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПостояннаяЧасть.ДатаОкончанияСрокаАренды,
	|	ТаблицаПостояннаяЧасть.СтавкаНДС
	|ИЗ
	|	ТаблицаПостояннаяЧасть КАК ТаблицаПостояннаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПеременнаяЧасть.Ссылка,
	|	ТаблицаПеременнаяЧасть.НомерСтроки,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.Услуга,
	|	ТаблицаПеременнаяЧасть.Количество,
	|	ТаблицаПеременнаяЧасть.ЕдиницаИзмерения,
	|	ТаблицаПеременнаяЧасть.СпособНачисления,
	|	ТаблицаПеременнаяЧасть.Ставка,
	|	ТаблицаПеременнаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПеременнаяЧасть.ДатаОкончанияСрокаАренды,
	|	ТаблицаПеременнаяЧасть.Счетчик,
	|	ТаблицаПеременнаяЧасть.КоэффициентОтнесенияСчетчика,
	|	ТаблицаПеременнаяЧасть.СтавкаНДС
	|ИЗ
	|	ТаблицаПеременнаяЧасть КАК ТаблицаПеременнаяЧасть";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСтатусыОбъектовАренды(НомераТаблиц)
	
	НомераТаблиц.Вставить("СтатусыОбъектовАрендыПостояннаяЧасть", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СтатусыОбъектовАрендыПеременнаяЧасть", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПостояннаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК ЗаключениеДоговора,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПостояннаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды,
	|	ТаблицаПостояннаяЧасть.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды,
	|	ЗНАЧЕНИЕ(Перечисление.АР_СтатусыОбъектовАренды.ВАренде) КАК Статус
	|ИЗ
	|	ТаблицаПостояннаяЧасть КАК ТаблицаПостояннаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ЗаключениеДоговораАренды КАК Реквизиты
	|		ПО ТаблицаПостояннаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПеременнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК ЗаключениеДоговора,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПеременнаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды,
	|	ТаблицаПеременнаяЧасть.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды,
	|	ТаблицаПеременнаяЧасть.Счетчик КАК Счетчик,
	|	ТаблицаПеременнаяЧасть.КоэффициентОтнесенияСчетчика КАК КоэффициентОтнесенияСчетчика,
	|	ЗНАЧЕНИЕ(Перечисление.АР_СтатусыОбъектовАренды.ВАренде) КАК Статус
	|ИЗ
	|	ТаблицаПеременнаяЧасть КАК ТаблицаПеременнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ЗаключениеДоговораАренды КАК Реквизиты
	|		ПО ТаблицаПеременнаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСтавки(НомераТаблиц)
	
	НомераТаблиц.Вставить("СтавкиПостояннаяЧасть", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СтавкиПеременнаяЧасть", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаПостояннаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПостояннаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.Ставка КАК Ставка,
	|	ТаблицаПостояннаяЧасть.СпособНачисления КАК СпособНачисления,
	|	ТаблицаПостояннаяЧасть.Количество КАК Площадь,
	|	ТаблицаПостояннаяЧасть.СтавкаНДС
	|ИЗ
	|	ТаблицаПостояннаяЧасть КАК ТаблицаПостояннаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ЗаключениеДоговораАренды КАК Реквизиты
	|		ПО ТаблицаПостояннаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаПеременнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПеременнаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.Ставка КАК Ставка,
	|	ТаблицаПеременнаяЧасть.СпособНачисления КАК СпособНачисления,
	|	ТаблицаПеременнаяЧасть.Количество КАК Площадь,
	|	ТаблицаПеременнаяЧасть.СтавкаНДС
	|ИЗ
	|	ТаблицаПеременнаяЧасть КАК ТаблицаПеременнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ЗаключениеДоговораАренды КАК Реквизиты
	|		ПО ТаблицаПеременнаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПараметрыНачисленияПени(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПараметрыНачисленияПени", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СрокиОплаты.НазначениеСрока КАК НазначениеСрока,
	|	СрокиОплаты.Услуга КАК Услуга,
	|	СрокиОплаты.СпособОпределенияСрокаОплаты КАК СпособОпределенияСрокаОплаты,
	|	СрокиОплаты.СрокОплаты КАК СрокОплаты,
	|	СрокиОплаты.ПериодСрокаОплаты КАК ПериодСрокаОплаты,
	|	СрокиОплаты.КоличествоДнейОтДатыСчетаАкта КАК КоличествоДнейОтДатыСчетаАкта,
	|	СрокиОплаты.УчитыватьТолькоРабочиеДниОтДатыСчетаАкта КАК УчитыватьТолькоРабочиеДниОтДатыСчетаАкта,
	|	СрокиОплаты.СпособНачисленияПени КАК СпособНачисленияПени,
	|	СрокиОплаты.СтавкаПени КАК СтавкаПени,
	|	СрокиОплаты.СпособПогашенияПени КАК СпособПогашенияПени,
	|	СрокиОплаты.КоличествоДнейОтсрочки КАК КоличествоДнейОтсрочки,
	|	СрокиОплаты.УчитыватьТолькоРабочиеДниОтсрочки КАК УчитыватьТолькоРабочиеДниОтсрочки,
	|	СрокиОплаты.УчитыватьТолькоРабочиеДниПриРасчетеПени КАК УчитыватьТолькоРабочиеДниПриРасчетеПени,
	|	СрокиОплаты.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СрокиОплаты.Ссылка.Дата КАК Период
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды.СрокиОплаты КАК СрокиОплаты
	|ГДЕ
	|	СрокиОплаты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаКурсыВалют(НомераТаблиц)
	
	НомераТаблиц.Вставить("КурсыВалют", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВалютаСпособаНачисления КАК Валюта,
	|	Реквизиты.Контрагент,
	|	Реквизиты.Курс,
	|	Реквизиты.Кратность,
	|	Реквизиты.Коэффициент,
	|	Реквизиты.СпособРасчета,
	|	Реквизиты.НижняяГраница,
	|	Реквизиты.ВерхняяГраница
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И Реквизиты.ИспользоватьСпециальныйКурсВалюты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Печать

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "СтруктураАссортимента";
	КомандаОтправки.Представление = НСтр("ru = 'Структура ассортимента'");
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ПротоколСогласованияЦены";
	КомандаОтправки.Представление = НСтр("ru = 'Протокол согласования цены'");
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ПередаточныйАкт";
	КомандаОтправки.Представление = НСтр("ru = 'Передаточный акт'");
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СтруктураАссортимента";
	КомандаПечати.Представление = НСтр("ru = 'Структура ассортимента'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПротоколСогласованияЦены";
	КомандаПечати.Представление = НСтр("ru = 'Протокол согласования цены'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПередаточныйАкт";
	КомандаПечати.Представление = НСтр("ru = 'Передаточный акт'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьПланировок";
	КомандаПечати.Представление = НСтр("ru = 'Печать планировок'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиПланировок";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьШаблоновWord";
	КомандаПечати.Представление = НСтр("ru = 'Печать по шаблону MS Word'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиШаблоновWord";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Заключение договора аренды""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок       = 160;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СтруктураАссортимента") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СтруктураАссортимента", "Структура ассортимента", ПечатьСтруктурыАссортимента(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПротоколСогласованияЦены") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПротоколСогласованияЦены", "Протокол согласования цены", ПечатьПротоколаСогласованияЦены(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПередаточныйАкт") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПередаточныйАкт", "Передаточный акт", ПечатьПередаточногоАкта(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьСтруктурыАссортимента(МассивОбъектов, ОбъектыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаключениеДоговораАренды_Ассортимент";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	АР_ЗаключениеДоговораАренды.Организация,
	|	АР_ЗаключениеДоговораАренды.Дата,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	АР_ЗаключениеДоговораАренды.НомерАссортимента,
	|	АР_ЗаключениеДоговораАренды.ДатаНачалаАссортимента КАК ДатаАссортиментаС,
	|	АР_ЗаключениеДоговораАренды.ДатаОкончанияАссортимента КАК ДатаАссортиментаПо,
	|	АР_ЗаключениеДоговораАренды.Ссылка
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды КАК АР_ЗаключениеДоговораАренды
	|ГДЕ
	|	АР_ЗаключениеДоговораАренды.Ссылка = &Ссылка";
	
	ЗапросАссортимент = Новый Запрос();
	ЗапросАссортимент.Текст =
	"ВЫБРАТЬ
	|	АР_СтруктураАссортимента.Наименование КАК СтруктураАссортимента
	|ИЗ
	|	Справочник.АР_СтруктураАссортимента КАК АР_СтруктураАссортимента
	|ГДЕ
	|	АР_СтруктураАссортимента.Владелец В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды
	|			ИЗ
	|				Документ.АР_ЗаключениеДоговораАренды.ПостояннаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты
	|			ГДЕ
	|				АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ОбъектАренды
	|			ИЗ
	|				Документ.АР_ЗаключениеДоговораАренды.ПеременнаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты
	|			ГДЕ
	|				АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Ссылка = &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтруктураАссортимента";
		
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ЗаключениеДоговораАренды.ПФ_MXL_СтруктураАссортимента");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
				
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ.Ссылка);
		
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Область = Макет.ПолучитьОбласть("Заголовок");		
		Область.Параметры.НомерАссортимента = Шапка.НомерАссортимента;
		Область.Параметры.ДатаАссортиментаС = Формат(Шапка.ДатаАссортиментаС, "ДФ='д ММММ гггг'");
		Область.Параметры.ДатаАссортиментаПо = Формат(Шапка.ДатаАссортиментаПо, "ДФ='д ММММ гггг'");
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
				
		ЗапросАссортимент.УстановитьПараметр("Ссылка", Документ.Ссылка);
		Ассортимент = ЗапросАссортимент.Выполнить().Выбрать();
		
		НомерСтроки = 1;
		Пока Ассортимент.Следующий() Цикл
			Область.Параметры.Номер = НомерСтроки;
			Область.Параметры.Заполнить(Ассортимент);
			ТабличныйДокумент.Вывести(Область);
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		Область.Параметры.РуководительКонтрагента = Шапка.РуководительКонтрагента;
		
		ТабличныйДокумент.Вывести(Область);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьСчетаЗаказа()

Функция ПечатьПротоколаСогласованияЦены(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаключениеДоговораАренды_ПротоколСогласованияЦены";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	АР_ЗаключениеДоговораАренды.Контрагент,
	|	АР_ЗаключениеДоговораАренды.Организация,
	|	АР_ЗаключениеДоговораАренды.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	АР_ЗаключениеДоговораАренды.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	АР_ЗаключениеДоговораАренды.ДоговорКонтрагента КАК Договор,
	|	АР_ЗаключениеДоговораАренды.Дата,
	|	АР_ЗаключениеДоговораАренды.НомерПротокола,
	|	АР_ЗаключениеДоговораАренды.ДатаПротокола,
	|	АР_ЗаключениеДоговораАренды.Ссылка
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды КАК АР_ЗаключениеДоговораАренды
	|ГДЕ
	|	АР_ЗаключениеДоговораАренды.Ссылка = &Ссылка";
	
	ЗапросОбъекты = Новый Запрос(); 
	ЗапросОбъекты.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыАренды.ОбъектАренды,
	|	ОбъектыАренды.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ОбъектыАренды.Количество КАК ПлощадьОбъекта,
	|	ОбъектыАренды.ЕдиницаИзмерения КАК ЕдиницаИзмеренияОбъекта,
	|	ОбъектыАренды.СпособНачисленияВалюта КАК Валюта,
	|	ОбъектыАренды.СпособНачисленияВалюта.ПараметрыПрописи КАК ВалютаПараметрыПрописи,
	|	ОбъектыАренды.СпособНачисленияПериодНачисления КАК ПериодАренды,
	|	ОбъектыАренды.Ставка,
	|	ОбъектыАренды.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды,
	|	ОбъектыАренды.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды,
	|	ОбъектыАренды.ОбъектАренды.ЗданиеИЭтаж КАК ОбъектАрендыЗданиеЭтаж
	|ИЗ
	|	(ВЫБРАТЬ
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Услуга.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Количество КАК Количество,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ОбъектАренды.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.СпособНачисления.Валюта КАК СпособНачисленияВалюта,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.СпособНачисления.ПериодНачисления КАК СпособНачисленияПериодНачисления,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Ставка КАК Ставка,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ДатаНачалаСрокаАренды КАК ДатаНачалаСрокаАренды,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ДатаОкончанияСрокаАренды КАК ДатаОкончанияСрокаАренды
	|	ИЗ
	|		Документ.АР_ЗаключениеДоговораАренды.ПостояннаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты
	|	ГДЕ
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка) КАК ОбъектыАренды";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ЗаключениеДоговораАренды.ПФ_MXL_ПротоколСогласованияЦены");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьЗаголовка = Макет.ПолучитьОбласть("Заголовок");
		
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ.Ссылка);
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		//только для первого листа выводим текст "Приложение 1"
		ОбластьЗаголовка.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовка);
		
		Область = Макет.ПолучитьОбласть("Протокол");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.ДатаПротокола = Формат(Шапка.ДатаПротокола, "ДФ='д ММММ гггг'");
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Область.Параметры.РуководительОрганизацииРП, Руководители.Руководитель.Пол) Тогда
			Область.Параметры.РуководительОрганизацииРП = Руководители.РуководительПредставление;
		КонецЕсли;
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		
		//теперь для каждого объекта формируем свой протокол согласования
		ЗапросОбъекты.УстановитьПараметр("Ссылка", Документ.Ссылка);
		
		ОбъектыАренды = ЗапросОбъекты.Выполнить().Выбрать();
		Индекс = 1;
		Пока ОбъектыАренды.Следующий() Цикл
			Область.Параметры.НомерПротокола = Шапка.НомерПротокола+"\"+Индекс;
			Область.Параметры.Заполнить(ОбъектыАренды);
			Область.Параметры.ДатаНачалаАренды = Формат(ОбъектыАренды.ДатаНачалаАренды,"ДФ='д ММММ гггг'");
			Область.Параметры.ДатаОкончанияАренды = Формат(ОбъектыАренды.ДатаОкончанияАренды,"ДФ='д ММММ гггг'");
			
			ФорматнаяСтрока = "Л=ru_RU;НП=Истина;НД=Истина;ДП=Ложь";
			ПараметрыПрописи = ОбъектыАренды.ВалютаПараметрыПрописи;
			
			Ставка = АР_ОбщиеПроцедуры.ПолучитьСтавку(ОбъектыАренды.Ставка);
			СтавкаПрописью = НРег(ЧислоПрописью(Ставка, ФорматнаяСтрока, ПараметрыПрописи));
			СтавкаЧислом = "" + Ставка + " " + Сред(СтавкаПрописью, Найти(СтавкаПрописью, Лев(ПараметрыПрописи, 3)));
			
			Область.Параметры.Ставка = СтавкаЧислом;
			Область.Параметры.СтавкаПрописью = СтавкаПрописью;
			Область.Параметры.ПериодНачисленияПрописью = НРег(ОбъектыАренды.ПериодАренды);
			
			СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ОбъектыАренды.ВидСтавкиНДС, Шапка.Дата); 
			Если ЗначениеЗаполнено(СтавкаНДС) И НЕ (СтавкаНДС = Перечисления.СтавкиНДС.БезНДС) Тогда
				ТекстНДС = "Арендная плата облагается НДС по ставке " + СтавкаНДС;
			Иначе
				ТекстНДС = "Арендная плата НДС не облагается";
			КонецЕсли;
			
			Область.Параметры.ТекстНДС = ТекстНДС;
			
			ТабличныйДокумент.Вывести(Область);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Индекс = Индекс+1;
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьПередаточногоАкта(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаключениеДоговораАренды_ПередаточныйАкт";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	АР_ЗаключениеДоговораАренды.Контрагент,
	|	АР_ЗаключениеДоговораАренды.Организация,
	|	АР_ЗаключениеДоговораАренды.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	АР_ЗаключениеДоговораАренды.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	АР_ЗаключениеДоговораАренды.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	АР_ЗаключениеДоговораАренды.ДоговорКонтрагента КАК Договор,
	|	АР_ЗаключениеДоговораАренды.Дата,
	|	АР_ЗаключениеДоговораАренды.НомерАкта,
	|	АР_ЗаключениеДоговораАренды.ДатаАкта,
	|	АР_ЗаключениеДоговораАренды.Ссылка
	|ИЗ
	|	Документ.АР_ЗаключениеДоговораАренды КАК АР_ЗаключениеДоговораАренды
	|ГДЕ
	|	АР_ЗаключениеДоговораАренды.Ссылка = &Ссылка";
	
	ЗапросОбъекты = Новый Запрос();
	ЗапросОбъекты.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыАренды.ОбъектАренды,
	|	ОбъектыАренды.Количество КАК Площадь,
	|	ОбъектыАренды.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОбъектыАренды.ДатаНачалаАренды
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Количество КАК Количество,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Услуга.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды
	|	ИЗ
	|		Документ.АР_ЗаключениеДоговораАренды.ПостояннаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты
	|	ГДЕ
	|		АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ОбъектАренды.Наименование + ""/"" + АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Услуга.Наименование,
	|		АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Количество,
	|		АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ЕдиницаИзмерения.Наименование,
	|		АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ДатаНачалаСрокаАренды
	|	ИЗ
	|		Документ.АР_ЗаключениеДоговораАренды.ПеременнаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты
	|	ГДЕ
	|		АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Ссылка = &Ссылка) КАК ОбъектыАренды";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ЗаключениеДоговораАренды.ПФ_MXL_ПередаточныйАкт");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Заголовок");
		
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ.Ссылка);
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		//только для первого листа выводим текст "Приложение 1"
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.ДатаАкта = Формат(Шапка.ДатаАкта,"ДФ='д ММММ гггг'");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Область.Параметры.РуководительОрганизации, Руководители.Руководитель.Пол) Тогда
			Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Область);
		
		
		//теперь для каждого объекта заполняем таблицу
		ЗапросОбъекты.УстановитьПараметр("Ссылка", Документ.Ссылка);
		ОбъектыАренды = ЗапросОбъекты.Выполнить().Выбрать();
		
		Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
		
		Индекс = 1;
		Пока ОбъектыАренды.Следующий() Цикл
			Область.Параметры.НомерСтроки = Индекс;
			Область.Параметры.Заполнить(ОбъектыАренды);
			Область.Параметры.ДатаНачалаАренды = Формат(ОбъектыАренды.ДатаНачалаАренды,"ДФ='д ММММ гггг'");
			
			ТабличныйДокумент.Вывести(Область);
			
			Индекс = Индекс+1;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		Область.Параметры.РуководительКонтрагента = Шапка.РуководительКонтрагента;
		
		ТабличныйДокумент.Вывести(Область);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
		
КонецФункции

#КонецЕсли
