#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаЗаявкиНаОбслуживание(НомераТаблиц)
	+ ТекстЗапросаФактическиеЗатраты(НомераТаблиц)
	+ ТекстЗапросаВыполнениеРабот(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаЗаявкиНаОбслуживание(НомераТаблиц)
	
	НомераТаблиц.Вставить("ЗаявкиРаботы", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗаявкиМатериалы", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Заявка КАК Заявка,
	|	Реквизиты.Организация,
	|	Реквизиты.ОбъектАренды,
	|	Реквизиты.ЭлементОбъекта,
	|	ТабличнаяЧасть.КомплексРабот,
	|	ТабличнаяЧасть.Работа КАК Номенклатура,
	|	ТабличнаяЧасть.Количество,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Реквизиты.Заявка.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	Документ.АР_ВыполнениеРаботПоОбслуживанию.Работы КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ВыполнениеРаботПоОбслуживанию КАК Реквизиты
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Заявка КАК Заявка,
	|	Реквизиты.Организация,
	|	Реквизиты.ОбъектАренды,
	|	Реквизиты.ЭлементОбъекта,
	|	ТабличнаяЧасть.КомплексРабот,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Количество,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Реквизиты.Заявка.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	Документ.АР_ВыполнениеРаботПоОбслуживанию.Материалы КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ВыполнениеРаботПоОбслуживанию КАК Реквизиты
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаФактическиеЗатраты(НомераТаблиц)
	
	НомераТаблиц.Вставить("ЗатратыРаботы", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗатратыМатериалы", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Заявка,
	|	Реквизиты.Организация,
	|	Реквизиты.ОбъектАренды,
	|	Реквизиты.ЭлементОбъекта,
	|	ТабличнаяЧасть.КомплексРабот,
	|	ТабличнаяЧасть.Работа КАК Номенклатура,
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.СтатьяЗатрат,
	|	ТабличнаяЧасть.Сумма
	|ИЗ
	|	Документ.АР_ВыполнениеРаботПоОбслуживанию.Работы КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ВыполнениеРаботПоОбслуживанию КАК Реквизиты
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Заявка,
	|	Реквизиты.Организация,
	|	Реквизиты.ОбъектАренды,
	|	Реквизиты.ЭлементОбъекта,
	|	ТабличнаяЧасть.КомплексРабот,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.СтатьяЗатрат,
	|	ТабличнаяЧасть.Сумма
	|ИЗ
	|	Документ.АР_ВыполнениеРаботПоОбслуживанию.Материалы КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ВыполнениеРаботПоОбслуживанию КАК Реквизиты
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВыполнениеРабот(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВыполнениеРабот", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Заявка,
	|	Реквизиты.Организация,
	|	Реквизиты.ОбъектАренды,
	|	Реквизиты.ЭлементОбъекта,
	|	ТабличнаяЧасть.КомплексРабот,
	|	ТабличнаяЧасть.Работа КАК Номенклатура,
	|	ТабличнаяЧасть.Количество,
	|	ТабличнаяЧасть.СтатьяЗатрат,
	|	ТабличнаяЧасть.Сумма,
	|	ТабличнаяЧасть.ТехОперация,
	|	ТабличнаяЧасть.ВнутренняяРабота
	|ИЗ
	|	Документ.АР_ВыполнениеРаботПоОбслуживанию.Работы КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ВыполнениеРаботПоОбслуживанию КАК Реквизиты
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Печать

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьШаблоновWord";
	КомандаПечати.Представление = НСтр("ru = 'Печать по шаблону MS Word'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиШаблоновWord";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Выполнение работ по обслуживанию""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок       = 160;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Заявка");
	
	Возврат Результат;
	
КонецФункции

#КонецЕсли