#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ЗаключениеДоговора", ДокументСсылка.ДокументОснование);
	Запрос.УстановитьПараметр("ДатаСреза", ДокументСсылка.Дата - 1);

	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаСтатусыОбъектовАренды(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаСтатусыОбъектовАренды(НомераТаблиц)
	
	НомераТаблиц.Вставить("СтатусыОбъектовАренды", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ДокументОснование КАК ЗаключениеДоговора,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	СтатусыОбъектовАренды.Услуга,
	|	СтатусыОбъектовАренды.ОбъектАренды,
	|	СтатусыОбъектовАренды.ДатаНачалаАренды,
	|	ВЫБОР
	|		КОГДА СтатусыОбъектовАренды.ДатаОкончанияАренды < Реквизиты.ДатаОкончанияДоговора
	|			ТОГДА СтатусыОбъектовАренды.ДатаОкончанияАренды
	|		ИНАЧЕ Реквизиты.ДатаОкончанияДоговора
	|	КОНЕЦ КАК ДатаОкончанияАренды,
	|	СтатусыОбъектовАренды.Счетчик,
	|	СтатусыОбъектовАренды.КоэффициентОтнесенияСчетчика,
	|	СтатусыОбъектовАренды.Статус
	|ИЗ
	|	РегистрСведений.АР_СтатусыОбъектовАренды.СрезПоследних(&ДатаСреза, ЗаключениеДоговора = &ЗаключениеДоговора) КАК СтатусыОбъектовАренды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_РасторжениеДоговораАренды КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Печать

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ПередаточныйАкт";
	КомандаОтправки.Представление = НСтр("ru = 'Передаточный акт'");
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "СоглашениеОРасторжении";
	КомандаОтправки.Представление = НСтр("ru = 'Соглашение о расторжении договора аренды'");
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПередаточныйАкт";
	КомандаПечати.Представление = НСтр("ru = 'Передаточный акт'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СоглашениеОРасторжении";
	КомандаПечати.Представление = НСтр("ru = 'Соглашение о расторжении договора аренды'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьШаблоновWord";
	КомандаПечати.Представление = НСтр("ru = 'Печать по шаблону MS Word'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиШаблоновWord";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Расторжение договора аренды""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок       = 160;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПередаточныйАкт") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПередаточныйАкт", "Передаточный акт", ПечатьПередаточногоАкта(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СоглашениеОРасторжении") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СоглашениеОРасторжении", "Соглашение о расторжении договора аренды", ПечатьСоглашенияОРасторжении(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьПередаточногоАкта(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_РасторжениеДоговораАренды_ПередаточныйАкт";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	АР_РасторжениеДоговораАренды.Контрагент,
	|	АР_РасторжениеДоговораАренды.Организация,
	|	АР_РасторжениеДоговораАренды.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	АР_РасторжениеДоговораАренды.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	АР_РасторжениеДоговораАренды.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	АР_РасторжениеДоговораАренды.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	АР_РасторжениеДоговораАренды.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	АР_РасторжениеДоговораАренды.ДоговорКонтрагента КАК Договор,
	|	АР_РасторжениеДоговораАренды.Дата,
	|	АР_РасторжениеДоговораАренды.НомерАкта,
	|	АР_РасторжениеДоговораАренды.ДатаАкта,
	|	АР_РасторжениеДоговораАренды.Ссылка
	|ИЗ
	|	Документ.АР_РасторжениеДоговораАренды КАК АР_РасторжениеДоговораАренды
	|ГДЕ
	|	АР_РасторжениеДоговораАренды.Ссылка = &Ссылка";
	
	ЗапросОбъекты = Новый Запрос();
	ЗапросОбъекты.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыАренды.ОбъектАренды,
	|	ОбъектыАренды.Количество КАК Площадь,
	|	ОбъектыАренды.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОбъектыАренды.ДатаОкончанияАренды
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		АР_РасторжениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды,
	|		АР_РасторжениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Количество КАК Количество,
	|		АР_РасторжениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Услуга.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|		АР_РасторжениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды
	|	ИЗ
	|		Документ.АР_РасторжениеДоговораАренды.ПостояннаяЧастьАренднойПлаты КАК АР_РасторжениеДоговораАрендыПостояннаяЧастьАренднойПлаты
	|	ГДЕ
	|		АР_РасторжениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ОбъектАренды.Наименование + ""/"" + АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Услуга.Наименование,
	|		АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Количество,
	|		АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ЕдиницаИзмерения.Наименование,
	|		АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ДатаОкончанияСрокаАренды
	|	ИЗ
	|		Документ.АР_РасторжениеДоговораАренды.ПеременнаяЧастьАренднойПлаты КАК АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты
	|	ГДЕ
	|		АР_РасторжениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Ссылка = &Ссылка) КАК ОбъектыАренды";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_РасторжениеДоговораАренды.ПФ_MXL_ПередаточныйАктВозврат");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Заголовок");
		
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ);
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		//только для первого листа выводим текст "Приложение 1"
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.ДатаАкта = Формат(Шапка.ДатаАкта,"ДФ='д ММММ гггг'");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Область.Параметры.РуководительОрганизации, Руководители.Руководитель.Пол) Тогда
			Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Область);
		
		
		//теперь для каждого объекта заполняем таблицу
		ЗапросОбъекты.УстановитьПараметр("Ссылка", Документ);
		ОбъектыАренды = ЗапросОбъекты.Выполнить().Выбрать();
		
		Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
		
		Индекс = 1;
		Пока ОбъектыАренды.Следующий() Цикл
			Область.Параметры.НомерСтроки = Индекс;
			Область.Параметры.Заполнить(ОбъектыАренды);
			Область.Параметры.ДатаОкончанияАренды = Формат(ОбъектыАренды.ДатаОкончанияАренды,"ДФ='д ММММ гггг'");
			
			ТабличныйДокумент.Вывести(Область);
			
			Индекс = Индекс+1;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		Область.Параметры.РуководительКонтрагента = Шапка.РуководительКонтрагента;
		
		ТабличныйДокумент.Вывести(Область);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
		
КонецФункции

Функция ПечатьСоглашенияОРасторжении(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_РасторжениеДоговораАренды_СоглашениеОРасторжении";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АР_РасторжениеДоговораАренды.Контрагент,
	|	АР_РасторжениеДоговораАренды.Организация,
	|	АР_РасторжениеДоговораАренды.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	АР_РасторжениеДоговораАренды.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	АР_РасторжениеДоговораАренды.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	АР_РасторжениеДоговораАренды.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	АР_РасторжениеДоговораАренды.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	АР_РасторжениеДоговораАренды.ДоговорКонтрагента КАК Договор,
	|	АР_РасторжениеДоговораАренды.ДатаОкончанияДоговора КАК ДатаРасторжения,
	|	АР_РасторжениеДоговораАренды.ДатаСоглашения КАК ДатаСоглашения,
	|	АР_РасторжениеДоговораАренды.НомерСоглашения,
	|	АР_РасторжениеДоговораАренды.Дата,
	|	АР_РасторжениеДоговораАренды.Ссылка
	|ИЗ
	|	Документ.АР_РасторжениеДоговораАренды КАК АР_РасторжениеДоговораАренды
	|ГДЕ
	|	АР_РасторжениеДоговораАренды.Ссылка = &Ссылка";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_РасторжениеДоговораАренды.ПФ_MXL_СоглашениеОРасторжении");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос.УстановитьПараметр("Ссылка", Документ);
		
		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Макет.Параметры.Заполнить(Шапка);
		Макет.Параметры.ДатаСоглашения = Формат(Шапка.ДатаСоглашения,"ДФ='д ММММ гггг'");
		Макет.Параметры.ДатаРасторжения = Формат(Шапка.ДатаРасторжения,"ДФ='д ММММ гггг'");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Макет.Параметры.РуководительОрганизацииРП, Руководители.Руководитель.Пол) Тогда
			Макет.Параметры.РуководительОрганизацииРП = Руководители.РуководительПредставление;
		КонецЕсли;
		Макет.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		
		ТабличныйДокумент.Вывести(Макет);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли
