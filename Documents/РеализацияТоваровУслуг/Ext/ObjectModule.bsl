#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Признак для передачи в счет-фактуру для того, чтобы не устанавливать статус повторно.
Перем УстановленСтатусДокумента; 

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура СкопироватьТабличныеЧастиСчетаНаОплату(ИменаТабличныхЧастей, СписокСчетовНаОплату, ОчищатьСтроки = Истина)
	Перем РеквизитыОснования, ВозвратнаяТараКРеализации;
	
	МассивИмен = СтрРазделить(ИменаТабличныхЧастей, ", ", Ложь);
	
	Если ОчищатьСтроки Тогда
		Для Каждого ИмяТабЧасти Из МассивИмен Цикл
			ЭтотОбъект[ИмяТабЧасти].Очистить();
		КонецЦикла;
	КонецЕсли; 
	
	СкопироватьТовары         = (МассивИмен.Найти("Товары") <> Неопределено);
	СкопироватьУслуги         = (МассивИмен.Найти("Услуги") <> Неопределено);
	СкопироватьВозвратнуюТару = (МассивИмен.Найти("ВозвратнаяТара") <> Неопределено);
	
	ТаблицаРеализацийПоСчету = Документы.РеализацияТоваровУслуг.РеализацииПоСчетам(СписокСчетовНаОплату);
	РеквизитыСчетовНаОплату = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокСчетовНаОплату, "Контрагент, ДоговорКонтрагента");
	
	Для каждого СчетНаОплату Из СписокСчетовНаОплату Цикл
		
		РеквизитыСчета = РеквизитыСчетовНаОплату[СчетНаОплату];
		ЗаполнятьСчетПоСтроке = (Контрагент = РеквизитыСчета.Контрагент) 
			И (РеквизитыСчета.ДоговорКонтрагента = ДоговорКонтрагента ИЛИ НЕ ЗначениеЗаполнено(РеквизитыСчета.ДоговорКонтрагента));
		
		РеализацииПоСчетуНаОплату = ОбщегоНазначения.ВыгрузитьКолонку(
			ТаблицаРеализацийПоСчету.Скопировать(Новый Структура("СчетНаОплату", СчетНаОплату)),
			"Реализация");
	
		ЭтаРеализация = РеализацииПоСчетуНаОплату.Найти(Ссылка);
		Если ЭтаРеализация <> Неопределено Тогда
			РеализацииПоСчетуНаОплату.Удалить(ЭтаРеализация);
		КонецЕсли;
		
		ПокупательНалоговыйАгентПоНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
		ВедетсяУчетНДСПоФЗ335 = УчетНДС.ВедетсяУчетНДСПоФЗ335(Дата);
		
		// Товары и Услуги
		Если СкопироватьТовары Или СкопироватьУслуги Тогда
			
			ТоварыУслугиКРеализации = Документы.РеализацияТоваровУслуг.ТоварыУслугиКРеализацииПоСчету(СчетНаОплату, РеализацииПоСчетуНаОплату);
			
			Для Каждого СтрокаОснования Из ТоварыУслугиКРеализации Цикл
				
				Если СтрокаОснования.ЭтоУслуга Тогда
					Если СкопироватьУслуги Тогда
						СтрокаУслуги = Услуги.Добавить();
						
						Если ЗаполнятьСчетПоСтроке Тогда
							СтрокаУслуги.СчетНаОплатуПокупателю = СчетНаОплату;
						КонецЕсли; 
						
						ЗаполнитьЗначенияСвойств(СтрокаУслуги, СтрокаОснования);
					КонецЕсли;
				Иначе
					Если СкопироватьТовары Тогда
						СтрокаТовары = Товары.Добавить();
						
						Если ЗаполнятьСчетПоСтроке Тогда
							СтрокаТовары.СчетНаОплатуПокупателю = СчетНаОплату;
						КонецЕсли; 
						
						ЗаполнитьЗначенияСвойств(СтрокаТовары, СтрокаОснования);
						Если ПокупательНалоговыйАгентПоНДС = Истина
							И ВедетсяУчетНДСПоФЗ335 Тогда 
							СтрокаТовары.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(Дата);
							СтрокаТовары.СуммаНДС = 0;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетНаОплату, "ДокументБезНДС, СуммаВключаетНДС");
			ДокументБезНДС = РеквизитыОснования.ДокументБезНДС;
			
		КонецЕсли;
		
		// Пересчет сумм в табличной части Товары
		Если СкопироватьТовары И РеквизитыОснования.СуммаВключаетНДС <> СуммаВключаетНДС Тогда
			Для Каждого СтрокаТЧ Из Товары Цикл
				СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
				СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, 0, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
				Если НЕ (ПокупательНалоговыйАгентПоНДС = Истина
					И ВедетсяУчетНДСПоФЗ335) Тогда 
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Пересчет сумм в табличной части Услуги
		Если СкопироватьУслуги И  РеквизитыОснования.СуммаВключаетНДС <> СуммаВключаетНДС Тогда
			Для Каждого СтрокаТЧ Из Услуги Цикл
				СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
				СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, СтрокаТЧ.Сумма, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
			КонецЦикла;
		КонецЕсли;
		
		// Возвратная тара
		Если ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") И СкопироватьВозвратнуюТару Тогда
			Если ВозвратнаяТараКРеализации = Неопределено Тогда
				ВозвратнаяТараКРеализации = Документы.РеализацияТоваровУслуг.ВозвратнаяТараКРеализацииПоСчету(СчетНаОплату, РеализацииПоСчетуНаОплату);
			ИНаче
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
					Документы.РеализацияТоваровУслуг.ВозвратнаяТараКРеализацииПоСчету(СчетНаОплату, РеализацииПоСчетуНаОплату), 
					ВозвратнаяТараКРеализации);
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла; 
	
	Если ВозвратнаяТараКРеализации <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВозвратнаяТараКРеализации, ВозвратнаяТара);
	КонецЕсли; 
КонецПроцедуры

Процедура ЗаполнитьПоПоступлению(ИмяТабЧасти, Режим, Поступление, ИмяТабЧастиИсточника = Неопределено, ЗаполнятьСчетаУчета = Истина) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Поступление) Тогда
		Возврат;
	КонецЕсли;
	
	ПлательщикНДС	= УчетнаяПолитика.ПлательщикНДС(Организация, Дата);
	// При реализации в страны ЕАЭС заполняем и отражаем колонку "Код ТН ВЭД" для целей счета-фактуры и книги продаж.
	СтранаРегистрацииКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "СтранаРегистрации");
	РеализацияВЕАЭС              = УчетНДС.ГосударствоЧленТаможенногоСоюза(СтранаРегистрацииКонтрагента);
	ВедетсяУчетНДСПоФЗ150        = УчетНДС.ВедетсяУчетНДСПоФЗ150(Дата);
	ЗаполнятьТНВЭД = РеализацияВЕАЭС И ВедетсяУчетНДСПоФЗ150;
	
	ПокупательНалоговыйАгентПоНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
	ВедетсяУчетНДСПоФЗ335 = УчетНДС.ВедетсяУчетНДСПоФЗ335(Дата);
	
	ТабличнаяЧасть = ЭтотОбъект[ИмяТабЧасти];
	СтрокиДляЗаполненияСчетов = Новый Массив;
	
	Если ТабличнаяЧасть.Количество() > 0 И Режим = "Заполнить" Тогда
		ТабличнаяЧасть.Очистить();
	КонецЕсли;
	
	ТабЧастьИсточник = ?(ИмяТабЧастиИсточника = Неопределено, ИмяТабЧасти, ИмяТабЧастиИсточника);

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Поступление", Поступление);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.НомерСтроки,
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Количество" +?(ТабЧастьИсточник = "Товары",",
	|	ПоступлениеТоваровУслугТовары.КоличествоМест,
	|	ПоступлениеТоваровУслугТовары.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслугТовары.Коэффициент,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.НомерГТД,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения", "") + "
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг."+ТабЧастьИсточник + " КАК ПоступлениеТоваровУслугТовары
	|
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Поступление
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеТоваровУслугТовары.НомерСтроки";
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ДанныеОбъекта = Новый Структура(
		"Дата, Организация, ДеятельностьНаПатенте, Склад,
		|ЭтоКомиссия, Реализация, ТипЦен, СуммаВключаетНДС, ДокументБезНДС");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЭтотОбъект);
	ДанныеОбъекта.Реализация	= Истина;
	ДанныеОбъекта.ЭтоКомиссия	= (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора")
		= Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
		
	Если Не ЗначениеЗаполнено(ДанныеОбъекта.ТипЦен) Тогда
		ДанныеОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	КонецЕсли;
		
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь);
		
	Для Каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТоваров);
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТоваров.Номенклатура);
		Если СведенияОНоменклатуре = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИмяТабЧасти = "Товары" И ТабЧастьИсточник = "Товары" Тогда
			
			Если НЕ ПлательщикНДС Тогда
				Если НЕ ЗаполнятьТНВЭД Тогда
					СтрокаТабличнойЧасти.СтавкаНДС = СведенияОНоменклатуре.СтавкаНДС;
				Иначе
					СтрокаТабличнойЧасти.СтавкаНДС = Перечисления.СтавкиНДС.НДС0;
				КонецЕсли;
			ИначеЕсли ПокупательНалоговыйАгентПоНДС = Истина
				И ВедетсяУчетНДСПоФЗ335 Тогда 
				СтрокаТабличнойЧасти.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(Дата);
				СтрокаТабличнойЧасти.СуммаНДС = 0;
			КонецЕсли;
			
			Если ЗаполнятьТНВЭД Тогда 
				СтрокаТабличнойЧасти.КодТНВЭД = СведенияОНоменклатуре.КодТНВЭД;
			КонецЕсли;
				
		КонецЕсли;
		
		Если ИмяТабЧасти = "Товары" И ТабЧастьИсточник = "Оборудование" Тогда
			
			СтрокаТабличнойЧасти.ЕдиницаИзмерения	= СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти.Коэффициент		= СведенияОНоменклатуре.Коэффициент;
			СтрокаТабличнойЧасти.СтавкаНДС			= СведенияОНоменклатуре.СтавкаНДС;
			
		КонецЕсли;
		
		Если ИмяТабЧасти = "Услуги" Тогда 
			
			СтрокаТабличнойЧасти.Содержание = СведенияОНоменклатуре.НаименованиеПолное;
			
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Цена = СведенияОНоменклатуре.Цена;
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ?(ИмяТабЧасти = "Услуги", 1, 0));
		
		Если ИмяТабЧасти <> "ВозвратнаяТара" 
			И НЕ (ПокупательНалоговыйАгентПоНДС = Истина
			И ВедетсяУчетНДСПоФЗ335)Тогда
			
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
			
		КонецЕсли;
		
		СтрокиДляЗаполненияСчетов.Добавить(СтрокаТабличнойЧасти);
		
	КонецЦикла;
	
	Если ЗаполнятьСчетаУчета Тогда
		
		СчетаУчетаВДокументах.ЗаполнитьСтроки(
			СтрокиДляЗаполненияСчетов, ИмяТабЧасти, ЭтотОбъект, Документы.РеализацияТоваровУслуг);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоСчету(ИменаТабличныхЧастей, СписокСчетовНаОплату, ОчишатьСтроки = Истина) Экспорт
	
	Если СписокСчетовНаОплату.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкопироватьТабличныеЧастиСчетаНаОплату(ИменаТабличныхЧастей, СписокСчетовНаОплату, ОчишатьСтроки);
	
	МассивИмен = СтрРазделить(ИменаТабличныхЧастей, ", ", Ложь);
	Для Каждого ИмяТабЧасти Из МассивИмен Цикл
		СчетаУчетаВДокументах.ЗаполнитьСтроки(ЭтотОбъект[ИмяТабЧасти], ИмяТабЧасти, ЭтотОбъект, Документы.РеализацияТоваровУслуг);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОснованию(МассивОснований)
	
	Основание = МассивОснований[0];
	
	// Поддерживается множественный ввод на основании только для счета, для остальных документов - берется первый из списка оснований
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		
		// Заполним реквизиты шапки по документу основанию.
		ВидОперации = Документы.РеализацияТоваровУслуг.ОпределитьВидОперацииПоДокументуОснованию(МассивОснований);
		
		ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, 
			"АдресДоставки, СтруктурнаяЕдиница, СпособДоставки, СпособДоставки.Контрагент");
		
		СпособДоставки = ДанныеОснования.СпособДоставки;
		АдресДоставки  = ДанныеОснования.АдресДоставки;
		Перевозчик     = ДанныеОснования.СпособДоставкиКонтрагент;
		
		СчетНаОплатуПокупателю = Основание;
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание, Истина);
		
		ПараметрыОбъекта = Новый Структура("ВидОперации, ДеятельностьНаПатенте");
		ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, ЭтотОбъект);
		
		МассивВидовДоговоров   = Документы.РеализацияТоваровУслуг.ПолучитьМассивВидовДоговоров(ВидОперации, ДеятельностьНаПатенте);
		ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
		
		Если МассивВидовДоговоров.Найти(ВидДоговораКонтрагента) = Неопределено Тогда
			ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
		
		// заполним банковский счет
		Если ЗначениеЗаполнено(ДанныеОснования.СтруктурнаяЕдиница)
		   И ТипЗнч(ДанныеОснования.СтруктурнаяЕдиница) = БухгалтерскийУчетКлиентСерверПереопределяемый.ТипЗначенияБанковскогоСчетаОрганизации() Тогда
			БанковскийСчетОрганизации = ДанныеОснования.СтруктурнаяЕдиница;
		КонецЕсли;
		
		ИменаТабличныхЧастей = "Товары, Услуги, ВозвратнаяТара";
		СкопироватьТабличныеЧастиСчетаНаОплату(ИменаТабличныхЧастей, МассивОснований);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, 
			"ВалютаДокумента, ПодразделениеОрганизации, ВидОперации, Организация, Склад");
			
		Если ДанныеОснования.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование Тогда
			ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование;
		Иначе
			ВидОперации = Документы.РеализацияТоваровУслуг.ОпределитьВидОперацииПоДокументуОснованию(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Основание));
		КонецЕсли;
		
		ТипСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеОснования.Склад, "ТипСклада");
		Если ТипСклада <> Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
			Склад = ДанныеОснования.Склад;
		КонецЕсли;
		
		Организация              = ДанныеОснования.Организация;
		ВалютаДокумента          = ДанныеОснования.ВалютаДокумента;
		ПодразделениеОрганизации = ДанныеОснования.ПодразделениеОрганизации;
		ЗаполнениеДокументов.Заполнить(ЭтотОбъект);
		
		// Флаги включения налогов.
		Если ЗначениеЗаполнено(ТипЦен) Тогда
			СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипЦен, "ЦенаВключаетНДС");
		Иначе
			СуммаВключаетНДС = Истина;
		КонецЕсли;
		
		// Счета учета будут заполнены позднее, при вызове ЗаполнениеДокументов.Заполнить()
		ЗаполнитьПоПоступлению("Товары", "Заполнить", Основание, "Товары", Ложь);
		ЗаполнитьПоПоступлению("ВозвратнаяТара", "Заполнить", Основание, "ВозвратнаяТара", Ложь);
		Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование Тогда
			ЗаполнитьПоПоступлению("Товары", "Добавить", Основание, "Оборудование", Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено Тогда
		
		Если ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("АдресТаблицыНоменклатуры") Тогда
			
			СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
			ЗаполнитьИзТаблицыНоменклатуры(ДанныеЗаполнения.АдресТаблицыНоменклатуры, СуммаВключаетНДС);
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
				И ДанныеЗаполнения.Свойство("Основание")
				И ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ФиксированныйМассив") Тогда
				
				Если ДанныеЗаполнения.Основание.Количество() > 0 
					И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание[0].Метаданные()) Тогда
				
					ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения.Основание);
					ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения.Основание[0], Истина);
					
				КонецЕсли; 
				
				Возврат;
		ИначеЕсли ТипДанныхЗаполнения = Тип("ФиксированныйМассив") 
				И ДанныеЗаполнения.Количество() > 0 
				И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения[0].Метаданные()) Тогда
					
				ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
				ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения[0], Истина);
				
				Возврат;
		Иначе
			Если ТипДанныхЗаполнения <> Тип("Структура")
				И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
				ДокументОснование = ДанныеЗаполнения;
			ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
				И ДанныеЗаполнения.Свойство("Основание")
				И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание.Метаданные()) Тогда
				ДокументОснование = ДанныеЗаполнения.Основание;
			ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
				И ДанныеЗаполнения.Свойство("ЭтоУниверсальныйДокумент") Тогда
				ЭтоУниверсальныйДокумент = ДанныеЗаполнения.ЭтоУниверсальныйДокумент;
			КонецЕсли;

			Если ДокументОснование <> Неопределено Тогда
				ЗаполнитьПоДокументуОснованию(
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОснование));
			Иначе
				СуммаВключаетНДС = Истина;
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		СуммаВключаетНДС = Истина;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(СпособДоставки) 
		И ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией") Тогда
		СпособДоставки = Справочники.СпособыДоставки.Самовывоз;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ТекущаяДата = Дата;
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	НомерЧекаККМ = 0;
	
	
	СчетНаОплатуПокупателю = Документы.СчетНаОплатуПокупателю.ПустаяСсылка();
	
	ЗачетАвансов.Очистить();
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.ПроверитьИзменитьАдресДоставки(ЭтотОбъект, ТекущаяДата);
	
	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
	ВалютаДокумента, Дата);
	
	КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;
	
	Если Товары.Количество() > 0 Тогда
		МассивТовары = Новый Массив(Товары.Количество());
		
		Товары.ЗагрузитьКолонку(МассивТовары, "Себестоимость");
		Товары.ЗагрузитьКолонку(МассивТовары, "ДокументОприходования");
		
		Если ОбъектКопирования.ЕстьМаркируемаяПродукцияГИСМ Тогда
			Товары.ЗагрузитьКолонку(МассивТовары, "КиЗ_ГИСМ");
		КонецЕсли;
	КонецЕсли;
	
	ШтрихкодыУпаковок.Очистить();
	
	РаботаСНоменклатурой.ОбновитьСодержаниеУслуг(Услуги, Дата);
	РаботаСНоменклатурой.ОбновитьСодержаниеУслуг(АгентскиеУслуги, Дата);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(Номер) Тогда
		НН=1;
		Пока НН<=9999 Цикл
			ПеремНомерДоп = Формат(НН, "ЧГ=0");
			НомерДоп_ =  Формат(Дата, "ДФ=yyyyMM")+ "/" +(ПеремНомерДоп);
			НомерДок = Прав(НомерДоп_, 11);
			ПоискДок=Документы.АР_НачислениеАренднойПлаты.НайтиПоНомеру(НомерДок,Дата);
			Если Не ЗначениеЗаполнено(ПоискДок) Тогда
				ПоискДок=Документы.РеализацияТоваровУслуг.НайтиПоНомеру(НомерДок,Дата);
				Если Не ЗначениеЗаполнено(ПоискДок) Тогда
					Номер=НомерДок;
					Прервать;
				КонецЕсли;
			КонецЕсли;
			НН=НН+1;
		КонецЦикла;
	КонецЕсли;		
	
	//Leva	
	//Если ЭтоНовый() и Дата>=НачалоМесяца("20130901") Тогда
	//	Запрос =  Новый Запрос;
	//	Запрос.Текст="ВЫБРАТЬ
	//	|	РеализацияТоваровУслуг.Номер КАК НомерДоп,
	//	|	РеализацияТоваровУслуг.Ссылка
	//	|ИЗ
	//	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	//	|ГДЕ
	//	|	РеализацияТоваровУслуг.Дата МЕЖДУ &НачМесяца И &КонМесяца
	//	|
	//	|СГРУППИРОВАТЬ ПО
	//	|	РеализацияТоваровУслуг.Ссылка,
	//	|	РеализацияТоваровУслуг.Номер
	//	|
	//	|ОБЪЕДИНИТЬ ВСЕ
	//	|
	//	|ВЫБРАТЬ
	//	|	АР_НачислениеАренднойПлаты.Номер,
	//	|	АР_НачислениеАренднойПлаты.Ссылка
	//	|ИЗ
	//	|	Документ.АР_НачислениеАренднойПлаты КАК АР_НачислениеАренднойПлаты
	//	|ГДЕ
	//	|	АР_НачислениеАренднойПлаты.Дата МЕЖДУ &НачМесяца И &КонМесяца
	//	|
	//	|СГРУППИРОВАТЬ ПО
	//	|	АР_НачислениеАренднойПлаты.Ссылка,
	//	|	АР_НачислениеАренднойПлаты.Номер";
	//	
	//	Запрос.УстановитьПараметр("НачМесяца",НачалоМесяца(Дата));
	//	Запрос.УстановитьПараметр("КонМесяца", КонецМесяца(Дата));
	//	ДатаДок = Запрос.Выполнить().Выбрать();
	//	ПеремНомерДоп = 0;
	//	
	//	Пока ДатаДок.Следующий() Цикл
	//		
	//		НомерДопРаб = Сред(ДатаДок.НомерДоп, 8, 11);
	//		
	//		Попытка
	//			НомерДР = Число(НомерДопРаб);
	//		Исключение
	//			НомерДР = 0;
	//		КонецПопытки;
	//		
	//		
	//		Если ЗначениеЗаполнено(НомерДопРаб) Тогда
	//			Попытка 
	//				
	//				ЧЛ=Число(НомерДопРаб);
	//				
	//				Если ПеремНомерДоп < Число(НомерДопРаб) тогда
	//					//Сообщить(Число(НомерДопРаб));
	//					ПеремНомерДоп = Число(НомерДопРаб);
	//				КонецЕсли;
	//				
	//			исключение
	//				
	//			КонецПопытки;
	//			
	//		КонецЕсли;
	//		
	//	КонецЦикла;
	//	ПеремНомерДоп = ПеремНомерДоп + 1;
	//	ПеремНомерДоп = Формат(ПеремНомерДоп, "ЧГ=0");
	//	Если ДатаДок.Количество()>0 Тогда             // 
	//		//ДатаДок.Следующий(); //позиционируем выборку на единственной записи в ней
	//		//РанееЗанятыйНомер = ДатаДок.НомерДоп;
	//		//РанееЗанятыйНомер = Прав(РанееЗанятыйНомер, СтрДлина(РанееЗанятыйНомер)-7); //отсекли префикс
	//		//РанееЗанятыйНомерЧисло = Число(РанееЗанятыйНомер);
	//		//НомерДоп_ =  Формат(Дата, "ДФ=yyyyMM")+ "/" +(сч+1) ;
	//		НомерДоп_ =  Формат(Дата, "ДФ=yyyyMM")+ "/" +(ПеремНомерДоп);
	//		Номер = Прав(НомерДоп_, 11); 
	//	ИНАЧЕ
	//		//первый док в этом месяце
	//		Номер =  Формат(Дата, "ДФ=yyyyMM") + "/" + "1";
	//		
	//	КонецЕсли;
	//КонецЕсли;
	
	//ДокументОбъект.Записать();
	//Leva
	
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару")
		И ВозвратнаяТара.Количество() > 0 Тогда
		ВозвратнаяТара.Очистить();
	КонецЕсли;
	
	ЭтоОтгрузка = ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности;
	
	Если ЭтоОтгрузка Тогда
		// Для вида операциия "Отгрузка ..." синхронизацию счета-фактуры делаем всегда, т.к. зависит от учетной политики.
		НачислятьНДСПоОтгрузке = УчетнаяПолитика.НачислятьНДСПоОтгрузке(Организация, Дата);
		ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект);
		Если Не НачислятьНДСПоОтгрузке Тогда
			ПараметрыДействия.СостояниеФлага = Истина;
		КонецЕсли;
		УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
	КонецЕсли;
	
	Если УчетнаяПолитика.СпособОценкиМПЗ(Организация, Дата) <> Перечисления.СпособыОценки.ФИФО 
		И НЕ ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов")
		И Товары.Количество() > 0 Тогда
		
		МассивТовары = Новый Массив(Товары.Количество());
		Товары.ЗагрузитьКолонку(МассивТовары, "ДокументОприходования");
		
	КонецЕсли; 

	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	
	ЭтоКомиссия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ДоговорКонтрагента, "ВидДоговора") = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	
	// Почистим лишние табличные части и реквизиты.
	Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары Тогда
	
		Услуги.Очистить();
		АгентскиеУслуги.Очистить();
		ВозвратнаяТара.Очистить();
	
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги Тогда
		
		Товары.Очистить();
		АгентскиеУслуги.Очистить();
		ВозвратнаяТара.Очистить();
		
		Если Справочники.Склады.ИспользуетсяНесколькоСкладов() Тогда
			Склад = Справочники.Склады.ПустаяСсылка();
		КонецЕсли; 
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование Тогда
		Для каждого СтрокаТаблицы Из ЭтотОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.НомерГТД)Тогда
				СтрокаТаблицы.НомерГТД = Неопределено;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения)Тогда
				СтрокаТаблицы.СтранаПроисхождения = Неопределено;
			КонецЕсли;
		КонецЦикла;
	
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности Тогда
		АгентскиеУслуги.Очистить();
		
	КонецЕсли;
	
	// Если передаем товар на комиссию, то услуг не может быть
	Если ЭтоКомиссия Тогда
		Услуги.Очистить();
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "Товары")
		+ УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "Услуги")
		+ УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "АгентскиеУслуги");
		
	// Для реализации не в страны ЕАЭС очистим колонку "Код ТН ВЭД"
	СтранаРегистрацииКонтрагента  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "СтранаРегистрации");
	РеализацияВЕАЭС               = УчетНДС.ГосударствоЧленТаможенногоСоюза(СтранаРегистрацииКонтрагента);
	ВедетсяУчетНДСПоФЗ150         = УчетНДС.ВедетсяУчетНДСПоФЗ150(Дата);
	ЗаполнятьКодТНВЭД = РеализацияВЕАЭС И ВедетсяУчетНДСПоФЗ150;
	
	// Если покупатель исполняет обязанности налогового агента, то очистим сумму НДС.
	ВедетсяУчетНДСПоФЗ335         = УчетНДС.ВедетсяУчетНДСПоФЗ335(Дата);
	ПокупательНалоговыйАгентПоНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
	
	Если НЕ ЗаполнятьКодТНВЭД Тогда 
		Для каждого СтрокаТаблицы Из ЭтотОбъект.Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.КодТНВЭД)Тогда
				СтрокаТаблицы.КодТНВЭД = Неопределено;
			КонецЕсли;
			Если ПокупательНалоговыйАгентПоНДС = Истина
				И ВедетсяУчетНДСПоФЗ335 Тогда 
				СтрокаТаблицы.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(Дата);
				СтрокаТаблицы.СуммаНДС = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);
	
	// Для вида операциия "Отгрузка ..." синхронизацию счета-фактуры делаем всегда, т.к. зависит от учетной политики.
	Если НЕ ЭтоОтгрузка И ПравоДоступа("Изменение", Метаданные.Документы.СчетФактураВыданный) Тогда
		ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект);
		Если ЭтоКомиссия Тогда 
			ПараметрыДействия.СостояниеФлага = Истина;
		КонецЕсли;
		УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.Документы.КорректировкаРеализации) Тогда
		Документы.КорректировкаРеализации.ОбновитьРеквизитыСвязанныхДокументовКорректировки(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	ИнтеграцияГИСМБП.УстановитьПризнакЕстьМаркируемаяПродукцияГИСМ(ЭтотОбъект);
	
	ВидЭлектронногоДокумента = Документы.РеализацияТоваровУслуг.ВидЭлектронногоДокумента(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСтатусДокумента();
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		УчетНДСПереопределяемый.ПроверитьСоответствиеРеквизитовСчетаФактурыВыданного(ЭтотОбъект,,,НЕ УстановленСтатусДокумента);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСтатусДокумента() Экспорт
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда 
		Возврат;
	КонецЕсли;
	
	НомерСчетаФактуры = "";
		
	Если ДополнительныеСвойства.Свойство("СтатусДокумента") Тогда
		// Запись из формы документа
		СтатусДокумента = ?(ДополнительныеСвойства.СтатусДокумента,
			Перечисления.СтатусыДокументовРеализации.Подписан, 
			Перечисления.СтатусыДокументовРеализации.НеПодписан);
			
		Если Не ДополнительныеСвойства.ТребуетсяСчетФактура Тогда
			СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.НеТребуется;
		ИначеЕсли ЭтотОбъект.ЭтоУниверсальныйДокумент Тогда
			СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.НеТребуется;
			Если ЗначениеЗаполнено(ДополнительныеСвойства.НомерСчетаФактуры) Тогда
				НомерСчетаФактуры = ДополнительныеСвойства.НомерСчетаФактуры;
			Иначе
				// УПД в статусе "2"
				НомерСчетаФактуры = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ЭтотОбъект.Номер, Истина, Ложь);
			КонецЕсли;
		ИначеЕсли ДополнительныеСвойства.ЕстьСчетФактура Тогда
			НомерСчетаФактуры = ДополнительныеСвойства.НомерСчетаФактуры;
			Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда 
				СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.Проведен;
			ИначеЕсли ПометкаУдаления Тогда
				СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.Отсутствует;
			Иначе
				СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.НеПроведен;
			КонецЕсли;
		Иначе
			СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.Отсутствует;
		КонецЕсли;
	Иначе
		// Необходимо сделать запись состояния счета-фактуры для случаев "Отсутствует", "Не требуется".
		СчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ЭтотОбъект.Ссылка);
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			// Движения по статусам сформирует счет-фактура
			Возврат;
		КонецЕсли;
		
		СтатусДокумента = Неопределено;
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
		НачислятьНДСПоОтгрузке = УчетнаяПолитика.НачислятьНДСПоОтгрузке(Организация, Дата);
		НеТребуетсяСФПриОтгрузке = НЕ НачислятьНДСПоОтгрузке 
			И ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности;
		Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента)
			ИЛИ ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером 
			ИЛИ ДокументБезНДС 
			ИЛИ НеТребуетсяСФПриОтгрузке Тогда 
			СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.НеТребуется;
		Иначе
			СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.Отсутствует;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановленСтатусДокумента = Истина;
	СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус            = СтатусДокумента;
	СтатусыДокумента.СтатусСФ          = СтатусСчетаФактуры;
	СтатусыДокумента.НомерСчетаФактуры = НомерСчетаФактуры;
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ЭтотОбъект.Ссылка, СтатусыДокумента);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПроверяемыеРеквизиты.Найти("Склад") = Неопределено Тогда
		ПроверяемыеРеквизиты.Добавить("Склад");
	КонецЕсли;

	// Определяем условия проведения документа:
	ЭтоКомиссия             = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ДоговорКонтрагента, "ВидДоговора") = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	ВестиУчетПоДоговорам    = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	
	// При реализации в страны ЕАЭС заполняем и отражаем колонку "Код ТН ВЭД" для целей счета-фактуры и книги продаж.
	СтранаРегистрацииКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "СтранаРегистрации");
	РеализацияВЕАЭС              = УчетНДС.ГосударствоЧленТаможенногоСоюза(СтранаРегистрацииКонтрагента);
	ВедетсяУчетНДСПоФЗ150        = УчетНДС.ВедетсяУчетНДСПоФЗ150(Дата);
	КонтролироватьЗаполнениеКодаТНВЭД = РеализацияВЕАЭС И ВедетсяУчетНДСПоФЗ150;
	
	// Исключаем из проверки реквизиты, заполнение которых стало необязательным:
	МассивНепроверяемыхРеквизитов = Новый Массив();

	// Не проверяем заполненность табличных частей (включая реквизиты), 
	// которые не используются при определенных видах операции и будут очищены в ПередЗаписью
	НеИспользуемыеТабличныеЧасти = Документы.РеализацияТоваровУслуг.НеИспользуемыеТабличныеЧасти(ВидОперации, ЭтоКомиссия);
	
	ОбщегоНазначенияБП.ИсключитьИзПроверкиНеиспользуемыеТабличныеЧасти(
		ПроверяемыеРеквизиты,
		НеИспользуемыеТабличныеЧасти);
		
	// Документ без данных о реализованных товарах или услугах
	// считаем заполненным некорректно.
	
	ОсновныеСписки = Новый Массив();
	ОсновныеСписки.Добавить("Товары");
	ОсновныеСписки.Добавить("ВозвратнаяТара");
	ОсновныеСписки.Добавить("Услуги");
	ОсновныеСписки.Добавить("АгентскиеУслуги");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ОсновныеСписки, НеИспользуемыеТабличныеЧасти);
	
	ОбщегоНазначенияБП.ИсключитьИзПроверкиОсновныеТабличныеЧасти(
		ЭтотОбъект, 
		ОсновныеСписки, 
		ПроверяемыеРеквизиты);
		
	Если НЕ ДеятельностьНаПатенте Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Патент");
	КонецЕсли;
	
	// Склад нужен только тогда, когда реализовываются материальные ценности
	Если Товары.Количество() + ВозвратнаяТара.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;

	// Проверяем корректность заполнения реквизитов шапки:
	
	Если НЕ ВестиУчетПоДоговорам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если ВестиУчетПоДоговорам И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ТекстСообщения = "";
		Если НЕ УчетВзаиморасчетов.ПроверитьВозможностьПроведенияВРеглУчете(
			ЭтотОбъект, ДоговорКонтрагента, ТекстСообщения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность",
				НСтр("ru = 'Договор'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"ДоговорКонтрагента", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;

	// Исключаем из проверки те реквизиты табличных частей, обязательность которых
	//  зависит от значений других рекивизитов в строках табличных частей:
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КодТНВЭД");
	
	// Проверяем табличную часть "Товары":
	Если Товары.Количество() > 0 Тогда
		ИмяСписка = НСтр("ru = 'Товары'");
		
		Для каждого СтрокаТовары Из Товары Цикл
			// Проверка кода ТН ВЭД.
			Если КонтролироватьЗаполнениеКодаТНВЭД 
				И СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.НДС0
				И НЕ ЗначениеЗаполнено(СтрокаТовары.КодТНВЭД) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Код ТН ВЭД'"),
					СтрокаТовары.НомерСтроки, ИмяСписка);
					
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТовары.НомерСтроки, "КодТНВЭД");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

	// Проверка заполнения табличной части "АгентскиеУслуги"
	ИмяСписка = НСтр("ru = 'Агентские услуги'");

	Если ЭтоКомиссия И АгентскиеУслуги.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Документ передачи на комиссию не может содержать услуг.'");
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список", "Корректность",,,
			ИмяСписка, ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "АгентскиеУслуги", "Объект", Отказ);
	КонецЕсли;

	// Проверка заполнения табличной части "Зачет авансов"
	Если СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	ИначеЕсли ЗачетАвансов.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки с документом аванса!'");
		Поле = "ПорядокУчетаРасчетов";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , Поле, Отказ);
	КонецЕсли;
	

	// Проверка табличной части "Возвратная тара"

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.Сумма");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.СчетУчета");
	КонецЕсли;

	// Эти реквизиты проверяются в документе с помощью специального, нетипового механизма. Проверка размещена в ПроверитьЗаполнениеСубконто()
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Субконто");
	МассивНепроверяемыхРеквизитов.Добавить("Услуги.Субконто");
	
	// Удаляем из проверяемых реквизитов все, по которым автоматическая проверка не нужна:
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	РеквизитыЗаСсылками = Документы.РеализацияТоваровУслуг.РеквизитыЗаСсылками(ВидОперации);
	
	Если Не СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками) Тогда
		ПроверитьЗаполнениеСубконто(Отказ);
	КонецЕсли;
	
	ПроверкаЗаполненияДокументов.ПроверитьРеквизитыЗаСсылками(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.РеализацияТоваровУслуг.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ

	// Таблица списанных товаров
	ТаблицаСписанныеТовары = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(ПараметрыПроведения.СписаниеТоваровТаблицаТовары,
		ПараметрыПроведения.СписаниеТоваровРеквизиты, Отказ);

	// Таблица списанной тары
	ТаблицаСписаннаяТара = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(ПараметрыПроведения.ТараТаблицаТовары,
		ПараметрыПроведения.ТараРеквизиты, Отказ);

	// Таблица взаиморасчетов с учетом зачета авансов
	ТаблицаВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента, ПараметрыПроведения.ЗачетАвансовТаблицаАвансов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);
	
	// Таблицы расчетов по авансам с учетом зачисления платежей через посредника
	ТаблицыЗачетаАвансовКассовыйМетод = УчетВзаиморасчетов.ПодготовитьТаблицыЗачетаАвансовКассовыйМетод(
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента, ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);
	
	СтруктураТаблицДляИП = 
		Документы.РеализацияТоваровУслуг.ПодготовитьСтруктуруТаблицИП(
			ПараметрыПроведения.Реквизиты, ТаблицаВзаиморасчеты);
			
	ТаблицаПрочихРасчетовИП = СтруктураТаблицДляИП.ТаблицаПрочихРасчетовИП;
	ТаблицаВзаиморасчетыИП  = СтруктураТаблицДляИП.ТаблицаВзаиморасчетыИП;
	
	// Таблицы выручки от реализации: собственных товаров и услуг и отдельно комиссионных
	ТаблицыРеализация = УчетДоходовРасходов.ПодготовитьТаблицыВыручкиОтРеализации(
		ПараметрыПроведения.РеализацияТаблицаДокумента, ТаблицаВзаиморасчеты, ТаблицаСписанныеТовары,
		ПараметрыПроведения.Реквизиты, Отказ);
	ТаблицаСобственныеТоварыУслуги = ТаблицыРеализация.СобственныеТоварыУслуги;
	ТаблицаТоварыУслугиКомитентов = ТаблицыРеализация.ТоварыУслугиКомитентов;
	ТаблицаРеализованныеТоварыКомитентов = ТаблицыРеализация.РеализованныеТоварыКомитентов;
	
	Документы.РеализацияТоваровУслуг.ДобавитьКолонкуСодержание(ТаблицыРеализация.СобственныеТоварыУслуги);
	Документы.РеализацияТоваровУслуг.ДобавитьКолонкуСодержание(ПараметрыПроведения.НДСТоварыОтгрузка);
	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура("ТаблицаТМЦ, ТаблицаРасчетов",
		ТаблицаСписанныеТовары,
		ТаблицыЗачетаАвансовКассовыйМетод.ТаблицаВзаиморасчетов);
	
	// Учет доходов и расходов ИП
	Если ПараметрыПроведения.Реквизиты[0].ВидОперации <> Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности
		И ПараметрыПроведения.Реквизиты[0].ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		
		СписанныеМПЗ = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуСписанныеМПЗ(
			ТаблицаСписанныеТовары, ПараметрыПроведения.РеализацияТаблицаДокумента, ПараметрыПроведения.Реквизиты)
	КонецЕсли;
	
	ТаблицыСписанияТоваровИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыСписанияМПЗ(
		СписанныеМПЗ, ПараметрыПроведения.Реквизиты, Отказ);
	ТаблицаОказаниеУслугИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОказаниеУслуг(
		ТаблицыРеализация.СобственныеТоварыУслуги, ПараметрыПроведения.Реквизиты);
	
	Если Не ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		ТаблицаСтатусовСчетов = СтатусыДокументов.ПодготовитьТаблицуСтатусовОтгрузкиПоСчетам(
			ПараметрыПроведения.ТаблицаСчетовНаОплату,
			ПараметрыПроведения.ОтгрузкаТоваровОказаниеУслугПоСчету,
			ПараметрыПроведения.ОтгрузкаВозвратнойТарыПоСчету,
			ПараметрыПроведения.Реквизиты);
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	// Движения по прочим расчетам
	УчетВзаиморасчетов.СформироватьДвиженияПоПрочимРасчетам(ТаблицаПрочихРасчетовИП, Движения, Отказ);

	УчетТоваров.СформироватьДвиженияСписаниеТоваров(ТаблицаСписанныеТовары,
		ПараметрыПроведения.СписаниеТоваровРеквизиты, Движения, Отказ);
		
	УчетТоваров.СформироватьДвиженияВозвратТоваровПоставщику(
		ПараметрыПроведения.ТараТаблицаТовары, ТаблицаСписаннаяТара,
		ПараметрыПроведения.ТараРеквизиты, Движения, Отказ);

	УчетТоваров.СформироватьДвиженияКорректировкаСтоимостиТары(
		ПараметрыПроведения.ТараТаблицаТовары, ТаблицаСписаннаяТара,
		ПараметрыПроведения.ТараРеквизиты, Движения, Отказ);

	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ЗачетАвансовРеквизиты, Движения, Отказ);
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансовКомитентов(ТаблицаТоварыУслугиКомитентов,
		ПараметрыПроведения.ЗачетАвансовКомитентовРеквизиты, Движения, Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияРасчетыПоЭквайрингуЗачетАвансов(
		ТаблицыЗачетаАвансовКассовыйМетод.ТаблицаПрочихРасчетов,
		ТаблицыЗачетаАвансовКассовыйМетод.ТаблицаПроводокВспомогательныеРасчеты,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Движения, Отказ);
		
	// Перенос задолженности при оплате курьеру
	ТаблицаВзаиморасчетовПереносЗадолженности = Документы.РеализацияТоваровУслуг.ПодготовитьТаблицуПереносаЗадолженности(
		ТаблицаВзаиморасчеты, 
		ПараметрыПроведения.РеквизитыПереносЗадолженностиДебиторскаяЗадолженность,
		Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияПереносЗадолженности(
		ТаблицаВзаиморасчетовПереносЗадолженности,
		ПараметрыПроведения.РеквизитыПереносЗадолженностиДебиторскаяЗадолженность, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРеализация(
		ТаблицаСобственныеТоварыУслуги, ТаблицаТоварыУслугиКомитентов, ТаблицаРеализованныеТоварыКомитентов,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияПереоценкаТоваровВРознице(
		ПараметрыПроведения.ПереоценкаТоваровВРозницеТаблицаТовары, ТаблицаСписанныеТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	РегистрыНакопления.РеализацияУслуг.ДобавитьДвижения(
		Движения.РеализацияУслуг,
		ПараметрыПроведения.ТаблицаРеализацияУслуг,
		ТаблицаСобственныеТоварыУслуги,
		ПараметрыПроведения.Реквизиты);
	// Учет НДС
	УчетНДС.СформироватьДвиженияРеализацияТоваровУслуг(
		ТаблицаСобственныеТоварыУслуги, ПараметрыПроведения.НДСТоварыРеализация, ТаблицаСписанныеТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДС.СформироватьДвиженияРеализацияКомиссионныхТоваров(
		ПараметрыПроведения.НДСТоварыНаКомиссииРеализация, 
		ТаблицаТоварыУслугиКомитентов,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетНДСБП.СформироватьДвиженияОтгрузкаКомиссионеру(
		ПараметрыПроведения.НДСТоварыОтгрузкаКомиссионеру, ТаблицаСписанныеТовары,  
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	НДСТоварыОтгрузка = Документы.РеализацияТоваровУслуг.ПодготовитьТаблицуНДСПоОтгрузкеСУчетомКурсаАвансов(
		ПараметрыПроведения.НДСТоварыОтгрузка, ТаблицаВзаиморасчеты, ПараметрыПроведения.Реквизиты);
		
	УчетНДС.СформироватьДвиженияОтгрузкаБезПереходаПраваСобственности(
		НДСТоварыОтгрузка, ТаблицаСписанныеТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Задолженность при отгрузке без перехода права собственности
	УчетВзаиморасчетов.СформироватьДвиженияЗадолженностьПоОтгрузке(
		ТаблицаВзаиморасчеты, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Движения регистра "Рублевые суммы документов в валюте"

	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ТаблицаСобственныеТоварыУслуги, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ТаблицаТоварыУслугиКомитентов, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеБезНДС(ПараметрыПроведения.ТараТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(
		НДСТоварыОтгрузка, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ПараметрыПроведения.РублевыеСуммыДокументаВВалюте, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// Учет УСН
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);
	
	// Учет доходов и расходов ИП
	ТаблицаИПМПЗОтгруженные	= УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияСписаниеМПЗ(
		ТаблицыСписанияТоваровИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	ТаблицаИПМПЗОтгруженныеУслуги = УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияОказаниеУслуг(
		ТаблицаОказаниеУслугИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаИПМПЗОтгруженныеУслуги, ТаблицаИПМПЗОтгруженные);
		
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияЗачетОплатыПокупателя(
		ТаблицаИПМПЗОтгруженные,
		СтруктураТаблицДляИП.ТаблицаВзаиморасчетыИП, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// ПЕРЕОЦЕНКА ВАЛЮТНЫХ ОСТАТКОВ
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	Если Не ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		СтатусыДокументов.СформироватьДвиженияСтатусовДокументов(
			ТаблицаСтатусовСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;
	
	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);

	// Регистрация в последовательности.
	РаботаСПоследовательностями.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ЭтотОбъект,
		Отказ,
		ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение,
		РаботаСПоследовательностями.ПодготовитьТаблицуСчетовТоваровДляАнализа(ТаблицаСписанныеТовары, ТаблицаСписаннаяТара),
		Перечисления.ВидыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов);

	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
		
	Движения.Записать();
	
	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект);	
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ, НЕ УстановленСтатусДокумента);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект);	
	ПараметрыДействия.СостояниеФлага = Ложь;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПроверитьЗаполнениеСубконто(Отказ)
	
	УчетДоходовИРасходовПредпринимателя.ПроверитьЗаполнениеСубконтоНоменклатурныеГруппы(
		ЭтотОбъект, 
		"СчетДоходов", 
		"Субконто", 
		НСтр("ru = 'Субконто'"), 
		"Товары", 
		НСтр("ru = 'Товары'"), 
		Отказ);
			
	УчетДоходовИРасходовПредпринимателя.ПроверитьЗаполнениеСубконтоНоменклатурныеГруппы(
		ЭтотОбъект,
		"СчетДоходов",
		"Субконто",
		НСтр("ru = 'Субконто'"),
		"Услуги",
		НСтр("ru = 'Услуги'"),
		Отказ);
			
КонецПроцедуры

Процедура ЗаполнитьИзТаблицыНоменклатуры(АдресТаблицыНоменклатуры, СуммаВключаетНДС)
	
	ТаблицаНоменклатуры = ПолучитьИзВременногоХранилища(АдресТаблицыНоменклатуры);
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ТаблицаНоменклатуры.ВыгрузитьКолонку("Номенклатура"), "Услуга, ВидСтавкиНДС");
	Для каждого Элемент Из ТаблицаНоменклатуры Цикл
		
		Если РеквизитыНоменклатуры[Элемент.Номенклатура].Услуга Тогда
			НоваяСтрока = Услуги.Добавить();
		Иначе
			НоваяСтрока = Товары.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
		
		НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(
			РеквизитыНоменклатуры[Элемент.Номенклатура].ВидСтавкиНДС, ОбщегоНазначения.ТекущаяДатаПользователя());
		НоваяСтрока.СуммаНДС  = УчетНДСКлиентСервер.РассчитатьСуммуНДС(НоваяСтрока.Сумма,
			СуммаВключаетНДС,
			УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
		
	КонецЦикла;

КонецПроцедуры

#Область Инициализация

УстановленСтатусДокумента = Ложь;

#КонецОбласти

#КонецЕсли