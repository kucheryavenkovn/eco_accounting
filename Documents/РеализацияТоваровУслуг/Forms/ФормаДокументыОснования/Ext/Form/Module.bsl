
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Основания") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ДокументыОснования, Параметры.Основания, "ДокументОснование");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Организация, Контрагент");
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Для Индекс = 1 По ДокументыОснования.Количество() Цикл
		
		Строка = ДокументыОснования[Индекс-1];
		Если НЕ ЗначениеЗаполнено(Строка.ДокументОснование) Тогда
			ТекстСообщенияПользователю = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", , НСтр("ru = 'Счет на оплату'"), Индекс, НСтр("ru = 'Документы основания'"));
			
			ПутьКДанными = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ДокументыОснования", Индекс, "ДокументОснование");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщенияПользователю, ,ПутьКДанными, ,Отказ);
		КонецЕсли; 
	КонецЦикла; 
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли; 
	
	МассивОснований = Новый Массив;
	Для каждого СтрокаОснования Из ДокументыОснования Цикл
		МассивОснований.Добавить(СтрокаОснования.ДокументОснование);
	КонецЦикла; 
	
	АдресХранилища = ПоместитьВоВременноеХранилище(МассивОснований);
	
	Закрыть(АдресХранилища);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицыДокументыОснования

&НаКлиенте
Процедура ДокументыОснованияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Отбор = Новый Структура("Организация, Контрагент");
		ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект);
		
		ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаВыбора", Новый Структура("Отбор", Отбор), Элементы.ДокументОснование);
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти 
