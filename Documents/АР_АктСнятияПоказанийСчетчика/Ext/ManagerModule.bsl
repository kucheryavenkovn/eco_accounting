#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаНачисления(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаНачисления(НомераТаблиц)
	
	НомераТаблиц.Вставить("Начисления", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Реквизиты.СпособЗаполнения = 1
	|			ТОГДА ТабличнаяЧасть.Услуга
	|		ИНАЧЕ Реквизиты.Услуга
	|	КОНЕЦ КАК УслугаАренды,
	|	Реквизиты.НачалоПериода КАК ПериодАренды,
	|	ВЫБОР
	|		КОГДА Реквизиты.СпособЗаполнения = 0
	|				И (Реквизиты.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.АР_СпособыРасчетаУслуг.СуммаПропорциональноПлощадиОбъектов)
	|					ИЛИ Реквизиты.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.АР_СпособыРасчетаУслуг.СуммаРавномерноПоДоговорам))
	|			ТОГДА 1
	|		ИНАЧЕ (ТабличнаяЧасть.ПоказаниеНаКонецПериода - ТабличнаяЧасть.ПоказаниеНаНачалоПериода) * ВЫБОР
	|				КОГДА ТабличнаяЧасть.КоэффициентОтнесенияСчетчика = 0
	|					ТОГДА 1
	|				ИНАЧЕ ТабличнаяЧасть.КоэффициентОтнесенияСчетчика
	|			КОНЕЦ
	|	КОНЕЦ КАК Количество,
	|	ТабличнаяЧасть.ОбъектАренды,
	|	ТабличнаяЧасть.НомерТелефона,
	|	ТабличнаяЧасть.Счетчик,
	|	ВЫБОР
	|		КОГДА Реквизиты.СпособЗаполнения = 0
	|				И (Реквизиты.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.АР_СпособыРасчетаУслуг.СуммаПропорциональноПлощадиОбъектов)
	|					ИЛИ Реквизиты.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.АР_СпособыРасчетаУслуг.СуммаРавномерноПоДоговорам))
	|			ТОГДА ТабличнаяЧасть.Сумма
	|		ИНАЧЕ ТабличнаяЧасть.Цена
	|	КОНЕЦ КАК Ставка,
	|	ТабличнаяЧасть.Сумма,
	|	ВЫБОР
	|		КОГДА Реквизиты.СпособЗаполнения = 1
	|			ТОГДА Реквизиты.ДоговорКонтрагента
	|		ИНАЧЕ ТабличнаяЧасть.ДоговорКонтрагента
	|	КОНЕЦ КАК ДоговорКонтрагента
	|ИЗ
	|	Документ.АР_АктСнятияПоказанийСчетчика.Показания КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_АктСнятияПоказанийСчетчика КАК Реквизиты
	|		ПО ТабличнаяЧасть.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И ТабличнаяЧасть.Сумма <> 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Печать

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "Акт";
	КомандаОтправки.Представление = НСтр("ru = 'Акт снятия показаний счетчика'");
	КомандаОтправки.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьШаблоновWord";
	КомандаПечати.Представление = НСтр("ru = 'Печать по шаблону MS Word'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиШаблоновWord";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт снятия показаний счетчика'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";

	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Регистрация объемов потребления услуг переменной части""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок       = 160;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Акт") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Акт", "Акт снятия показаний счетчика", ПечатьАкта(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_АктСнятияПоказанийСчетчика_Акт";
		
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	АР_АктСнятияПоказанийСчетчика.Ссылка,
	|	АР_АктСнятияПоказанийСчетчика.Номер,
	|	АР_АктСнятияПоказанийСчетчика.Дата,
	|	АР_АктСнятияПоказанийСчетчика.Организация,
	|	АР_АктСнятияПоказанийСчетчика.Контрагент,
	|	АР_АктСнятияПоказанийСчетчика.ДоговорКонтрагента,
	|	АР_АктСнятияПоказанийСчетчика.НачалоПериода,
	|	АР_АктСнятияПоказанийСчетчика.ОкончаниеПериода,
	|	АР_АктСнятияПоказанийСчетчика.Услуга,
	|	АР_АктСнятияПоказанийСчетчика.ДокументОснование,
	|	АР_АктСнятияПоказанийСчетчика.СуммаДокумента,
	|	АР_АктСнятияПоказанийСчетчика.Комментарий,
	|	АР_АктСнятияПоказанийСчетчика.РучнаяКорректировка,
	|	АР_АктСнятияПоказанийСчетчика.СпособРасчета,
	|	АР_АктСнятияПоказанийСчетчика.ПоказаниеДокумента,
	|	АР_АктСнятияПоказанийСчетчика.Ответственный,
	|	АР_АктСнятияПоказанийСчетчика.СпособЗаполнения,
	|	ВложенныйЗапрос.Телефон КАК ЭтоУслугаТелефоннойСвязи
	|ИЗ
	|	Документ.АР_АктСнятияПоказанийСчетчика КАК АР_АктСнятияПоказанийСчетчика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(ВЫБОР
	|					КОГДА Показания.Ссылка.СпособЗаполнения = 1
	|						ТОГДА Показания.Услуга.АР_ТелефонИнтернет
	|					ИНАЧЕ Показания.Ссылка.Услуга.АР_ТелефонИнтернет
	|				КОНЕЦ) КАК Телефон,
	|			Показания.Ссылка КАК Ссылка
	|		ИЗ
	|			Документ.АР_АктСнятияПоказанийСчетчика.Показания КАК Показания
	|		ГДЕ
	|			Показания.Ссылка = &Ссылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Показания.Ссылка) КАК ВложенныйЗапрос
	|		ПО АР_АктСнятияПоказанийСчетчика.Ссылка = ВложенныйЗапрос.Ссылка
	|ГДЕ
	|	АР_АктСнятияПоказанийСчетчика.Ссылка = &Ссылка";
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АР_АктСнятияПоказанийСчетчикаПоказания.НомерСтроки,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Контрагент КАК Арендатор,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.ДоговорКонтрагента КАК Договор,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Услуга КАК Услуга,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.ОбъектАренды,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Площадь,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.НомерТелефона,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Счетчик,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.КоэффициентОтнесенияСчетчика,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.ФактическоеНачало,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.ФактическоеОкончание,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.ПоказаниеНаНачалоПериода,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.ПоказаниеНаКонецПериода,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Цена КАК Тариф,
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Сумма,
	|	(АР_АктСнятияПоказанийСчетчикаПоказания.ПоказаниеНаКонецПериода - АР_АктСнятияПоказанийСчетчикаПоказания.ПоказаниеНаНачалоПериода) * ВЫБОР
	|		КОГДА АР_АктСнятияПоказанийСчетчикаПоказания.КоэффициентОтнесенияСчетчика = 0
	|			ТОГДА 1
	|		ИНАЧЕ АР_АктСнятияПоказанийСчетчикаПоказания.КоэффициентОтнесенияСчетчика
	|	КОНЕЦ КАК Разница
	|ИЗ
	|	Документ.АР_АктСнятияПоказанийСчетчика.Показания КАК АР_АктСнятияПоказанийСчетчикаПоказания
	|ГДЕ
	|	АР_АктСнятияПоказанийСчетчикаПоказания.Ссылка = &Ссылка";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_АктСнятияПоказанийСчетчика.ПФ_MXL_Макет");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ);
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.ТекстЗаголовка = "Акт снятия показаний счетчика №" + ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Истина) + " от " + Формат(Шапка.Дата,"ДФ='д ММММ гггг'") + " г.";
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если Шапка.СпособЗаполнения = 1 Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокАрендатор");
			ОбластьМакета.Параметры.Арендатор = "Арендатор: " + ПолучитьПолноеНаименованиеСправочника(Шапка.Контрагент);
			ОбластьМакета.Параметры.Договор = "Договор: " + Шапка.ДоговорКонтрагента.Наименование;
			ТабличныйДокумент.Вывести(ОбластьМакета);	
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокУслуга");
			ОбластьМакета.Параметры.Услуга = "Услуга: " + ПолучитьПолноеНаименованиеСправочника(Шапка.Услуга);
			ТабличныйДокумент.Вывести(ОбластьМакета);	
		КонецЕсли;
		
		ЕстьСчетчики = ПолучитьФункциональнуюОпцию("АР_ВестиУчетТарифицируемыхУслугПоСчетчикам");
		
		// Вывод шапки таблицы
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Номер");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		Если Шапка.СпособЗаполнения = 0 Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Арендатор");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Услуга");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|ОбъектАренды");
		ТабличныйДокумент.Присоединить(ОбластьМакета);
		Если (Шапка.СпособЗаполнения = 0) и НЕ Шапка.ЭтоУслугаТелефоннойСвязи Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Площадь");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		Если Шапка.ЭтоУслугаТелефоннойСвязи Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Телефон");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		Если ЕстьСчетчики Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Счетчик");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|ФактическиеПоказания");
		ТабличныйДокумент.Присоединить(ОбластьМакета);
		Если Шапка.СпособЗаполнения = 1 или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ПоказаниеПропорциональноПлощадиОбъектов)
			или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ФиксированнойСуммой)
			или (Не ЗначениеЗаполнено(Шапка.СпособРасчета)) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Показания");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		Если Шапка.СпособЗаполнения = 1 или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ПоказаниеПропорциональноПлощадиОбъектов)
			или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ФиксированнойСуммой)
			или (Не ЗначениеЗаполнено(Шапка.СпособРасчета)) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Разница");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		Если Шапка.СпособЗаполнения = 1 или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ПоказаниеПропорциональноПлощадиОбъектов)
			или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ФиксированнойСуммой)
			или (Не ЗначениеЗаполнено(Шапка.СпособРасчета)) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Тариф");
			ТабличныйДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Сумма");
		ТабличныйДокумент.Присоединить(ОбластьМакета);
		
		Запрос.УстановитьПараметр("Ссылка", Документ);
		Выборка = Запрос.Выполнить().Выбрать();
		
		ПлощадьИтого = 0; 	
		РазницаИтого = 0;	
		СуммаИтого = 0;
		
		ВыводимАрендатора = Ложь;
		ВыводимУслугу = Ложь;
		ВыводимТелефон = Ложь;
		ВыводимСчетчики = Ложь;
		ВыводимПоказания = Ложь;
		ВыводимТариф = Ложь;
		ВыводимПлощадь = Ложь;
		ВыводимРазницу = Ложь;
		
		Пока Выборка.Следующий() Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("Строка|Номер");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			Если Шапка.СпособЗаполнения = 0 Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Арендатор");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ОбластьМакета.Параметры.Арендатор = ПолучитьПолноеНаименованиеСправочника(Выборка.Арендатор);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ВыводимАрендатора = Истина;
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Услуга");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ОбластьМакета.Параметры.Услуга = ПолучитьПолноеНаименованиеСправочника(Выборка.Услуга);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ВыводимУслугу = Истина;
			КонецЕсли;
			ОбластьМакета = Макет.ПолучитьОбласть("Строка|ОбъектАренды");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Присоединить(ОбластьМакета);
			Если (Шапка.СпособЗаполнения = 0) и НЕ Шапка.ЭтоУслугаТелефоннойСвязи Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Площадь");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ПлощадьИтого = ПлощадьИтого + Выборка.Площадь;
				ВыводимПлощадь = Истина;
			КонецЕсли;
			Если Шапка.ЭтоУслугаТелефоннойСвязи Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Телефон");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ВыводимТелефон = Истина;
			КонецЕсли;
			Если ЕстьСчетчики Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Счетчик");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ВыводимСчетчики = Истина;			
			КонецЕсли;
			ОбластьМакета = Макет.ПолучитьОбласть("Строка|ФактическиеПоказания");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Присоединить(ОбластьМакета);
			Если Шапка.СпособЗаполнения = 1 или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ПоказаниеПропорциональноПлощадиОбъектов)
				или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ФиксированнойСуммой)
				или (Не ЗначениеЗаполнено(Шапка.СпособРасчета)) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Показания");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ВыводимПоказания = Истина;
			КонецЕсли;
			Если Шапка.СпособЗаполнения или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ПоказаниеПропорциональноПлощадиОбъектов)
				или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ФиксированнойСуммой)
				или (Не ЗначениеЗаполнено(Шапка.СпособРасчета)) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Разница");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				РазницаИтого = РазницаИтого + Выборка.Разница;
				ВыводимРазницу = Истина;
			КонецЕсли;
			Если Шапка.СпособЗаполнения или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ПоказаниеПропорциональноПлощадиОбъектов)
				или (Шапка.СпособРасчета = Перечисления.АР_СпособыРасчетаУслуг.ФиксированнойСуммой)
				или (Не ЗначениеЗаполнено(Шапка.СпособРасчета)) Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Строка|Тариф");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьМакета);
				ВыводимТариф = Истина;
			КонецЕсли;
			ОбластьМакета = Макет.ПолучитьОбласть("Строка|Сумма");
			СуммаИтого = СуммаИтого + Выборка.Сумма;
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Присоединить(ОбластьМакета);  
		КонецЦикла; 
		
		ТабличныйДокумент.Вывести(Макет.ПолучитьОбласть("Итоги|Номер"));
		Если ВыводимАрендатора Тогда
			ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|Арендатор"));
		КонецЕсли;
		Если ВыводимУслугу Тогда
			ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|Услуга"));
		КонецЕсли;
		ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|ОбъектАренды"));
		Если ВыводимПлощадь Тогда
			ОбластьИтогов = Макет.ПолучитьОбласть("Итоги|Площадь");
			ОбластьИтогов.Параметры.ПлощадьИтого = ПлощадьИтого;
			ТабличныйДокумент.Присоединить(ОбластьИтогов);
		КонецЕсли;
		Если ВыводимТелефон Тогда
			ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|Телефон"));
		КонецЕсли;
		Если ВыводимСчетчики Тогда
			ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|Счетчик"));
		КонецЕсли;
		ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|ФактическиеПоказания"));
		Если ВыводимПоказания Тогда
			ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|Показания"));
		КонецЕсли;
		Если ВыводимРазницу Тогда
			ОбластьИтогов = Макет.ПолучитьОбласть("Итоги|Разница");
			ОбластьИтогов.Параметры.РазницаИтого = РазницаИтого;
			ТабличныйДокумент.Присоединить(ОбластьИтогов);
		КонецЕсли;
		Если ВыводимТариф Тогда
			ТабличныйДокумент.Присоединить(Макет.ПолучитьОбласть("Итоги|Тариф"));
		КонецЕсли;
		ОбластьИтогов = Макет.ПолучитьОбласть("Итоги|Сумма");
		ОбластьИтогов.Параметры.СуммаИтого = СуммаИтого;
		ТабличныйДокумент.Присоединить(ОбластьИтогов);
	
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Ответственный = Шапка.Ответственный.Наименование;
		ТабличныйДокумент.Вывести(ОбластьМакета);

		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
		
КонецФункции

Функция ПолучитьПолноеНаименованиеСправочника(Ссылка)
	
	Возврат ?(ЗначениеЗаполнено(Ссылка), ?(ЗначениеЗаполнено(Ссылка.НаименованиеПолное), Ссылка.НаименованиеПолное, Ссылка.Наименование), "");
	
КонецФункции

#КонецЕсли
