#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 9, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедуриИФункции

Функция ТекстЗапросаДанныеДляОбновленияЦенДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(КорректировкаПоступленияТовары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	КорректировкаПоступленияТовары.Цена КАК Цена,
	|	&Валюта КАК Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.КорректировкаПоступления.Товары КАК КорректировкаПоступленияТовары
	|ГДЕ
	|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
	|	И КорректировкаПоступленияТовары.Номенклатура ССЫЛКА Справочник.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаПоступленияУслуги.Номенклатура,
	|	КорректировкаПоступленияУслуги.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.КорректировкаПоступления.Услуги КАК КорректировкаПоступленияУслуги
	|ГДЕ
	|	КорректировкаПоступленияУслуги.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаПоступленияАгентскиеУслуги.Номенклатура,
	|	КорректировкаПоступленияАгентскиеУслуги.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.КорректировкаПоступления.АгентскиеУслуги КАК КорректировкаПоступленияАгентскиеУслуги
	|ГДЕ
	|	КорректировкаПоступленияАгентскиеУслуги.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Цена,
	|	Валюта";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьПоПоступлению(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументПоступления) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Товары.Очистить();
	Объект.Услуги.Очистить();
	Объект.АгентскиеУслуги.Очистить();
	
	МетаданныеДокумента = Объект.ДокументПоступления.Метаданные();
	ИмяВидаДокумента = МетаданныеДокумента.Имя;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Объект.ДокументПоступления);
	Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Количество КАК Количество,
	|	ПоступлениеТоваровУслугТовары.Количество КАК КоличествоПоДокументу,
	|	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
	|	ПоступлениеТоваровУслугТовары.Цена КАК ЦенаПоДокументу,
	|	ПоступлениеТоваровУслугТовары.Сумма КАК Сумма,
	|	ПоступлениеТоваровУслугТовары.Сумма КАК СуммаПоДокументу,
	|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДСПоДокументу,
	|	ПоступлениеТоваровУслугТовары.НомерГТД КАК НомерГТД,
	|	ПоступлениеТоваровУслугТовары.НомерГТД КАК НомерГТДПоДокументу,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхожденияПоДокументу,
	|	ИСТИНА КАК ЕстьВДокументеПоступления,
	|	""Товары"" КАК ТЧ,
	|	НЕОПРЕДЕЛЕНО КАК Содержание,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК СчетРасчетов,
	|	ПоступлениеТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугУслуги.Количество,
	|	ПоступлениеТоваровУслугУслуги.Количество,
	|	ПоступлениеТоваровУслугУслуги.Цена,
	|	ПоступлениеТоваровУслугУслуги.Цена,
	|	ПоступлениеТоваровУслугУслуги.Сумма,
	|	ПоступлениеТоваровУслугУслуги.Сумма,
	|	ПоступлениеТоваровУслугУслуги.СтавкаНДС,
	|	ПоступлениеТоваровУслугУслуги.СуммаНДС,
	|	ПоступлениеТоваровУслугУслуги.СуммаНДС,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ИСТИНА,
	|	""Услуги"",
	|	ПоступлениеТоваровУслугУслуги.Содержание,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ПоступлениеТоваровУслугУслуги.ИдентификаторСтроки
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугУслуги.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Количество,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Количество,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Цена,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Цена,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Сумма,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Сумма,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.СтавкаНДС,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.СуммаНДС,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.СуммаНДС,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ИСТИНА,
	|	""АгентскиеУслуги"",
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Содержание,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Контрагент,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.ДоговорКонтрагента,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.СчетРасчетов,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.ИдентификаторСтроки
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслугАгентскиеУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Ссылка = &Ссылка";
	
	Запрос.Текст = Текст;
	Результат = Запрос.Выполнить().Выбрать();
	
	РеквизитыДокументаПоступления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументПоступления, "Организация, Дата, НДСВключенВСтоимость");
	
	Пока Результат.Следующий() Цикл
		Если Результат.ТЧ = "Товары" Тогда
			СтрокаТоваров = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоваров, Результат);
		ИначеЕсли Результат.ТЧ = "Услуги" Тогда
			СтрокаУслуг = Объект.Услуги.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаУслуг, Результат);
		ИначеЕсли Результат.ТЧ = "АгентскиеУслуги" Тогда
			СтрокаУслуг = Объект.АгентскиеУслуги.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаУслуг, Результат);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ЗаполнитьПоДокументу(Объект) Экспорт
	
	Если ТипЗнч(Объект.ДокументПоступления) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		ЗаполнитьПоПоступлению(Объект);
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("УдалитьУчитыватьНДС");
	
	Возврат МассивРеквизитов;
	
КонецФункции

Функция ПолучитьПредставлениеДокумента(ДокументСсылка) Экспорт
	
	ТекстПредставлениеКраткое	= "";
	ТекстПредставление 			= "";
	ТекстОсновноеПредставление	= "Акт о расхождениях";
			
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ПредставлениеНомера, Дата");
			
		ТекстПредставлениеКраткое = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 от %2'"),
			РеквизитыДокумента.ПредставлениеНомера, Формат(РеквизитыДокумента.Дата,	"ДЛФ=Д"));
		ТекстПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2'"), ТекстОсновноеПредставление, ТекстПредставлениеКраткое);

	Иначе
		ТекстПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (создание)'"), ТекстОсновноеПредставление);
	КонецЕсли;
	
	СтруктураПредставления = Новый Структура;
	СтруктураПредставления.Вставить("ПредставлениеДокументаКраткое", ТекстПредставлениеКраткое);
	СтруктураПредставления.Вставить("ПредставлениеДокумента", 		 ТекстПредставление);
	
	Возврат СтруктураПредставления;
	
КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	// Товарная накладная (ТОРГ-2)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "ТОРГ2";
	КомандаПечати.Представление  = НСтр("ru = 'Акт о расхождениях (ТОРГ-2)'");
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьТОРГ2";
	КомандаПечати.Порядок        = 90;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Акт о расхождениях""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыТОРГ2(МассивОбъектов, СоответствиеОбъектовПечати = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АктОРасхождениях.ДокументПоступления КАК ДокументПоступленияСсылка,
		|	АктОРасхождениях.Ссылка КАК АктОРасхожденияхСсылка
		|ПОМЕСТИТЬ ВТ_ДокументыСРасхождениями
		|ИЗ
		|	Документ.АктОРасхождениях КАК АктОРасхождениях
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ПО АктОРасхождениях.ДокументПоступления = ПоступлениеТоваровУслуг.Ссылка
		|ГДЕ
		|	АктОРасхождениях.Ссылка В(&МассивОбъектов)
		|	И НЕ ПоступлениеТоваровУслуг.Ссылка ЕСТЬ NULL
		|	И НЕ АктОРасхождениях.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыСРасхождениями.АктОРасхожденияхСсылка КАК ДокументОснование,
		|	СтрокиОснований.ДокументОснование КАК ДокументОснованиеРеализация,
		|	СчетФактураПолученный.НомерИсправления КАК НомерИсправления,
		|	СчетФактураПолученный.НомерВходящегоДокумента КАК НомерСчетаФактуры,
		|	СчетФактураПолученный.ДатаВходящегоДокумента КАК ДатаСчетаФактуры
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СтрокиОснований
		|		ПО ДокументыСРасхождениями.ДокументПоступленияСсылка = СтрокиОснований.ДокументОснование
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
		|		ПО (СтрокиОснований.Ссылка = СчетФактураПолученный.Ссылка)
		|ГДЕ
		|	НЕ СчетФактураПолученный.Ссылка ЕСТЬ NULL
		|	И НЕ СчетФактураПолученный.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетФактураПолученный.НомерИсправления УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктОРасхождениях.Ссылка КАК Ссылка,
		|	АктОРасхождениях.Номер КАК НомерДокумента,
		|	АктОРасхождениях.Дата КАК ДатаДокумента,
		|	АктОРасхождениях.Организация КАК Организация,
		|	АктОРасхождениях.Организация.Префикс КАК Префикс,
		|	ПоступлениеТоваров.ВалютаДокумента КАК Валюта,
		|	ПоступлениеТоваров.Грузоотправитель КАК Грузоотправитель,
		|	ПоступлениеТоваров.Грузополучатель КАК Грузополучатель,
		|	ПоступлениеТоваров.Контрагент КАК Поставщик,
		|	ПоступлениеТоваров.ДоговорКонтрагента.Номер КАК НомерДоговораПоставки,
		|	ПоступлениеТоваров.ДоговорКонтрагента.Дата КАК ДатаДоговораПоставки,
		|	АктОРасхождениях.Склад КАК Склад,
		|	"""" КАК НомерКоммерческогоАкта,
		|	"""" КАК ДатаКоммерческогоАкта,
		|	"""" КАК НомерУдостоверенияОКачестве,
		|	"""" КАК ДатаУдостоверенияОКачестве,
		|	"""" КАК НомерВетеринарногоСвидетельства,
		|	"""" КАК ДатаВетеринарногоСвидетельства,
		|	"""" КАК НомерЖелезнодорожнойНакладной,
		|	"""" КАК ДатаЖелезнодорожнойНакладной,
		|	"""" КАК НомерКоносамента,
		|	"""" КАК ДатаКоносамента,
		|	"""" КАК СпособДоставки,
		|	"""" КАК НомерТранспортногоСредства,
		|	"""" КАК ДатаОтправленияТовара,
		|	"""" КАК ПунктОтправления,
		|	"""" КАК СкладОтправителяТовара,
		|	"""" КАК СостояниеТранспортаПоДокументам,
		|	"""" КАК СостояниеТранспортаПоФакту,
		|	"""" КАК УсловияХраненияТовараДоВскрытия,
		|	"""" КАК ТемператураПриРазгрузке,
		|	"""" КАК СостояниеТарыИУпаковки,
		|	"""" КАК СодержаниеНаружнойМаркировки,
		|	"""" КАК ДатаВскрытияТары,
		|	"""" КАК ОрганизацияВзвесившаяИОпломбировавшаяТовар,
		|	"""" КАК ПорядокОтбораТовараДляВыборочнойПроверки,
		|	"""" КАК СпособОпределенияКоличества,
		|	"""" КАК МестоОпределенияКоличества,
		|	"""" КАК СведенияОбИсправностиВесоизмерительныхПриборов,
		|	"""" КАК ПрочиеДанные,
		|	"""" КАК ПодробноеОписаниеДефектов,
		|	"""" КАК ЗаключениеКомиссии,
		|	"""" КАК Приложение,
		|	"""" КАК ДокументУдостоверяющийПолномочияПредставителяПартнера,
		|	"""" КАК НомерДокументаУдостоверяющегоПолномочияПредставителяПартнера,
		|	"""" КАК ДатаВыдачиДокументаУдостоверяющегоПолномочияПредставителяПартнера,
		|	"""" КАК КоличествоЛистовПриложения,
		|	"""" КАК РешениеРуководителя,
		|	"""" КАК КладовщикПринявшийТовар,
		|	"""" КАК ПредседательКомиссии,
		|	"""" КАК ДолжностьПредседателяКомиссии,
		|	"""" КАК ЧленКомиссии1,
		|	"""" КАК ДолжностьЧленаКомиссии1,
		|	"""" КАК ЧленКомиссии2,
		|	"""" КАК ДолжностьЧленаКомиссии2,
		|	"""" КАК ЧленКомиссии3,
		|	"""" КАК ДолжностьЧленаКомиссии3,
		|	"""" КАК МассаБруттоПоДаннымПроизводителя,
		|	"""" КАК МассаБруттоВПунктеОтправления,
		|	"""" КАК МассаБруттоВПунктеНазначения,
		|	"""" КАК КоличествоМестПоДокументам,
		|	"""" КАК КоличествоМестПоФакту,
		|	"""" КАК КоличествоМестРасхождение,
		|	"""" КАК МассаБруттоПоДокументам,
		|	"""" КАК МассаБруттоПоФакту,
		|	"""" КАК МассаБруттоРасхождение,
		|	"""" КАК МассаНеттоПоДокументам,
		|	"""" КАК МассаНеттоПоФакту,
		|	"""" КАК МассаНеттоРасхождение,
		|	"""" КАК МассаТарыПоДокументам,
		|	"""" КАК МассаТарыПоФакту,
		|	"""" КАК МассаТарыРасхождение,
		|	"""" КАК СтепеньЗаполненияПоДокументам,
		|	"""" КАК СтепеньЗаполненияПоФакту,
		|	"""" КАК ОшибкаНетРасхождений,
		|	АктОРасхождениях.СуммаВключаетНДС КАК ЦенаВключаетНДС,
		|	ПоступлениеТоваров.Ссылка КАК ДокументПоступления,
		|	ПоступлениеТоваров.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
		|	ПоступлениеТоваров.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
		|	АктОРасхождениях.ВидОперации КАК ВидОперации
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях КАК АктОРасхождениях
		|		ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхождениях.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваров
		|		ПО ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваров.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктОРасхожденияхТовары.Ссылка КАК Ссылка,
		|	АктОРасхожденияхТовары.НомерСтроки КАК НомерСтроки,
		|	АктОРасхожденияхТовары.Номенклатура.Код КАК Код,
		|	АктОРасхожденияхТовары.Номенклатура КАК Номенклатура,
		|	АктОРасхожденияхТовары.Номенклатура.Артикул КАК Артикул,
		|	АктОРасхожденияхТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
		|	НоменклатураПоставщиков.Наименование КАК НоменклатураПоставщика,
		|	НоменклатураПоставщиков.Артикул КАК НоменклатураПоставщикаАртикул,
		|	АктОРасхожденияхТовары.КоличествоПоДокументу КАК КоличествоПоДокументам,
		|	АктОРасхожденияхТовары.Количество КАК КоличествоПоФакту,
		|	АктОРасхожденияхТовары.ЦенаПоДокументу КАК Цена,
		|	АктОРасхожденияхТовары.Цена КАК ЦенаПоФакту,
		|	АктОРасхожденияхТовары.СтавкаНДС КАК СтавкаНДС,
		|	АктОРасхожденияхТовары.СуммаНДСПоДокументу КАК СуммаНДСПоДокументам,
		|	АктОРасхожденияхТовары.СуммаНДС КАК СуммаНДСПоФакту,
		|	АктОРасхожденияхТовары.СуммаПоДокументу КАК СуммаПоДокументам,
		|	АктОРасхожденияхТовары.Сумма КАК СуммаПоФакту,
		|	ЕСТЬNULL(ПоступлениеТоваровУслугТовары.ЕдиницаИзмерения, АктОРасхожденияхТовары.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ПоступлениеТоваровУслугТовары.ЕдиницаИзмерения.Код, АктОРасхожденияхТовары.Номенклатура.ЕдиницаИзмерения.Код) КАК КодПоОКЕИ,
		|	ЕСТЬNULL(ПоступлениеТоваровУслугТовары.ЕдиницаИзмерения.Наименование, АктОРасхожденияхТовары.Номенклатура.ЕдиницаИзмерения.Наименование) КАК ЕдиницаИзмеренияНаименование,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхТовары.Количество - АктОРасхожденияхТовары.КоличествоПоДокументу > 0
		|			ТОГДА АктОРасхожденияхТовары.Количество - АктОРасхожденияхТовары.КоличествоПоДокументу
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоИзлишек,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхТовары.КоличествоПоДокументу - АктОРасхожденияхТовары.Количество > 0
		|			ТОГДА АктОРасхожденияхТовары.КоличествоПоДокументу - АктОРасхожденияхТовары.Количество
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоНедостача,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхТовары.Сумма - АктОРасхожденияхТовары.СуммаПоДокументу > 0
		|			ТОГДА АктОРасхожденияхТовары.Сумма - АктОРасхожденияхТовары.СуммаПоДокументу
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаИзлишек,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхТовары.СуммаПоДокументу - АктОРасхожденияхТовары.Сумма > 0
		|			ТОГДА АктОРасхожденияхТовары.СуммаПоДокументу - АктОРасхожденияхТовары.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаНедостача,
		|	АктОРасхожденияхТовары.Ссылка.СуммаВключаетНДС КАК ЦенаВключаетНДСПоФакту,
		|	АктОРасхожденияхТовары.Ссылка.СуммаВключаетНДС КАК ЦенаВключаетНДСПоДокументам,
		|	"""" КАК НомерПаспорта,
		|	"""" КАК ТекстовоеОписание
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ВТ_ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.Товары КАК АктОРасхожденияхТовары
		|		ПО ВТ_ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхТовары.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО (АктОРасхожденияхТовары.Номенклатура = НоменклатураПоставщиков.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ПО ВТ_ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваровУслугТовары.Ссылка
		|ГДЕ
		|	НЕ АктОРасхожденияхТовары.Ссылка ЕСТЬ NULL
		|	И АктОРасхожденияхТовары.ИдентификаторСтроки = ПоступлениеТоваровУслугТовары.ИдентификаторСтроки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктОРасхожденияхУслуги.Ссылка,
		|	АктОРасхожденияхУслуги.НомерСтроки,
		|	АктОРасхожденияхУслуги.Номенклатура.Код,
		|	АктОРасхожденияхУслуги.Номенклатура,
		|	АктОРасхожденияхУслуги.Номенклатура.Артикул,
		|	АктОРасхожденияхУслуги.Номенклатура.НаименованиеПолное,
		|	НоменклатураПоставщиков.Наименование,
		|	НоменклатураПоставщиков.Артикул,
		|	АктОРасхожденияхУслуги.КоличествоПоДокументу,
		|	АктОРасхожденияхУслуги.Количество,
		|	АктОРасхожденияхУслуги.ЦенаПоДокументу,
		|	АктОРасхожденияхУслуги.Цена,
		|	АктОРасхожденияхУслуги.СтавкаНДС,
		|	АктОРасхожденияхУслуги.СуммаНДСПоДокументу,
		|	АктОРасхожденияхУслуги.СуммаНДС,
		|	АктОРасхожденияхУслуги.СуммаПоДокументу,
		|	АктОРасхожденияхУслуги.Сумма,
		|	ЕСТЬNULL(АктОРасхожденияхУслуги.Номенклатура.ЕдиницаИзмерения, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(АктОРасхожденияхУслуги.Номенклатура.ЕдиницаИзмерения.Код, """"),
		|	ЕСТЬNULL(АктОРасхожденияхУслуги.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхУслуги.Количество - АктОРасхожденияхУслуги.КоличествоПоДокументу > 0
		|			ТОГДА АктОРасхожденияхУслуги.Количество - АктОРасхожденияхУслуги.КоличествоПоДокументу
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхУслуги.КоличествоПоДокументу - АктОРасхожденияхУслуги.Количество > 0
		|			ТОГДА АктОРасхожденияхУслуги.КоличествоПоДокументу - АктОРасхожденияхУслуги.Количество
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхУслуги.Сумма - АктОРасхожденияхУслуги.СуммаПоДокументу > 0
		|			ТОГДА АктОРасхожденияхУслуги.Сумма - АктОРасхожденияхУслуги.СуммаПоДокументу
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхУслуги.СуммаПоДокументу - АктОРасхожденияхУслуги.Сумма > 0
		|			ТОГДА АктОРасхожденияхУслуги.СуммаПоДокументу - АктОРасхожденияхУслуги.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	АктОРасхожденияхУслуги.Ссылка.СуммаВключаетНДС,
		|	АктОРасхожденияхУслуги.Ссылка.СуммаВключаетНДС,
		|	"""",
		|	""""
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ВТ_ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.Услуги КАК АктОРасхожденияхУслуги
		|		ПО ВТ_ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхУслуги.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО (АктОРасхожденияхУслуги.Номенклатура = НоменклатураПоставщиков.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|		ПО ВТ_ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваровУслугУслуги.Ссылка
		|ГДЕ
		|	НЕ АктОРасхожденияхУслуги.Ссылка ЕСТЬ NULL
		|	И АктОРасхожденияхУслуги.ИдентификаторСтроки = ПоступлениеТоваровУслугУслуги.ИдентификаторСтроки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктОРасхожденияхАгентскиеУслуги.Ссылка,
		|	АктОРасхожденияхАгентскиеУслуги.НомерСтроки,
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура.Код,
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура,
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура.Артикул,
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура.НаименованиеПолное,
		|	НоменклатураПоставщиков.Наименование,
		|	НоменклатураПоставщиков.Артикул,
		|	АктОРасхожденияхАгентскиеУслуги.КоличествоПоДокументу,
		|	АктОРасхожденияхАгентскиеУслуги.Количество,
		|	АктОРасхожденияхАгентскиеУслуги.ЦенаПоДокументу,
		|	АктОРасхожденияхАгентскиеУслуги.Цена,
		|	АктОРасхожденияхАгентскиеУслуги.СтавкаНДС,
		|	АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу,
		|	АктОРасхожденияхАгентскиеУслуги.СуммаНДС,
		|	АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу,
		|	АктОРасхожденияхАгентскиеУслуги.Сумма,
		|	ЕСТЬNULL(АктОРасхожденияхАгентскиеУслуги.Номенклатура.ЕдиницаИзмерения, НЕОПРЕДЕЛЕНО),
		|	ЕСТЬNULL(АктОРасхожденияхАгентскиеУслуги.Номенклатура.ЕдиницаИзмерения.Код, """"),
		|	ЕСТЬNULL(АктОРасхожденияхАгентскиеУслуги.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхАгентскиеУслуги.Количество - АктОРасхожденияхАгентскиеУслуги.КоличествоПоДокументу > 0
		|			ТОГДА АктОРасхожденияхАгентскиеУслуги.Количество - АктОРасхожденияхАгентскиеУслуги.КоличествоПоДокументу
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхАгентскиеУслуги.КоличествоПоДокументу - АктОРасхожденияхАгентскиеУслуги.Количество > 0
		|			ТОГДА АктОРасхожденияхАгентскиеУслуги.КоличествоПоДокументу - АктОРасхожденияхАгентскиеУслуги.Количество
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхАгентскиеУслуги.Сумма - АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу > 0
		|			ТОГДА АктОРасхожденияхАгентскиеУслуги.Сумма - АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу - АктОРасхожденияхАгентскиеУслуги.Сумма > 0
		|			ТОГДА АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу - АктОРасхожденияхАгентскиеУслуги.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	АктОРасхожденияхАгентскиеУслуги.Ссылка.СуммаВключаетНДС,
		|	АктОРасхожденияхАгентскиеУслуги.Ссылка.СуммаВключаетНДС,
		|	"""",
		|	""""
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ВТ_ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.АгентскиеУслуги КАК АктОРасхожденияхАгентскиеУслуги
		|		ПО ВТ_ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхАгентскиеУслуги.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО (АктОРасхожденияхАгентскиеУслуги.Номенклатура = НоменклатураПоставщиков.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслугАгентскиеУслуги
		|		ПО ВТ_ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваровУслугАгентскиеУслуги.Ссылка
		|ГДЕ
		|	НЕ АктОРасхожденияхАгентскиеУслуги.Ссылка ЕСТЬ NULL
		|	И АктОРасхожденияхАгентскиеУслуги.ИдентификаторСтроки = ПоступлениеТоваровУслугАгентскиеУслуги.ИдентификаторСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктОРасхожденияхПослеПриемкиТовары.Ссылка КАК Ссылка,
		|	АктОРасхожденияхПослеПриемкиТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ПоступлениеТоваровУслугТовары.КоличествоМест, """") КАК КоличествоМест,
		|	НоменклатураПоставщиков.Наименование КАК НоменклатураПоставщикаНаименование,
		|	НоменклатураПоставщиков.Артикул КАК НоменклатураПоставщикаАртикул,
		|	АктОРасхожденияхПослеПриемкиТовары.НомерСтроки КАК НомерСтроки,
		|	"""" КАК ТекстовоеОписание,
		|	АктОРасхожденияхПослеПриемкиТовары.Номенклатура КАК Номенклатура,
		|	АктОРасхожденияхПослеПриемкиТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.Товары КАК АктОРасхожденияхПослеПриемкиТовары
		|		ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхПослеПриемкиТовары.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО (АктОРасхожденияхПослеПриемкиТовары.Номенклатура = НоменклатураПоставщиков.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ПО ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваровУслугТовары.Ссылка
		|ГДЕ
		|	АктОРасхожденияхПослеПриемкиТовары.КоличествоПоДокументу > 0
		|	И АктОРасхожденияхПослеПриемкиТовары.ИдентификаторСтроки = ПоступлениеТоваровУслугТовары.ИдентификаторСтроки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктОРасхожденияхУслуги.Ссылка,
		|	АктОРасхожденияхУслуги.Номенклатура.ЕдиницаИзмерения,
		|	NULL,
		|	НоменклатураПоставщиков.Наименование,
		|	НоменклатураПоставщиков.Артикул,
		|	АктОРасхожденияхУслуги.НомерСтроки,
		|	"""",
		|	АктОРасхожденияхУслуги.Номенклатура,
		|	АктОРасхожденияхУслуги.Номенклатура.НаименованиеПолное
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.Товары КАК АктОРасхожденияхУслуги
		|		ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхУслуги.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО (АктОРасхожденияхУслуги.Номенклатура = НоменклатураПоставщиков.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|		ПО ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваровУслугУслуги.Ссылка
		|ГДЕ
		|	АктОРасхожденияхУслуги.КоличествоПоДокументу > 0
		|	И АктОРасхожденияхУслуги.ИдентификаторСтроки = ПоступлениеТоваровУслугУслуги.ИдентификаторСтроки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктОРасхожденияхАгентскиеУслуги.Ссылка,
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура.ЕдиницаИзмерения,
		|	NULL,
		|	НоменклатураПоставщиков.Наименование,
		|	НоменклатураПоставщиков.Артикул,
		|	АктОРасхожденияхАгентскиеУслуги.НомерСтроки,
		|	"""",
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура,
		|	АктОРасхожденияхАгентскиеУслуги.Номенклатура.НаименованиеПолное
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.АгентскиеУслуги КАК АктОРасхожденияхАгентскиеУслуги
		|		ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхАгентскиеУслуги.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|		ПО (АктОРасхожденияхАгентскиеУслуги.Номенклатура = НоменклатураПоставщиков.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслугАгентскиеУслуги
		|		ПО ДокументыСРасхождениями.ДокументПоступленияСсылка = ПоступлениеТоваровУслугАгентскиеУслуги.Ссылка
		|ГДЕ
		|	АктОРасхожденияхАгентскиеУслуги.ИдентификаторСтроки = ПоступлениеТоваровУслугАгентскиеУслуги.ИдентификаторСтроки
		|	И АктОРасхожденияхАгентскиеУслуги.КоличествоПоДокументу > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПериодыКурсовВалютДокументов.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ПериодыКурсовВалютДокументов.Ссылка.ВалютаДокумента = КурсыВалют.Валюта
		|				ТОГДА ЕСТЬNULL(КурсыВалют.Курс, 1)
		|		КОНЕЦ) КАК КурсВалютыДокумента,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ПериодыКурсовВалютДокументов.Ссылка.ВалютаДокумента = КурсыВалют.Валюта
		|				ТОГДА ЕСТЬNULL(КурсыВалют.Кратность, 1)
		|		КОНЕЦ) КАК КратностьВалютыДокумента,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
		|				ТОГДА ЕСТЬNULL(КурсыВалют.Курс, 1)
		|		КОНЕЦ) КАК КурсВалютыРегламентированногоУчета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
		|				ТОГДА ЕСТЬNULL(КурсыВалют.Кратность, 1)
		|		КОНЕЦ) КАК КратностьВалютыРегламентированногоУчета,
		|	ВЫБОР
		|		КОГДА &ВалютаРегламентированногоУчета = ПериодыКурсовВалютДокументов.Ссылка.ВалютаДокумента
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПересчетНеТребуется
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеДокумента.Ссылка КАК Ссылка,
		|		МАКСИМУМ(ВЫБОР
		|				КОГДА ДанныеДокумента.ВалютаДокумента = КурсыВалют.Валюта
		|					ТОГДА КурсыВалют.Период
		|			КОНЕЦ) КАК ПериодВалютаДокумента,
		|		МАКСИМУМ(ВЫБОР
		|				КОГДА &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
		|					ТОГДА КурсыВалют.Период
		|			КОНЕЦ) КАК ПериодВалютаРегламентированногоУчета
		|	ИЗ
		|		ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях КАК ДанныеДокумента
		|			ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = ДанныеДокумента.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|			ПО (ДанныеДокумента.Дата >= КурсыВалют.Период)
		|				И (ДанныеДокумента.ВалютаДокумента = КурсыВалют.Валюта
		|					ИЛИ &ВалютаРегламентированногоУчета = КурсыВалют.Валюта)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ДанныеДокумента.Ссылка) КАК ПериодыКурсовВалютДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО (ПериодыКурсовВалютДокументов.Ссылка.ВалютаДокумента = КурсыВалют.Валюта
		|					И ПериодыКурсовВалютДокументов.ПериодВалютаДокумента = КурсыВалют.Период
		|				ИЛИ &ВалютаРегламентированногоУчета = КурсыВалют.Валюта
		|					И ПериодыКурсовВалютДокументов.ПериодВалютаРегламентированногоУчета = КурсыВалют.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПериодыКурсовВалютДокументов.Ссылка,
		|	ВЫБОР
		|		КОГДА &ВалютаРегламентированногоУчета = ПериодыКурсовВалютДокументов.Ссылка.ВалютаДокумента
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктОРасхожденияхШтрихкодыУпаковок.Ссылка КАК Ссылка,
		|	АктОРасхожденияхШтрихкодыУпаковок.НомерСтроки КАК НомерСтроки,
		|	АктОРасхожденияхШтрихкодыУпаковок.ШтрихкодУпаковки КАК Штрихкод,
		|	АктОРасхожденияхШтрихкодыУпаковок.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
		|	ЛОЖЬ КАК Обработан,
		|	АктОРасхожденияхШтрихкодыУпаковок.ШтрихкодУпаковки.Номенклатура КАК Номенклатура
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.ШтрихкодыУпаковок КАК АктОРасхожденияхШтрихкодыУпаковок
		|		ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхШтрихкодыУпаковок.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктОРасхожденияхУпаковкиРасхождения.Ссылка КАК Ссылка,
		|	АктОРасхожденияхУпаковкиРасхождения.НомерСтроки КАК НомерСтроки,
		|	АктОРасхожденияхУпаковкиРасхождения.ШтрихкодУпаковки КАК Штрихкод,
		|	АктОРасхожденияхУпаковкиРасхождения.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
		|	АктОРасхожденияхУпаковкиРасхождения.Номенклатура КАК Номенклатура,
		|	АктОРасхожденияхУпаковкиРасхождения.ТипРасхождения КАК ТипРасхождения,
		|	ЛОЖЬ КАК Обработан
		|ИЗ
		|	ВТ_ДокументыСРасхождениями КАК ДокументыСРасхождениями
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях.УпаковкиРасхождения КАК АктОРасхожденияхУпаковкиРасхождения
		|		ПО ДокументыСРасхождениями.АктОРасхожденияхСсылка = АктОРасхожденияхУпаковкиРасхождения.Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", 
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДанныхПечати = Новый Структура;
	СтруктураДанныхПечати.Вставить("ДанныеСчетовФактур"        , ПакетРезультатов[1]);
	СтруктураДанныхПечати.Вставить("ДанныеПечати"              , ПакетРезультатов[2]);
	СтруктураДанныхПечати.Вставить("ДанныеТовары"              , ПакетРезультатов[3]);
	СтруктураДанныхПечати.Вставить("ДанныеТоваровПоДокументам" , ПакетРезультатов[4]);
	СтруктураДанныхПечати.Вставить("ДанныеКурсовВалют"         , ПакетРезультатов[5]);
	СтруктураДанныхПечати.Вставить("ШтрихкодыУпаковок"         , ПакетРезультатов[6]);
	СтруктураДанныхПечати.Вставить("УпаковкиРасхождения"       , ПакетРезультатов[7]);
	
	Возврат СтруктураДанныхПечати;
	
КонецФункции

#КонецОбласти 

#КонецОбласти 

#КонецЕсли
