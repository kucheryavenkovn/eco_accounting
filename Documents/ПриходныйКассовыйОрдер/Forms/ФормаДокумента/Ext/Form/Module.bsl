
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриходныйКассовыйОрдерФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
	Если ПлательщикНПД И ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧекеНПД) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСтатусОфлайнЧековНПД", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.Патенты.Форма.ФормаЭлемента" Тогда
		
		ЗаполнитьПатентОбработкаВыбора(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ПриходныйКассовыйОрдерФормы.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриходныйКассовыйОрдерФормы.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = Объект.Организация
		И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.ПолучениеНаличныхВБанке") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
			ОрганизацияПриИзмененииСервер();
		Иначе
			Элементы.Контрагент.Видимость = ПриходныйКассовыйОрдерФормыВызовСервера.ВидимостьБанковскогоСчета(Объект.Организация);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_Организации" Тогда
		Если Источник = Объект.Организация Тогда
			
			ОбработатьИзмененияВОрганизацииНаСервере();
			
			Модифицированность = Истина;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ЧекиНПДКлиентСервер.ИмяСобытияДляОповещенияОбИзмененииЧека()
		И Параметр.ДокументОснование = Объект.Ссылка Тогда
		
		ЧекиНПДКлиент.ДобавитьПричинуАннулированияЧекаВКомментарий(Параметр, ЭтотОбъект);
		УстановитьСведенияОЧекеНПД(Параметр);
		
	ИначеЕсли ИмяСобытия = ЧекиНПДКлиент.ИмяСобытияИзменениеСтатусаОфлайнЧека()
		И Параметр = Объект.Организация
		И ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧекеНПД) Тогда
		
		ОбновитьСтатусОфлайнЧекаНПД();
		
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийРеквизитовШапки

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		ВидОперацииПриИзмененииСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.ВидОперацииАвтоПодбор(ЭтотОбъект, 
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыПриОплатеПриИзменении(Элемент)
	
	Объект.БезЗакрывающихДокументов = РасчетыПриОплате = "БезДокументов";
	ВидОперацииПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииКлиент(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	КонтрагентПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.КонтрагентАвтоПодбор(ЭтотОбъект, 
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.КонтрагентОкончаниеВводаТекста(ЭтотОбъект, 
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.КонтрагентОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКассаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СчетКасса) Тогда
		СчетКассаПриИзмененииСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДокументаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВалютаДокумента) Тогда
		ВалютаДокументаПриИзмененииСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПодразделениеОрганизацииПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятоОтНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПринятоОтНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятоОтОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПринятоОтОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении()
	
	ПриходныйКассовыйОрдерФормыКлиент.СуммаДокументаПриИзменении(ЭтотОбъект);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.РозничнаяВыручка")
		И Объект.РасшифровкаПлатежа.Количество() = 1 И ПрименениеУСН И УчетВПродажныхЦенах 
		И Объект.ВыручкаСНТТ Тогда
		ЗаполнитьОтражениеВУСН();
	ИначеЕсли ПрименениеУСН И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПКО.ПрочийПриход") Тогда
		ЗаполнитьОтражениеВУСН();
		ПриходныйКассовыйОрдерФормыКлиентСервер.СформироватьПрочееТекстДоходыУСН(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.СчетУчетаПриИзменении(ЭтотОбъект);
	
	Если ПрименениеУСН Тогда
		ЗаполнитьОтражениеВУСН(Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДвиженияДенежныхСредствПрочиеПриИзменении(Элемент)
	
	Если ПрименениеУСН Тогда
		ЗаполнитьОтражениеВУСН(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.СубконтоКт1ПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.СубконтоКт2ПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.СубконтоКт3ПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.СубконтоКт1НачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.СубконтоКт2НачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.СубконтоКт3НачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееДоходыУСНПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПрочееДоходыУСНПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РозничнаяВыручкаРасходыУСНПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РозничнаяВыручкаРасходыУСНПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьНаПатентеПриИзменении(Элемент)
	
	ДеятельностьНаПатентеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьНаТорговомСбореПриИзменении(Элемент)
	
	ДеятельностьНаТорговомСбореПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПатентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Патент) И НЕ Объект.ДеятельностьНаПатенте Тогда
		Объект.ДеятельностьНаПатенте = Истина;
		ДеятельностьНаПатентеПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеобходимоЗаполнитьПатентОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.ТекстНеобходимоЗаполнитьПатентОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятоОтПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПринятоОтПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.ОснованиеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриложениеПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПриложениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(ЭтотОбъект, 
		Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЧекНПДНажатие(Элемент)
	
	ЧекиНПДКлиент.ОткрытьФормуЧека(ЭтотОбъект, Объект.Ссылка, Объект.Организация, Объект.Контрагент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыРасшифровкаПлатежа

&НаКлиенте
Процедура РасшифровкаПлатежаПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПередУдалением(Элемент, Отказ)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаПриНачалеРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДПриИзменении(Элемент)
	
	СтрокаПлатеж = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаПлатеж.СуммаПлатежа)
		Или Не ЗначениеЗаполнено(СтрокаПлатеж.УслугаНПД) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПлатеж.СуммаПлатежа = ЦенаУслугиНПД(СтрокаПлатеж.УслугаНПД, Объект.Организация, Объект.Дата);
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаПриИзменении(Элемент)
	
	РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаОбработкаВыбора(ЭтотОбъект, 
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаАвтоПодбор(ЭтотОбъект, 
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиСтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСпособПогашенияЗадолженностиПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСпособПогашенияЗадолженностиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСделкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСделкаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаКурсВзаиморасчетовПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаКурсВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаВзаиморасчетовПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСуммаВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСтавкаНДСПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСтавкаНДСПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСчетУчетаРасчетовСКонтрагентомПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСчетУчетаРасчетовСКонтрагентомПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеАвансаПредставлениеПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаОтражениеАвансаПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеАвансаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаОтражениеАвансаПредставлениеОбработкаВыбора(ЭтотОбъект,
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(ЭтотОбъект,
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыРасшифровкаВыручки

&НаКлиенте
Процедура РасшифровкаВыручкиПриИзменении(Элемент)
	
	СуммаДоИзменения = Объект.СуммаДокумента;
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиПриИзменении(ЭтотОбъект);
	
	Если ПрименениеУСН И СуммаДоИзменения <> Объект.СуммаДокумента Тогда
		ЗаполнитьОтражениеВУСН(Истина, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиПриНачалеРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиПередУдалением(Элемент, Отказ)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиУслугаНПДПриИзменении(Элемент)
	
	СтрокаПлатеж = Элементы.РасшифровкаВыручки.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаПлатеж.СуммаПлатежа)
		Или Не ЗначениеЗаполнено(СтрокаПлатеж.УслугаНПД) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПлатеж.СуммаПлатежа = ЦенаУслугиНПД(СтрокаПлатеж.УслугаНПД, Объект.Организация, Объект.Дата);
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиУслугаНПДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиУслугаНПДАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиУслугаНПДОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиСуммаПлатежаПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиСтавкаНДСПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаВыручкиСтавкаНДСПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиОтражениеДоходаПредставлениеПриИзменении(Элемент)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыручкиОтражениеДоходаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(ЭтотОбъект,
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ПриходныйКассовыйОрдерФормыКлиент.ПоказатьКонтекстныеНовости(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекНПД(Команда)
	
	Если Не ПриходныйКассовыйОрдерФормыКлиент.ПроверитьЗаполнениеДляФормированияЧекаНПД(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Объект.Проведен Или Модифицированность Тогда
		ПараметрыЗаписи = Новый Структура();
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
	ЧекиНПДКлиент.ОткрытьФормуЧека(ЭтотОбъект, Объект.Ссылка, Объект.Организация, Объект.Контрагент);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

&НаСервере
Процедура ЗаполнитьОтражениеВУСН(ЗаполнитьСуммы = Истина, ЗаполнитьСодержание = Ложь)
	
	ПриходныйКассовыйОрдерФормы.ЗаполнитьОтражениеВУСН(ЭтотОбъект, ЗаполнитьСуммы, ЗаполнитьСодержание);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзмененииКлиент(Элемент) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;
	
	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата,
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
	
	// Если есть договоры в у.е., то необходимо получение курсов валют.
	Если НЕ ТребуетсяВызовСервера Тогда
		ТребуетсяВызовСервера = ЕстьРасчетыВУсловныхЕдиницах;
	КонецЕсли;
	
	// Проверка на изменение сроков действия патентов.
	Если НЕ ТребуетсяВызовСервера Тогда
		ТребуетсяВызовСервера = ПрименяетсяУСНПатент;
	КонецЕсли;
	
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииСервер();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииСервер()
	
	ПриходныйКассовыйОрдерФормы.ДатаПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзмененияВОрганизацииНаСервере()
	
	ПриходныйКассовыйОрдерФормы.ОбработатьИзмененияВОрганизацииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииСервер()
	
	ПриходныйКассовыйОрдерФормы.КонтрагентПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДеятельностьНаПатентеПриИзмененииНаСервере()
	
	ПриходныйКассовыйОрдерФормы.ДеятельностьНаПатентеПриИзмененииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДеятельностьНаТорговомСбореПриИзмененииНаСервере()
	
	ПриходныйКассовыйОрдерФормы.ДеятельностьНаТорговомСбореПриИзмененииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПатентОбработкаВыбора(ВыбранныйПатент)
	
	Если ТипЗнч(ВыбранныйПатент) <> Тип("СправочникСсылка.Патенты") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПатентОбработкаВыбораНаСервере(ВыбранныйПатент);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПатентОбработкаВыбораНаСервере(ВыбранныйПатент)
	
	ПриходныйКассовыйОрдерФормы.ЗаполнитьПатентОбработкаВыбораНаСервере(ЭтотОбъект, ВыбранныйПатент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииКлиент()
	
	СтрокаПлатеж = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	ПриходныйКассовыйОрдерФормыКлиентСервер.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	Если СтрокаПлатеж.ДоговорКонтрагента = СвойстваПлатежа.ДоговорКонтрагента Тогда
		Возврат;
	КонецЕсли;
	
	РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииСервер(СтрокаПлатеж.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииСервер(ИдСтроки)
	
	СтрокаПлатеж = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдСтроки);
	
	ПриходныйКассовыйОрдерФормы.РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииНаСервере(ЭтотОбъект, СтрокаПлатеж);
	
КОнецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	ПриходныйКассовыйОрдерФормы.ОрганизацияПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СчетКассаПриИзмененииСервер()
	
	ПриходныйКассовыйОрдерФормы.СчетКассаПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВалютаДокументаПриИзмененииСервер()
	
	ПриходныйКассовыйОрдерФормы.ВалютаДокументаПриИзмененииСервер(ЭтотОбъект, Новый Массив);
	
КонецПроцедуры

&НаСервере
Процедура ВидОперацииПриИзмененииСервер()
	
	ПриходныйКассовыйОрдерФормы.ВидОперацииПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПОДКЛЮЧАЕМЫЕ ОБРАБОТЧИКИ

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

#КонецОбласти

#Область ЧекиНПД

&НаСервереБезКонтекста
Функция ЦенаУслугиНПД(УслугаНПД, Организация, Дата)
	
	Возврат ЧекиНПД.ЦенаУслугиНПД(УслугаНПД, Организация, Дата);
	
КонецФункции

&НаСервере
Процедура УстановитьСведенияОЧекеНПД(НовыеСведенияОЧекеНПД)
	
	СведенияОЧекеНПД = НовыеСведенияОЧекеНПД;
	ПриходныйКассовыйОрдерФормы.УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСтатусОфлайнЧековНПД()
	
	ЧекиНПДКлиент.ОбновитьСтатусыОфлайнЧековНПД();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусОфлайнЧекаНПД()
	
	ПриходныйКассовыйОрдерФормы.ОбновитьСтатусОфлайнЧекаНПД(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ПроцедурыАрендыИУправленияНедвижимости

//++ЭЛИАС 20141128  
&НаКлиенте
Процедура Подключаемый_ПриИзменении_ВыполнитьПереопределяемоеСобытие(Элемент)
	
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры //--ЭЛИАС 20141128

//++ЭЛИАС 20141128  
&НаКлиенте
Процедура Подключаемый_РасшифровкаПлатежаДоговорКонтрагентаПриИзменении_ВыполнитьПереопределяемоеСобытие(Элемент)
	
	РасшифровкаПлатежаДоговорКонтрагентаПриИзменении(Элемент);
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры //--ЭЛИАС 20141128

//++ЭЛИАС 20141128  
&НаКлиенте
Процедура Подключаемый_ДатаПриИзменении_ВыполнитьПереопределяемоеСобытие(Элемент)
	
	ДатаПриИзменении(Элемент);
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры //--ЭЛИАС 20141128

//++ЭЛИАС 20141128  
&НаКлиенте
Процедура Подключаемый_ВидОперацииПриИзменении_ВыполнитьПереопределяемоеСобытие(Элемент)
	
	ВидОперацииПриИзменении(Элемент);
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры //--ЭЛИАС 20141128

//++ЭЛИАС 20141128  
&НаКлиенте
Процедура Подключаемый_ОрганизацияПриИзменении_ВыполнитьПереопределяемоеСобытие(Элемент)
	
	ОрганизацияПриИзменении(Элемент);
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры //--ЭЛИАС 20141128

//++ЭЛИАС 20141128  
&НаКлиенте
Процедура Подключаемый_КонтрагентПриИзменении_ВыполнитьПереопределяемоеСобытие(Элемент)
	
	КонтрагентПриИзменении(Элемент);
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры //--ЭЛИАС 20141128

//++ЭЛИАС 20120628
&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	
	Если Объект.Проведен Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Заполнение возможно только в непроведенном документе'"));
		Возврат;
	КонецЕсли;

	Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("СпроситьОбОчисткеТабЧасти", ЭтотОбъект, Истина);
		ПоказатьВопрос(Оповещение, "Перед заполнением табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Нет);
	Иначе
		ЗаполнитьПоОстаткамЗавершение();
	КонецЕсли;
	
КонецПроцедуры //--ЭЛИАС 20120628

//++ЭЛИАС 20120628
&НаКлиенте
Процедура ЗаполнитьПоОстаткамЗавершение()

	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,, НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Организация", "Объект");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,, НСтр("ru = 'Контрагент'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Контрагент", "Объект");
		Возврат;
	КонецЕсли;
	
	АдресВременногоХранилища = ПоместитьРасшифровкуВоВременноеХранилище();
	СтруктураПараметров = Новый Структура("Организация, Контрагент, СуммаДляПодбора, АдресВременногоХранилища", Объект.Организация, Объект.Контрагент, Объект.СуммаДокумента, АдресВременногоХранилища);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамОбработкаВыбора", ЭтотОбъект);
	ОткрытьФорму("Обработка.АР_ПодборПараметровРасшифровкиПлатежаДляАренды.Форма.ПараметрыЗаполнения",СтруктураПараметров, ЭтаФорма,, ВариантОткрытияОкна.ОтдельноеОкно,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры //--ЭЛИАС 20120628

//++ЭЛИАС 20140626  
&НаСервере
Функция ПоместитьРасшифровкуВоВременноеХранилище()
	
	РасшифровкаПлатежа = Объект.РасшифровкаПлатежа.Выгрузить();
	Возврат ПоместитьВоВременноеХранилище(РасшифровкаПлатежа);
	
КонецФункции //--ЭЛИАС 20140626

//++ЭЛИАС 20140626  
&НаКлиенте
Процедура ЗаполнитьПоОстаткамОбработкаВыбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		ЗаполнитьРасшифровкуПлатежаПоТаблицеЗначений(РезультатЗакрытия);	
		Модифицированность = Истина;
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры //--ЭЛИАС 20140626

//++ЭЛИАС 20120628
&НаКлиенте
Процедура ЗаполнитьПоСчетам(Команда)
	
	Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("СпроситьОбОчисткеТабЧасти", ЭтотОбъект, Ложь);
		ПоказатьВопрос(Оповещение, "Перед заполнением табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Нет);
	Иначе
		ЗаполнитьПоСчетамЗавершение();
	КонецЕсли;
	
КонецПроцедуры //--ЭЛИАС 20120628

//++ЭЛИАС 20140625  
&НаКлиенте
Процедура СпроситьОбОчисткеТабЧасти(РезультатВопроса, ЗаполнитьПоОстаткам) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Если ЗаполнитьПоОстаткам Тогда
			ЗаполнитьПоОстаткамЗавершение();
		Иначе	
			ЗаполнитьПоСчетамЗавершение();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //--ЭЛИАС 20140625

//++ЭЛИАС 20120628
&НаКлиенте
Процедура ЗаполнитьПоСчетамЗавершение()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,, НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Организация", "Объект");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,, НСтр("ru = 'Контрагент'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Контрагент", "Объект");
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("Организация, Контрагент", Объект.Организация, Объект.Контрагент);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьПоСчетамОбработкаВыбора", ЭтотОбъект);
	ОткрытьФорму("Обработка.АР_ПодборПараметровРасшифровкиПлатежаДляАренды.Форма.ПодборПоСчетам",СтруктураПараметров, ЭтаФорма,, ВариантОткрытияОкна.ОтдельноеОкно,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры //--ЭЛИАС 20120628

//++ЭЛИАС 20140626  
&НаКлиенте
Процедура ЗаполнитьПоСчетамОбработкаВыбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатЗакрытия = Неопределено И ЗначениеЗаполнено(РезультатЗакрытия.АдресВременногоХранилища) Тогда
		ЗаполнитьРасшифровкуПлатежаПоТаблицеЗначений(РезультатЗакрытия.АдресВременногоХранилища);
		Если НЕ ЗначениеЗаполнено(Объект.Основание) Тогда
			Объект.Основание = РезультатЗакрытия.НазначениеПлатежа;
		КонецЕсли;
		Модифицированность = Истина;
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры //--ЭЛИАС 20140626

//++ЭЛИАС 20120622
&НаСервере
Процедура ЗаполнитьРасшифровкуПлатежаПоТаблицеЗначений(АдресВременногоХранилища)
	
	ПриходныйКассовыйОрдерФормы.ИнициализироватьСвойстваПлатежа(ЭтаФорма);
	ТаблицаЗначений = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Объект.РасшифровкаПлатежа.Загрузить(ТаблицаЗначений);
	
	ВидРасчетовПоДоговору = БухгалтерскийУчетПереопределяемый.ОпределениеВидаРасчетовПоПараметрамДоговора(
		Объект.ДоговорКонтрагента);
	ВалютаВзаиморасчетов  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ВалютаВзаиморасчетов");
	ВалютаДокумента = Объект.ВалютаДокумента;
	
	СтруктураКурсаДокумента = АР_ПроцедурыНачисления.ПолучитьКурсВалюты(ВалютаДокумента, Объект.Дата, Объект.ДоговорКонтрагента);
	
	МассивУдаляемыхСтрок = Новый Массив;
	
	Для каждого СтрокаПлатеж Из Объект.РасшифровкаПлатежа Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
			СтрокаПлатеж.ДоговорКонтрагента = СвойстваПлатежа.ДоговорКонтрагента;
		КонецЕсли;
		
		Если НЕ ВалютаДокумента = ВалютаРегламентированногоУчета
			И НЕ СтрокаПлатеж.ДоговорКонтрагента.ВалютаВзаиморасчетов = ВалютаДокумента
			И НЕ СтрокаПлатеж.ДоговорКонтрагента.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Данные по  договору """ + СтрокаПлатеж.ДоговорКонтрагента + """ не заполнены: валюта взаиморасчетов договора должна быть Рубли или должна совпадать с валютой документа.");
			МассивУдаляемыхСтрок.Добавить(СтрокаПлатеж);
		КонецЕсли;
		
		Если СтрокаПлатеж.ДоговорКонтрагента <> СвойстваПлатежа.ДоговорКонтрагента Тогда
			ПараметрыОбъекта = ПриходныйКассовыйОрдерФормыКлиентСервер.ТекущиеПараметрыОбъекта(ЭтаФорма);
			ПараметрыОбъекта.ДоговорКонтрагента = СтрокаПлатеж.ДоговорКонтрагента;
			СвойстваПлатежа = ПриходныйКассовыйОрдерФормы.СвойстваСтрокРасшифровкиПлатежаСервер(ПараметрыОбъекта, Ложь);
		КонецЕсли;
		
		СтрокаПлатеж.СуммаПлатежа = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаПлатеж.СуммаПлатежа,
			ВалютаВзаиморасчетов, ВалютаДокумента,
			СвойстваПлатежа.КурсВзаиморасчетов, СтруктураКурсаДокумента.Курс,
			СвойстваПлатежа.КратностьВзаиморасчетов, СтруктураКурсаДокумента.Кратность);
			
		СтавкаНДС = СтрокаПлатеж.СтавкаНДС;
		
		ИсключаемыеСвойства = "";
		Если СтрокаПлатеж.Свойство("СуммаПлатежа") И СвойстваПлатежа.Свойство("СуммаПлатежа") Тогда
			ИсключаемыеСвойства = ИсключаемыеСвойства + "СуммаПлатежа";
		КонецЕсли;
		Если СтрокаПлатеж.Свойство("СуммаУслуг") И СвойстваПлатежа.Свойство("СуммаУслуг") Тогда
			ИсключаемыеСвойства = ИсключаемыеСвойства + ?(ЗначениеЗаполнено(ИсключаемыеСвойства), ", ", "") + "СуммаУслуг";
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаПлатеж, СвойстваПлатежа,, ИсключаемыеСвойства);
		
		Если ЗначениеЗаполнено(СтавкаНДС) Тогда
			СтрокаПлатеж.СтавкаНДС = СтавкаНДС;	
		КонецЕсли;
		
		ПриходныйКассовыйОрдерФормыКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатеж);
		ПриходныйКассовыйОрдерФормы.РассчитатьКурсИСуммуВзаиморасчетов(ЭтаФорма, СтрокаПлатеж);
		
	КонецЦикла;                                           	
	
	Для К = 1 По МассивУдаляемыхСтрок.Количество() Цикл
		Объект.РасшифровкаПлатежа.Удалить(МассивУдаляемыхСтрок.Количество() - К);
	КонецЦикла;
	
	Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	СчетаУчетаВДокументах.ЗаполнитьПередОтображениемПользователю(ДокументОбъект);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	Для Каждого СтрокаПлатеж Из Объект.РасшифровкаПлатежа Цикл
		Если СтрокаПлатеж.АР_Пени Тогда
			ПараметрыНачисленияПени = АР_ПроцедурыНачисления.ПолучитьПараметрыНачисленияПени(СтрокаПлатеж.ДоговорКонтрагента, Объект.Дата);
			ОбособленныйУчетПени = ?(ПараметрыНачисленияПени = Неопределено, Ложь, ПараметрыНачисленияПени.СпособПогашенияПени = ПредопределенноеЗначение("Перечисление.АР_СпособыПогашенияПени.ВестиОбособленныйУчетПени"));
			Если ОбособленныйУчетПени Тогда
				СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(
					СтрокаПлатеж.ДоговорКонтрагента.Организация, СтрокаПлатеж.ДоговорКонтрагента.Владелец, СтрокаПлатеж.ДоговорКонтрагента);
				СтрокаПлатеж.СчетУчетаРасчетовСКонтрагентом	= СчетаУчета.СчетРасчетовПоПретензиям;
				СтрокаПлатеж.СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетРасчетовПоПретензиям;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПриходныйКассовыйОрдерФормы.УправлениеФормой(ЭтаФорма);

КонецПроцедуры //--ЭЛИАС 20120622

//++ЭЛИАС 20170320 
&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()
	
	ПриходныйКассовыйОрдерФормы.ЗаполнитьДобавленныеКолонкиТаблиц(ЭтаФорма);
	
КонецПроцедуры //--ЭЛИАС 20170320

//++ЭЛИАС 20170320 
&НаКлиенте
Процедура Подключаемый_РасшифровкаПлатежаВыбор_ВыполнитьПереопределяемоеСобытие(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	АР_МодификацияКонфигурацииКлиент.ВыполнитьПереопределяемоеСобытиеВыбор(ЭтаФорма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);	
	
КонецПроцедуры //--ЭЛИАС 20170320

#КонецОбласти
