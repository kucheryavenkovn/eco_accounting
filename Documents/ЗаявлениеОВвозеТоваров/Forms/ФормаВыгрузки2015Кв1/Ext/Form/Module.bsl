
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.СтруктураРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.СтруктураРеквизитов);
		СтруктураРеквизитов = Параметры.СтруктураРеквизитов;
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ФИОУпПред") Тогда
		
		ФИОУполнПредст = РегламентированнаяОтчетность.РазложитьФИО(СтруктураРеквизитов.ФИОУпПред);
		
	КонецЕсли;	
	
	Если СтруктураРеквизитов.Свойство("ФИОРук") Тогда
		
		ФИОПодписант = РегламентированнаяОтчетность.РазложитьФИО(СтруктураРеквизитов.ФИОРук);
		
	КонецЕсли;	
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПрОтпрПриИзменении(Элемент)
	
	Если ПрОтпр = 1 Тогда
		
		ОтправительЯвляетсяЮЛ = НЕ СтруктураРеквизитов.ЭтоПБОЮЛ;
		
		НаимОтпр  = СтруктураРеквизитов.НалогоплательщикНаимЮЛ;
		ИННЮЛОтпр = СтруктураРеквизитов.НалогоплательщикИННЮЛ;
		КППОтпр   = СтруктураРеквизитов.НалогоплательщикКПП;
		ИННФЛОтпр = СтруктураРеквизитов.НалогоплательщикИННФЛ;
		
		ФИОФЛОтпр = РазложитьФИО();
		
		ФамилияФЛОтпр  = ФИОФЛОтпр.Фамилия;
		ИмяФЛОтпр      = ФИОФЛОтпр.Имя;
		ОтчествоФЛОтпр = ФИОФЛОтпр.Отчество;
		
	Иначе	
		
		Если СтруктураРеквизитов.Свойство("ИННУпПред") Тогда
		
			ИННФЛОтпр = СтруктураРеквизитов.ИННУпПред;
			
		КонецЕсли;	
		
		Если НЕ ФИОУполнПредст = Неопределено Тогда
			
			ФамилияФЛОтпр  = ФИОУполнПредст.Фамилия;
			ИмяФЛОтпр      = ФИОУполнПредст.Имя;
			ОтчествоФЛОтпр = ФИОУполнПредст.Отчество;
			
		КонецЕсли;
						
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция РазложитьФИО()
	
	Возврат РегламентированнаяОтчетность.РазложитьФИО(СтруктураРеквизитов.НалогоплательщикФИО);
	
КонецФункции

&НаКлиенте
Процедура ОтправительЯвляетсяЮЛПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрПодп1ПриИзменении(Элемент)
	
	Если ПрПодп1 <> 3 Тогда
		
		НаимДовПодп1  = "";
		НомерДовПодп1 = "";
		ДатаДовПодп1  = '00010101';
		
		Если НЕ ФИОПодписант = Неопределено Тогда
			
			ФамилияПодп1  = ФИОПодписант.Фамилия;
			ИмяПодп1      = ФИОПодписант.Имя;
			ОтчествоПодп1 = ФИОПодписант.Отчество;
			
		КонецЕсли;
		
	Иначе
		
		Если НЕ ФИОУполнПредст = Неопределено Тогда
			
			ФамилияПодп1  = ФИОУполнПредст.Фамилия;
			ИмяПодп1      = ФИОУполнПредст.Имя;
			ОтчествоПодп1 = ФИОУполнПредст.Отчество;
			
		КонецЕсли;
						
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмпортерСовпадаетСОтправителемПриИзменении(Элемент)
	
	Если Элемент <> Ложь Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	Элементы.КтоИмпортирует.Видимость = НЕ ИмпортерСовпадаетСОтправителем;
	Элементы.ГруппаИмпортирует.Видимость = НЕ ИмпортерСовпадаетСОтправителем;
	Элементы.ГруппаИННКППИмпортирует.Видимость = НЕ ИмпортерСовпадаетСОтправителем;
	
	КтоИмпортируетПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяИмпортируетПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННИмпортируетПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННИмпортируетЮЛПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КППИмпортируетПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КтоИмпортируетПриИзменении(Элемент)
	
	Если Элемент <> Ложь Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	Если КтоИмпортирует = 1 Тогда
		
		ФамилияФЛИмпорт = "";
		ИмяФЛИмпорт = "";
		ОтчествоФЛИмпорт = "";
		ИННФЛИмпорт = "";
		
	ИначеЕсли КтоИмпортирует = 2 Тогда
		
		НаимИмпорт = "";
		ИННЮЛИмпорт = "";
		КППИмпорт = "";
		
	КонецЕсли;
		
	Элементы.ГруппаИмпортируетФЛ.Видимость = КтоИмпортирует = 2;
	Элементы.ГруппаИмпортируетЮЛ.Видимость = КтоИмпортирует = 1;
	Элементы.ГруппаИННКППИмпортируетФЛ.Видимость = КтоИмпортирует = 2;
	Элементы.ГруппаИННКППИмпортируетЮЛ.Видимость = КтоИмпортирует = 1;
	
	ИмпортерЯвляетсяЮЛ = ?(КтоИмпортирует = 1, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеИмпортируетПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоИмпортируетПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ФамилияИмпортируетПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ ФормаЗаполнена() Тогда
		Возврат;
	КонецЕсли; 
	
	Модифицированность = Ложь;
	
	ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, ЭтотОбъект);
	
	Закрыть(СтруктураРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ФормаЗаполнена()
	
	Статус = Истина;
	
	Если НЕ ЗначениеЗаполнено(КодИФНС) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнен код налогового органа!'");
		Сообщение.Сообщить();
		
		Статус = Ложь;
		
	ИначеЕсли СтрДлина(СокрЛП(КодИФНС)) <> 4 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Код налогового органа должен состоять из 4 цифр!'");
		Сообщение.Сообщить();
		
		Статус = Ложь;
		
	КонецЕсли;
	
	Если (ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННЮЛОтпр)) ИЛИ (НЕ ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННФЛОтпр)) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнен ИНН отправителя!'");
		Сообщение.Сообщить();
		
		Статус = Ложь;
		
	КонецЕсли; 
	
	Если ПрПодп1 = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(НомерДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовПодп1) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнены сведения о доверенности подписанта 1!'");
			Сообщение.Сообщить();
			
			Статус = Ложь;
			
		КонецЕсли;
	КонецЕсли;
			
	Возврат Статус;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ОтправленоПредставителем = (Форма.ПрОтпр = 2);
	
	Форма.Элементы.ГруппаОтправительЮрЛицо.Видимость  = Форма.ОтправительЯвляетсяЮЛ;
	Форма.Элементы.ГруппаОтправительФизЛицо.Видимость = НЕ Форма.ОтправительЯвляетсяЮЛ;
	
	Если Форма.ПрПодп1 = 3 Тогда
		
		Форма.Элементы.НаимДовПодп1.Видимость  = Истина;
		Форма.Элементы.НомерДовПодп1.Видимость = Истина;
		Форма.Элементы.ДатаДовПодп1.Видимость  = Истина;
		
	Иначе
		
		Форма.Элементы.НаимДовПодп1.Видимость  = Ложь;
		Форма.Элементы.НомерДовПодп1.Видимость = Ложь;
		Форма.Элементы.ДатаДовПодп1.Видимость  = Ложь;
		
	КонецЕсли;
	
	Форма.Элементы.ОтправительЯвляетсяЮЛ.Доступность = ОтправленоПредставителем;
	
	Форма.Элементы.КтоИмпортирует.Видимость = НЕ Форма.ИмпортерСовпадаетСОтправителем;
	Форма.Элементы.ГруппаИмпортирует.Видимость = НЕ Форма.ИмпортерСовпадаетСОтправителем;
	Форма.Элементы.ГруппаИННКППИмпортирует.Видимость = НЕ Форма.ИмпортерСовпадаетСОтправителем;
	
	Если Форма.КтоИмпортирует = 0 Тогда
		Форма.КтоИмпортирует = 1;
	КонецЕсли;
	
	Форма.Элементы.ГруппаИмпортируетФЛ.Видимость = Форма.КтоИмпортирует = 2;
	Форма.Элементы.ГруппаИмпортируетЮЛ.Видимость = Форма.КтоИмпортирует = 1;
	Форма.Элементы.ГруппаИННКППИмпортируетФЛ.Видимость = Форма.КтоИмпортирует = 2;
	Форма.Элементы.ГруппаИННКППИмпортируетЮЛ.Видимость = Форма.КтоИмпортирует = 1;
	
	Форма.ИмпортерЯвляетсяЮЛ = ?(Форма.КтоИмпортирует = 1, Истина, Ложь);
	
КонецПроцедуры

#КонецОбласти