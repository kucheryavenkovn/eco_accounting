#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Мероприятия.Сотрудник, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.РегистрацияТрудовойДеятельности;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведенияДокумента(СсылкаНаДокумент) Экспорт
	
	ДанныеДляПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РегистрацияТрудовойДеятельностиМероприятия.Ссылка КАК Ссылка,
		|	РегистрацияТрудовойДеятельностиМероприятия.Сотрудник КАК ФизическоеЛицо,
		|	РегистрацияТрудовойДеятельностиМероприятия.Ссылка.Организация КАК Организация,
		|	РегистрацияТрудовойДеятельностиМероприятия.ИдМероприятия КАК ИдМероприятия,
		|	ВЫБОР
		|		КОГДА РегистрацияТрудовойДеятельностиМероприятия.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Отменено,
		|	РегистрацияТрудовойДеятельностиМероприятия.СотрудникЗаписи КАК Сотрудник,
		|	РегистрацияТрудовойДеятельностиМероприятия.ДатаМероприятия КАК ДатаМероприятия,
		|	РегистрацияТрудовойДеятельностиМероприятия.ВидМероприятия КАК ВидМероприятия,
		|	РегистрацияТрудовойДеятельностиМероприятия.Сведения КАК Сведения,
		|	РегистрацияТрудовойДеятельностиМероприятия.Подразделение КАК Подразделение,
		|	РегистрацияТрудовойДеятельностиМероприятия.Должность КАК Должность,
		|	РегистрацияТрудовойДеятельностиМероприятия.РазрядКатегория КАК РазрядКатегория,
		|	РегистрацияТрудовойДеятельностиМероприятия.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
		|	РегистрацияТрудовойДеятельностиМероприятия.ТрудоваяФункция КАК ТрудоваяФункция,
		|	РегистрацияТрудовойДеятельностиМероприятия.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
		|	РегистрацияТрудовойДеятельностиМероприятия.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	РегистрацияТрудовойДеятельностиМероприятия.СерияДокументаОснования КАК СерияДокументаОснования,
		|	РегистрацияТрудовойДеятельностиМероприятия.НомерДокументаОснования КАК НомерДокументаОснования,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОснованиеУвольнения КАК ОснованиеУвольнения,
		|	РегистрацияТрудовойДеятельностиМероприятия.ДатаС КАК ДатаС,
		|	РегистрацияТрудовойДеятельностиМероприятия.ДатаПо КАК ДатаПо,
		|	РегистрацияТрудовойДеятельностиМероприятия.ДатаОтмены КАК ДатаОтмены,
		|	РегистрацияТрудовойДеятельностиМероприятия.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	РегистрацияТрудовойДеятельностиМероприятия.ПредставлениеДолжности КАК ПредставлениеДолжности,
		|	РегистрацияТрудовойДеятельностиМероприятия.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
		|	РегистрацияТрудовойДеятельностиМероприятия.ОписаниеДолжности КАК ОписаниеДолжности,
		|	РегистрацияТрудовойДеятельностиМероприятия.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Документ.РегистрацияТрудовойДеятельности.Мероприятия КАК РегистрацияТрудовойДеятельностиМероприятия
		|ГДЕ
		|	РегистрацияТрудовойДеятельностиМероприятия.Ссылка В(&СсылкаНаДокумент)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки";
	
	ДвиженияДокумента = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДвиженияДокумента.Добавить(ЭлектронныеТрудовыеКнижки.ЗаписьДвиженияМероприятияТрудовойДеятельности(Выборка, Ложь));
	КонецЦикла;
	
	ДанныеДляПроведения.Вставить("МероприятияТрудовойДеятельности", ДвиженияДокумента);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#Область ПроцедурыИФункцииПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

Функция ДанныеДляПроведенияМероприятияТрудовойДеятельностиСЗВТД(СсылкаНаДокумент)
	
	ДанныеДляПроведения = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("ПустойИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
		|	ТаблицаДокумента.Сотрудник КАК ФизическоеЛицо,
		|	ТаблицаДокумента.ДатаМероприятия КАК ДатаМероприятия,
		|	ТаблицаДокумента.ВидМероприятия КАК ВидМероприятия,
		|	ТаблицаДокумента.Сведения КАК Сведения,
		|	ТаблицаДокумента.Должность КАК Должность,
		|	ТаблицаДокумента.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
		|	ТаблицаДокумента.РазрядКатегория КАК РазрядКатегория,
		|	ТаблицаДокумента.Подразделение КАК Подразделение,
		|	ТаблицаДокумента.ТрудоваяФункция КАК ТрудоваяФункция,
		|	ТаблицаДокумента.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
		|	ТаблицаДокумента.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	ТаблицаДокумента.НомерДокументаОснования КАК НомерДокументаОснования,
		|	ТаблицаДокумента.СерияДокументаОснования КАК СерияДокументаОснования,
		|	ТаблицаДокумента.ОснованиеУвольнения КАК ОснованиеУвольнения,
		|	ТаблицаДокумента.ДатаС КАК ДатаС,
		|	ТаблицаДокумента.ДатаПо КАК ДатаПо,
		|	ТаблицаДокумента.ДатаОтмены КАК ДатаОтмены,
		|	ТаблицаДокумента.ИдМероприятия КАК ИдМероприятия,
		|	ТаблицаДокумента.СотрудникЗаписи КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Отменено,
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	ТаблицаДокумента.ПредставлениеДолжности КАК ПредставлениеДолжности,
		|	ТаблицаДокумента.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
		|	ТаблицаДокумента.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
		|	ТаблицаДокумента.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
		|	ТаблицаДокумента.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
		|	ТаблицаДокумента.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
		|	ТаблицаДокумента.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
		|	ТаблицаДокумента.ОписаниеДолжности КАК ОписаниеДолжности
		|ИЗ
		|	Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.Мероприятия КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ПО ТаблицаДокумента.Сотрудник = МероприятияТрудовойДеятельности.ФизическоеЛицо
		|			И ТаблицаДокумента.СотрудникЗаписи = МероприятияТрудовойДеятельности.Сотрудник
		|			И ТаблицаДокумента.Ссылка.Организация = МероприятияТрудовойДеятельности.Организация
		|			И ТаблицаДокумента.ИдМероприятия = МероприятияТрудовойДеятельности.ИдМероприятия
		|			И (ВЫБОР
		|				КОГДА ТаблицаДокумента.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)
		|					ТОГДА ЛОЖЬ
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ = МероприятияТрудовойДеятельности.Отменено)
		|			И (НЕ МероприятияТрудовойДеятельности.Регистратор В (&Ссылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПрочие КАК МероприятияТрудовойДеятельностиПрочие
		|		ПО ТаблицаДокумента.Сотрудник = МероприятияТрудовойДеятельностиПрочие.ФизическоеЛицо
		|			И ТаблицаДокумента.СотрудникЗаписи = МероприятияТрудовойДеятельностиПрочие.Сотрудник
		|			И ТаблицаДокумента.Ссылка.Организация = МероприятияТрудовойДеятельностиПрочие.Организация
		|			И ТаблицаДокумента.ИдМероприятия = МероприятияТрудовойДеятельностиПрочие.ИдМероприятия
		|			И (ВЫБОР
		|				КОГДА ТаблицаДокумента.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)
		|					ТОГДА ЛОЖЬ
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ = МероприятияТрудовойДеятельностиПрочие.Отменено)
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В(&Ссылка)
		|	И МероприятияТрудовойДеятельности.ИдМероприятия ЕСТЬ NULL
		|	И МероприятияТрудовойДеятельностиПрочие.ИдМероприятия ЕСТЬ NULL
		|	И ТаблицаДокумента.Ссылка.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ДвиженияДокумента = Новый Массив;
		ДанныеДляПроведения.Вставить(Выборка.Ссылка, ДвиженияДокумента);
		
		Пока Выборка.Следующий() Цикл
			Запись = ЭлектронныеТрудовыеКнижки.ЗаписьДвиженияМероприятияТрудовойДеятельности(Выборка, Ложь);
			ДвиженияДокумента.Добавить(Запись);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура СформироватьНаборыЗаписейМероприятияТрудовойДеятельности(МероприятияТрудовойДеятельности, ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Ссылка.ДокументПринятВПФР КАК ДокументПринятВПФР
		|ИЗ
		|	Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.Мероприятия КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ПО ТаблицаДокумента.ИдМероприятия = МероприятияТрудовойДеятельности.ИдМероприятия
		|			И (ВЫБОР
		|				КОГДА ТаблицаДокумента.ДатаОтмены = ДАТАВРЕМЯ(1, 1, 1)
		|					ТОГДА ЛОЖЬ
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ = МероприятияТрудовойДеятельности.Отменено)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПрочие КАК МероприятияТрудовойДеятельностиПрочие
		|		ПО ТаблицаДокумента.ИдМероприятия = МероприятияТрудовойДеятельностиПрочие.ИдМероприятия
		|ГДЕ
		|	НЕ ТаблицаДокумента.Ссылка В (&МассивОбновленных)
		|	И ТаблицаДокумента.Ссылка.Проведен
		|	И ЕСТЬNULL(МероприятияТрудовойДеятельности.ИдМероприятия, МероприятияТрудовойДеятельностиПрочие.ИдМероприятия) ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументПринятВПФР УБЫВ";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ОбрабатываемыеДокументы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	РеквизитыОбновляемыхДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ОбрабатываемыеДокументы, "Дата,Организация,Ответственный,Комментарий");
	
	ДанныеДляПроведенияДокументов = ДанныеДляПроведенияМероприятияТрудовойДеятельностиСЗВТД(ОбрабатываемыеДокументы);
	Для Каждого Регистратор Из ОбрабатываемыеДокументы Цикл
		
		МассивОбновленных.Добавить(Регистратор);
		ДанныеДляПроведения = ДанныеДляПроведенияДокументов.Получить(Регистратор);
		Если ДанныеДляПроведения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Документы.РегистрацияТрудовойДеятельности.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыОбновляемыхДокументов.Получить(Регистратор));
		
		Для Каждого СтрокаДанных Из ДанныеДляПроведения Цикл
			
			СтрокаМероприятия = ДокументОбъект.Мероприятия.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаМероприятия, СтрокаДанных);
			СтрокаМероприятия.Сотрудник = СтрокаДанных.ФизическоеЛицо;
			СтрокаМероприятия.СотрудникЗаписи = СтрокаДанных.Сотрудник;
			Если ЗначениеЗаполнено(СтрокаДанных.Сотрудник) Тогда
				СтрокаМероприятия.СотрудникЗаписи = СтрокаДанных.Сотрудник;
			Иначе
				СтрокаМероприятия.СотрудникЗаписи = КадровыйУчет.ОсновнойСотрудникФизическогоЛица(
					СтрокаМероприятия.Сотрудник, ДокументОбъект.Организация, СтрокаМероприятия.ДатаМероприятия);
			КонецЕсли;
			
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Истина, Истина, РежимЗаписиДокумента.Запись);
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, , , РежимЗаписиДокумента.Проведение);
		Исключение
			
			ИнформацияОшибки = ИнформацияОбОшибке();
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Электронные трудовые книжки'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				ДокументОбъект.Метаданные(),
				ДокументОбъект.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОшибки));
			
		КонецПопытки;
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОтменитьЗадвоенныеМероприятияКадровыхПриказов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДатаНачалаУчета", ЭлектронныеТрудовыеКнижки.ДатаНачалаУчета());
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельности.Организация КАК Организация,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельности.Отменено КАК Отменено,
		|	МероприятияТрудовойДеятельности.ВидМероприятия КАК ВидМероприятия,
		|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
		|	МероприятияТрудовойДеятельности.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	МероприятияТрудовойДеятельности.Регистратор КАК Регистратор,
		|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТМероприятияКадровыхПриказов
		|ИЗ
		|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
		|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
		|			И МероприятияТрудовойДеятельности.Отменено = МероприятияТрудовойДеятельностиПереданные.Отменено
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельностиОтмененные
		|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиОтмененные.ИдМероприятия
		|			И (МероприятияТрудовойДеятельностиОтмененные.Отменено)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(МероприятияТрудовойДеятельности.Регистратор) <> ТИП(Документ.РегистрацияТрудовойДеятельности)
		|	И МероприятияТрудовойДеятельности.ВидМероприятия В (ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Перевод), ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Увольнение))
		|	И НЕ МероприятияТрудовойДеятельности.Отменено
		|	И МероприятияТрудовойДеятельностиПереданные.ИдМероприятия ЕСТЬ NULL
		|	И МероприятияТрудовойДеятельностиОтмененные.ИдМероприятия ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельности.Организация КАК Организация,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельности.Отменено КАК Отменено,
		|	МероприятияТрудовойДеятельности.ВидМероприятия КАК ВидМероприятия,
		|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
		|	МероприятияТрудовойДеятельности.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	МероприятияТрудовойДеятельности.Регистратор КАК Регистратор,
		|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник,
		|	МероприятияТрудовойДеятельностиПереданные.Регистратор.ОтчетныйПериод КАК РегистраторОтчетныйПериод
		|ПОМЕСТИТЬ ВТМероприятияРегистрацийТрудовойДеятельности
		|ИЗ
		|	РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельностиПереданные КАК МероприятияТрудовойДеятельностиПереданные
		|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиПереданные.ИдМероприятия
		|			И МероприятияТрудовойДеятельности.Отменено = МероприятияТрудовойДеятельностиПереданные.Отменено
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельностиОтмененные
		|		ПО МероприятияТрудовойДеятельности.ИдМероприятия = МероприятияТрудовойДеятельностиОтмененные.ИдМероприятия
		|			И (МероприятияТрудовойДеятельностиОтмененные.Отменено)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(МероприятияТрудовойДеятельности.Регистратор) = ТИП(Документ.РегистрацияТрудовойДеятельности)
		|	И МероприятияТрудовойДеятельности.ВидМероприятия В (ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Перевод), ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Увольнение))
		|	И НЕ МероприятияТрудовойДеятельности.Отменено
		|	И НЕ МероприятияТрудовойДеятельностиПереданные.ИдМероприятия ЕСТЬ NULL
		|	И МероприятияТрудовойДеятельностиОтмененные.ИдМероприятия ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА КОНЕЦПЕРИОДА(МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод, МЕСЯЦ) > &ТекущаяДата
		|				ТОГДА &ТекущаяДата
		|			ИНАЧЕ КОНЕЦПЕРИОДА(ВЫБОР
		|						КОГДА МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод < &ДатаНачалаУчета
		|							ТОГДА &ДатаНачалаУчета
		|						ИНАЧЕ МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод
		|					КОНЕЦ, МЕСЯЦ)
		|		КОНЕЦ) КАК ДатаОтменыМероприятия,
		|	КОНЕЦПЕРИОДА(ВЫБОР
		|			КОГДА КОНЕЦПЕРИОДА(МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод, МЕСЯЦ) > &ТекущаяДата
		|				ТОГДА &ТекущаяДата
		|			ИНАЧЕ КОНЕЦПЕРИОДА(ВЫБОР
		|						КОГДА МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод < &ДатаНачалаУчета
		|							ТОГДА &ДатаНачалаУчета
		|						ИНАЧЕ МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод
		|					КОНЕЦ, МЕСЯЦ)
		|		КОНЕЦ, МЕСЯЦ) КАК Месяц,
		|	МероприятияКадровыхПриказов.Организация КАК Организация,
		|	МероприятияКадровыхПриказов.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияКадровыхПриказов.Отменено КАК Отменено
		|ПОМЕСТИТЬ ВТЗакрываемыеМероприятия
		|ИЗ
		|	ВТМероприятияКадровыхПриказов КАК МероприятияКадровыхПриказов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМероприятияРегистрацийТрудовойДеятельности КАК МероприятияРегистрацийТрудовойДеятельности
		|		ПО МероприятияКадровыхПриказов.ФизическоеЛицо = МероприятияРегистрацийТрудовойДеятельности.ФизическоеЛицо
		|			И МероприятияКадровыхПриказов.Организация = МероприятияРегистрацийТрудовойДеятельности.Организация
		|			И МероприятияКадровыхПриказов.Отменено = МероприятияРегистрацийТрудовойДеятельности.Отменено
		|			И МероприятияКадровыхПриказов.ВидМероприятия = МероприятияРегистрацийТрудовойДеятельности.ВидМероприятия
		|			И МероприятияКадровыхПриказов.ДатаМероприятия = МероприятияРегистрацийТрудовойДеятельности.ДатаМероприятия
		|			И МероприятияКадровыхПриказов.ЯвляетсяСовместителем = МероприятияРегистрацийТрудовойДеятельности.ЯвляетсяСовместителем
		|			И МероприятияКадровыхПриказов.Сотрудник = МероприятияРегистрацийТрудовойДеятельности.Сотрудник
		|
		|СГРУППИРОВАТЬ ПО
		|	МероприятияКадровыхПриказов.Организация,
		|	МероприятияКадровыхПриказов.ИдМероприятия,
		|	МероприятияКадровыхПриказов.Отменено,
		|	КОНЕЦПЕРИОДА(ВЫБОР
		|			КОГДА КОНЕЦПЕРИОДА(МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод, МЕСЯЦ) > &ТекущаяДата
		|				ТОГДА &ТекущаяДата
		|			ИНАЧЕ КОНЕЦПЕРИОДА(ВЫБОР
		|						КОГДА МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод < &ДатаНачалаУчета
		|							ТОГДА &ДатаНачалаУчета
		|						ИНАЧЕ МероприятияРегистрацийТрудовойДеятельности.РегистраторОтчетныйПериод
		|					КОНЕЦ, МЕСЯЦ)
		|		КОНЕЦ, МЕСЯЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗакрываемыеМероприятия.ИдМероприятия КАК ИдМероприятия
		|ИЗ
		|	ВТЗакрываемыеМероприятия КАК ЗакрываемыеМероприятия";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗакрываемыеМероприятия.Месяц КАК Месяц,
		|	ЗакрываемыеМероприятия.ДатаОтменыМероприятия КАК ДатаОтменыМероприятия,
		|	МероприятияТрудовойДеятельности.Регистратор КАК Регистратор,
		|	МероприятияТрудовойДеятельности.НомерСтроки КАК НомерСтроки,
		|	МероприятияТрудовойДеятельности.Активность КАК Активность,
		|	МероприятияТрудовойДеятельности.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МероприятияТрудовойДеятельности.Организация КАК Организация,
		|	МероприятияТрудовойДеятельности.ИдМероприятия КАК ИдМероприятия,
		|	МероприятияТрудовойДеятельности.Отменено КАК Отменено,
		|	МероприятияТрудовойДеятельности.Сотрудник КАК Сотрудник,
		|	МероприятияТрудовойДеятельности.ДатаМероприятия КАК ДатаМероприятия,
		|	МероприятияТрудовойДеятельности.ВидМероприятия КАК ВидМероприятия,
		|	МероприятияТрудовойДеятельности.Сведения КАК Сведения,
		|	МероприятияТрудовойДеятельности.Подразделение КАК Подразделение,
		|	МероприятияТрудовойДеятельности.Должность КАК Должность,
		|	МероприятияТрудовойДеятельности.РазрядКатегория КАК РазрядКатегория,
		|	МероприятияТрудовойДеятельности.КодПоРееструДолжностей КАК КодПоРееструДолжностей,
		|	МероприятияТрудовойДеятельности.ТрудоваяФункция КАК ТрудоваяФункция,
		|	МероприятияТрудовойДеятельности.НаименованиеДокументаОснования КАК НаименованиеДокументаОснования,
		|	МероприятияТрудовойДеятельности.ДатаДокументаОснования КАК ДатаДокументаОснования,
		|	МероприятияТрудовойДеятельности.СерияДокументаОснования КАК СерияДокументаОснования,
		|	МероприятияТрудовойДеятельности.НомерДокументаОснования КАК НомерДокументаОснования,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольнения КАК ОснованиеУвольнения,
		|	МероприятияТрудовойДеятельности.ДатаС КАК ДатаС,
		|	МероприятияТрудовойДеятельности.ДатаПо КАК ДатаПо,
		|	МероприятияТрудовойДеятельности.ДатаОтмены КАК ДатаОтмены,
		|	МероприятияТрудовойДеятельности.ЯвляетсяСовместителем КАК ЯвляетсяСовместителем,
		|	МероприятияТрудовойДеятельности.ПредставлениеДолжности КАК ПредставлениеДолжности,
		|	МероприятияТрудовойДеятельности.ПредставлениеПодразделения КАК ПредставлениеПодразделения,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияТекстОснования КАК ОснованиеУвольненияТекстОснования,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияСтатья КАК ОснованиеУвольненияСтатья,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияЧасть КАК ОснованиеУвольненияЧасть,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияПункт КАК ОснованиеУвольненияПункт,
		|	МероприятияТрудовойДеятельности.ОснованиеУвольненияПодПункт КАК ОснованиеУвольненияПодПункт,
		|	МероприятияТрудовойДеятельности.ОписаниеДолжности КАК ОписаниеДолжности
		|ИЗ
		|	ВТЗакрываемыеМероприятия КАК ЗакрываемыеМероприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ПО ЗакрываемыеМероприятия.ИдМероприятия = МероприятияТрудовойДеятельности.ИдМероприятия
		|			И ЗакрываемыеМероприятия.Отменено = МероприятияТрудовойДеятельности.Отменено
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗакрываемыеМероприятия.Месяц,
		|	Организация,
		|	ФизическоеЛицо,
		|	ДатаМероприятия,
		|	ВидМероприятия,
		|	Отменено";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Месяц") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
			
			ДокументОбъект = Документы.РегистрацияТрудовойДеятельности.СоздатьДокумент();
			ДокументОбъект.Организация = Выборка.Организация;
			ДокументОбъект.Дата = Выборка.ДатаОтменыМероприятия;
			
			Пока Выборка.Следующий() Цикл
				
				СтрокаМероприятия = ДокументОбъект.Мероприятия.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаМероприятия, Выборка);
				СтрокаМероприятия.Сотрудник = Выборка.ФизическоеЛицо;
				СтрокаМероприятия.СотрудникЗаписи = Выборка.Сотрудник;
				СтрокаМероприятия.ДатаОтмены = Выборка.ДатаОтменыМероприятия;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Истина, Истина, РежимЗаписиДокумента.Запись);
			Попытка
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, , , РежимЗаписиДокумента.Проведение);
			Исключение
				
				ИнформацияОшибки = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Электронные трудовые книжки'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка,
					ДокументОбъект.Метаданные(),
					ДокументОбъект.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОшибки));
				
			КонецПопытки;
			
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли