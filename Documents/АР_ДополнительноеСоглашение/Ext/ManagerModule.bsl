#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДокументСсылка.Дата - 1);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицыДокумента(НомераТаблиц)
		+ ТекстЗапросаСтатусыОбъектовАренды(НомераТаблиц)
		+ ТекстЗапросаСтавки(НомераТаблиц)
		+ ТекстЗапросаПараметрыНачисленияПени(НомераТаблиц)
		+ ТекстЗапросаКурсыВалют(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаПостояннаяЧасть"     , НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаПеременнаяЧасть"     , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПостояннаяЧасть.Ссылка,
	|	ТаблицаПостояннаяЧасть.НомерСтроки,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.Услуга,
	|	ТаблицаПостояннаяЧасть.Количество,
	|	ТаблицаПостояннаяЧасть.СпособНачисления,
	|	ТаблицаПостояннаяЧасть.Ставка,
	|	ТаблицаПостояннаяЧасть.СтавкаНДС,
	|	ТаблицаПостояннаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПостояннаяЧасть.ДатаОкончанияСрокаАренды
	|ПОМЕСТИТЬ ТаблицаПостояннаяЧасть
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение.ПостояннаяЧастьАренднойПлаты КАК ТаблицаПостояннаяЧасть
	|ГДЕ
	|	ТаблицаПостояннаяЧасть.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПеременнаяЧасть.Ссылка,
	|	ТаблицаПеременнаяЧасть.НомерСтроки,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.Услуга,
	|	ТаблицаПеременнаяЧасть.Количество,
	|	ТаблицаПеременнаяЧасть.ЕдиницаИзмерения,
	|	ТаблицаПеременнаяЧасть.СпособНачисления,
	|	ТаблицаПеременнаяЧасть.Ставка,
	|	ТаблицаПеременнаяЧасть.СтавкаНДС,
	|	ТаблицаПеременнаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПеременнаяЧасть.Ставка,
	|	ТаблицаПеременнаяЧасть.ДатаОкончанияСрокаАренды,
	|	ТаблицаПеременнаяЧасть.Счетчик,
	|	ТаблицаПеременнаяЧасть.КоэффициентОтнесенияСчетчика
	|ПОМЕСТИТЬ ТаблицаПеременнаяЧасть
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение.ПеременнаяЧастьАренднойПлаты КАК ТаблицаПеременнаяЧасть
	|ГДЕ
	|	ТаблицаПеременнаяЧасть.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаПостояннаяЧасть", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПеременнаяЧасть", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПостояннаяЧасть.Ссылка,
	|	ТаблицаПостояннаяЧасть.НомерСтроки,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.Услуга,
	|	ТаблицаПостояннаяЧасть.Количество,
	|	ТаблицаПостояннаяЧасть.СпособНачисления,
	|	ТаблицаПостояннаяЧасть.Ставка,
	|	ТаблицаПостояннаяЧасть.СтавкаНДС,
	|	ТаблицаПостояннаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПостояннаяЧасть.ДатаОкончанияСрокаАренды
	|ИЗ
	|	ТаблицаПостояннаяЧасть КАК ТаблицаПостояннаяЧасть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПеременнаяЧасть.Ссылка,
	|	ТаблицаПеременнаяЧасть.НомерСтроки,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.Услуга,
	|	ТаблицаПеременнаяЧасть.Количество,
	|	ТаблицаПеременнаяЧасть.ЕдиницаИзмерения,
	|	ТаблицаПеременнаяЧасть.СпособНачисления,
	|	ТаблицаПеременнаяЧасть.Ставка,
	|	ТаблицаПеременнаяЧасть.СтавкаНДС,
	|	ТаблицаПеременнаяЧасть.ДатаНачалаСрокаАренды,
	|	ТаблицаПеременнаяЧасть.ДатаОкончанияСрокаАренды,
	|	ТаблицаПеременнаяЧасть.Счетчик,
	|	ТаблицаПеременнаяЧасть.КоэффициентОтнесенияСчетчика
	|ИЗ
	|	ТаблицаПеременнаяЧасть КАК ТаблицаПеременнаяЧасть";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСтатусыОбъектовАренды(НомераТаблиц)
	
	НомераТаблиц.Вставить("СтатусыОбъектовАрендыПостояннаяЧасть", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СтатусыОбъектовАрендыПеременнаяЧасть", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПостояннаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ДокументОснование КАК ЗаключениеДоговора,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПостояннаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды,
	|	ТаблицаПостояннаяЧасть.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды,
	|	ЗНАЧЕНИЕ(Перечисление.АР_СтатусыОбъектовАренды.ВАренде) КАК Статус
	|ИЗ
	|	ТаблицаПостояннаяЧасть КАК ТаблицаПостояннаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ДополнительноеСоглашение КАК Реквизиты
	|		ПО ТаблицаПостояннаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПеременнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ДокументОснование КАК ЗаключениеДоговора,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПеременнаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды,
	|	ТаблицаПеременнаяЧасть.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды,
	|	ТаблицаПеременнаяЧасть.Счетчик КАК Счетчик,
	|	ТаблицаПеременнаяЧасть.КоэффициентОтнесенияСчетчика КАК КоэффициентОтнесенияСчетчика,
	|	ЗНАЧЕНИЕ(Перечисление.АР_СтатусыОбъектовАренды.ВАренде) КАК Статус
	|ИЗ
	|	ТаблицаПеременнаяЧасть КАК ТаблицаПеременнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ДополнительноеСоглашение КАК Реквизиты
	|		ПО ТаблицаПеременнаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСтавки(НомераТаблиц)
	
	НомераТаблиц.Вставить("СтавкиПостояннаяЧасть", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СтавкиПеременнаяЧасть", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаПостояннаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПостояннаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПостояннаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПостояннаяЧасть.Ставка КАК Ставка,
	|	ТаблицаПостояннаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПостояннаяЧасть.СпособНачисления КАК СпособНачисления,
	|	ТаблицаПостояннаяЧасть.Количество КАК Площадь,
	|	Реквизиты.СрокДействия КАК СрокДействия
	|ИЗ
	|	ТаблицаПостояннаяЧасть КАК ТаблицаПостояннаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ДополнительноеСоглашение КАК Реквизиты
	|		ПО ТаблицаПостояннаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаПеременнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	Реквизиты.ДоговорКонтрагента КАК Договор,
	|	ТаблицаПеременнаяЧасть.Услуга КАК Услуга,
	|	ТаблицаПеременнаяЧасть.ОбъектАренды КАК ОбъектАренды,
	|	ТаблицаПеременнаяЧасть.Ставка КАК Ставка,
	|	ТаблицаПеременнаяЧасть.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаПеременнаяЧасть.СпособНачисления КАК СпособНачисления,
	|	ТаблицаПеременнаяЧасть.Количество КАК Площадь,
	|	Реквизиты.СрокДействия КАК СрокДействия
	|ИЗ
	|	ТаблицаПеременнаяЧасть КАК ТаблицаПеременнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ДополнительноеСоглашение КАК Реквизиты
	|		ПО ТаблицаПеременнаяЧасть.Ссылка = Реквизиты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПараметрыНачисленияПени(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПараметрыНачисленияПени", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СрокиОплаты.НазначениеСрока КАК НазначениеСрока,
	|	СрокиОплаты.Услуга КАК Услуга,
	|	СрокиОплаты.СпособОпределенияСрокаОплаты КАК СпособОпределенияСрокаОплаты,
	|	СрокиОплаты.СрокОплаты КАК СрокОплаты,
	|	СрокиОплаты.ПериодСрокаОплаты КАК ПериодСрокаОплаты,
	|	СрокиОплаты.КоличествоДнейОтДатыСчетаАкта КАК КоличествоДнейОтДатыСчетаАкта,
	|	СрокиОплаты.УчитыватьТолькоРабочиеДниОтДатыСчетаАкта КАК УчитыватьТолькоРабочиеДниОтДатыСчетаАкта,
	|	СрокиОплаты.СпособНачисленияПени КАК СпособНачисленияПени,
	|	СрокиОплаты.СтавкаПени КАК СтавкаПени,
	|	СрокиОплаты.СпособПогашенияПени КАК СпособПогашенияПени,
	|	СрокиОплаты.КоличествоДнейОтсрочки КАК КоличествоДнейОтсрочки,
	|	СрокиОплаты.УчитыватьТолькоРабочиеДниОтсрочки КАК УчитыватьТолькоРабочиеДниОтсрочки,
	|	СрокиОплаты.УчитыватьТолькоРабочиеДниПриРасчетеПени КАК УчитыватьТолькоРабочиеДниПриРасчетеПени,
	|	СрокиОплаты.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СрокиОплаты.Ссылка.Дата КАК Период,
	|	СрокиОплаты.Ссылка.СрокДействия КАК СрокДействия
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение.СрокиОплаты КАК СрокиОплаты
	|ГДЕ
	|	СрокиОплаты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаКурсыВалют(НомераТаблиц)
	
	НомераТаблиц.Вставить("КурсыВалют", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВалютаСпособаНачисления КАК Валюта,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Курс КАК Курс,
	|	Реквизиты.Кратность КАК Кратность,
	|	Реквизиты.Коэффициент КАК Коэффициент,
	|	Реквизиты.СпособРасчета КАК СпособРасчета,
	|	Реквизиты.НижняяГраница КАК НижняяГраница,
	|	Реквизиты.ВерхняяГраница КАК ВерхняяГраница,
	|	Реквизиты.СрокДействия КАК СрокДействия
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И Реквизиты.ИспользоватьСпециальныйКурсВалюты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КурсыВалютДляРасчетовСАрендаторамиСрезПоследних.ДоговорКонтрагента,
	|	Реквизиты.Дата,
	|	КурсыВалютДляРасчетовСАрендаторамиСрезПоследних.Валюта,
	|	КурсыВалютДляРасчетовСАрендаторамиСрезПоследних.Контрагент,
	|	0,
	|	0,
	|	1,
	|	ЗНАЧЕНИЕ(Перечисление.АР_СпособыРасчетаКурсаВалюты.Расчетный),
	|	0,
	|	0,
	|	ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	РегистрСведений.АР_КурсыВалютДляРасчетовСАрендаторами.СрезПоследних(
	|			&ДатаСреза,
	|			СрокДействия >= &ДатаСреза
	|				ИЛИ СрокДействия = ДАТАВРЕМЯ(1, 1, 1)) КАК КурсыВалютДляРасчетовСАрендаторамиСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АР_ДополнительноеСоглашение КАК Реквизиты
	|		ПО (Реквизиты.ДоговорКонтрагента = КурсыВалютДляРасчетовСАрендаторамиСрезПоследних.ДоговорКонтрагента)
	|			И (Реквизиты.Ссылка = &Ссылка)
	|			И (НЕ Реквизиты.ИспользоватьСпециальныйКурсВалюты)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Печать

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "СтруктураАссортимента";
	КомандаОтправки.Представление = НСтр("ru = 'Структура ассортимента'");
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ПротоколСогласованияЦены";
	КомандаОтправки.Представление = НСтр("ru = 'Протокол согласования цены'");
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ПередаточныйАкт";
	КомандаОтправки.Представление = НСтр("ru = 'Передаточный акт'");
	
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "СоглашениеОПролонгации";
	КомандаОтправки.Представление = НСтр("ru = 'Соглашение о пролонгации'");
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СтруктураАссортимента";
	КомандаПечати.Представление = НСтр("ru = 'Структура ассортимента'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПротоколСогласованияЦены";
	КомандаПечати.Представление = НСтр("ru = 'Протокол согласования цены'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПередаточныйАкт";
	КомандаПечати.Представление = НСтр("ru = 'Передаточный акт'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СоглашениеОПролонгации";
	КомандаПечати.Представление = НСтр("ru = 'Соглашение о пролонгации'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьПланировок";
	КомандаПечати.Представление = НСтр("ru = 'Печать планировок'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиПланировок";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьШаблоновWord";
	КомандаПечати.Представление = НСтр("ru = 'Печать по шаблону MS Word'");
	КомандаПечати.Обработчик    = "АР_ОбщиеПроцедурыКлиент.ВыполнитьКомандуПечатиШаблоновWord";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокумента";
	
	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Дополнительное соглашение""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок       = 160;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СтруктураАссортимента") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СтруктураАссортимента", "Структура ассортимента", ПечатьСтруктурыАссортимента(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПротоколСогласованияЦены") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПротоколСогласованияЦены", "Протокол согласования цены", ПечатьПротоколаСогласованияЦены(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПередаточныйАкт") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПередаточныйАкт", "Передаточный акт", ПечатьПередаточногоАкта(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СоглашениеОПролонгации") Тогда
        УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СоглашениеОПролонгации", "Соглашение о пролонгации", ПечатьСоглашенияОПролонгации(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьСтруктурыАссортимента(МассивОбъектов, ОбъектыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ДополнительноеСоглашение_Ассортимент";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	ДополнительноеСоглашение.Организация,
	|	ДополнительноеСоглашение.Дата,
	|	ДополнительноеСоглашение.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	ДополнительноеСоглашение.НомерАссортимента,
	|	ДополнительноеСоглашение.ДатаНачалаАссортимента КАК ДатаАссортиментаС,
	|	ДополнительноеСоглашение.ДатаОкончанияАссортимента КАК ДатаАссортиментаПо,
	|	ДополнительноеСоглашение.Ссылка
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение КАК ДополнительноеСоглашение
	|ГДЕ
	|	ДополнительноеСоглашение.Ссылка = &Ссылка";
	
	ЗапросАссортимент = Новый Запрос();
	ЗапросАссортимент.Текст =
	"ВЫБРАТЬ
	|	АР_СтруктураАссортимента.Наименование КАК СтруктураАссортимента
	|ИЗ
	|	Справочник.АР_СтруктураАссортимента КАК АР_СтруктураАссортимента
	|ГДЕ
	|	АР_СтруктураАссортимента.Владелец В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды
	|			ИЗ
	|				Документ.АР_ДополнительноеСоглашение.ПостояннаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты
	|			ГДЕ
	|				АР_ЗаключениеДоговораАрендыПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.ОбъектАренды
	|			ИЗ
	|				Документ.АР_ДополнительноеСоглашение.ПеременнаяЧастьАренднойПлаты КАК АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты
	|			ГДЕ
	|				АР_ЗаключениеДоговораАрендыПеременнаяЧастьАренднойПлаты.Ссылка = &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтруктураАссортимента";
		
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ДополнительноеСоглашение.ПФ_MXL_СтруктураАссортимента");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
				
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ.Ссылка);
		
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Область = Макет.ПолучитьОбласть("Заголовок");		
		Область.Параметры.НомерАссортимента = Шапка.НомерАссортимента;
		Область.Параметры.ДатаАссортиментаС = Формат(Шапка.ДатаАссортиментаС, "ДФ='д ММММ гггг'");
		Область.Параметры.ДатаАссортиментаПо = Формат(Шапка.ДатаАссортиментаПо, "ДФ='д ММММ гггг'");
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
				
		ЗапросАссортимент.УстановитьПараметр("Ссылка", Документ.Ссылка);
		Ассортимент = ЗапросАссортимент.Выполнить().Выбрать();
		
		НомерСтроки = 1;
		Пока Ассортимент.Следующий() Цикл
			Область.Параметры.Номер = НомерСтроки;
			Область.Параметры.Заполнить(Ассортимент);
			ТабличныйДокумент.Вывести(Область);
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		Область.Параметры.РуководительКонтрагента = Шапка.РуководительКонтрагента;
		
		ТабличныйДокумент.Вывести(Область);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьСчетаЗаказа()

Функция ПечатьПротоколаСогласованияЦены(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ДополнительноеСоглашение_ПротоколСогласованияЦены";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	ДополнительноеСоглашение.Контрагент,
	|	ДополнительноеСоглашение.Организация,
	|	ДополнительноеСоглашение.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	ДополнительноеСоглашение.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	ДополнительноеСоглашение.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	ДополнительноеСоглашение.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	ДополнительноеСоглашение.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	ДополнительноеСоглашение.ДоговорКонтрагента КАК Договор,
	|	ДополнительноеСоглашение.Дата,
	|	ДополнительноеСоглашение.НомерПротокола,
	|	ДополнительноеСоглашение.ДатаПротокола,
	|	ДополнительноеСоглашение.Ссылка
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение КАК ДополнительноеСоглашение
	|ГДЕ
	|	ДополнительноеСоглашение.Ссылка = &Ссылка";
	
	ЗапросОбъекты = Новый Запрос(); 
	ЗапросОбъекты.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыАренды.ОбъектАренды,
	|	ОбъектыАренды.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|	ОбъектыАренды.Количество КАК ПлощадьОбъекта,
	|	ОбъектыАренды.ЕдиницаИзмерения КАК ЕдиницаИзмеренияОбъекта,
	|	ОбъектыАренды.СпособНачисленияВалюта КАК Валюта,
	|	ОбъектыАренды.СпособНачисленияВалюта.ПараметрыПрописи КАК ВалютаПараметрыПрописи,
	|	ОбъектыАренды.СпособНачисленияПериодНачисления КАК ПериодАренды,
	|	ОбъектыАренды.Ставка,
	|	ОбъектыАренды.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды,
	|	ОбъектыАренды.ДатаОкончанияСрокаАренды КАК ДатаОкончанияАренды,
	|	ОбъектыАренды.ОбъектАренды.ЗданиеИЭтаж КАК ОбъектАрендыЗданиеЭтаж
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды,
	|		ПостояннаяЧастьАренднойПлаты.Услуга.ВидСтавкиНДС КАК ВидСтавкиНДС,
	|		ПостояннаяЧастьАренднойПлаты.Количество КАК Количество,
	|		ПостояннаяЧастьАренднойПлаты.ОбъектАренды.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|		ПостояннаяЧастьАренднойПлаты.СпособНачисления.Валюта КАК СпособНачисленияВалюта,
	|		ПостояннаяЧастьАренднойПлаты.СпособНачисления.ПериодНачисления КАК СпособНачисленияПериодНачисления,
	|		ПостояннаяЧастьАренднойПлаты.Ставка КАК Ставка,
	|		ПостояннаяЧастьАренднойПлаты.ДатаНачалаСрокаАренды КАК ДатаНачалаСрокаАренды,
	|		ПостояннаяЧастьАренднойПлаты.ДатаОкончанияСрокаАренды КАК ДатаОкончанияСрокаАренды
	|	ИЗ
	|		Документ.АР_ДополнительноеСоглашение.ПостояннаяЧастьАренднойПлаты КАК ПостояннаяЧастьАренднойПлаты
	|	ГДЕ
	|		ПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка) КАК ОбъектыАренды";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ДополнительноеСоглашение.ПФ_MXL_ПротоколСогласованияЦены");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьЗаголовка = Макет.ПолучитьОбласть("Заголовок");
		
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ.Ссылка);
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		//только для первого листа выводим текст "Приложение 1"
		ОбластьЗаголовка.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовка);
		
		Область = Макет.ПолучитьОбласть("Протокол");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.ДатаПротокола = Формат(Шапка.ДатаПротокола, "ДФ='д ММММ гггг'");
		Область.Параметры.ДатаДокумента = Формат(Документ.Ссылка.Дата, "ДЛФ=DD");
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Область.Параметры.РуководительОрганизацииРП, Руководители.Руководитель.Пол) Тогда
			Область.Параметры.РуководительОрганизацииРП = Руководители.РуководительПредставление;
		КонецЕсли;
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		
		//теперь для каждого объекта формируем свой протокол согласования
		ЗапросОбъекты.УстановитьПараметр("Ссылка", Документ.Ссылка);
		
		ОбъектыАренды = ЗапросОбъекты.Выполнить().Выбрать();
		Индекс = 1;
		Пока ОбъектыАренды.Следующий() Цикл
			Область.Параметры.НомерПротокола = Шапка.НомерПротокола+"\"+Индекс;
			Область.Параметры.Заполнить(ОбъектыАренды);
			
			ФорматнаяСтрока = "Л=ru_RU;НП=Истина;НД=Истина;ДП=Ложь";
			ПараметрыПрописи = ОбъектыАренды.ВалютаПараметрыПрописи;
			
			Ставка = АР_ОбщиеПроцедуры.ПолучитьСтавку(ОбъектыАренды.Ставка);
			СтавкаПрописью = НРег(ЧислоПрописью(Ставка, ФорматнаяСтрока, ПараметрыПрописи));
			СтавкаЧислом = "" + Ставка + " " + Сред(СтавкаПрописью, Найти(СтавкаПрописью, Лев(ПараметрыПрописи, 3)));
			
			Область.Параметры.Ставка = СтавкаЧислом;
			Область.Параметры.СтавкаПрописью = СтавкаПрописью;
			Область.Параметры.ПериодНачисленияПрописью = НРег(ОбъектыАренды.ПериодАренды);
			
			СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ОбъектыАренды.ВидСтавкиНДС, Шапка.Дата); 
			Если ЗначениеЗаполнено(СтавкаНДС) И НЕ (СтавкаНДС = Перечисления.СтавкиНДС.БезНДС) Тогда
				ТекстНДС = "Арендная плата облагается НДС по ставке " + СтавкаНДС;
			Иначе
				ТекстНДС = "Арендная плата НДС не облагается";
			КонецЕсли;
			
			Область.Параметры.ТекстНДС = ТекстНДС;
			
			ТабличныйДокумент.Вывести(Область);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			Индекс = Индекс+1;
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьПередаточногоАкта(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ДополнительноеСоглашение_ПередаточныйАкт";
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.Текст =
	"ВЫБРАТЬ
	|	ДополнительноеСоглашение.Контрагент,
	|	ДополнительноеСоглашение.Организация,
	|	ДополнительноеСоглашение.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	ДополнительноеСоглашение.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	ДополнительноеСоглашение.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	ДополнительноеСоглашение.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	ДополнительноеСоглашение.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	ДополнительноеСоглашение.ДоговорКонтрагента КАК Договор,
	|	ДополнительноеСоглашение.Дата,
	|	ДополнительноеСоглашение.НомерАкта,
	|	ДополнительноеСоглашение.ДатаАкта,
	|	ДополнительноеСоглашение.Ссылка
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение КАК ДополнительноеСоглашение
	|ГДЕ
	|	ДополнительноеСоглашение.Ссылка = &Ссылка";
	
	ЗапросОбъекты = Новый Запрос();
	ЗапросОбъекты.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыАренды.ОбъектАренды,
	|	ОбъектыАренды.Количество КАК Площадь,
	|	ОбъектыАренды.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОбъектыАренды.ДатаНачалаАренды
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПостояннаяЧастьАренднойПлаты.ОбъектАренды КАК ОбъектАренды,
	|		ПостояннаяЧастьАренднойПлаты.Количество КАК Количество,
	|		ПостояннаяЧастьАренднойПлаты.Услуга.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмерения,
	|		ПостояннаяЧастьАренднойПлаты.ДатаНачалаСрокаАренды КАК ДатаНачалаАренды
	|	ИЗ
	|		Документ.АР_ДополнительноеСоглашение.ПостояннаяЧастьАренднойПлаты КАК ПостояннаяЧастьАренднойПлаты
	|	ГДЕ
	|		ПостояннаяЧастьАренднойПлаты.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПеременнаяЧастьАренднойПлаты.ОбъектАренды.Наименование + ""/"" + ПеременнаяЧастьАренднойПлаты.Услуга.Наименование,
	|		ПеременнаяЧастьАренднойПлаты.Количество,
	|		ПеременнаяЧастьАренднойПлаты.ЕдиницаИзмерения.Наименование,
	|		ПеременнаяЧастьАренднойПлаты.ДатаНачалаСрокаАренды
	|	ИЗ
	|		Документ.АР_ДополнительноеСоглашение.ПеременнаяЧастьАренднойПлаты КАК ПеременнаяЧастьАренднойПлаты
	|	ГДЕ
	|		ПеременнаяЧастьАренднойПлаты.Ссылка = &Ссылка) КАК ОбъектыАренды";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ДополнительноеСоглашение.ПФ_MXL_ПередаточныйАкт");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Заголовок");
		
		ЗапросШапка.УстановитьПараметр("Ссылка", Документ.Ссылка);
		Шапка = ЗапросШапка.Выполнить().Выбрать();
		Шапка.Следующий();
		
		//только для первого листа выводим текст "Приложение 1"
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.ДатаАкта = Формат(Шапка.ДатаАкта,"ДФ='д ММММ гггг'");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Область.Параметры.РуководительОрганизации, Руководители.Руководитель.Пол) Тогда
			Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Область);
		
		
		//теперь для каждого объекта заполняем таблицу
		ЗапросОбъекты.УстановитьПараметр("Ссылка", Документ.Ссылка);
		ОбъектыАренды = ЗапросОбъекты.Выполнить().Выбрать();
		
		Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
		
		Индекс = 1;
		Пока ОбъектыАренды.Следующий() Цикл
			Область.Параметры.НомерСтроки = Индекс;
			Область.Параметры.Заполнить(ОбъектыАренды);
			Область.Параметры.ДатаНачалаАренды = Формат(ОбъектыАренды.ДатаНачалаАренды,"ДФ='д ММММ гггг'");
			
			ТабличныйДокумент.Вывести(Область);
			
			Индекс = Индекс+1;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		Область.Параметры.РуководительКонтрагента = Шапка.РуководительКонтрагента;
		
		ТабличныйДокумент.Вывести(Область);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
		
КонецФункции

Функция ПечатьСоглашенияОПролонгации(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ДополнительноеСоглашение_СоглашениеОПролонгации";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АР_ДополнительноеСоглашение.Контрагент,
	|	АР_ДополнительноеСоглашение.Организация,
	|	АР_ДополнительноеСоглашение.Организация.НаименованиеПолное КАК ПолноеНаименованиеОрганизации,
	|	АР_ДополнительноеСоглашение.Контрагент.НаименованиеПолное КАК ПолноеНаименованиеКонтрагента,
	|	АР_ДополнительноеСоглашение.Контрагент.АР_ГенеральныйДиректорКратко КАК РуководительКонтрагента,
	|	АР_ДополнительноеСоглашение.Контрагент.АР_ГенДиректорКраткоРодительныйПадеж КАК РуководительКонтрагентаРодительныйПадеж,
	|	АР_ДополнительноеСоглашение.Контрагент.АР_НаОсновании КАК КонтрагентНаОсновании,
	|	АР_ДополнительноеСоглашение.ДоговорКонтрагента КАК Договор,
	|	АР_ДополнительноеСоглашение.Дата,
	|	АР_ДополнительноеСоглашение.Номер КАК НомерСоглашения,
	|	АР_ДополнительноеСоглашение.ДатаОкончанияДоговора,
	|	АР_ДополнительноеСоглашение.Ссылка
	|ИЗ
	|	Документ.АР_ДополнительноеСоглашение КАК АР_ДополнительноеСоглашение
	|ГДЕ
	|	АР_ДополнительноеСоглашение.Ссылка = &Ссылка";
	
	ПервыйДокумент = Истина;
	
	Для каждого Документ Из МассивОбъектов Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АР_ДополнительноеСоглашение.ПФ_MXL_СоглашениеОПролонгации");
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос.УстановитьПараметр("Ссылка", Документ);
		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Область = Макет.ПолучитьОбласть("Протокол");
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата,);
		
		Область.Параметры.Заполнить(Шапка);
		Область.Параметры.ДатаСоглашения = Формат(Шапка.Дата,"ДФ='д ММММ гггг'");
		Область.Параметры.ДатаОкончанияДоговора = Формат(Шапка.ДатаОкончанияДоговора,"ДФ='д ММММ гггг'");
		Если Не ФизическиеЛицаЗарплатаКадры.Просклонять(Руководители.Руководитель, 2, Область.Параметры.РуководительОрганизацииРП, Руководители.Руководитель.Пол) Тогда
			Область.Параметры.РуководительОрганизацииРП = Руководители.РуководительПредставление;
		КонецЕсли;
		Область.Параметры.РуководительОрганизации = Руководители.РуководительПредставление;
		
		ТабличныйДокумент.Вывести(Область);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли
