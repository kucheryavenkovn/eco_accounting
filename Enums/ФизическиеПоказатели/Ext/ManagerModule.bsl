#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сравнивает показатели на идентичность (например, если имена показателей являются синонимами).
// Показатели считаются идентичными, если один из них можно заменить другим без потери смысла.
// Например, "Площадь торгового зала" и "Площадь помещения".
//
// Параметры:
//   Показатель1 - ПеречислениеСсылка.ФизическиеПоказатели - сравниваемый показатель
//   Показатель2 - ПеречислениеСсылка.ФизическиеПоказатели - сравниваемый показатель
//
// Возвращаемое значение:
//  Булево - Истина, если показатели заполнены и идентичны.
//
Функция ПоказателиИдентичны(Показатель1, Показатель2) Экспорт
	
	Если Не ЗначениеЗаполнено(Показатель1) Или Не ЗначениеЗаполнено(Показатель2) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Показатель1 = Показатель2 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ИдентичныеПоказатели = ИдентичныеПоказатели();
	
	Возврат ИдентичныеПоказатели[Показатель1] = Показатель2
		Или ИдентичныеПоказатели[Показатель2] = Показатель1;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИдентичныеПоказатели()
	
	ИдентичныеПоказатели = Новый Соответствие;
	
	ИдентичныеПоказатели.Вставить(КоличествоЗемельныхУчастков, КоличествоОбъектов);
	
	ИдентичныеПоказатели.Вставить(КоличествоТорговыхАппаратов, КоличествоОбъектов);
	ИдентичныеПоказатели.Вставить(КоличествоТорговыхМест, КоличествоОбъектов);
	ИдентичныеПоказатели.Вставить(КоличествоТранспортныхСредств, КоличествоОбъектов);
	
	ИдентичныеПоказатели.Вставить(ПлощадьСтоянки, ПлощадьЗемельногоУчастка);
	
	ИдентичныеПоказатели.Вставить(ПлощадьЗалаОбслуживания, ПлощадьПомещения);
	ИдентичныеПоказатели.Вставить(ПлощадьТорговогоЗала, ПлощадьПомещения);
	ИдентичныеПоказатели.Вставить(ПлощадьТорговогоМеста, ПлощадьПомещения);
	
	ИдентичныеПоказатели.Вставить(ПлощадьИнформационногоПоля, ПлощадьПоверхности);
	ИдентичныеПоказатели.Вставить(ПлощадьСветоизлучающейПоверхности, ПлощадьПоверхности);
	ИдентичныеПоказатели.Вставить(ПлощадьЭкспонирующейПоверхности, ПлощадьПоверхности);
	
	ИдентичныеПоказатели.Вставить(КоличествоРаботниковВключаяИП, СредняяЧисленностьНаемныхРаботников);
	
	Возврат ИдентичныеПоказатели;
	
КонецФункции

#КонецОбласти

#КонецЕсли